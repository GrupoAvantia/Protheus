#INCLUDE 'MATR902.CH'
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATR902  ³ Autor ³ Nereu Humberto Junior ³ Data ³01.08.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Kardex  Fisico FIFO/LIFO                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MATR902()
Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef()
	oReport:PrintDialog()
Else
	MATR902R3()
EndIf

Return   
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³Nereu Humberto Junior  ³ Data ³01.08.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local lCusFifo  := GetMV("MV_CUSFIFO",.F.)
Local Titulo    := OemToAnsi(IIf(lCusFifo,STR0001,STR0032)) //'KARDEX FISICO FIFO/LIFO'
Local cPicD1Qt  := PesqPict("SD1","D1_QUANT")
Local cTamD1Qt  := TamSX3('D1_QUANT')[1]
Local cPicD1Cust:= PesqPict("SD1","D1_CUSTO")
Local cTamD1Cust:= TamSX3('D1_CUSTO')[1]
Local cPicD2Qt  := PesqPict("SD2","D2_QUANT")
Local cTamD2Qt  := TamSX3('D2_QUANT')[1]
Local cPicD2Cust:= PesqPict("SD2","D2_CUSTO1")
Local cTamD2Cust:= TamSX3('D2_CUSTO1')[1]
Local cTamD1Doc := TamSX3('D1_DOC')[1]
Local cTamD1CF  := TamSX3('D1_CF')[1]
Local aOrdem    := {}
Local oSection1
Local oSection2
Local oSection3
Local oReport 
Local oCell         
Local lVer116     := (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6" .Or. VAL(GetVersao(.F.))  > 11)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao do fonte        ³
//³ SIGACUSA.PRX aplicados no rpo do cliente, assim verificando     |
//| a necessidade de uma atualizacao nestes fontes. NAO REMOVER !!!	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !(FindFunction("SIGACUSA_V") .And. SIGACUSA_V() >= 20060321)
    Final("Atualizar SIGACUSA.PRX !!!")
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inclusao de Perguntas                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:= TReport():New("MATR902",Titulo,"MTR902", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003) //'KARDEX FISICO FIFO/LIFO'##"Este programa emitir  uma rela‡„o com as movimenta‡”es"##"dos produtos selecionados, ordenados sequencialmente."
oReport:SetLandscape()    
oReport:SetTotalInLine(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01        	// Do produto                           ³
//³ mv_par02        	// Ate o produto                        ³
//³ mv_par03        	// Do tipo                              ³
//³ mv_par04        	// Ate o tipo                           ³
//³ mv_par05        	// Da data                              ³
//³ mv_par06        	// Ate a data                           ³
//³ mv_par07        	// Lista produtos s/movimento           ³
//³ mv_par08        	// Qual Local (almoxarifado)            ³
//³ mv_par09        	// (d)OCUMENTO/(s)EQUENCIA              ³
//³ mv_par10            // moeda selecionada ( 1 a 5 )          ³
//³ mv_par11            // Pagina Inicial                       ³
//³ mv_par12            // Agrupar por Lote ?                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR902",.F.)

Aadd( aOrdem, STR0004 ) // " Codigo Produto "
Aadd( aOrdem, STR0005 ) // " Tipo do Produto"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Secao 1 - Dados do Produto                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New(oReport,STR0063,{"SB1"},aOrdem) //"Produtos (Parte 1)"
oSection1:SetTotalInLine(.F.)
oSection1:SetReadOnly()
oSection1:SetLineStyle()

TRCell():New(oSection1,"B1_COD"		,"SB1",/*Titulo*/						,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_DESC"	,"SB1",/*Titulo*/						,/*Picture*/,30			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_UM"		,"SB1",STR0057							,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_TIPO"	,"SB1",STR0058							,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_GRUPO"	,"SB1",STR0059							,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"nCusMed"	,"   ",IIf(lCusFifo,STR0060,STR0056)	,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"nQtdSal"	,"   ",STR0055							,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"nVlrSal"	,"   ",STR0056							,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Secao 2 - Cont. dos dados do Produto            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New(oSection1,STR0064,{"SB1","SB2","NNR"}) //"Produtos (Parte 2)"
oSection2:SetTotalInLine(.F.)
oSection2:SetReadOnly()
oSection2:SetLineStyle()

TRCell():New(oSection2,"B1_POSIPI"	,"SB1",STR0061,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

If lVer116
	TRCell():New(oSection2,"NNR_DESCRI","NNR",STR0062,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| Posicione("NNR",1,xFilial("NNR")+MV_PAR08,"NNR_DESCRI") })
Else
	TRCell():New(oSection2,"B2_LOCALIZ","SB2",STR0062,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Secao 3 - Movimentos                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection3 := TRSection():New(oSection2,STR0065,{"SD1","SD2","SD3"}) //"Movimentação dos Produtos"
oSection3:SetHeaderPage()
oSection3:SetTotalInLine(.F.)
oSection3:SetTotalText(STR0024) //"T O T A I S  :"
oSection3:SetReadOnly()

TRCell():New(oSection3,"dDtMov"		,"   ",STR0039+CRLF+STR0040							,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTES"		,"   ",STR0041										,"@!"		,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cCF"		,"   ",STR0042										,"@!"		,cTamD1CF	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cDoc"		,"   ",STR0010+CRLF+STR0043							,"@!"		,cTamD1Doc	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco1"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nENTQtd"	,"   ",STR0044+CRLF+STR0045							,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"nENTCus"	,"   ",STR0044+CRLF+STR0046							,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"cTraco2"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nCusMov"	,"   ",IIf(lCusFifo,STR0047,STR0056)+CRLF+STR0048	,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"cTraco3"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nSAIQtd"	,"   ",STR0049+CRLF+STR0045							,cPicD2Qt	,cTamD2Qt	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"nSAICus"	,"   ",STR0049+CRLF+STR0046							,cPicD2Cust	,cTamD2Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"cTraco4"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nSALDQtd"	,"   ",STR0050+CRLF+STR0045							,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"nSALDCus"	,"   ",STR0050+CRLF+STR0051							,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection3,"cTraco5"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"cCCPVPJOP"	,"   ",STR0052+CRLF+STR0053							,"@!"		,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Secao 4 - SubTotal                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection4 := TRSection():New(oSection2,"STR0028",{"SD1","SD2","SD3"}) //"SubTotal
oSection4:SetHeaderPage()
oSection4:SetTotalInLine(.F.)
oSection4:SetTotalText(STR0024) //"T O T A I S  :"
oSection4:SetReadOnly()

TRCell():New(oSection4,"dDtMov"		,"   ",STR0039+CRLF+STR0040							,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection4,"cTES"		,"   ",STR0041										,"@!"		,5			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection4,"cCF"		,"   ",STR0042										,"@!"		,3			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection4,"cDoc"		,"   ",STR0010+CRLF+STR0043							,"@!"		,cTamD1Doc	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection4,"cTraco1"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection4,"nENTQtd"	,"   ",STR0044+CRLF+STR0045							,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"nENTCus"	,"   ",STR0044+CRLF+STR0046							,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"cTraco2"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection4,"nCusMov"	,"   ",IIf(lCusFifo,STR0047,STR0056)+CRLF+STR0048	,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"cTraco3"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection4,"nSAIQtd"	,"   ",STR0049+CRLF+STR0045							,cPicD2Qt	,cTamD2Qt	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"nSAICus"	,"   ",STR0049+CRLF+STR0046							,cPicD2Cust	,cTamD2Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"cTraco4"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection4,"nSALDQtd"	,"   ",STR0050+CRLF+STR0045							,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"nSALDCus"	,"   ",STR0050+CRLF+STR0051							,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection4,"cTraco5"	,"   ","|"+CRLF+"|"									,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection4,"cCCPVPJOP"	,"   ",STR0052+CRLF+STR0053							,"@!"		,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSection4:Cell("cDoc"     ):Hide()
oSection4:Cell("cTES"     ):Hide()
oSection4:Cell("cCF"      ):Hide()
oSection4:Cell("cTraco1"  ):Hide()
oSection4:Cell("cTraco2"  ):Hide()
oSection4:Cell("nCusMov"  ):Hide()
oSection4:Cell("cTraco3"  ):Hide()
oSection4:Cell("cTraco4"  ):Hide()
oSection4:Cell("cTraco5"  ):Hide()
oSection4:Cell("cCCPVPJOP"):Hide()

oSection4:Cell("dDtMov"   ):HideHeader()
oSection4:Cell("cTES"     ):HideHeader()
oSection4:Cell("cCF"      ):HideHeader()
oSection4:Cell("cDoc"     ):HideHeader()
oSection4:Cell("cTraco1"  ):HideHeader()
oSection4:Cell("nENTQtd"  ):HideHeader()
oSection4:Cell("nENTCus"  ):HideHeader()
oSection4:Cell("cTraco2"  ):HideHeader()
oSection4:Cell("nCusMov"  ):HideHeader()
oSection4:Cell("cTraco3"  ):HideHeader()
oSection4:Cell("nSAIQtd"  ):HideHeader()
oSection4:Cell("nSAICus"  ):HideHeader()
oSection4:Cell("cTraco4"  ):HideHeader()
oSection4:Cell("nSALDQtd" ):HideHeader()
oSection4:Cell("nSALDCus" ):HideHeader()
oSection4:Cell("cTraco5"  ):HideHeader()
oSection4:Cell("cCCPVPJOP"):HideHeader()

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Nereu Humberto Junior  ³ Data ³01.08.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatorio                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

Local oSection1 := oReport:Section(1) 
Local oSection2 := oReport:Section(1):Section(1)  
Local oSection3 := oReport:Section(1):Section(1):Section(1)  
Local oSection4 := oReport:Section(1):Section(1):Section(2)
Local nOrdem    := oReport:Section(1):GetOrder() 
Local cMoeda    := GetMV("MV_SIMB"+Str(mv_par10,1))
Local cAlias    := ""
Local cSeqIni   := "999999"
Local cProdAnt  := ""
Local cLocalAnt := ""
Local cCampo1   := ""
Local cCampo2   := ""
Local cCampo3   := ""
Local cCampo4   := ""
Local lFirst    := .T.
Local aSalAtu   := {}
Local aSalAtuLot:= {}
Local nTipo     := 0
Local nEntrada  := 0
Local nSEntrada := 0
Local nSaida    := 0
Local nSSaida	:= 0
Local nCEntrada := 0
Local nSCEntrada:= 0
Local nCSaida   := 0
Local nSCSaida	:= 0
Local nCusFif   := 0
Local nCusFifLot:= 0
Local nInd      := 0
Local cPicD1Qt  := PesqPict("SD1","D1_QUANT")
Local cPicD2Qt  := PesqPict("SD2","D2_QUANT")
Local cPicD3Qt  := PesqPict("SD3","D3_QUANT")
Local cPicB2Qt  := PesqPict("SB2","B2_QATU")
Local cPicB2Qt2 := PesqPict("SB2","B2_QTSEGUM")
Local cPicD1Cust:= PesqPict("SD1","D1_CUSTO")    
Local cPicD2Cust:= PesqPict("SD2","D2_CUSTO1")
Local cCond1    :=''
Local cCond2    :=''
Local cTRBSD1   := CriaTrab(,.F.)
Local cTRBSD2   := CriaTrab(,.F.)
Local cTRBSD3   := CriaTrab(,.F.)
Local cTRBSD8   := Subs(cTrbSD3,1,7)+"A"
Local cLoteAnt  := ""
Local lImpSMov  := .F.
Local lImpS3    := .F.
Local lCusFifo  := GetMV("MV_CUSFIFO",.F.)

#IFNDEF TOP
	Local cCondicao := ""
#ELSE
	Local cAliasTop := ""
#ENDIF

If mv_par12 == 1
	TRFunction():New(oSection4:Cell("nENTQtd" ),NIL,"SUM",/*oBreak*/,"",cPicD1Qt	,/*uFormula*/,.F.,.T.) 
	TRFunction():New(oSection4:Cell("nENTCus" ),NIL,"SUM",/*oBreak*/,"",cPicD1Cust	,/*uFormula*/,.F.,.T.) 
	
	TRFunction():New(oSection4:Cell("nSAIQtd" ),NIL,"SUM",/*oBreak*/,"",cPicD2Qt	,/*uFormula*/,.F.,.T.) 
	TRFunction():New(oSection4:Cell("nSAICus" ),NIL,"SUM",/*oBreak*/,"",cPicD2Cust	,/*uFormula*/,.F.,.T.)

	TRFunction():New(oSection4:Cell("nSALDQtd" ),NIL,"SUM",/*oBreak*/,"",cPicD2Qt	,/*uFormula*/,.F.,.T.) 
	TRFunction():New(oSection4:Cell("nSALDCus" ),NIL,"SUM",/*oBreak*/,"",cPicD2Cust,/*uFormula*/,.F.,.T.)
Else
  	TRFunction():New(oSection3:Cell("nENTQtd" ),NIL,"SUM",/*oBreak*/,"",cPicD1Qt	,/*uFormula*/,.T.,.F.) 
 	TRFunction():New(oSection3:Cell("nENTCus" ),NIL,"SUM",/*oBreak*/,"",cPicD1Cust	,/*uFormula*/,.T.,.F.) 
	
	TRFunction():New(oSection3:Cell("nSAIQtd" ),NIL,"SUM",/*oBreak*/,"",cPicD2Qt	,/*uFormula*/,.T.,.F.) 
	TRFunction():New(oSection3:Cell("nSAICus" ),NIL,"SUM",/*oBreak*/,"",cPicD2Cust	,/*uFormula*/,.T.,.F.)
	
	TRFunction():New(oSection3:Cell("nSALDQtd"),NIL,"ONPRINT",/*oBreak*/,"",cPicD1Qt	,{|| oSection3:Cell("nSALDQtd"):GetValue(.T.) },.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)
	TRFunction():New(oSection3:Cell("nSALDCus"),NIL,"ONPRINT",/*oBreak*/,"",cPicD1Cust	,{|| oSection3:Cell("nSALDCus"):GetValue(.T.) },.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)
EndIf

oReport:SetTitle(OemToAnsi(IIf(lCusFifo,STR0008,STR0033))+mv_par08) // "Kardex Fisico-Financeiro FIFO/LIFO (Calculo) L O C A L : "
If nOrdem == 1
	oReport:SetTitle(oReport:Title()+OemToAnsi(STR0014)+STR0004+OemToAnsi(STR0015)+cMoeda+Str(mv_par10,1)+")")
Else
	oReport:SetTitle(oReport:Title()+OemToAnsi(STR0014)+STR0005+OemToAnsi(STR0015)+cMoeda+Str(mv_par10,1)+")")
Endif

If mv_par09 $ "Ss"
	oSection3:Cell("cDoc"):SetTitle(STR0011+CRLF+STR0043)							
Else
	oSection3:Cell("cDoc"):SetTitle(STR0010+CRLF+STR0043)							
EndIf

dbSelectArea("SB2")
dbSetOrder(1)

dbSelectArea("SD1")
IndRegua("SD1",cTrbSD1,"D1_FILIAL+D1_COD+D1_LOCAL+D1_DOC+D1_SERIE+D1_ITEM",,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD1")
#IFNDEF TOP
	dbSetIndex(cTrbSD1+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SD2")
IndRegua("SD2",cTrbSD2,"D2_FILIAL+D2_COD+D2_LOCAL+D2_DOC+D2_SERIE+D2_ITEM",,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD2")
#IFNDEF TOP
	dbSetIndex(cTrbSD2+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SD3")
IndRegua("SD3",cTrbSD3,"D3_FILIAL+D3_COD+D3_LOCAL+D3_SEQCALC+D3_NUMSEQ",,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD3")
#IFNDEF TOP
	dbSetIndex(cTrbSD3+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SD8")
If mv_par12 == 1
	cFiltro := "D8_FILIAL+D8_PRODUTO+D8_LOCAL+D8_SEQ+"+IIF(mv_par09 $ "Ss","D8_SEQCALC","D8_DOC")
Else
	cFiltro := "D8_FILIAL+D8_PRODUTO+D8_LOCAL+"+IIF(mv_par09 $ "Ss","D8_SEQCALC+D8_SEQ","D8_DOC")
EndIf	
IndRegua("SD8",cTrbSD8,cFiltro,,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD8")
#IFNDEF TOP
	dbSetIndex(cTrbSD8+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SB1")
If nOrdem == 1
	dbSetOrder(1)
ElseIf nOrdem == 2
	dbSetOrder(2)
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatorio                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao SQL                            ³	
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeSqlExpr(oReport:uParam)
	
	cAliasTop := GetNextAlias()
	lQuery := .T.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query do relatorio da secao 1                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):BeginQuery()	
	
	BeginSql Alias cAliasTop
	
		SELECT B1_COD, B1_DESC, B1_UM, B1_TIPO, B1_GRUPO, B1_POSIPI	 
	
		FROM %table:SB1% SB1

		WHERE SB1.B1_FILIAL  =  %xFilial:SB1%	AND
              SB1.B1_COD     >= %Exp:mv_par01% 	AND
              SB1.B1_COD     <= %Exp:mv_par02% AND          
              SB1.B1_TIPO    >= %Exp:mv_par03% 	AND
              SB1.B1_TIPO    <= %Exp:mv_par04% AND                   
		      SB1.%NotDel%                     	         
              
		ORDER BY %Order:SB1%
		
	EndSql 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Metodo EndQuery ( Classe TRSection )                                    ³
	//³                                                                        ³
	//³Prepara o relatorio para executar o Embedded SQL.                       ³
	//³                                                                        ³
	//³ExpA1 : Array com os parametros do tipo Range                           ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
	
#ELSE
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao Advpl                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAliasTop := "SB1"
	MakeAdvplExpr(oReport:uParam)

	cCondicao := 'B1_FILIAL == "'+xFilial("SB1")+'".And.' 
	cCondicao += 'B1_COD >= "'+mv_par01+'".And.B1_COD <="'+mv_par02+'".And.'
	cCondicao += 'B1_TIPO >= "'+mv_par03+'".And.B1_TIPO <="'+mv_par04+'"'

	oReport:Section(1):SetFilter(cCondicao,IndexKey())
	
#ENDIF		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da impressao do fluxo do relatório                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetMeter(SB1->(LastRec()))
While !oReport:Cancel() .And. !(cAliasTop)->(Eof()) 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se houve interrupcao pelo operador                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If oReport:Cancel()
		Exit
	EndIf	

	oReport:IncMeter()
	
	// Filtra Mao de Obra 
	If IsProdMod((cAliasTop)->B1_COD)
		dbSkip()
		Loop
	Endif
	
	dbSelectArea("SB2")
	dbSeek(xFilial("SB2")+(cAliasTop)->B1_COD+mv_par08)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se nao encontrar no arquivo de dados, nao lista              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Eof()
		dbSelectArea(cAliasTop)
		dbSkip()
		Loop
	EndIf
	
	cProdAnt  := (cAliasTop)->B1_COD
	cLocalAnt := SB2->B2_LOCAL
	
	dbSelectArea("SD3")
	dbSeek(xFilial("SD3")+(cAliasTop)->B1_COD+SB2->B2_LOCAL)
	
	dbSelectArea("SD8")
	dbSeek(xFilial("SD8")+(cAliasTop)->B1_COD+SB2->B2_LOCAL)
	
	oSection3:Init() 

	While .T.
		lImpSMov  := .F.
		lImpS3    := .F.
		dbSelectArea("SD3")
		If !Eof() .And. D3_FILIAL == xFilial("SD3") .And. D3_COD == cProdAnt .And. D3_LOCAL == cLocalAnt .And. IsProdMod(D3_COD)
			If D3_EMISSAO < mv_par05 .Or. D3_EMISSAO > mv_par06
				dbSkip()
				Loop
			Else
				If mv_par11 == 1
					If D3_NUMSEQ < cSeqIni
						cSeqIni := D3_NUMSEQ
						cAlias  := Alias()
					EndIf
				Else
					If D3_SEQCALC+D3_NUMSEQ < cSeqIni
						cSeqIni := D3_SEQCALC+D3_NUMSEQ
						cAlias  := Alias()
					EndIf
				EndIf
			EndIf
		EndIf
		
		dbSelectArea("SD8")
		If !Eof() .And. D8_FILIAL == xFilial("SD8") .And. D8_PRODUTO == cProdAnt .And. D8_LOCAL == cLocalAnt .And. Empty(cAlias)
			If !Empty(D8_DATA) .And. (D8_DATA < mv_par05 .Or. D8_DATA > mv_par06)
				dbSkip()
				Loop
			Else
				If mv_par11 == 1
					If D8_SEQ < cSeqIni
						cSeqIni := D8_SEQ
						cAlias  := Alias()
					EndIf
				Else
					If D8_SEQCALC+D8_SEQ < cSeqIni
						cSeqIni := D8_SEQCALC+D8_SEQ
						cAlias  := Alias()
					EndIf
				EndIf
			EndIf
		EndIf
		
		If !Empty(cAlias)

			dbSelectArea(cAlias)

			If mv_par12 == 1
				cDocumento := &(IIf(cAlias=="SD3",Subs(cAlias,2,2)+"_NUMSEQ",Subs(cAlias,2,2)+"_SEQ"))
			Endif	
            cCampo1 := Subs(cAlias,2,2)+IIF(cAlias=="SD8","_DATA","_EMISSAO")
			cCampo2 := Subs(cAlias,2,2)+"_TM"
			//-- Movimento Interno
			If cAlias=="SD3"
				cCampo3 := Subs(cAlias,2,2)+"_CF"
			ElseIf cAlias=="SD8"
				//-- Movimento Interno
				If Empty((cAlias)->D8_ITEM)
					cCampo3 := Subs(cAlias,2,2)+"_CF"
				Else	
					//-- Documento de Entrada
					If 	(cAlias)->D8_TM <= '500'
						SD1->(dbSeek(xFilial("SD1")+(cAlias)->D8_PRODUTO+(cAlias)->D8_LOCAL+(cAlias)->D8_DOC+(cAlias)->D8_SERIE+(cAlias)->D8_ITEM))
						cCampo3 := "SD1->D1_CF"
					//-- Documento de Saida
					Else
            			SD2->(dbSeek(xFilial("SD2")+(cAlias)->D8_PRODUTO+(cAlias)->D8_LOCAL+(cAlias)->D8_DOC+(cAlias)->D8_SERIE+(cAlias)->D8_ITEM))
						cCampo3 := "SD2->D2_CF"
					EndIf
					dbSelectArea(cAlias)
				EndIf
			EndIf	

			If cAlias == "SD3"
				cCampo4 := Subs(cAlias,2,2)+IIf(mv_par09 $ "Ss","_NUMSEQ","_DOC" )
			Else
				cCampo4 := Subs(cAlias,2,2)+IIf(mv_par09 $ "Ss","_SEQ","_DOC" )
			EndIf
			
			If lFirst
				nEntrada	:= 0
				nSEntrada	:= 0
				nSaida		:= 0
				nSSaida		:= 0
				nCEntrada	:= 0
				nSCEntrada	:= 0
				nCSaida		:= 0
				nSCSaida	:= 0
				nCusFif		:= 0
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o Saldo Inicial do Produto                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				aSalAtu := CalcEstFF((cAliasTop)->B1_COD,mv_par08,mv_par05)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o Custo Fifo/Lifo do Produto                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If aSalAtu[1] > 0
					nCusFif := aSalAtu[mv_par10+1]/aSalAtu[1]
				ElseIf aSalAtu[1] == 0 .And. aSalAtu[mv_par10+1] == 0
					nCusFif := 0
				Else
					If mv_par10 == 1
						nCusFif := SB2->B2_CM1
					ElseIf mv_par10 == 2
						nCusFif := SB2->B2_CM2
					ElseIf mv_par10 == 3
						nCusFif := SB2->B2_CM3
					ElseIf mv_par10 == 4
						nCusFif := SB2->B2_CM4
					ElseIf mv_par10 == 5
						nCusFif := SB2->B2_CM5
					EndIf
				EndIf
				
				oSection1:Init()
				oSection2:Init()

				oSection1:Cell("nCusMed"):SetValue(nCusFif)
				oSection1:Cell("nQtdSal"):SetValue(aSalAtu[1])
				oSection1:Cell("nVlrSal"):SetValue(aSalAtu[mv_par10+1])
				
				oSection1:PrintLine()
				oSection2:PrintLine()
				
				lFirst  := .F.
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cabecalho Saldo Incial por Lote FIFO/LIFO                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If mv_par12 == 1

				//Verifica se houve quebra de Lote
				If cLoteAnt != cDocumento

					//Calcula o Saldo Inicial do Lote
					aSalAtuLot := CalcEstFF((cAliasTop)->B1_COD,mv_par08,mv_par05,,,cDocumento)
				
					//Calcula o Custo Fifo/Lifo do Lote
					If aSalAtuLot[1] > 0
						nCusFifLot := aSalAtuLot[mv_par10+1]/aSalAtuLot[1]
					ElseIf aSalAtuLot[1] == 0 .and. aSalAtuLot[mv_par10+1] == 0
						nCusFifLot := 0
					Else
						nCusFifLot := &(("SB2->B2_CM") + Str(mv_par10,1))
					EndIf
                    
                    nLin := oReport:Row()
					oReport:PrintText(OemToAnsi(IIf(lCusFifo,STR0029,STR0037))+cDocumento) // "Lote Fifo/Lifo:"
					oReport:SkipLine()
					oReport:PrintText(OemToAnsi(IIf(lCusFifo,STR0030,STR0038))+TransForm(nCusFifLot,cPicD1Cust)+"        "+TransForm(aSalAtuLot[1],cPicD1Cust)+"        "+TransForm(aSalAtuLot[mv_par10+1],cPicD1Cust),nLin,oSection3:Cell('nENTCus'):ColPos()) // "Custo Fifo/Lifo Inicial :"
					oReport:SkipLine()
				EndIf
				cLoteAnt := cDocumento
			Endif	

			If IF(cAlias == "SD3",!Empty(D3_EMISSAO),!Empty(D8_DATA))
				oSection3:Cell("dDtMov"):SetValue(&cCampo1)
				oSection3:Cell("cTES"  ):SetValue(&cCampo2)	
				oSection3:Cell("cCF"   ):SetValue(&cCampo3)
				oSection3:Cell("cDoc"  ):SetValue(&cCampo4)	

				If cAlias == "SD3"
					If D3_TM <= "500"

						oSection3:Cell("nENTQtd"):Show()
						oSection3:Cell("nENTCus"):Show()
						oSection3:Cell("nCusMov"):Show()

						oSection3:Cell("nENTQtd"):SetValue(D3_QUANT)			
						oSection3:Cell("nENTCus"):SetValue(&(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))))			
						oSection3:Cell("nCusMov"):SetValue(&(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))))																	
						
						oSection3:Cell("nSAIQtd"):Hide()
						oSection3:Cell("nSAICus"):Hide()							
						oSection3:Cell("nSAIQtd"):SetValue(0)
						oSection3:Cell("nSAICus"):SetValue(0)						
						
						nEntrada		:= nEntrada   + D3_QUANT
						nSEntrada		:= nSEntrada  + D3_QUANT
						aSalAtu[1]		:= aSalAtu[1] + D3_QUANT
						nCEntrada		:= nCEntrada  + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCEntrada		:= nSCEntrada + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] + D3_QTSEGUM
						//Saldos por Lote Fifo/Lifo
                        If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] + D3_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] + D3_QTSEGUM
						Endif	
					Else
						oSection3:Cell("nENTQtd"):Hide()
						oSection3:Cell("nENTCus"):Hide()
						oSection3:Cell("nENTQtd"):SetValue(0)
						oSection3:Cell("nENTCus"):SetValue(0)
						
						oSection3:Cell("nCusMov"):Show()
						oSection3:Cell("nSAIQtd"):Show()
						oSection3:Cell("nSAICus"):Show()

						oSection3:Cell("nCusMov"):SetValue((&(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) / D3_QUANT))			
						oSection3:Cell("nSAIQtd"):SetValue(D3_QUANT)			
						oSection3:Cell("nSAICus"):SetValue(&(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))))																							
						
						nSaida			:= nSaida  + D3_QUANT
						nSSaida			:= nSSaida + D3_QUANT
						aSalAtu[1]		:= aSalAtu[1] - D3_QUANT
						nCSaida			:= nCSaida  + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCSaida		:= nSCSaida + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] - &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] - D3_QTSEGUM
						//Saldos por Lote Fifo/Lifo
						If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] - D3_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] - &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] - D3_QTSEGUM
						EndIf	
					EndIf
				Else
					If D8_TM <= "500"

						oSection3:Cell("nENTQtd"):Show()
						oSection3:Cell("nENTCus"):Show()
						oSection3:Cell("nCusMov"):Show()

						oSection3:Cell("nENTQtd"):SetValue(D8_QUANT)			
						oSection3:Cell("nENTCus"):SetValue(&(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))))			
						oSection3:Cell("nCusMov"):SetValue((&(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) / D8_QUANT))																	
						
						oSection3:Cell("nSAIQtd"):Hide()
						oSection3:Cell("nSAICus"):Hide()							
						oSection3:Cell("nSAIQtd"):SetValue(0)
						oSection3:Cell("nSAICus"):SetValue(0)						

						nEntrada   		:= nEntrada   + D8_QUANT
						nSEntrada		:= nSEntrada  + D8_QUANT
						aSalAtu[1]		:= aSalAtu[1] + D8_QUANT
						nCEntrada		:= nCEntrada  + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCEntrada		:= nSCEntrada + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] + 0
						//Saldos por Lote Fifo/Lifo
						If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] + D8_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] + 0
						Endif	
					Else
						oSection3:Cell("nENTQtd"):Hide()
						oSection3:Cell("nENTCus"):Hide()
						oSection3:Cell("nENTQtd"):SetValue(0)
						oSection3:Cell("nENTCus"):SetValue(0)
						
						oSection3:Cell("nCusMov"):Show()
						oSection3:Cell("nSAIQtd"):Show()
						oSection3:Cell("nSAICus"):Show()

						oSection3:Cell("nCusMov"):SetValue((&(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) / D8_QUANT))			
						oSection3:Cell("nSAIQtd"):SetValue(D8_QUANT)			
						oSection3:Cell("nSAICus"):SetValue(&(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))))																													
						
						nSaida	   		:= nSaida  + D8_QUANT
						nSSaida			:= nSSaida + D8_QUANT
						aSalAtu[1]		:= aSalAtu[1] - D8_QUANT
						nCSaida			:= nCSaida  + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCSaida		:= nSCSaida + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] - &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] - 0
						//Saldos por Lote Fifo/Lifo
						If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] - D8_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] - &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] - 0
						Endif	
					Endif
				Endif
			    
                If mv_par12 == 1
					oSection3:Cell("nSALDQtd"):SetValue(aSalAtuLot[1])			
					oSection3:Cell("nSALDCus"):SetValue(aSalAtuLot[mv_par10+1])								
				Else
					oSection3:Cell("nSALDQtd"):SetValue(aSalAtu[1])			
					oSection3:Cell("nSALDCus"):SetValue(aSalAtu[mv_par10+1])								
				EndIf	
				
				Do Case
					Case cAlias == "SD3"  && movimentos (SD3)
						If Empty(D3_OP)
							oSection3:Cell("cCCPVPJOP"):SetValue('CC'+D3_CC)
						Else
							oSection3:Cell("cCCPVPJOP"):SetValue('OP'+SUBS(D3_OP,1,6))
						EndIf
					Case cAlias == "SD8"  && movimentos (SD8)
						If Empty(D8_OP)
							oSection3:Cell("cCCPVPJOP"):SetValue('CC')
						Else
							oSection3:Cell("cCCPVPJOP"):SetValue('OP'+SUBS(D8_OP,1,6))
						EndIf
				EndCase
			Endif
			
			oSection3:PrintLine()
			
			dbSkip()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ SubTotal por Lote/Documento                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If mv_par12 == 1

				If cLoteAnt != &(IIf(cAlias=="SD3",Subs(cAlias,2,2)+"_NUMSEQ",Subs(cAlias,2,2)+"_SEQ"))

				    //Aviso de Lote sem movimentacao
				    If nSEntrada == 0 .And. nSSaida == 0 .And. nSCEntrada == 0 .And. nSCSaida == 0
						oReport:PrintText(STR0031) // "NAO HOUVE MOVIMENTACAO PARA ESTE LOTE"
				    Endif

					oReport:SkipLine()

					oSection4:Init()

					oSection4:Cell("dDtMov"  ):SetValue(STR0028)
					oSection4:Cell("nENTQtd" ):SetValue(nSEntrada)
					oSection4:Cell("nENTCus" ):SetValue(nSCEntrada)
					oSection4:Cell("nSAIQtd" ):SetValue(nSSaida)
					oSection4:Cell("nSAICus" ):SetValue(nSCSaida)
					oSection4:Cell("nSALDQtd"):SetValue(aSalAtuLot[1])
					oSection4:Cell("nSALDCus"):SetValue(aSalAtuLot[mv_par10+1])
					
					oSection4:PrintLine()
					
					oSection4:Finish()
					
					oReport:SkipLine()

					nSEntrada	:= 0
					nSCEntrada	:= 0
					nSSaida		:= 0
					nSCSaida	:= 0
					
				EndIf
			EndIf	
			cSeqIni := "999999"
			cAlias  := ""
			
		Else
			If !lFirst
				oReport:PrintText(STR0025+TransForm(aSalAtu[7],cPicB2Qt2),,oSection3:Cell('nSAICus'):ColPos()) //"QTD. NA SEGUNDA UM: "
				oReport:ThinLine()		
				lFirst := .T.
				lImpS3 := .T.
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se deve ou nao listar os produtos s/movimento       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If mv_par07 == 1

					nCusFif := 0
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Calcula o Saldo Inicial do Produto                            ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					aSalAtu := CalcEstFF(SB1->B1_COD,mv_par08,mv_par05)
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Calcula o Custo Fifo/Lifo do Produto                               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If AsalAtu[1] > 0
						nCusFif := aSalAtu[mv_par10+1]/aSalAtu[1]
					ElseIf AsalAtu[1] == 0 .and. AsalAtu[mv_par10+1] == 0
						nCusFif := 0
					Else
						nCusFif := &(("SB2->B2_CM") + Str(mv_par10,1))
					EndIf

					oSection1:Init()
					oSection2:Init()
										
					oSection1:Cell("nCusMed"):SetValue(nCusFif)
					oSection1:Cell("nQtdSal"):SetValue(aSalAtu[1])
					oSection1:Cell("nVlrSal"):SetValue(aSalAtu[mv_par10+1])
					
					oSection1:PrintLine()
					oSection2:PrintLine()					

					oReport:PrintText(STR0026) // "NAO HOUVE MOVIMENTACAO PARA ESTE PRODUTO"
					oReport:ThinLine()		
					lFirst  := .T.
					lImpSMov := .T.
				EndIf
			EndIf
			Exit
		EndIf
	EndDo

	oSection1:Finish()
	oSection2:Finish()
	If !lImpSMov .And. lImpS3
		oSection3:Finish()
	EndIf	
	lFirst  := .T.
	dbSelectArea(cAliasTop)
	dbSkip()
EndDo


dbSelectArea("SB1")
dbSetOrder(1)

dbSelectArea("SB2")
dbSetOrder(1)

dbSelectArea("SD1")
RetIndex("SD1")
Ferase(cTrbSD1+OrdBagExt())
dbSetOrder(1)

dbSelectArea("SD2")
RetIndex("SD2")
Ferase(cTrbSD2+OrdBagExt())
dbSetOrder(1)

dbSelectArea("SD3")
RetIndex("SD3")
Ferase(cTrbSD3+OrdBagExt())
dbSetOrder(1)

dbSelectArea("SD8")
RetIndex("SD8")
Ferase(cTrbSD8+OrdBagExt())
dbSetOrder(1)   	
   	
Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MATR902R3 ³ Autor ³ Patricia A. Salomao   ³ Data ³12.06.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Kardex  Fisico FIFO/LIFO (Antigo)                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcos Ferr ³01/06/06³82608 ³Adicionado o parametro 12 utilizado para  ³±±
±±³			   ³		³	   ³quebrar o relatorio por Lote Fifo/Lifo.   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function MATR902R3()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1     := OemToAnsi(STR0002) //'Este Programa emitir uma relacao com as movimentacoes'
Local cDesc2     := OemToAnsi(STR0003) //'dos produtos selecionados, ordenados sequencialmente.'
Local cDesc3     := ''
Local cString    := 'SB1'
Local lEnd       := .F.
Local Tamanho    := 'G'
Local Titulo     := OemToAnsi(IIf(GetMV("MV_CUSFIFO",.F.),STR0001,STR0032)) //'KARDEX FISICO FIFO/LIFO'
Local wnRel      := 'MATR902'
Private nLastKey := 0
Private aReturn  := {OemToAnsi(STR0004), 1, OemToAnsi(STR0005), 1, 2, 1, '', 1} //'Zebrado'###'Administra‡ao'
Private cPerg    := 'MTR902' 
Private aOrd     := { OemToAnsi(STR0004),OemToAnsi(STR0005)} //" Codigo Produto "," Tipo do Produto"
Private lCusFIFO := GetMV("MV_CUSFIFO",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao do fonte        ³
//³ SIGACUSA.PRX aplicados no rpo do cliente, assim verificando     |
//| a necessidade de uma atualizacao nestes fontes. NAO REMOVER !!!	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20060321)
    Final("Atualizar SIGACUSA.PRX !!!")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inclusao de Perguntas                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01        	// Do produto                           ³
//³ mv_par02        	// Ate o produto                        ³
//³ mv_par03        	// Do tipo                              ³
//³ mv_par04        	// Ate o tipo                           ³
//³ mv_par05        	// Da data                              ³
//³ mv_par06        	// Ate a data                           ³
//³ mv_par07        	// Lista produtos s/movimento           ³
//³ mv_par08        	// Qual Local (almoxarifado)            ³
//³ mv_par09        	// (d)OCUMENTO/(s)EQUENCIA              ³
//³ mv_par10            // moeda selecionada ( 1 a 5 )          ³
//³ mv_par11            // Pagina Inicial                       ³
//³ mv_par12            // Agrupar por Lote ?                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnRel := SetPrint(cString, wnRel, cPerg, Titulo, cDesc1, cDesc2, cDesc3, .F.,aOrd, .F., Tamanho)

If nLastKey == 27
	Set Filter To
	Return Nil
Endif

SetDefault(aReturn, cString)

If nLastKey == 27
	Set Filter To
	Return Nil
Endif

RptStatus({|lEnd| C902Imp(@lEnd, wnRel, Tamanho, Titulo)}, Titulo)

Set Filter To

Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ C902Imp  ³ Autor ³ Patricia A. Salomao   ³ Data ³12.06.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Kardex Fisico FIFO/LIFO                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function C902Imp(lEnd, wnRel, Tamanho, Titulo)

Local cAlias    := ""
Local cSeqIni   := "999999"
Local cProdAnt  := ""
Local cLocalAnt := ""
Local cCampo1   := ""
Local cCampo2   := ""
Local cCampo3   := ""
Local cCampo4   := ""
Local lFirst    := .T.
Local aSalAtu   := {}
Local aSalAtuLot:= {}
Local nTipo     := 0
Local cMoeda    := ' ' 
Local nEntrada  := 0
Local nSEntrada := 0
Local nSaida    := 0
Local nSSaida	:= 0
Local nCEntrada := 0
Local nSCEntrada:= 0
Local nCSaida   := 0
Local nSCSaida	:= 0
Local nCusFif   := 0
Local nCusFifLot:= 0
Local nInd      := 0
Local nIncCol	:= 0
Local cPicD1Qt  := PesqPict("SD1","D1_QUANT"    ,18)
Local cPicD3Qt  := PesqPict("SD3","D3_QUANT"    ,18)
Local cPicB2Qt  := PesqPict("SB2","B2_QATU"     ,18)
Local cPicB2Qt2 := PesqPict("SB2","B2_QTSEGUM"  ,18)
Local cPicD1Cust:= PesqPict("SD1","D1_CUSTO"    ,18)    
Local cCond1    :=''
Local cCond2    :=''
Local cTRBSD1   := CriaTrab(,.F.)
Local cTRBSD2   := CriaTrab(,.F.)
Local cTRBSD3   := CriaTrab(,.F.)
Local cTRBSD8   := Subs(cTrbSD3,1,7)+"A"
Local cLoteAnt   := ""
Local lVer116     := (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6" .Or. VAL(GetVersao(.F.))  > 11)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Titulo          := OemToAnsi(IIf(lCusFifo,STR0008,STR0033))+mv_par08 // "Kardex Fisico-Financeiro FIFO/LIFO (Calculo) L O C A L : "
Private cbTxt   := SPACE(10)
Private cbCont  := 0           	


Private Li      := 80
Private M_pag   := mv_par11
Private Cabec1  := IIF(mv_par09 $ "Ss",OemToAnsi(IIf(lCusFifo,STR0027,STR0036)),OemToAnsi(IIf(lCusFifo,STR0012,STR0034)))	// "OPERACAO            DOCUMENTO    |            E  N  T  R  A  D  A  S        |        CUSTO FIFO/LIFO    |               S  A  I  D  A  S           |                   S   A   L   D   O          |  CLI,FOR,"
Private Cabec2  := OemToAnsi(STR0013)   										// "DATA       TES C.F  NUMERO       |         QUANTIDADE          CUSTO TOTAL  |       DO MOVIMENTO        |         QUANTIDADE          CUSTO TOTAL  |         QUANTIDADE             VALOR TOTAL   |  CC ou OP"
//                                           1234567890 123 123  123456789012 | 123456789012345678   123456789012345678  |  123456789012345678  | 123456789012345678   123456789012345678   | 123456789012345678      123456789012345678   |  F-12345678901234567890
//                                           0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22
//                                           0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa os codigos de caracter Comprimido/Normal da impressora  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nTipo := IIF(aReturn[4]==1,GetMV("MV_COMP"),GetMV("MV_NORM"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona a ordem escolhida ao titulo do relatorio                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cMoeda := GETMV("MV_SIMB"+Str(mv_par10,1))
If Type("NewHead")#"U"
	NewHead := NewHead + OemToAnsi(STR0014)+AllTrim(aOrd[aReturn[8]])+OemToAnsi(STR0015)+cMoeda+Str(mv_par10,1)+")"  //" (Por "," ,em "
Else
	Titulo  := Titulo + OemToAnsi(STR0014)+AllTrim(aOrd[aReturn[8]])+OemToAnsi(STR0015)+cMoeda+Str(mv_par10,1)+")" //" (Por "," ,em "
EndIf

If cPaisLoc == "MEX" .And. !(mv_par09 $ "Ss")
	nIncCol := 7
	Cabec1  := IIf(lCusFifo,STR0066,STR0067)
	Cabec2  := STR0068    						
Endif	

dbSelectArea("SB2")
dbSetOrder(1)

dbSelectArea("SD1")
IndRegua("SD1",cTrbSD1,"D1_FILIAL+D1_COD+D1_LOCAL+D1_DOC+D1_SERIE+D1_ITEM",,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD1")
#IFNDEF TOP
	dbSetIndex(cTrbSD1+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SD2")
IndRegua("SD2",cTrbSD2,"D2_FILIAL+D2_COD+D2_LOCAL+D2_DOC+D2_SERIE+D2_ITEM",,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD2")
#IFNDEF TOP
	dbSetIndex(cTrbSD2+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SD3")
IndRegua("SD3",cTrbSD3,"D3_FILIAL+D3_COD+D3_LOCAL+D3_SEQCALC+D3_NUMSEQ",,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD3")
#IFNDEF TOP
	dbSetIndex(cTrbSD3+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SD8")
If mv_par12 == 1
	cFiltro := "D8_FILIAL+D8_PRODUTO+D8_LOCAL+D8_SEQ+"+IIF(mv_par09 $ "Ss","D8_SEQCALC","D8_DOC")
Else
	cFiltro := "D8_FILIAL+D8_PRODUTO+D8_LOCAL+"+IIF(mv_par09 $ "Ss","D8_SEQCALC+D8_SEQ","D8_DOC")
EndIf	
IndRegua("SD8",cTrbSD8,cFiltro,,,OemToAnsi(STR0016) )  //"Selecionando Registros"
nInd := RetIndex("SD8")
#IFNDEF TOP
	dbSetIndex(cTrbSD8+OrdBagExt())
#ENDIF
dbSetOrder(nInd+1)

dbSelectArea("SB1")
If aReturn[8]==1
	dbSetOrder(1)
	dbseek(xFilial()+mv_par01,.T.)
	cCond1:="B1_COD"
	cCond2:="mv_par02"
ElseIf aReturn[8] == 2
	dbSetOrder(2)
	dbseek(xFilial()+mv_par03,.T.)
	cCond1:="B1_TIPO"
	cCond2:="mv_par04"
Endif

SetRegua(LastRec())

While !Eof() .and. B1_FILIAL == xFilial() .and. &cCond1 <= &cCond2
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se houve interrupcao pelo operador                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	IF lEnd
		@Prow()+1,001 PSay STR0017		//"CANCELADO PELO OPERADOR"
		Exit
	EndIf
	
	IncRegua()
	
	// Filtra por Tipo
	If B1_TIPO < mv_par03 .or. B1_TIPO > mv_par04
		dbSkip()
		Loop
	Endif
	
	// Filtra por Produto
	If B1_COD < mv_par01 .or. B1_COD > mv_par02
		dbSkip()
		Loop
	Endif
	
	// Filtra Mao de Obra 
	If IsProdMod(B1_COD)
		dbSkip()
		Loop
	Endif
	
	dbSelectArea("SB2")
	dbSeek(xFilial()+SB1->B1_COD+mv_par08)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se nao encontrar no arquivo de dados, nao lista              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If Eof()
		dbSelectArea("SB1")
		dbSkip()
		Loop
	EndIf
	
	cProdAnt  := SB1->B1_COD
	cLocalAnt := B2_LOCAL
	
	dbSelectArea("SD3")
	dbSeek(xFilial()+SB1->B1_COD+SB2->B2_LOCAL)
	
	dbSelectArea("SD8")
	dbSeek(xFilial()+SB1->B1_COD+SB2->B2_LOCAL)
	
	While .T.
		
		dbSelectArea("SD3")
		If !Eof() .And. D3_FILIAL == xFilial() .And. D3_COD == cProdAnt .And. D3_LOCAL == cLocalAnt .And. IsProdMod(D3_COD)
			If D3_EMISSAO < mv_par05 .Or. D3_EMISSAO > mv_par06
				dbSkip()
				Loop
			Else
				If mv_par11 == 1
					If D3_NUMSEQ < cSeqIni
						cSeqIni := D3_NUMSEQ
						cAlias  := Alias()
					EndIf
				Else
					If D3_SEQCALC+D3_NUMSEQ < cSeqIni
						cSeqIni := D3_SEQCALC+D3_NUMSEQ
						cAlias  := Alias()
					EndIf
				Endif
			Endif
		EndIf
		
		dbSelectArea("SD8")
		If !Eof() .And. D8_FILIAL == xFilial() .And. D8_PRODUTO == cProdAnt .And. D8_LOCAL == cLocalAnt .And. Empty(cAlias)
			If !Empty(D8_DATA) .And. (D8_DATA < mv_par05 .Or. D8_DATA > mv_par06)
				dbSkip()
				Loop
			Else
				If mv_par11 == 1
					If D8_SEQ < cSeqIni
						cSeqIni := D8_SEQ
						cAlias  := Alias()
					EndIf
				Else
					If D8_SEQCALC+D8_SEQ < cSeqIni
						cSeqIni := D8_SEQCALC+D8_SEQ
						cAlias  := Alias()
					EndIf
				Endif
			EndIf
		Endif
		
		If !Empty(cAlias)

			dbSelectArea(cAlias)

			If mv_par12 == 1
				cDocumento := &(IIF(cAlias=="SD3",Subs(cAlias,2,2)+"_NUMSEQ",Subs(cAlias,2,2)+"_SEQ"))
			Endif	
            cCampo1 := Subs(cAlias,2,2)+IIF(cAlias=="SD8","_DATA","_EMISSAO")
			cCampo2 := Subs(cAlias,2,2)+"_TM"
			//-- Movimento Interno
			If cAlias=="SD3"
				cCampo3 := Subs(cAlias,2,2)+"_CF"
			ElseIf cAlias=="SD8"
				//-- Movimento Interno
				If Empty((cAlias)->D8_ITEM)
					cCampo3 := Subs(cAlias,2,2)+"_CF"
				Else	
					//-- Documento de Entrada
					If 	(cAlias)->D8_TM <= '500'
						SD1->(dbSeek(xFilial("SD1")+(cAlias)->D8_PRODUTO+(cAlias)->D8_LOCAL+(cAlias)->D8_DOC+(cAlias)->D8_SERIE+(cAlias)->D8_ITEM))
						cCampo3 := "SD1->D1_CF"
					//-- Documento de Saida
					Else
            			SD2->(dbSeek(xFilial("SD2")+(cAlias)->D8_PRODUTO+(cAlias)->D8_LOCAL+(cAlias)->D8_DOC+(cAlias)->D8_SERIE+(cAlias)->D8_ITEM))
						cCampo3 := "SD2->D2_CF"
					EndIf
					dbSelectArea(cAlias)
				EndIf
			EndIf	
			If cAlias == "SD3"
				cCampo4 := Subs(cAlias,2,2)+IIF(mv_par09 $ "Ss","_NUMSEQ","_DOC" )
			Else
				cCampo4 := Subs(cAlias,2,2)+IIF(mv_par09 $ "Ss","_SEQ","_DOC" )
			Endif
			
			IF li > 58
				cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
			EndIF
			
			If lFirst
				nEntrada	:= 0
				nSEntrada	:= 0
				nSaida		:= 0
				nSSaida		:= 0
				nCEntrada	:= 0
				nSCEntrada	:= 0
				nCSaida		:= 0
				nSCSaida	:= 0
				nCusFif		:= 0
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o Saldo Inicial do Produto                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				aSalAtu := CalcEstFF(SB1->B1_COD,mv_par08,mv_par05)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o Custo Fifo/Lifo do Produto                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If aSalAtu[1] > 0
					nCusFif := aSalAtu[mv_par10+1]/aSalAtu[1]
				ElseIf aSalAtu[1] == 0 .And. aSalAtu[mv_par10+1] == 0
					nCusFif := 0
				Else
					If mv_par10 == 1
						nCusFif := SB2->B2_CM1
					ElseIF mv_par10 == 2
						nCusFif := SB2->B2_CM2
					ElseIF mv_par10 == 3
						nCusFif := SB2->B2_CM3
					ElseIF mv_par10 == 4
						nCusFif := SB2->B2_CM4
					ElseIF mv_par10 == 5
						nCusFif := SB2->B2_CM5
					Endif
				Endif
				
				@ Li,000 PSAY SB1->B1_COD
				@ Li,019 PSAY SubStr(SB1->B1_DESC,1,30)
				@ Li,056 PSAY OemToAnsi(STR0018) +SB1->B1_UM		//"UM : "
				@ Li,068 PSAY OemToAnsi(STR0019) +SB1->B1_TIPO 		//"TIPO : "
				@ Li,083 PSAY OemToAnsi(STR0020) +SB1->B1_GRUPO		//"GRUPO : "
				@ Li,105 PSAY OemToAnsi(IIf(lCusFifo,STR0021,STR0035))	// "Custo Fifo/Lifo  : "  
				@ Li,122 PSAY nCusFif	  Picture cPicD1Cust
				@ Li,144 PSAY aSalAtu[1] Picture cPicD1Cust
				@ Li,168 PSAY aSalAtu[mv_par10+1] Picture cPicD1Cust
				Li++
				@ Li,000 PSAY OemToAnsi(STR0022)+SUBS(SB1->B1_POSIPI,1,4)+"."+SUBS(SB1->B1_POSIPI,5,4)+"."+SUBS(SB1->B1_POSIPI,9,2) //"Posicao IPI : "
				If lVer116
					@ Li,035 PSAY OemToAnsi(STR0023)+Posicione("NNR",1,xFilial("NNR")+SB2->B2_LOCAL,"NNR_DESCRI") //"Localizacao : "
				Else
					@ Li,035 PSAY OemToAnsi(STR0023)+SB2->B2_LOCALIZ 	//"Localizacao : "
				EndIf					
				Li+= 2
				lFirst  := .F.
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cabecalho Saldo Incial por Lote FIFO/LIFO                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If mv_par12 == 1

				//Verifica se houve quebra de Lote
				If cLoteAnt != cDocumento

					//Calcula o Saldo Inicial do Lote
					aSalAtuLot := CalcEstFF(SB1->B1_COD,mv_par08,mv_par05,,,cDocumento)
				
					//Calcula o Custo Fifo/Lifo do Lote
					If aSalAtuLot[1] > 0
						nCusFifLot := aSalAtuLot[mv_par10+1]/aSalAtuLot[1]
					ElseIf aSalAtuLot[1] == 0 .and. aSalAtuLot[mv_par10+1] == 0
						nCusFifLot := 0
					Else
						nCusFifLot := &(("SB2->B2_CM") + Str(mv_par10,1))
					EndIf

					@ Li,000 PSAY OemToAnsi(IIf(lCusFifo,STR0029,STR0037)) // "Lote Fifo/Lifo:"
					@ Li,010 PSAY cDocumento
					@ Li,058+nIncCol PSAY OemToAnsi(IIf(lCusFifo,STR0030,STR0038)) // "Custo Fifo/Lifo Inicial :"
					@ Li,079+nIncCol PSAY nCusFifLot Picture cPicD1Cust
					@ Li,144+nIncCol PSAY aSalAtuLot[1] Picture cPicD1Cust
					@ Li,168+nIncCol PSAY aSalAtuLot[mv_par10+1] Picture cPicD1Cust
					Li++
				EndIf
				cLoteAnt := cDocumento
			Endif	

			If IF(cAlias == "SD3",!Empty(D3_EMISSAO),!Empty(D8_DATA))
				@Li , 000 PSAY &cCampo1
				@Li , 011 PSAY &cCampo2
				@Li , 015 PSAY &cCampo3
				@Li , 020 PSAY &cCampo4 
				@Li , 033+nIncCol PSAY "|"
				If cAlias == "SD3"
					If D3_TM <= "500"
						@Li ,035+nIncCol PSAY D3_QUANT Picture cPicD3Qt
						@Li ,056+nIncCol PSAY &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) Picture cPicD1Cust
						@Li ,076+nIncCol PSAY "|"
						@Li ,079+nIncCol PSAY &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) Picture cPicD1Cust
						@Li ,099+nIncCol PSAY "|"
						nEntrada		:= nEntrada   + D3_QUANT
						nSEntrada		:= nSEntrada  + D3_QUANT
						aSalAtu[1]		:= aSalAtu[1] + D3_QUANT
						nCEntrada		:= nCEntrada  + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCEntrada		:= nSCEntrada + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] + D3_QTSEGUM
						//Saldos por Lote Fifo/Lifo
                        If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] + D3_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] + D3_QTSEGUM
						Endif	
					Else
						@Li ,076+nIncCol PSAY "|"
						@Li ,079+nIncCol PSAY (&(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) / D3_QUANT) Picture cPicD1Cust
						@Li ,099+nIncCol PSAY "|"
						@Li ,101+nIncCol PSAY D3_QUANT Picture cPicD3Qt
						@Li ,122+nIncCol PSAY &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))Picture cPicD1Cust
						nSaida			:= nSaida  + D3_QUANT
						nSSaida			:= nSSaida + D3_QUANT
						aSalAtu[1]		:= aSalAtu[1] - D3_QUANT
						nCSaida			:= nCSaida  + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCSaida		:= nSCSaida + &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] - &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] - D3_QTSEGUM
						//Saldos por Lote Fifo/Lifo
						If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] - D3_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] - &(("D3_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] - D3_QTSEGUM
						Endif	
					EndIf
				Else
					If D8_TM <= "500"
						@Li ,035+nIncCol PSAY D8_QUANT Picture cPicD3Qt
						@Li ,056+nIncCol PSAY &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) Picture cPicD1Cust
						@Li ,076+nIncCol PSAY "|"
						@Li ,079+nIncCol PSAY (&(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) / D8_QUANT) Picture cPicD1Cust
						@Li ,099+nIncCol PSAY "|"
						nEntrada   		:= nEntrada + D8_QUANT
						nSEntrada		:= nSEntrada + D8_QUANT
						aSalAtu[1]		:= aSalAtu[1] + D8_QUANT
						nCEntrada		:=  nCEntrada  + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCEntrada		:=  nSCEntrada + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] + 0
						//Saldos por Lote Fifo/Lifo
						If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] + D8_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] + 0
						Endif	
					Else
						@Li ,076+nIncCol PSAY "|"
						@Li ,079+nIncCol PSAY (&(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) / D8_QUANT) Picture cPicD1Cust
						@Li ,099+nIncCol PSAY "|"
						@Li ,101+nIncCol PSAY D8_QUANT Picture cPicD3Qt
						@Li ,122+nIncCol PSAY &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1))) Picture cPicD1Cust
						nSaida	   		:= nSaida  + D8_QUANT
						nSSaida			:= nSSaida + D8_QUANT
						aSalAtu[1]		:= aSalAtu[1] - D8_QUANT
						nCSaida			:= nCSaida  + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						nSCSaida		:= nSCSaida + &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] - &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
						aSalAtu[7]		:= aSalAtu[7] - 0
						//Saldos por Lote Fifo/Lifo
						If mv_par12 == 1
							aSalAtuLot[1]	:= aSalAtuLot[1] - D8_QUANT
							aSalAtuLot[mv_par10+1] := aSalAtuLot[mv_par10+1] - &(("D8_CUSTO") +If(mv_par10==1,"1",Str(mv_par10,1)))
							aSalAtuLot[7]	:= aSalAtuLot[7] - 0
						Endif	
					Endif
				Endif
			
				@ Li,142+nIncCol PSAY "|"
                If mv_par12 == 1
					@ Li,144+nIncCol PSAY aSalAtuLot[1] Picture cPicB2Qt
					@ Li,168+nIncCol PSAY aSalAtuLot[mv_par10+1] Picture cPicD1Cust
				Else
					@ Li,144+nIncCol PSAY aSalAtu[1] Picture cPicB2Qt
					@ Li,168+nIncCol PSAY aSalAtu[mv_par10+1] Picture cPicD1Cust
				EndIf	
				@ Li,189+nIncCol PSAY "|"
				Do Case
					Case cAlias == "SD3"  && movimentos (SD3)
						If Empty(D3_OP)
							@ LI,192+nIncCol PSAY 'CC'+D3_CC
						Else
							@ LI,192+nIncCol PSAY 'OP'+SUBS(D3_OP,1,6)
						EndIf
					Case cAlias == "SD8"  && movimentos (SD8)
						If Empty(D8_OP)
							@ LI,192+nIncCol PSAY 'CC'
						Else
							@ LI,192+nIncCol PSAY 'OP'+SUBS(D8_OP,1,6)
						EndIf
				EndCase
			Li++
			Endif
			
			dbSkip()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ SubTotal por Lote/Documento                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If mv_par12 == 1

				If cLoteAnt != &(IIF(cAlias=="SD3",Subs(cAlias,2,2)+"_NUMSEQ",Subs(cAlias,2,2)+"_SEQ"))

				    //Aviso de Lote sem movimentacao
				    If nSEntrada == 0 .And. nSSaida == 0 .And. nSCEntrada == 0 .And. nSCSaida == 0
						@Li ,000 PSAY OemToAnsi(STR0031) // "NAO HOUVE MOVIMENTACAO PARA ESTE LOTE"
						Li++
				    Endif
					Li++
					@ li,000 PSAY OemToAnsi(STR0028) // "SubTotal:"
					@ Li,035+nIncCol PSAY nSEntrada		Picture cPicD1Qt
					@ Li,056+nIncCol PSAY nSCEntrada	Picture cPicD1Cust
					@ Li,101+nIncCol PSAY nSSaida		Picture cPicD1Qt
					@ Li,122+nIncCol PSAY nSCSaida		Picture cPicD1Cust
					@ Li,144+nIncCol PSAY aSalAtuLot[1]	Picture cPicB2Qt
					@ Li,168+nIncCol PSAY aSalAtuLot[mv_par10+1] Picture cPicD1Cust
					Li+=2
					nSEntrada	:= 0
					nSCEntrada	:= 0
					nSSaida		:= 0
					nSCSaida	:= 0
				EndIf
			Endif	
			cSeqIni := "999999"
			cAlias  := ""
			
		Else
			
			If !lFirst
				Li := Li + 1
				@ li,000 PSAY OemToAnsi(STR0024) // "T O T A I S  :"
				@ Li,035+nIncCol PSAY nEntrada	    Picture cPicD1Qt
				@ Li,056+nIncCol PSAY nCEntrada	    Picture cPicD1Cust
				@ Li,101+nIncCol PSAY nSaida		Picture cPicD1Qt
				@ Li,122+nIncCol PSAY nCSaida		Picture cPicD1Cust
				@ Li,144+nIncCol PSAY aSalAtu[1]   Picture cPicB2Qt
				@ Li,168+nIncCol PSAY aSalAtu[mv_par10+1] Picture cPicD1Cust
				Li := Li + 1
				@ Li,122+nIncCol PSAY OemToAnsi(STR0025) // "QTD. NA SEGUNDA UM: "
				@ Li,144+nIncCol PSAY aSalAtu[7]   Picture cPicB2Qt2
				Li := Li + 1
				@Li ,   0 PSAY __PrtThinLine()
				Li := Li + 1
				lFirst := .T.
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se deve ou nao listar os produtos s/movimento       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If mv_par07 == 1
					If li > 58
						cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
					EndIf
					nCusFif := 0
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Calcula o Saldo Inicial do Produto                            ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					aSalAtu := CalcEstFF(SB1->B1_COD,mv_par08,mv_par05)
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Calcula o Custo Fifo/Lifo do Produto                               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					If AsalAtu[1] > 0
						nCusFif := aSalAtu[mv_par10+1]/aSalAtu[1]
					ElseIf AsalAtu[1] == 0 .and. AsalAtu[mv_par10+1] == 0
						nCusFif := 0
					Else
						nCusFif := &(("SB2->B2_CM") + Str(mv_par10,1))
					EndIf
					
					@ Li,000 PSAY SB1->B1_COD
					@ Li,019 PSAY SubStr(SB1->B1_DESC,1,30)
					@ Li,056 PSAY OemToAnsi(STR0018)+SB1->B1_UM //"TIPO : "
					@ Li,068 PSAY OemToAnsi(STR0019)+SB1->B1_TIPO  //"TIPO : "
					@ Li,083 PSAY OemToAnsi(STR0020)+SB1->B1_GRUPO  //"GRUPO : "
					@ Li,105 PSAY OemToAnsi(IIF(lCusFifo,STR0021,STR0035)) // "Custo Fifo/Lifo  : "
					@ Li,122 PSAY nCusFif	  Picture cPicD1Cust
					@ Li,144 PSAY aSalAtu[1] Picture cPicD1Cust
					@ Li,168 PSAY aSalAtu[mv_par10+1] Picture cPicD1Cust
					Li++
					@ Li,000 PSAY OemToAnsi(STR0022)+SUBS(SB1->B1_POSIPI,1,4)+"."+SUBS(SB1->B1_POSIPI,5,4)+"."+SUBS(SB1->B1_POSIPI,9,2) //"Posicao IPI : "
					If lVer116
						@ Li,035 PSAY OemToAnsi(STR0023)+Posicione("NNR",1,xFilial("NNR")+SB2->B2_LOCAL,"NNR_DESCRI")//"Localizacao : "
					Else
						@ Li,035 PSAY OemToAnsi(STR0023)+SB2->B2_LOCALIZ //"Localizacao : "
					EndIf
					Li+=2
					
					@Li ,  0 PSAY OemToAnsi(STR0026) // "NAO HOUVE MOVIMENTACAO PARA ESTE PRODUTO"
					Li++
					@Li ,  0 PSAY __PrtThinLine()
					Li++
					lFirst  := .T.
				EndIf
			EndIf
			Exit
		EndIf
	EndDo
	lFirst  := .T.
	dbSelectArea("SB1")
	dbSkip()
EndDo

If li != 80
	roda(cbcont,cbtxt,Tamanho)
EndIf

dbSelectArea("SB1")
dbSetOrder(1)
dbSelectArea("SB2")
dbSetOrder(1)
dbSelectArea("SD1")
RetIndex("SD1")
Ferase(cTrbSD1+OrdBagExt())
dbSetOrder(1)
dbSelectArea("SD2")
RetIndex("SD2")
Ferase(cTrbSD2+OrdBagExt())
dbSetOrder(1)
dbSelectArea("SD3")
RetIndex("SD3")
Ferase(cTrbSD3+OrdBagExt())
dbSetOrder(1)
dbSelectArea("SD8")
RetIndex("SD8")
Ferase(cTrbSD8+OrdBagExt())
dbSetOrder(1)

If aReturn[5] == 1
	Set Printer to
	dbCommitAll()
	ourspool(wnrel)
Endif

MS_FLUSH()
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AjustaSX1³ Autor ³ Marcos V. Ferreira    ³ Data ³06.09.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Ajusta perguntas do SX1                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR902                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function AjustaSX1()

Local aHelpPor12 := {"Agrupa os itens do relatorio por Lote ","FIFO respeitando a pergunta","Documento/Sequencia"} 
Local aHelpEng12 := {"Group the report items by FIFO Lot    ","considering the question   ","Document/Sequence   "} 
Local aHelpSpa12 := {"Agrupa los itemes del informe por lote","FIFO siempre respetando la ","pregunta documento/secuencia"} 
Local aAreaAnt	 := GetArea()
Local nTamSX1    := Len(SX1->X1_GRUPO)

dbSelectArea("SX1")
dbSetOrder(1)

If dbSeek(PADR("MTR902",nTamSX1)+"07") .And. SX1->X1_TIPO == "C"
	RecLock("SX1",.F.)
	Replace X1_TIPO     with "N"
	Replace X1_PRESEL   with 1
	Replace X1_GSC   	with "C"
	Replace X1_DEF01    with "Sim"
	Replace X1_DEFSPA1  with "Si"
	Replace X1_DEFENG1  with "Yes"
	Replace X1_DEF02    with "Nao"
	Replace X1_DEFSPA2  with "No"
	Replace X1_DEFENG2  with "No"
	Replace X1_CNT01    with " "
	MsUnLock()
EndIf

If dbSeek(PADR("MTR902",nTamSX1)+"12") .And. !(AllTrim(SX1->X1_PERGUNT) == "Agrupar por Lote ?")
	RecLock("SX1",.F.)
	dbDelete()        
	MsUnLock()
EndIf

PutSx1("MTR902", "12","Agrupar por Lote ?","Agrupar por Lote ?","Group by Lot ?","mv_chc","N", 1, 0, 2,"C","","","","","mv_par12","Sim","Si","Yes","","Nao","No","No","","","","","","","","","",aHelpPor12,aHelpEng12,aHelpSpa12,"")

RestArea(aAreaAnt)
Return
