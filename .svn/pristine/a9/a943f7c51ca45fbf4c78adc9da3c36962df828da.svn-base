#INCLUDE "MATR901.CH"
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR901  ³ Autor ³ Nereu Humberto Junior ³ Data ³ 18.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Kardex Fisico - Financeiro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATR901()
Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef()
	oReport:PrintDialog()
Else
	MATR901R3()
EndIf

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³Nereu Humberto Junior  ³ Data ³18.07.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatorio                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local aOrdem    := {}
Local cTamD1Doc := TamSX3('D1_DOC')[1]
Local cPicD1Qt  := PesqPict("SD1","D1_QUANT" ,18)
Local cTamD1Qt  := TamSX3('D1_QUANT')[1]
Local cPicD1Cust:= PesqPict("SD1","D1_CUSTO",18)
Local cTamD1Cust:= TamSX3('D1_CUSTO')[1]
Local cPicD2Qt  := PesqPict("SD2","D2_QUANT" ,18)
Local cTamD2Qt  := TamSX3('D2_QUANT')[1]
Local cPicD2Cust:= PesqPict("SD2","D2_CUSTO1",18)
Local cTamD2Cust:= TamSX3('D2_CUSTO1')[1]
Local cPicB2Qt  := PesqPictQt("B2_QATU",18)
Local oSection1
Local oSection2
Local oSection3
Local oReport 
Local oCell         
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se utiliza custo unificado por Empresa/Filial       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lCusUnif := IIf(FindFunction("A330CusFil"),A330CusFil(),GetMV("MV_CUSFIL",.F.))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta perguntas no SX1                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta perguntas no SX1 a fim de preparar o relatorio p/     ³
//³ custo unificado por empresa                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCusUnif
	MTR901CUnf(lCusUnif)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:= TReport():New("MATR901",STR0001,"MTR901", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003) //"Kardex Fisico"##"Este programa emitir  uma rela‡„o com as movimenta‡”es"##"dos produtos selecionados, ordenados sequencialmente."
oReport:SetLandscape()    
oReport:SetTotalInLine(.F.)

Pergunte("MTR901",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Sessao 1 - Dados do Produto                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New(oReport,STR0063,{"SB1","SB2"}) //"Produtos (Parte 1)"
oSection1 :SetTotalInLine(.F.)
oSection1 :SetReadOnly()
oSection1 :SetLineStyle()

TRCell():New(oSection1,"B1_COD"		,"SB1",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_DESC"	,"SB1",/*Titulo*/	,/*Picture*/,30			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_UM"		,"SB1",STR0057		,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_TIPO"	,"SB1",STR0058		,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_GRUPO"	,"SB1",STR0059		,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"nCusMed"	,"   ",STR0060		,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"nQtdSal"	,"   ",STR0055		,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"nVlrSal"	,"   ",STR0056		,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Sessao 2 - Cont. dos dados do Produto           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New(oSection1,STR0064,{"SB1","SB2"}) //"Produtos (Parte 2)"
oSection2 :SetTotalInLine(.F.)
oSection2 :SetReadOnly()
oSection2 :SetLineStyle()

TRCell():New(oSection2,"B1_POSIPI"	,"SB1",STR0061,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"B2_LOCALIZ","SB2",STR0062,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da Sessao 3 - Movimentos                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection3 := TRSection():New(oSection2,STR0065,{"SD1","SD2","SD3"}) //"Movimentação dos Produtos"
oSection3 :SetHeaderPage()
oSection3 :SetTotalInLine(.F.)
oSection3 :SetTotalText(STR0021) //"T O T A I S  :"
oSection3 :SetReadOnly()

TRCell():New(oSection3,"dDtMov"		,"   ",STR0039+CRLF+STR0040	,/*Picture*/,10         ,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTES"		,"   ",STR0041				,"@!"		,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cCF"		,"   ",STR0042				,"@!"		,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cDoc"		,"   ",STR0043+CRLF+STR0044	,"@!"		,cTamD1Doc	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco1"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nENTQtd"	,"   ",STR0045+CRLF+STR0046	,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco2"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nENTCus"	,"   ",STR0045+CRLF+STR0047	,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco3"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nCusMov"	,"   ",STR0048+CRLF+STR0049	,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco4"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nSAIQtd"	,"   ",STR0050+CRLF+STR0046	,cPicD2Qt	,cTamD2Qt	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco5"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nSAICus"	,"   ",STR0050+CRLF+STR0047	,cPicD2Cust	,cTamD2Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco6"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nSALDQtd"	,"   ",STR0051+CRLF+STR0046	,cPicD1Qt	,cTamD1Qt	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco7"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"nSALDCus"	,"   ",STR0051+CRLF+STR0052	,cPicD1Cust	,cTamD1Cust	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,"cTraco8"	,"   ","|"+CRLF+"|"			,/*Picture*/,1			,/*lPixel*/,{|| "|" })
TRCell():New(oSection3,"cCCPVPJOP"	,"   ",STR0053+CRLF+STR0054	,"@!"		,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

TRFunction():New(oSection3:Cell("nENTQtd"),NIL,"SUM",/*oBreak*/,"",cPicD1Qt	,/*uFormula*/,.T.,.F.) 
TRFunction():New(oSection3:Cell("nENTCus"),NIL,"SUM",/*oBreak*/,"",cPicD1Cust	,/*uFormula*/,.T.,.F.) 

TRFunction():New(oSection3:Cell("nSAIQtd"),NIL,"SUM",/*oBreak*/,"",cPicD2Qt	,/*uFormula*/,.T.,.F.) 
TRFunction():New(oSection3:Cell("nSAICus"),NIL,"SUM",/*oBreak*/,"",cPicD2Cust	,/*uFormula*/,.T.,.F.) 

TRFunction():New(oSection3:Cell("nSALDQtd"),NIL,"ONPRINT",/*oBreak*/,"",cPicB2Qt	,{|| oSection3:Cell("nSALDQtd"):GetValue(.T.) },.T.,.F.) 
TRFunction():New(oSection3:Cell("nSALDCus"),NIL,"ONPRINT",/*oBreak*/,"",cPicD1Cust,{|| oSection3:Cell("nSALDCus"):GetValue(.T.) },.T.,.F.) 

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Nereu Humberto Junior  ³ Data ³21.06.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

Local oSection1:= oReport:Section(1) 
Local oSection2:= oReport:Section(1):Section(1)  
Local oSection3:= oReport:Section(1):Section(1):Section(1)  
Local cSelectD1 := '', cWhereD1 := '%%'
Local cSelectD2 := '', cWhereD2 := '%%'
Local cSelectD3 := '', cWhereD3 := '%%'
Local cWhereRE  := ''
Local cAlias    := ''
Local cProdAnt  := ''
Local cLocalAnt := ''
Local cSeqIni   := '999999'
Local nTipo     := 0
Local nEntrada	:= 0
Local nSaida    := 0
Local nCEntrada	:= 0 
Local nCSaida   := 0
Local nTam		:=18
Local cPicB2Qt2 := PesqPictQt("B2_QTSEGUM" ,nTam)
Local aSalAtu   := {0,0,0,0,0,0,0}
Local lQuery 	:= .F.
Local nTamCod   := TamSX3("B1_COD")[1]
Local cDepTrf   := SuperGetMv("MV_DEPTRANS",.F.,"95")	// Dep.transferencia
Local lTranSB2  := SuperGetMv("MV_TRANSB2" ,.F.,.F.)	// Atualiza saldos de transferencia

#IFNDEF TOP
	Local cCondicao := ''
#ELSE
	Local cAliasTop := ''
#ENDIF

Private bBloco := { |nV,nX| Trim(nV)+IIf(Valtype(nX)='C',"",Str(nX,1)) }
Private nDecs  := MsDecimais(mv_par10)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³MV_CUSREP - Parametro utilizado para habilitar o calculo do   ³
//³            Custo de Reposicao.                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lCusRep  := SuperGetMv("MV_CUSREP",.F.,.F.) .And. (FindFunction("MA330AvRep") .And. MA330AvRep())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se utiliza custo unificado por Empresa/Filial       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lCusUnif := IIf(FindFunction("A330CusFil"),A330CusFil(),GetMV("MV_CUSFIL",.F.))
lCusUnif :=	lCusUnif .And. mv_par08 == "**"
      
oReport:SetTitle(OemToAnsi(STR0008)+iif(MV_PAR11==1,OemToAnsi(STR0009),OemToAnsi(STR0010) ) + Iif(lCusUnif,"",OemToAnsi(STR0011)+" "+mv_par08))     // "KARDEX FISICO-FINANCEIRO "###"(SEQUENCIA)"###"(CALCULO)"###"L O C A L :"
oReport:SetTitle(oReport:Title()+STR0017+STR0004+STR0018+AllTrim(&("MV_SIMB"+Str(mv_par10,1)))+")") //" (Por "###" ,em "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Genera segundo arquivo de Trabalho                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSetOrder(1)
Set Softseek On
dbseek(cFilial+mv_par01)
Set Softseek Off
cCond1:="SB1->B1_COD"
cCond2:="mv_par02"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo de Trabalho                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTamQ:=TamSX3("B2_QATU")
aTamT:=TamSX3("B2_VFIM1")
aTamC:=TamSX3("B2_CM1")
aTamX:=TamSX3("F4_CF")
aTamF:=TamSX3("A2_COD")
aTamL:=TamSX3("A2_LOJA")

aCampos := {}
AADD(aCampos,{"PRODUTO"  ,"C",nTamCod,0})
AADD(aCampos,{"EMISSAO"  ,"D",08,0})
AADD(aCampos,{"SEQCALC"  ,"C",14,0})
AADD(aCampos,{"NUMSEQ"   ,"C",14,0})
AADD(aCampos,{"TES"      ,"C",03,0})
AADD(aCampos,{"CF"       ,"C",aTamX[1],aTamX[2]})
AADD(aCampos,{"TIPODOC"  ,"C",03,0})
AADD(aCampos,{"NUMDOC"   ,"C",TamSX3('D1_DOC')[1],0})
AADD(aCampos,{"SERIE"    ,"C",03,0})
AADD(aCampos,{"QUANT"    ,"N",aTamQ[1],aTamQ[2]})
AADD(aCampos,{"CUSTOTAL" ,"N",aTamT[1],aTamT[2]})
AADD(aCampos,{"CLIFOR"   ,"C",aTamF[1],aTamF[2]})
AADD(aCampos,{"LOJA"     ,"C",aTamL[1],aTamL[2]})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatório                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao SQL                            ³	
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeSqlExpr(oReport:uParam)
	
	If cPaisLoc == "BRA"
		cAliasTop := "TRB"
	Else 
		cAliasTop := "TRC"
	EndIf	
	lQuery := .T.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query do relatorio da secao 1                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):BeginQuery()	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Complemento do SELECT da tabela SD1                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSelectD1 := "%
   	If mv_par10 == 1
		If lCusRep .And. mv_par13 == 2
			cSelectD1 += " D1_CUSRP1 CUSTOTAL,"
		Else
			cSelectD1 += " D1_CUSTO CUSTOTAL,"
		EndIf	
   	Else
		If lCusRep .And. mv_par13 == 2
   			cSelectD1 += " D1_CUSRP"+str(mv_par10,1)+" CUSTOTAL,"
   		Else
   			cSelectD1 += " D1_CUSTO"+str(mv_par10,1)+" CUSTOTAL,"
   		EndIf	
   	EndIf	
   	cSelectD1 += "%"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Complemento do Where da tabela SD1                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	cWhereD1 := "%" 
	If !lCusUnif
		cWhereD1 += " SD1.D1_LOCAL ='"+mv_par08+"' AND"
	EndIf
	cWhereD1 += "%" 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Complemento do SELECT da tabela SD2                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCusRep .And. mv_par13 == 2
	    cSelectD2 := "% D2_CUSRP"
		cSelectD2 += Str(mv_par10,1) // Coloca a Moeda do Custo
	Else
	    cSelectD2 := "% D2_CUSTO"
		cSelectD2 += Str(mv_par10,1) // Coloca a Moeda do Custo
	EndIf
	cSelectD2 += " CUSTOTAL,"
    cSelectD2 += "%"	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Complemento do Where da tabela SD1                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	cWhereD2 := "%" 
	If !lCusUnif
		cWhereD2 += " SD2.D2_LOCAL ='"+mv_par08+"' AND "
	EndIf
	cWhereD2 += "%"    
	
	cWhereRE := "%" 
	cWhereRE += " (SD2.D2_REMITO   = '" + Space(TamSx3('D2_REMITO')[1]) + "' OR SD2.D2_TPDCENV IN ('1','A')) "	
	cWhereRE += "%" 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Complemento do SELECT da tabelas SD3                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCusRep .And. mv_par13 == 2
		cSelectD3 := "% D3_CUSRP"
		cSelectD3 += Str(mv_par10,1) // Coloca a Moeda do Custo
	Else
		cSelectD3 := "% D3_CUSTO"
		cSelectD3 += Str(mv_par10,1) // Coloca a Moeda do Custo
	EndIf	
	cSelectD3 += " CUSTOTAL, "	
	cSelectD3 += "%"    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Complemento do WHERE da tabela SD3                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    cWhereD3 := "%"
   	If !lCusUnif 
   		cWhereD3 += " SD3.D3_LOCAL ='"+mv_par08+"' AND"
   	EndIf
	If !lTranSB2 
		cWhereD3 += " SD3.D3_LOCAL <> '"+cDepTrf+"' AND"
	EndIf
   	If SuperGetMV('MV_D3ESTOR', .F., 'N') == 'N'
   		cWhereD3 += " SD3.D3_ESTORNO <> 'S' AND"
   	EndIf  
	If SuperGetMV('MV_D3SERVI', .F., 'N') == 'N' .And. IntDL()
		cWhereD3 += " ( (D3_SERVIC = '   ') OR (D3_SERVIC <> '   ' AND D3_TM <= '500' AND D3_STSERV = '3') ) AND"
	EndIf		      	
	cWhereD3 += "%"	

	cOrder := "%"
   	If mv_par11 == 1
   		If UPPER(TcGetDB()) != "INFORMIX"    
			cOrder += " PRODUTO, EMISSAO, NUMSEQ, TES"     		
   		Else
   			cOrder += " 2, 5, 4, 6"
   		EndIf
   	Else                                 
    	If UPPER(TcGetDB()) != "INFORMIX"    
   			cOrder += " PRODUTO, SEQCALC, TES"
    	Else
      		cOrder += " 2, 3, 6 "
       	EndIf	
	EndIf
	cOrder += "%"
	
	BeginSql Alias cAliasTop
	
		SELECT 	 B1_TIPO TIPO, 				
		         D1_COD PRODUTO,
		         D1_SEQCALC SEQCALC,
		         D1_NUMSEQ NUMSEQ,
		         D1_DTDIGIT EMISSAO,
		         D1_TES TES,
		         D1_CF CF, 
		         D1_ESPECIE TIPODOC,
		         D1_DOC NUMDOC,
		         D1_SERIE SERIE,
		         D1_QUANT QUANT,
		         D1_FORNECE CLIFOR,
		         D1_LOJA LOJA,
				 %Exp:cSelectD1%		
				 SD1.R_E_C_N_O_
	
		FROM %table:SD1% SD1,%table:SF4% SF4,%table:SB1% SB1

		WHERE SD1.D1_FILIAL  =  %xFilial:SD1%	AND
              SD1.D1_COD     >= %Exp:mv_par01% 	AND
              SD1.D1_COD     <= %Exp:mv_par02% AND          
		      %Exp:cWhereD1%
		      SD1.D1_DTDIGIT >= %Exp:mv_par05% 	AND
			  SD1.D1_DTDIGIT <= %Exp:mv_par06%	AND		
			  SD1.D1_ORIGLAN <> 'LF'           	AND
			  SD1.D1_REMITO  = ' '             	AND
			  SD1.D1_TIPO_NF >= ' '            	AND
			  SD1.%NotDel%						AND
			  SF4.F4_FILIAL  =  %xFilial:SF4%	AND 	
			  SF4.F4_CODIGO  = SD1.D1_TES 		AND
			  SF4.F4_ESTOQUE = 'S' 				AND
			  SF4.%NotDel%                     	AND      
			  SB1.B1_FILIAL  =  %xFilial:SB1%	AND 	
			  SB1.B1_COD     = SD1.D1_COD 		AND
              SB1.B1_TIPO    >= %Exp:mv_par03% 	AND
              SB1.B1_TIPO    <= %Exp:mv_par04% 	AND          			  
              SB1.%NotDel%                     	         
              
	    UNION
	    
		SELECT 	B1_TIPO TIPO,
		        D2_COD PRODUTO,
		        D2_SEQCALC SEQCALC,
		        D2_NUMSEQ NUMSEQ,
		        D2_EMISSAO EMISSAO,
		        D2_TES TES,
		        D2_CF CF,
		        D2_ESPECIE TIPODOC,
		        D2_DOC NUMDOC,
		        D2_SERIE SERIE,
		        D2_QUANT QUANT,
		        D2_CLIENTE CLIFOR,
		        D2_LOJA LOJA,
				%Exp:cSelectD2%			
				SD2.R_E_C_N_O_

		FROM %table:SD2% SD2,%table:SF4% SF4,%table:SB1% SB1

		WHERE SD2.D2_FILIAL  =  %xFilial:SD2%	AND
              SD2.D2_COD     >= %Exp:mv_par01% 	AND
              SD2.D2_COD     <= %Exp:mv_par02% AND          
		      %Exp:cWhereD2%
		      SD2.D2_EMISSAO >= %Exp:mv_par05% 	AND
			  SD2.D2_EMISSAO <= %Exp:mv_par06%	AND		
			  SD2.D2_ORIGLAN <> 'LF'           	AND
			  %Exp:cWhereRE%                   	AND
			  SD2.%NotDel%						AND
			  SF4.F4_FILIAL  =  %xFilial:SF4%	AND 	
			  SF4.F4_CODIGO  = SD2.D2_TES 		AND
			  SF4.F4_ESTOQUE = 'S' 				AND
			  SF4.%NotDel%                     	AND      
			  SB1.B1_FILIAL  =  %xFilial:SB1%	AND 	
			  SB1.B1_COD     = SD2.D2_COD 		AND
              SB1.B1_TIPO    >= %Exp:mv_par03% 	AND
              SB1.B1_TIPO    <= %Exp:mv_par04% 	AND          			  
              SB1.%NotDel%                     	         

		UNION		
	
		SELECT 	B1_TIPO TIPO,
		        D3_COD PRODUTO,
		        D3_SEQCALC SEQCALC,
		        D3_NUMSEQ NUMSEQ,
		        D3_EMISSAO EMISSAO, 
		        D3_TM TES, 
		        D3_CF CF, 
		        D3_CF TIPODOC,
		        D3_DOC NUMDOC,
		        ' ' SERIE,
		        D3_QUANT QUANT,
				' ' CLIFOR,
				' ' LOJA,
				%Exp:cSelectD3%			
				SD3.R_E_C_N_O_
				
		FROM %table:SD3% SD3,%table:SB1% SB1

		WHERE SD3.D3_FILIAL  =  %xFilial:SD3%	AND
              SD3.D3_COD     >= %Exp:mv_par01% 	AND
              SD3.D3_COD     <= %Exp:mv_par02% AND
		      SD3.D3_EMISSAO >= %Exp:mv_par05% 	AND
			  SD3.D3_EMISSAO <= %Exp:mv_par06%	AND
			  %Exp:cWhereD3%                      
			  SD3.%NotDel%						AND
			  SB1.B1_FILIAL  =  %xFilial:SB1%	AND 	
			  SB1.B1_COD     = SD3.D3_COD 		AND
              SB1.B1_TIPO    >= %Exp:mv_par03% 	AND
              SB1.B1_TIPO    <= %Exp:mv_par04% 	AND          			  
              SB1.%NotDel%                     	                       

		ORDER BY %Exp:cOrder%
		
	EndSql 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Metodo EndQuery ( Classe TRSection )                                    ³
	//³                                                                        ³
	//³Prepara o relatorio para executar o Embedded SQL.                       ³
	//³                                                                        ³
	//³ExpA1 : Array com os parametros do tipo Range                           ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
	
	If cPaisLoc == "BRA" 
		aEval(aCampos, {|e| If(e[2]!= "C", TCSetField("TRB", e[1], e[2],e[3],e[4]),Nil)})
	Else                
		aEval(aCampos, {|e| If(e[2]!= "C", TCSetField("TRC", e[1], e[2],e[3],e[4]),Nil)})
		MTR901FillTrb(aCampos)	
	EndIf	
#ELSE
	dbSelectArea("SB2")
	dbSetOrder(1)
	
	dbSelectArea("SD1")
	dbSetOrder(5)
	
	dbSelectArea("SD2")
	dbSetOrder(1)
	
	dbSelectArea("SD3")
	dbSetOrder(3)

	cNomeArq:=CriaTrab(aCampos)
		
	dbUseArea( .T., __cRDDNTTS, cNomeArq, "TRB", if(.F. .Or. .F., !.F., NIL), .F. )
	If mv_par11 == 1
	   IndRegua("TRB",cNomeArq,'PRODUTO+Dtos(EMISSAO)+NUMSEQ+TES',,,STR0011)  //"Selecionando Registros..."
	Else
 	   IndRegua("TRB",cNomeArq,'PRODUTO+SEQCALC+TES',,,STR0011)  //"Selecionando Registros..."
	Endif	
	
	dbSelectArea("SB1")
	dbSetOrder(1)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao Advpl                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeAdvplExpr(oReport:uParam)

	cCondicao := 'B1_FILIAL == "'+xFilial("SB1")+'".And.' 
	cCondicao += 'B1_COD >= "'+mv_par01+'".And.B1_COD <="'+mv_par02+'".And.'
	cCondicao += 'B1_TIPO >= "'+mv_par03+'".And.B1_TIPO <="'+mv_par04+'"'

	oReport:Section(1):SetFilter(cCondicao,IndexKey())
	
	DbSelectArea("SB1")
	While !Eof() 

   		dbSelectArea("SB2")
	   	dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",mv_par08))
    	
	   	lGravado  := .F.
   		cProdAnt  := SB1->B1_COD
		cLocalAnt := SB2->B2_LOCAL

		dbSelectArea("SD1")
		dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))
		dbSelectArea("SD2")
	   	dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))
   		dbSelectArea("SD3")
	   	dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

	   	dbSelectArea("SD1")
	   	dbSeek(xFilial("SD1")+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

   		While ! Eof() .And. D1_FILIAL == xFilial("SD1") .And. D1_COD == cProdAnt .And. IIf(lCusUnif,.T.,D1_LOCAL == cLocalAnt)

			dbSelectArea("SF4")
			dbSeek(cFilial+SD1->D1_TES)
			dbSelectArea("SD1")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Para paises do MercoSul, desprezar as Notas Fiscais que foram ³
			//³ ingressadas por Remitos de Salida...                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        	If ! Empty(D1_REMITO)
           		dbSkip()
	           	Loop
        	EndIf
        	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Notas geradas pelo EIC considerar somente notas de FOB       |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        	If D1_TIPO_NF $ '6789AB'
           		dbSkip()
	           	Loop
        	EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If D1_ORIGLAN $ "LF" .Or. SF4->F4_ESTOQUE != "S"
         		dbSkip()
	         	Loop
    		Else
         		If D1_DTDIGIT < mv_par05 .Or. D1_DTDIGIT > mv_par06
            		dbSkip()
					loop
				Else
					cSeqIni := IIF(mv_par11==1,D1_NUMSEQ,D1_SEQCALC+D1_NUMSEQ)
					cAlias := Alias()
	         	EndIf
			EndIf

			dbSelectArea("TRB")
			RecLock("TRB",.T.)
			If SD1->D1_ESPECIE $ "FT /NF /   "
	        	cTipoDoc := "FAC"
			Else
         		cTipoDoc := SD1->D1_ESPECIE
			EndIf
     	   
			Replace PRODUTO   With SD1->D1_COD
			Replace SEQCALC   With SD1->D1_SEQCALC
			Replace NUMSEQ    With SD1->D1_NUMSEQ 
			Replace EMISSAO   With SD1->D1_DTDIGIT
			Replace TES       With SD1->D1_TES
			Replace CF        With SD1->D1_CF
			Replace TIPODOC   With cTipoDoc
			Replace NUMDOC    With SD1->D1_DOC
			Replace SERIE     With SD1->D1_SERIE
			Replace QUANT     With SD1->D1_QUANT

			If SD1->D1_QUANT > 0
				If mv_par10 == 1
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP1
                	Else
		       			Replace CUSTOTAL With SD1->D1_CUSTO
		       		EndIf	
				ElseIf mv_par10 == 2
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP2
                	Else
		           		Replace CUSTOTAL With SD1->D1_CUSTO2
		           	EndIf	
				ElseIf mv_par10 == 3
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP3
                	Else
	            		Replace CUSTOTAL With SD1->D1_CUSTO3
	            	EndIf	
				ElseIf mv_par10 == 4
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP4
                	Else
		         		Replace CUSTOTAL With SD1->D1_CUSTO4
		         	EndIf	
				ElseIf mv_par10 == 5
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP5
                	Else
		    			Replace CUSTOTAL With SD1->D1_CUSTO5
		    		EndIf	
				EndIf
			Else
         		If mv_par10 == 1
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP1
                	Else
		         		Replace CUSTOTAL With SD1->D1_CUSTO
		         	EndIf	
	        	ElseIf mv_par10 == 2
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP2
                	Else
		         		Replace CUSTOTAL With SD1->D1_CUSTO2
		         	EndIf	
     	      	ElseIf mv_par10 == 3
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP3
                	Else
		           		Replace CUSTOTAL With SD1->D1_CUSTO3
		           	EndIf	
     	      	ElseIf mv_par10 == 4
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP4
                	Else
	    	        	Replace CUSTOTAL With SD1->D1_CUSTO4
	    	        EndIf	
	         	ElseIf mv_par10 == 5
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD1->D1_CUSRP5
                	Else
	          			Replace CUSTOTAL With SD1->D1_CUSTO5
	          		EndIf	
	         	EndIf
			Endif
      	   
			MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  		//³ Control para imprimir sin movimientos                   ³
	    	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	     	lGravado := .T.

			dbSelectArea("SD1")
			dbSkip()
		EndDo
  		dbSelectArea("SD2")
		dbSeek(xFilial("SD2")+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

		While ! Eof() .And. D2_FILIAL == xFilial("SD2") .And. D2_COD == cProdAnt .And. IIf(lCusUnif,.T.,D2_LOCAL == cLocalAnt)

			dbSelectArea("SF4")
			dbSeek(cFilial+SD2->D2_TES)
	    	dbSelectArea("SD2")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	    	//³ Para paises do MercoSul, desprezar as Notas Fiscais que foram ³
			//³ ingressadas por Remitos de Salida...                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ! Empty(D2_REMITO) .And. !(D2_TPDCENV $ "1A")
				SD2->(DbSkip())
		  		Loop
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  		//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
	      	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	      	If D2_ORIGLAN == "LF" .Or. SF4->F4_ESTOQUE != "S"
				SD2->(DbSkip())
		       	Loop
			Else
	      		If D2_EMISSAO < mv_par05 .or. D2_EMISSAO > mv_par06
	       			dbSkip()
		         	loop
				Else
     	    		If mv_par11 == 1
	          	 		If D2_NUMSEQ < cSeqIni
    	          			cSeqIni := D2_NUMSEQ
	    	            	cAlias	:= Alias()
     	    	        Endif
	              	Else
    		           	If D2_SEQCALC+D2_NUMSEQ < cSeqIni
            	    		cSeqIni := D2_SEQCALC+D2_NUMSEQ
	            	     	cAlias	:= Alias()
	     	            Endif
					EndIf
     			EndIf
			Endif		

			dbSelectArea("TRB")

			RecLock("TRB",.T.)
			If SD2->D2_ESPECIE $ "FT /NF /   "
	    		cTipoDoc := "FAC"
			Else
         		cTipoDoc := SD2->D2_ESPECIE
			EndIf
     	   
			Replace PRODUTO   With SD2->D2_COD
	      	Replace SEQCALC   With SD2->D2_SEQCALC
     	   	Replace NUMSEQ    With SD2->D2_NUMSEQ 
	      	Replace EMISSAO   With SD2->D2_EMISSAO
	      	Replace TES       With SD2->D2_TES
     	   	Replace CF        With SD2->D2_CF
	      	Replace TIPODOC   With cTipoDoc
     	  	Replace NUMDOC    With SD2->D2_DOC
	      	Replace SERIE     With SD2->D2_SERIE
     	   	Replace QUANT  With SD2->D2_QUANT
	      	If SD2->D2_QUANT <> 0
				If mv_par10 == 1
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD2->D2_CUSRP1
                	Else
		            	Replace CUSTOTAL With SD2->D2_CUSTO1
		            EndIf	
				ElseIf mv_par10 == 2
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD2->D2_CUSRP2
                	Else
	        			Replace CUSTOTAL With SD2->D2_CUSTO2
	        		EndIf	
				ElseIf mv_par10 == 3
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD2->D2_CUSRP3
                	Else
	         			Replace CUSTOTAL With SD2->D2_CUSTO3
	         		EndIf	
	        	ElseIf mv_par10 == 4
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD2->D2_CUSRP4
                	Else
	          			Replace CUSTOTAL With SD2->D2_CUSTO4
	          		EndIf	
	         	ElseIf mv_par10 == 5
					If lCusRep .And. mv_par13 == 2
		       			Replace CUSTOTAL With SD2->D2_CUSRP5
                	Else
	     	    		Replace CUSTOTAL With SD2->D2_CUSTO5
	     	    	EndIf	
         		EndIf
			Endif
			MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Controle para imprimir sin movimientos                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lGravado := .T.

			dbSelectArea("SD2")
			dbSkip()
		EndDo

		dbSelectArea("SD3")
		dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

		While !Eof()  .And. D3_FILIAL == xFilial("SD3") .And. D3_COD == cProdAnt .And. IIf(lCusUnif,.T.,D3_LOCAL == cLocalAnt)

			If !D3Valido()
   				SD3->(DbSkip())
	     		Loop
        	Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Nao imprimir os produtos que estao no armazem de transito                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc <> "BRA" .And. !lTranSB2 .And. AllTrim(SD3->D3_LOCAL) == AllTrim(cDepTrf)
				dbSkip()
				Loop
			EndIf	
		
			If D3_EMISSAO < mv_par05 .Or. D3_EMISSAO > mv_par06
				dbSkip()
	   			Loop
	      	Else
				If mv_par11 == 1
		   			If D3_NUMSEQ < cSeqIni
     	 				cSeqIni := D3_NUMSEQ
        	  			cAlias := Alias()
	        	    EndIf
	      		Else
        			If D3_SEQCALC+D3_NUMSEQ < cSeqIni
		          		cSeqIni := D3_SEQCALC+D3_NUMSEQ
     		            cAlias := Alias()
            	 	EndIf
	   			EndIf
			EndIf

			dbSelectArea("TRB")
   			RecLock("TRB",.T.)
	    	Replace PRODUTO   With SD3->D3_COD
	    	Replace SEQCALC   With SD3->D3_SEQCALC
     	   	Replace NUMSEQ    With SD3->D3_NUMSEQ 
	      	Replace EMISSAO   With SD3->D3_EMISSAO
     	   	Replace TES       With SD3->D3_TM
	      	Replace CF        With SD3->D3_CF
	       	Replace TIPODOC   With SD3->D3_CF
		    Replace NUMDOC    With SD3->D3_DOC
     		Replace SERIE     With ""
			Replace QUANT  With SD3->D3_QUANT
	      	If mv_par10 == 1
				If lCusRep .And. mv_par13 == 2
	       			Replace CUSTOTAL With SD3->D3_CUSRP1
               	Else
			      	Replace CUSTOTAL With SD3->D3_CUSTO1
			    EndIf	
	    	ElseIf mv_par10 == 2
				If lCusRep .And. mv_par13 == 2
	       			Replace CUSTOTAL With SD3->D3_CUSRP2
               	Else
		    	  	Replace CUSTOTAL With SD3->D3_CUSTO2
		    	EndIf  	
	  	   	ElseIf mv_par10 == 3
				If lCusRep .And. mv_par13 == 2
	       			Replace CUSTOTAL With SD3->D3_CUSRP3
               	Else
			 	  	Replace CUSTOTAL With SD3->D3_CUSTO3
			 	EndIf  	
		   	ElseIf mv_par10 == 4
				If lCusRep .And. mv_par13 == 2
	       			Replace CUSTOTAL With SD3->D3_CUSRP4
               	Else
		  		  	Replace CUSTOTAL With SD3->D3_CUSTO4
		  		EndIf  	
	  	   	ElseIf mv_par10 == 5
				If lCusRep .And. mv_par13 == 2
	       			Replace CUSTOTAL With SD3->D3_CUSRP5
               	Else
			 	  	Replace CUSTOTAL With SD3->D3_CUSTO5
			 	EndIf  	
	      	EndIf
	      	MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Control para imprimir sin movimientos                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lGravado := .T.

			dbSelectArea("SD3")
			dbSkip()
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Graba registro cerado para imprimir sin movimientos     ³
	 	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	   	If !lGravado .And. ( mv_par07 == 1 )
			RecLock("TRB", .T.)
			Replace PRODUTO   With cProdAnt
			MsUnLock()
		EndIf

	   	dbSelectArea("SB1")
   		dbSkip()
	EndDo

#ENDIF		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da impressao do fluxo do relatório                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1") 
dbSetOrder(1)
SB1->(dbSeek(xFilial()+mv_par01,.T.))

dbSelectArea("TRB")
oReport:SetMeter(TRB->(LastRec()))
dbGoTop()
	 
While !oReport:Cancel() .And. !("TRB")->(Eof()) .Or. If(lQuery.And.mv_par07 == 1,&cCond1 <= &cCond2 .And. !SB1->(Eof()),.F.)

	nEntradas := 0
	nSalidas  := 0
	nCEntrada := 0
	nCSalidas := 0
	lImpCabec := .T.
   
	cProduto := PRODUTO

	If mv_par07 == 2
		SB1->(MsSeek(xFilial()+TRB->PRODUTO))   
	EndIf
	
	If oReport:Cancel()
		Exit
	EndIf	

	If !lQuery .Or. (lQuery .And. TRB->PRODUTO == SB1->B1_COD)
		While !Eof() .And. PRODUTO == cProduto
	
			If oReport:Cancel()
				Exit
			EndIf				
			
			oReport:IncMeter()
	
			If lImpCabec
	        	MTR901S12(aSalAtu,oSection1,oSection2,lQuery)
				lImpCabec := .F.
			EndIf
	
			dbSelectArea("TRB")
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica la fecha para productos sin movimientos        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oSection3:Init() 
			If lQuery
			 	If TIPODOC $ "FT /NF /   "     
			    	TIPODOC := "FAC"
			 	EndIf
		  	EndIf
			If CtoD("  /  /  ") != EMISSAO

				oSection3:Cell("dDtMov"):SetValue(EMISSAO)			
				oSection3:Cell("cTES"):SetValue(TES)														
				oSection3:Cell("cCF"):SetValue(TIPODOC)			
				oSection3:Cell("cDoc"):SetValue(IIF(mv_par09 == 1,NUMDOC,NUMSEQ))			

				If TES <= "500"

					oSection3:Cell("nENTQtd"):Show()
					oSection3:Cell("nENTCus"):Show()
					
					oSection3:Cell("nENTQtd"):SetValue(QUANT)			
					oSection3:Cell("nENTCus"):SetValue(Round(CUSTOTAL,aTamT[2]))					            

					nCusto := 0
	            	If QUANT != 0
					   	nCusto := Round(CUSTOTAL,aTamT[2])/QUANT
					  	oSection3:Cell("nCusMov"):Show()
					  	oSection3:Cell("nCusMov"):SetValue(nCusto)		               
	    	        Else
					  	oSection3:Cell("nCusMov"):Hide()
					  	oSection3:Cell("nCusMov"):SetValue(0)		               	    	        
	    	        EndIf
					oSection3:Cell("nSAIQtd"):Hide()
					oSection3:Cell("nSAICus"):Hide()
					oSection3:Cell("nSAIQtd"):SetValue(0)			
					oSection3:Cell("nSAICus"):SetValue(0)								
				Else                
		   			nCusto := 0
		            If QUANT != 0
					   	nCusto := Round(CUSTOTAL,aTamT[2])/QUANT
					  	oSection3:Cell("nCusMov"):Show()
					  	oSection3:Cell("nCusMov"):SetValue(nCusto)		               	        	       
	    	        Else
					  	oSection3:Cell("nCusMov"):Hide()
					  	oSection3:Cell("nCusMov"):SetValue(0)		               	    	        
	            	EndIf

					oSection3:Cell("nSAIQtd"):Show()
					oSection3:Cell("nSAICus"):Show()
					oSection3:Cell("nSAIQtd"):SetValue(QUANT)			
					oSection3:Cell("nSAICus"):SetValue(Round(CUSTOTAL,aTamT[2]))										            

					oSection3:Cell("nENTQtd"):Hide()
					oSection3:Cell("nENTCus"):Hide()
					oSection3:Cell("nENTQtd"):SetValue(0)			
					oSection3:Cell("nENTCus"):SetValue(0)					            					
				EndIf
	         
			 	If TRB->TES > "500"
		        	aSalAtu[1] -= QUANT
		            aSalAtu[mv_par10+1] -= Round(CUSTOTAL,aTamT[2])  
	     			aSalAtu[7] -= ConvUm(SB1->B1_COD,QUANT,0,2)
			 	Else
			    	aSalAtu[1] += QUANT
	    	        aSalAtu[mv_par10+1] += IIF(QUANT >0,( QUANT * nCusto ),Round(CUSTOTAL,aTamT[2]))
		    		aSalAtu[7] += ConvUm(SB1->B1_COD,QUANT,0,2)
			 	EndIf
	
		        If aSalAtu[1] != 0
					oSection3:Cell("nSALDQtd"):Show()
					oSection3:Cell("nSALDCus"):Show()
					oSection3:Cell("nSALDQtd"):SetValue(aSalAtu[1])			
					oSection3:Cell("nSALDCus"):SetValue(aSalAtu[mv_par10+1])			
	        	Else
					oSection3:Cell("nSALDQtd"):Hide()
					oSection3:Cell("nSALDCus"):Hide()
					oSection3:Cell("nSALDQtd"):SetValue(0)			
					oSection3:Cell("nSALDCus"):SetValue(0)				        	
	        	EndIf

				cMovto := ""
	
				If TRB->TES > "500" .And. ALLTRIM(TRB->TIPODOC) $ "REM|NF|FAC"
					cMovto := OemToAnsi(STR0030) //"CLI"
				ElseIf TRB->TES > "500" .And. ALLTRIM(TRB->TIPODOC) $ "RE0|DE0|RE8|DE8"
					cMovto := OemToAnsi(STR0032) //"CC"
				ElseIf TRB->TES <= "500" .And. ALLTRIM(TRB->TIPODOC) $ "RE6|DE6"
					cMovto := OemToAnsi(STR0034) //"MAN"
				ElseIf TRB->TES <= "500" .And. ALLTRIM(TRB->TIPODOC) $ "RE8|DE8"
					cMovto := OemToAnsi(STR0032) //"CC"
				ElseIf TRB->TES <= "500" .And. ALLTRIM(TRB->TIPODOC) $ "REM|NF|FAC"
					cMovto := OemToAnsi(STR0031) //"PROV"
				Else
					cMovto := OemToAnsi(STR0033) //"OP"
				EndIf

				oSection3:Cell("cCCPVPJOP"):SetValue(cMovto)				        	
	
				nEntradas += Iif(TES <= "500",QUANT,0)
	         	nSalidas  += Iif(TES >  "500",QUANT,0)
	         	nCEntrada += Iif(TES <= "500",Round(CUSTOTAL,aTamT[2]),0)
	         	nCSalidas += Iif(TES >  "500",Round(CUSTOTAL,aTamT[2]),0)
	
				oSection3:PrintLine()
			EndIf
	     	dbSelectArea("TRB")
			dbSkip()
		EndDo
	EndIf	
		
	If (nEntradas + nSalidas) > 0

		oSection1:Finish()
		oSection2:Finish()
		oSection3:Finish()

		oReport:PrintText(STR0022+TransForm(aSalAtu[7],cPicB2Qt2),,oSection3:Cell('nSAICus'):ColPos()) //"QTD. NA SEGUNDA UM: "											      	
		oReport:ThinLine()		

	   	If lQuery
			SB1->(DbSkip())
		EndIf   
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
      	//³ Verifica se deve ou nao listar os produtos s/movimento ³
      	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
      	If mv_par07 == 1
			If !lQuery
				dbSelectArea("TRB")
	         	oReport:PrintText(STR0023)   //"NAO HOUVE MOVIMENTACAO PARA ESTE PRODUTO"
	         	oReport:ThinLine()		
	         	oReport:SkipLine()		
			Else
            	If TRB->PRODUTO <> SB1->B1_COD .And. SB1->B1_FILIAL==xFilial("SB1");
               		.And. &cCond1 <= &cCond2 .And. !SB1->(eof()) .And.;
					(SB1->B1_TIPO >= mv_par03 .AND. SB1->B1_TIPO <= mv_par04)

			      	MTR901S12(aSalAtu,oSection1,oSection2,lQuery)

					oSection1:Finish()
					oSection2:Finish()

					oReport:PrintText(STR0023)   //"NAO HOUVE MOVIMENTACAO PARA ESTE PRODUTO"		         	
					oReport:ThinLine()		
					oReport:SkipLine()		
				EndIf
				SB1->(DbSkip())
			Endif
      	EndIf
	EndIf
EndDo

dbSelectArea("SB1")
DbClearFilter()
dbSetOrder(1)

dbSelectArea("TRB")
dbCloseArea()

If !lQuery
    Ferase(cNomeArq+GetDbExtension())	// Elimina arquivos de Trabalho
    Ferase(cNomeArq+OrdBagExt())		// Elimina arquivos de Trabalho
EndIf

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MATR901R3 ³ Autor ³ Paulo Boschetti       ³ Data ³ 06.10.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Kardex fisico - financeiro (Antigo)                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Rodrigo     ³24/06/98³XXXXXX³Acerto no tamanho do documento para 12    ³±±
±±³            ³        ³      ³posicoes                                  ³±±
±±³Rodrigo     ³17/07/98³13742A³Acerto na impressao de devolucoes conforme³±±
±±³            ³        ³      ³MATR910                                   ³±±
±±³Rodrigo Sart³05/11/98³XXXXXX³ Acerto p/ Bug Ano 2000                   ³±±
±±³Bruno Sobies³18/12/98³Melhor³Exclucao impressao do CF nas localizacoes ³±±
±±³Cesar       ³25/03/99³20051A³Alteracao do Lay-Out p/ Sair #OP Completa ³±±
±±³Diego       ³14/12/99³Melhor³Control para Productos sin movimientos    ³±±
±±³JAurelio    ³27/06/01³      ³Uso da funcao PESQPICT para impressao da  ³±± 
±±³            ³        ³      ³variavel NUMDOC (Linha 830) e inclusao de ³±±
±±³            ³        ³      ³variaveis STR na impressao da variavel    ³±±
±±³            ³        ³      ³cMovto(Linha 878)                         ³±± 								
±±³JAurelio    ³02/07/01³      ³Impressao da Posicao do IPI(so ira ocorrer³±±   
±±³            ³        ³      ³se o pais for BRASIL. Lin 1100)			  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATR901R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL titulo   := STR0001     //"Kardex Fisico"
LOCAL cDesc1   := STR0002     //"Este programa emitir  uma rela‡„o com as movimenta‡”es"
LOCAL cDesc2   := STR0003     //"dos produtos selecionados, ordenados sequencialmente."
LOCAL cDesc3   := ""
LOCAL cString  := "SB1"

PRIVATE Tamanho  := "G"
PRIVATE wnrel    := "MATR901"
PRIVATE aReturn  := {OemToAnsi(STR0006), 1,OemToAnsi(STR0007), 1, 2, 1, "",1 }     //"Zebrado"###"Administracao"
PRIVATE aLinha   := { }
PRIVATE nLastKey := 0
PRIVATE cPerg    :="MTR901"
PRIVATE bBloco   := { |nV,nX| Trim(nV)+IIf(Valtype(nX)='C',"",Str(nX,1)) }
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se utiliza custo unificado por Empresa/Filial       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE lCusUnif := IIf(FindFunction("A330CusFil"),A330CusFil(),GetMV("MV_CUSFIL",.F.))
PRIVATE nDecs	 :=	2
PRIVATE lQuery

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta perguntas no SX1 a fim de preparar o relatorio p/     ³
//³ custo unificado por empresa                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCusUnif
	MTR901CUnf(lCusUnif)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta o dicionario de dados SX1                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR901",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,,.F.,Tamanho,,.F.)

If nLastKey = 27
   DbClearFilter()
   Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
   DbClearFilter()
   Return
Endif

RptStatus({|lEnd| R901Imp(@lEnd,wnRel,tamanho,titulo)},titulo)

Set Device to Screen

If aReturn[5] = 1
   Set Printer TO
   dbCommitAll()
   OurSpool(wnrel)
Endif

MS_FLUSH()

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R901IMP  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 16.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR901                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R901Imp(lEnd,WnRel,tamanho,titulo)
Local cSeqIni   := "999999"
Local cAlias    := ""
Local cProdAnt  := ""
Local cLocalAnt := ""
Local cCampo1   := ""
Local cCampo2   := ""
Local cCampo3   := ""
Local cCampo4   := ""
Local cCusto    := ""
Local cQuery	:= ""

Local nIncCol   := 0
Local nTam		:=18
Local nTipo     := 0
Local nEntrada	:= 0
Local nSaida    := 0
Local nCEntrada	:= 0
Local nCSaida   := 0

Local lFirst    := .T. 
Local lFirst1   := .T.
Local lSb1Ant   := .F.

Local aSalAtu   := {0,0,0,0,0,0,0}

Local cPicB2Qt  := PesqPictQt("B2_QATU" ,nTam)
Local cPicB2Qt2 := PesqPictQt("B2_QTSEGUM" ,nTam)
Local cPicD1Qt  := PesqPictQt("D1_QUANT" ,nTam)
Local cPicD2Qt  := PesqPictQt("D2_QUANT" ,nTam)
Local cPicD3Qt  := PesqPictQt("D3_QUANT" ,nTam)
Local cPicB2Cust:= PesqPict("SB2","B2_CM"+Str(mv_par10,1),nTam)
Local cPicD1Cust:= PesqPict("SD1","D1_CUSTO"+IIF(mv_par10 == 1 ,"",Str(mv_par10,1)),nTam,mv_par10)
Local cPicD2Cust:= PesqPict("SD2","D2_CUSTO"+Str(mv_par10,1),nTam,mv_par10)
Local cPicD3Cust:= PesqPict("SD3","D3_CUSTO"+Str(mv_par10,1),nTam,mv_par10)
Local cTRBSD1   := CriaTrab(,.F.)
Local cTRBSD2   := Subs(cTrbSD1,1,7)+"A"
Local cTRBSD3   := Subs(cTrbSD1,1,7)+"B"

Local lRemInt   := SuperGetMv("MV_REMINT",.F.,.F.)
Local cDepTrf   := SuperGetMv("MV_DEPTRANS",.F.,"95")	// Dep.transferencia
Local lTranSB2  := SuperGetMv("MV_TRANSB2" ,.F.,.F.)	// Atualiza saldos de transferencia

Local CbTxt,CbCont,cabec1,cabec2
Local lDev  // Flag que indica se nota ‚ devolu‡ao (.T.) ou nao (.F.)
Local nInd
Local nTamCod   := TamSX3("B1_COD")[1]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³MV_CUSREP - Parametro utilizado para habilitar o calculo do   ³
//³            Custo de Reposicao.                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lCusRep  := SuperGetMv("MV_CUSREP",.F.,.F.) .And. (FindFunction("MA330AvRep") .And. MA330AvRep())

nDecs	:=	MsDecimais(MV_PAR10)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt := SPACE(10)
cbcont:= 0
Li    := 80
m_pag := mv_par12

lCusUnif :=	lCusUnif .And. mv_par08 == "**"

Titulo := OemToAnsi(STR0008)+IIf(MV_PAR11==1,OemToAnsi(STR0009),OemToAnsi(STR0010) ) + IIf(lCusUnif,"",OemToAnsi(STR0011)+" "+mv_par08)     // "KARDEX FISICO-FINANCEIRO "###"(SEQUENCIA)"###"(CALCULO)"###"L O C A L :"
If cPaisLoc == "MEX" 
	cabec1 := OemToAnsi(STR0012)+IIF(mv_par09 == 1, OemToAnsi(STR0066), OemToAnsi(STR0067) ) + OemToAnsi(STR0015)
	cabec2 := OemToAnsi(STR0068)
	nIncCol:= 6
Else
	cabec1 := OemToAnsi(STR0012)+IIF(mv_par09 == 1, OemToAnsi(STR0013), OemToAnsi(STR0014) ) + OemToAnsi(STR0015)         //"    OPERACAO      "###"   DOCUMENTO   "###"   SEQUENCIA   "###" |               E  N  T  R  A  D  A  S             |         CUSTO MEDIO   |                  S  A  I  D  A  S                |                   S   A   L   D   O              |CLI,FOR,"
	cabec2 := OemToAnsi(STR0016)     //"    DATA   TES C.F     NUMERO     |            QUANTIDADE             CUSTO TOTAL    |        DO MOVIMENTO   |            QUANTIDADE             CUSTO TOTAL    |            QUANTIDADE               VALOR TOTAL  |CC ou OP"
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os codigos de caracter Comprimido/Normal da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,15,nTam)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona a ordem escolhida ao titulo do relatorio          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("NewHead")#"U"
   NewHead += STR0017+STR0004+STR0018+AllTrim(&("MV_SIMB"+Str(mv_par10,1)))+")"     //" (Por "###" ,em "
Else
   Titulo  += STR0017+STR0004+STR0018+AllTrim(&("MV_SIMB"+Str(mv_par10,1)))+")"     //" (Por "###" ,em "
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Genera segundo arquivo de Trabalho                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lQuery := .F.

#IFDEF TOP                                                                
    If TcSrvType() != "AS/400"
       lQuery := .T.
    EndIf
#ENDIF

aCampos := {}
AADD(aCampos,{"FILIAL"   ,"C",02,0})
AADD(aCampos,{"COD"      ,"C",nTamCod,0})
AADD(aCampos,{"LOCAL"    ,"C",02,0})
AADD(aCampos,{"QINI"     ,"N",18,2})
AADD(aCampos,{"QISEGUM"  ,"N",18,2})
AADD(aCampos,{"VINI"     ,"N",18,2})

cNomArqA:=CriaTrab(aCampos)
dbUseArea( .T., __cRDDNTTS, cNomArqA, "TRA", If(.F. .Or. .F., !.F., NIL), .F. )
IndRegua("TRA",cNomArqA,"FILIAL+COD+LOCAL",,,STR0011)  //"Selecionando Registros..."

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo de Trabalho                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTamQ:=TamSX3("B2_QATU")
aTamT:=TamSX3("B2_VFIM1")
aTamC:=TamSX3("B2_CM1")
aTamX:=TamSX3("F4_CF")

aCampos := {}
AADD(aCampos,{"PRODUTO"  ,"C",nTamCod,0})
AADD(aCampos,{"EMISSAO"  ,"D",08,0})
AADD(aCampos,{"SEQCALC"  ,"C",14,0})
AADD(aCampos,{"NUMSEQ"   ,"C",14,0})
AADD(aCampos,{"TES"      ,"C",03,0})
AADD(aCampos,{"CF"       ,"C",aTamX[1],aTamX[2]})
AADD(aCampos,{"TIPODOC"  ,"C",03,0})
AADD(aCampos,{"NUMDOC"   ,"C",TamSX3('D1_DOC')[1],0})
AADD(aCampos,{"SERIE"    ,"C",03,0})
AADD(aCampos,{"QUANT"    ,"N",aTamQ[1],aTamQ[2]})
AADD(aCampos,{"CUSTOTAL" ,"N",aTamT[1],aTamT[2]})
AADD(aCampos,{"CLIFOR"   ,"C",Tamsx3("D1_FORNECE")[1],Tamsx3("D1_FORNECE")[2]})
AADD(aCampos,{"LOJA"     ,"C",Tamsx3("D1_LOJA")[1],Tamsx3("D1_LOJA")[2]})

dbSelectArea("SB1")

dbSetOrder(1)
Set Softseek On
dbseek(cFilial+mv_par01)
Set Softseek Off
cCond1:="SB1->B1_COD"
cCond2:="mv_par02"

If !lQuery         

	cNomeArq:=CriaTrab(aCampos)
	
	dbUseArea( .T., __cRDDNTTS, cNomeArq, "TRB", If(.F. .Or. .F., !.F., NIL), .F. )

	If mv_par11 == 1
	   IndRegua("TRB",cNomeArq,'PRODUTO+Dtos(EMISSAO)+NUMSEQ+TES',,,STR0011)  //"Selecionando Registros..."
	Else
  	   IndRegua("TRB",cNomeArq,'PRODUTO+SEQCALC+TES',,,STR0011)  //"Selecionando Registros..."
	Endif	

	dbSelectArea("SB2")
	dbSetOrder(1)

	dbSelectArea("SD1")
	dbSetOrder(5)

	dbSelectArea("SD2")
	dbSetOrder(1)

	dbSelectArea("SD3")
	dbSetOrder(3)

EndIf

SetRegua(LastRec())

#IFDEF TOP                                                                

	If TcSrvType() != "AS/400" 

		cQuery := " SELECT B1_TIPO TIPO,D1_COD PRODUTO, D1_SEQCALC SEQCALC, D1_NUMSEQ NUMSEQ,"
      	cQuery += " D1_DTDIGIT EMISSAO, D1_TES TES, D1_CF CF, D1_ESPECIE TIPODOC,"
      	cQuery += " D1_DOC NUMDOC, D1_SERIE SERIE, D1_QUANT QUANT,"

      	If mv_par10 == 1
			If lCusRep .And. mv_par13 == 2
				cQuery += " D1_CUSRP1 CUSTOTAL,"
			Else
				cQuery += " D1_CUSTO CUSTOTAL,"
			EndIf	
	   	Else
			If lCusRep .And. mv_par13 == 2
	    		cQuery += " D1_CUSRP"+STR(mv_par10,1)+" CUSTOTAL,"
    		Else
	    		cQuery += " D1_CUSTO"+STR(mv_par10,1)+" CUSTOTAL,"
	    	EndIf	
      	EndIf

      	cQuery += " D1_FORNECE CLIFOR, D1_LOJA LOJA,"
	   	cQuery += " SD1.R_E_C_N_O_"
	   	cQuery += " FROM "+RetSqlName("SD1")+" SD1,"+RetSqlName("SF4")+" SF4,"
	   	cQuery +=  RetSqlName("SB1")+" SB1"
      	cQuery += " WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"'"
      	cQuery += " AND SD1.D1_COD BETWEEN '"+mv_par01+"' AND '"+mv_par02+"'" 

      	If !lCusUnif 
	  		cQuery += " AND SD1.D1_LOCAL ='"+mv_par08+"'"
      	EndIf

      	cQuery += " AND SD1.D1_DTDIGIT BETWEEN '"+Dtos(mv_par05)+"' AND '"+Dtos(mv_par06)+"'"
      	cQuery += " AND SD1.D1_ORIGLAN <> 'LF'"       
      	cQuery += " AND SD1.D1_REMITO = '" + Space(TamSx3('D1_REMITO')[1]) + "'"

      	cQuery += " AND SD1.D1_TIPO_NF IN (' ','1','2','3','4,','5') " //Notas geradas pelo EIC considerar somente notas de FOB

		If lRemInt
			If TcGetDb() = "MSSQL"
				cQuery += " AND ( ( (SD1.D1_TIPO_NF + SD1.D1_TIPODOC) <> '510' ) OR ( (SD1.D1_TIPO_NF + SD1.D1_TIPODOC) = '510' AND D1_CONHEC = '' ) )
			Else	
				cQuery += " AND ( ( (SD1.D1_TIPO_NF || SD1.D1_TIPODOC) <> '510' ) OR ( (SD1.D1_TIPO_NF || SD1.D1_TIPODOC) = '510' AND D1_CONHEC = '' ) )
			EndIf	
		EndIf	
		
      	cQuery += " AND SD1.D_E_L_E_T_ = ' '" 
      	cQuery += " AND SF4.F4_FILIAL ='"+xFilial("SF4")+"'"
      	cQuery += " AND SF4.F4_CODIGO = SD1.D1_TES "
      	cQuery += " AND SF4.F4_ESTOQUE = 'S'"
      	cQuery += " AND SF4.D_E_L_E_T_ = ' '" 
      	cQuery += " AND SB1.B1_FILIAL ='"+xFilial("SB1")+"'"
      	cQuery += " AND SB1.B1_COD = SD1.D1_COD "
      	cQuery += " AND SB1.B1_TIPO BETWEEN '"+mv_par03+"' AND '"+mv_par04+"'"
      	cQuery += " AND SB1.D_E_L_E_T_ = ' '" 

      	cQuery += " UNION "

      	cQuery += " SELECT B1_TIPO TIPO,D2_COD PRODUTO, D2_SEQCALC SEQCALC, D2_NUMSEQ NUMSEQ,"
      	cQuery += " D2_EMISSAO EMISSAO, D2_TES TES, D2_CF CF, D2_ESPECIE TIPODOC,"
      	cQuery += " D2_DOC NUMDOC, D2_SERIE SERIE, D2_QUANT QUANT,"
		If lCusRep .And. mv_par13 == 2
			cQuery += " D2_CUSRP"+str(mv_par10,1)+" CUSTOTAL, "
      	Else
	      	cQuery += " D2_CUSTO"+str(mv_par10,1)+" CUSTOTAL, "
	    EndIf	
      	cQuery += " D2_CLIENTE CLIFOR, D2_LOJA LOJA, "
	  	cQuery += " SD2.R_E_C_N_O_"
	   	cQuery += " FROM "+RetSqlName("SD2")+" SD2,"+RetSqlName("SF4")+" SF4, "
	   	cQuery +=  RetSqlName("SB1")+" SB1 "
      	cQuery += " WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"'"
      	cQuery += " AND SD2.D2_COD BETWEEN '"+mv_par01+"' AND '"+mv_par02+"'" 
      	If !lCusUnif 
      		cQuery += " AND SD2.D2_LOCAL ='"+mv_par08+"'"
      	EndIf
      	cQuery += " AND SD2.D2_EMISSAO BETWEEN '"+Dtos(mv_par05)+"' AND '"+Dtos(mv_par06)+"'"
      	cQuery += " AND SD2.D2_ORIGLAN <> 'LF'"
 		cQuery += " AND (SD2.D2_REMITO   = '" + Space(TamSx3('D2_REMITO')[1]) + "' OR SD2.D2_TPDCENV IN ('1','A')) "
      	cQuery += " AND SD2.D_E_L_E_T_ = ' '" 
      	cQuery += " AND SF4.F4_FILIAL ='"+xFilial("SF4")+"'"
      	cQuery += " AND SF4.F4_CODIGO = SD2.D2_TES"
      	cQuery += " AND SF4.F4_ESTOQUE = 'S' "
      	cQuery += " AND SF4.D_E_L_E_T_ = ' '" 
      	cQuery += " AND SB1.B1_FILIAL ='"+xFilial("SB1")+"'"
      	cQuery += " AND SB1.B1_COD = SD2.D2_COD "
      	cQuery += " AND SB1.B1_TIPO BETWEEN '"+mv_par03+"' AND '"+mv_par04+"'"
      	cQuery += " AND SB1.D_E_L_E_T_ = ' '" 

      	cQuery += " UNION "

      	cQuery += " SELECT B1_TIPO TIPO,D3_COD PRODUTO, D3_SEQCALC SEQCALC, D3_NUMSEQ NUMSEQ,"
      	cQuery += " D3_EMISSAO EMISSAO, D3_TM TES, D3_CF CF, D3_CF TIPODOC,"
      	cQuery += " D3_DOC NUMDOC,"+"' '"+" SERIE, D3_QUANT QUANT,"
		If lCusRep .And. mv_par13 == 2
	      	cQuery += " D3_CUSRP"+str(mv_par10,1)+" CUSTOTAL, "
	    Else
	      	cQuery += " D3_CUSTO"+str(mv_par10,1)+" CUSTOTAL, "
	    EndIf	
      	cQuery += " ' ' CLIFOR, ' ' LOJA, "
	   	cQuery += " SD3.R_E_C_N_O_"
	   	cQuery += " FROM "+RetSqlName("SD3")+" SD3,"
	   	cQuery +=  RetSqlName("SB1")+" SB1"
      	cQuery += " WHERE SD3.D3_FILIAL='"+xFilial("SD3")+"'"
      	cQuery += " AND SD3.D3_COD BETWEEN '"+mv_par01+"' AND '"+mv_par02+"'" 
      	If !lCusUnif 
      		cQuery += " AND SD3.D3_LOCAL ='"+mv_par08+"'"
      	EndIf
		If !lTranSB2 
			cQuery += " AND SD3.D3_LOCAL <> '"+cDepTrf+"'"
		EndIf
      	cQuery += " AND SD3.D3_EMISSAO BETWEEN '"+Dtos(mv_par05)+"' AND '"+Dtos(mv_par06)+"'"
      	If SuperGetMV('MV_D3ESTOR', .F., 'N') == 'N'
      		cQuery += " AND SD3.D3_ESTORNO <> 'S'"
      	EndIf  
		If SuperGetMV('MV_D3SERVI', .F., 'N') == 'N' .And. IntDL()
			cQuery += " AND ( (D3_SERVIC = '   ') OR (D3_SERVIC <> '   ' AND D3_TM <= '500' AND D3_STSERV = '3') )"
		EndIf		
      	cQuery += " AND SD3.D_E_L_E_T_ = ' '" 
      	cQuery += " AND SB1.B1_FILIAL ='"+xFilial("SB1")+"'"
      	cQuery += " AND SB1.B1_COD = D3_COD "
      	cQuery += " AND SB1.B1_TIPO BETWEEN '"+mv_par03+"' AND '"+mv_par04+"'"
      	cQuery += " AND SB1.D_E_L_E_T_ = ' '" 
     	If mv_par11 == 1
     		If UPPER(TcGetDB()) != "INFORMIX"    
				cQuery += " ORDER BY PRODUTO, EMISSAO, NUMSEQ, TES"     		
     		Else
     			cQuery += " ORDER BY 2, 5, 4, 6"
     		EndIf
     	Else                                 
     		If UPPER(TcGetDB()) != "INFORMIX"    
   				cQuery += " ORDER BY PRODUTO, SEQCALC, TES"
     		Else
        		cQuery += " ORDER BY 2, 3, 6 "
        	EndIf	
		EndIf

	   	cQuery:= ChangeQuery(cQuery)

		If Select("TRB") > 0		
			TRB->(DbCloseArea())
		EndIf
		If Select("TRC") > 0		
			TRC->(DbCloseArea())
		EndIf
	   	
	   	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),If(cPaisLoc=="BRA","TRB","TRC"),.T.,.T.)

		If cPaisLoc == "BRA"
			aEval(aCampos, {|e| If(e[2]!= "C", TCSetField("TRB", e[1], e[2],e[3],e[4]),Nil)})
		Else
			aEval(aCampos, {|e| If(e[2]!= "C", TCSetField("TRC", e[1], e[2],e[3],e[4]),Nil)})
			MTR901FillTrb(aCampos)	
		EndIf
	EndIf   
#ENDIF

If !lQuery
	DbSelectArea("SB1")
	While !Eof() .And. B1_FILIAL == cFilial .And. &cCond1 <= &cCond2

		If lEnd
			@PROW()+1,001 PSAY STR0020    //"CANCELADO PELO OPERADOR"
			Exit
	   	EndIf

		IncRegua()

   		// Filtra por Tipo
	   	If B1_TIPO < mv_par03 .Or. B1_TIPO > mv_par04
			dbSkip()
     		Loop
	   	EndIf

   		// Filtra por Produto
	   	If B1_COD < mv_par01 .Or. B1_COD > mv_par02
    	 	dbSkip()
        	Loop
	   	EndIf

   		dbSelectArea("SB2")
	   	dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",mv_par08))
    	
	   	lGravado  := .F.
   		cProdAnt  := SB1->B1_COD
		cLocalAnt := SB2->B2_LOCAL

		dbSelectArea("SD1")
		dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))
		dbSelectArea("SD2")
	   	dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))
   		dbSelectArea("SD3")
	   	dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

	   	dbSelectArea("SD1")
	   	dbSeek(xFilial("SD1")+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

   		While ! Eof() .And. D1_FILIAL == xFilial("SD1") .And. D1_COD == cProdAnt .And. IIf(lCusUnif,.T.,D1_LOCAL == cLocalAnt)

			dbSelectArea("SF4")
			dbSeek(cFilial+SD1->D1_TES)
			dbSelectArea("SD1")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Para paises do MercoSul, desprezar as Notas Fiscais que foram ³
			//³ ingressadas por Remitos de Salida...                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        	If !Empty(D1_REMITO)
           		dbSkip()
	           	Loop
        	EndIf
        	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Notas geradas pelo EIC considerar somente notas de FOB       |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        	If D1_TIPO_NF $ '6789AB'
           		dbSkip()
	           	Loop
        	EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If D1_ORIGLAN $ "LF" .Or. SF4->F4_ESTOQUE != "S"
         		dbSkip()
	         	Loop
    		Else
         		If D1_DTDIGIT < mv_par05 .Or. D1_DTDIGIT > mv_par06
            		dbSkip()
					loop
				Else
					cSeqIni := IIF(mv_par11==1,D1_NUMSEQ,D1_SEQCALC+D1_NUMSEQ)
					cAlias := Alias()
	         	EndIf
			EndIf

			dbSelectArea("TRB")
			RecLock("TRB",.T.)
			If SD1->D1_ESPECIE $ "FT /NF /   "
	        	cTipoDoc := "FAC"
			Else
         		cTipoDoc := SD1->D1_ESPECIE
			EndIf
     	   
			Replace PRODUTO   With SD1->D1_COD
			Replace SEQCALC   With SD1->D1_SEQCALC
			Replace NUMSEQ    With SD1->D1_NUMSEQ 
			Replace EMISSAO   With SD1->D1_DTDIGIT
			Replace TES       With SD1->D1_TES
			Replace CF        With SD1->D1_CF
			Replace TIPODOC   With cTipoDoc
			Replace NUMDOC    With SD1->D1_DOC
			Replace SERIE     With SD1->D1_SERIE
			Replace QUANT     With SD1->D1_QUANT

			If SD1->D1_QUANT > 0
				If mv_par10 == 1
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP1
					Else	
		     			Replace CUSTOTAL With SD1->D1_CUSTO
		     		EndIf	
				ElseIf mv_par10 == 2
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP2
					Else	
		           		Replace CUSTOTAL With SD1->D1_CUSTO2
		           	EndIf	
				ElseIf mv_par10 == 3
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP3
					Else	
	            		Replace CUSTOTAL With SD1->D1_CUSTO3
	            	EndIf	
				ElseIf mv_par10 == 4
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP4
					Else	
		         		Replace CUSTOTAL With SD1->D1_CUSTO4
		         	EndIf	
				ElseIf mv_par10 == 5
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP5
					Else	
		    			Replace CUSTOTAL With SD1->D1_CUSTO5
		    		EndIf	
				EndIf
			Else
         		If mv_par10 == 1
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP1
					Else	
		         		Replace CUSTOTAL With SD1->D1_CUSTO
		         	EndIf	
	        	ElseIf mv_par10 == 2
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP2
					Else	
		         		Replace CUSTOTAL With SD1->D1_CUSTO2
		         	EndIf	
     	      	ElseIf mv_par10 == 3
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP3
					Else	
		           		Replace CUSTOTAL With SD1->D1_CUSTO3
		           	EndIf	
     	      	ElseIf mv_par10 == 4
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP4
					Else	
	     	        	Replace CUSTOTAL With SD1->D1_CUSTO4
	     	        EndIf	
	         	ElseIf mv_par10 == 5
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD1->D1_CUSRP5
					Else	
	          			Replace CUSTOTAL With SD1->D1_CUSTO5
	          		EndIf	
	         	EndIf
			Endif
      	   
			MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  		//³ Control para imprimir sin movimientos                   ³
	    	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	     	lGravado := .T.

			dbSelectArea("SD1")
			dbSkip()
		EndDo
  		dbSelectArea("SD2")
		dbSeek(xFilial("SD2")+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

		While ! Eof() .And. D2_FILIAL == xFilial("SD2") .And. D2_COD == cProdAnt .And. IIf(lCusUnif,.T.,D2_LOCAL == cLocalAnt)

			dbSelectArea("SF4")
			dbSeek(cFilial+SD2->D2_TES)
	    	dbSelectArea("SD2")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	    	//³ Para paises do MercoSul, desprezar as Notas Fiscais que foram ³
			//³ ingressadas por Remitos de Salida...                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ! Empty(D2_REMITO) .And. !(D2_TPDCENV $ "1A")
				SD2->(DbSkip())
		  		Loop
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  		//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
	      	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	      	If D2_ORIGLAN == "LF" .Or. SF4->F4_ESTOQUE != "S"
				SD2->(DbSkip())
		       	Loop
			Else
	      		If D2_EMISSAO < mv_par05 .or. D2_EMISSAO > mv_par06
	       			dbSkip()
		         	loop
				Else
     	    		If mv_par11 == 1
	          	 		If D2_NUMSEQ < cSeqIni
    	          			cSeqIni := D2_NUMSEQ
	    	            	cAlias	:= Alias()
     	    	        EndIf
	              	Else
    		           	If D2_SEQCALC+D2_NUMSEQ < cSeqIni
            	    		cSeqIni := D2_SEQCALC+D2_NUMSEQ
	            	     	cAlias	:= Alias()
	     	            EndIf
					EndIf
     			EndIf
			EndIf

			dbSelectArea("TRB")

			RecLock("TRB",.T.)
			If SD2->D2_ESPECIE $ "FT /NF /   "
	    		cTipoDoc := "FAC"
			Else
         		cTipoDoc := SD2->D2_ESPECIE
			EndIf
     	   
			Replace PRODUTO   With SD2->D2_COD
	      	Replace SEQCALC   With SD2->D2_SEQCALC
     	   	Replace NUMSEQ    With SD2->D2_NUMSEQ 
	      	Replace EMISSAO   With SD2->D2_EMISSAO
	      	Replace TES       With SD2->D2_TES
     	   	Replace CF        With SD2->D2_CF
	      	Replace TIPODOC   With cTipoDoc
     	  	Replace NUMDOC    With SD2->D2_DOC
	      	Replace SERIE     With SD2->D2_SERIE
     	   	Replace QUANT  With SD2->D2_QUANT
	      	If SD2->D2_QUANT <> 0
				If mv_par10 == 1
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD2->D2_CUSRP1
					Else	
		            	Replace CUSTOTAL With SD2->D2_CUSTO1
		            EndIf	
				ElseIf mv_par10 == 2
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD2->D2_CUSRP2
					Else	
	        			Replace CUSTOTAL With SD2->D2_CUSTO2
	        		EndIf	
				ElseIf mv_par10 == 3
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD2->D2_CUSRP3
					Else	
	         			Replace CUSTOTAL With SD2->D2_CUSTO3
	         		EndIf	
	        	ElseIf mv_par10 == 4
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD2->D2_CUSRP4
					Else	
	          			Replace CUSTOTAL With SD2->D2_CUSTO4
	          		EndIf	
	         	ElseIf mv_par10 == 5
					If lCusRep .And. mv_par13 == 2
		     			Replace CUSTOTAL With SD2->D2_CUSRP5
					Else	
	     	    		Replace CUSTOTAL With SD2->D2_CUSTO5
	     	    	EndIf	
         		EndIf
			EndIf
			MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Controle para imprimir sin movimientos                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lGravado := .T.

			dbSelectArea("SD2")
			dbSkip()
		EndDo

		dbSelectArea("SD3")
		dbSeek(cFilial+SB1->B1_COD+IIf(lCusUnif,"",SB2->B2_LOCAL))

		While !Eof()  .And. D3_FILIAL == xFilial("SD3") .And. D3_COD == cProdAnt .And. IIf(lCusUnif,.T.,D3_LOCAL == cLocalAnt)

			If !D3Valido()
   				SD3->(DbSkip())
	     		Loop
        	EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Nao imprimir os produtos que estao no armazem de transito                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc <> "BRA" .And. !lTranSB2 .And. AllTrim(SD3->D3_LOCAL) == AllTrim(cDepTrf)
				dbSkip()
				Loop
			EndIf	
	
			If D3_EMISSAO < mv_par05 .or. D3_EMISSAO > mv_par06
				dbSkip()
	   			Loop
	      	Else
				If mv_par11 == 1
		   			If D3_NUMSEQ < cSeqIni
     	 				cSeqIni := D3_NUMSEQ
        	  			cAlias := Alias()
	        	    EndIf
	      		Else
        			If D3_SEQCALC+D3_NUMSEQ < cSeqIni
		          		cSeqIni := D3_SEQCALC+D3_NUMSEQ
     		            cAlias := Alias()
            	 	EndIf
	   			EndIf
			EndIf

			dbSelectArea("TRB")
   			RecLock("TRB",.T.)
	    	Replace PRODUTO   With SD3->D3_COD
	    	Replace SEQCALC   With SD3->D3_SEQCALC
     	   	Replace NUMSEQ    With SD3->D3_NUMSEQ 
	      	Replace EMISSAO   With SD3->D3_EMISSAO
     	   	Replace TES       With SD3->D3_TM
	      	Replace CF        With SD3->D3_CF
	       	Replace TIPODOC   With SD3->D3_CF
		    Replace NUMDOC    With SD3->D3_DOC
     		Replace SERIE     With ""  ////SD3->D3_SERIE
			Replace QUANT  With SD3->D3_QUANT
	      	If mv_par10 == 1
				If lCusRep .And. mv_par13 == 2
	     			Replace CUSTOTAL With SD3->D3_CUSRP4
				Else	
			      	Replace CUSTOTAL With SD3->D3_CUSTO1
			    EndIf	
	    	ElseIf mv_par10 == 2
				If lCusRep .And. mv_par13 == 2
	     			Replace CUSTOTAL With SD3->D3_CUSRP2
				Else	
		    	  	Replace CUSTOTAL With SD3->D3_CUSTO2
		    	EndIf  	
	  	   	ElseIf mv_par10 == 3
				If lCusRep .And. mv_par13 == 2
	     			Replace CUSTOTAL With SD3->D3_CUSRP3
				Else	
			 	  	Replace CUSTOTAL With SD3->D3_CUSTO3
			 	EndIf	
		   	ElseIf mv_par10 == 4
				If lCusRep .And. mv_par13 == 2
	     			Replace CUSTOTAL With SD3->D3_CUSRP4
				Else	
		  		  	Replace CUSTOTAL With SD3->D3_CUSTO4
		  		EndIf	
	  	   	ElseIf mv_par10 == 5
				If lCusRep .And. mv_par13 == 2
	     			Replace CUSTOTAL With SD3->D3_CUSRP5
				Else	
			 	  	Replace CUSTOTAL With SD3->D3_CUSTO5
			 	EndIf  	
	      	EndIf
	      	MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Control para imprimir sin movimientos                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lGravado := .T.

			dbSelectArea("SD3")
			dbSkip()
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Graba registro cerado para imprimir sin movimientos     ³
	 	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	   	If !lGravado .And. ( mv_par07 == 1 )
			RecLock("TRB", .T.)
			Replace PRODUTO   With cProdAnt
			MsUnLock()
		EndIf

	   	dbSelectArea("SB1")
   		dbSkip()
	EndDo
EndIf

dbSelectArea("SB1") 
dbSetOrder(1)
SB1->(dbSeek(xFilial()+mv_par01,.T.))

dbSelectArea("TRB")
dbGoTop()
SetRegua(RecCount())
If TRB->PRODUTO <> SB1->B1_COD
	lSb1Ant := .T.
EndIf
	 
While  !TRB->(Eof()) .Or. If(lQuery.And.mv_par07 == 1,&cCond1 <= &cCond2 .And. !SB1->(Eof()),.F.)

	nEntradas := 0
	nSalidas  := 0
	nCEntrada := 0
	nCSalidas := 0
	lImpCabec := .T.
   
	cProduto := PRODUTO

	If mv_par07 == 2
		SB1->(MsSeek(xFilial()+TRB->PRODUTO))   
	EndIf

	If !lQuery .Or. (lQuery .And. TRB->PRODUTO == SB1->B1_COD)

		While !Eof() .And. PRODUTO == cProduto
	
			If Li > 58
	        	cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
			EndIf
			
			IncRegua()
	
			If lImpCabec
	        	MTR901CAB(@aSalAtu,cAlias,cPicB2Cust,nTam,nIncCol)
				lImpCabec := .F.
			EndIf
	
			dbSelectArea("TRB")
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica la fecha para productos sin movimientos        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lQuery
			 	If TIPODOC $ "FT /NF /   "     
			    	TIPODOC := "FAC"
			 	EndIf
		  	EndIf
			If CtoD("  /  /  ") != EMISSAO
	        	@ Li, 000 PSAY EMISSAO
				@ Li ,011 PSAY TES
				@ Li ,015 PSAY TIPODOC 
	         
				If mv_par09 = 1
					@ Li ,020 PSAY NUMDOC Picture PesqPict("SD1","D1_DOC")
				Else
	            	@ Li ,020 PSAY NUMSEQ Picture PesqPict("SD1","D1_NUMSEQ")
				EndIf
	         
				If TES <= "500"
	            	@ Li,038+nIncCol PSAY QUANT    Picture cPicD1Qt
		            @ Li,062+nIncCol PSAY Round(CUSTOTAL,aTamT[2])  Picture cPicD1Cust
	    	        @ Li,083+nIncCol PSAY "|"
					nCusto := 0
	            	If QUANT != 0
					   	nCusto := Round(CUSTOTAL,aTamT[2])/QUANT
		               @ Li,085+nIncCol PSAY nCusto  Picture cPicD1Cust
	    	        EndIf
	        	    @ Li,104+nIncCol PSAY "|"
				Else                
	            	@ Li,083+nIncCol PSAY "|"
		   			nCusto := 0
		            If QUANT != 0
					   	nCusto := Round(CUSTOTAL,aTamT[2])/QUANT
	        	       @ Li,085+nIncCol PSAY nCusto  Picture cPicD1Cust
	            	EndIf
		            @ Li,104+nIncCol PSAY "|"
		            @ Li,108+nIncCol PSAY QUANT                     Picture cPicD1Qt
		            @ Li,132+nIncCol PSAY Round(CUSTOTAL,aTamT[2]) Picture cPicD1Cust
				EndIf
	         
			 	If TRB->TES > "500"
		        	aSalAtu[1] -= QUANT
		            aSalAtu[mv_par10+1] -= Round(CUSTOTAL,aTamT[2])  
	     			aSalAtu[7] -= ConvUm(SB1->B1_COD,QUANT,0,2)
			 	Else
			    	aSalAtu[1] += QUANT
	    	        aSalAtu[mv_par10+1] += IIF(QUANT >0,( QUANT * nCusto ),Round(CUSTOTAL,aTamT[2]))
		    		aSalAtu[7] += ConvUm(SB1->B1_COD,QUANT,0,2)
			 	EndIf
	
	        	@ Li, 153+nIncCol PSAY "|"
	
		        If aSalAtu[1] != 0
		           @ Li,160+nIncCol PSAY aSalAtu[1]          Picture cPicD1Qt
	    	       @ Li,182+nIncCol PSAY aSalAtu[mv_par10+1] Picture cPicD1Cust
	        	EndIf
	
				cMovto := ""
				@ Li,203+nIncCol PSAY "|"
	
				If TRB->TES > "500" .And. ALLTRIM(TRB->TIPODOC) $ "REM|NF|FAC"
					cMovto := OemToAnsi(STR0030)
				ElseIf TRB->TES > "500" .And. ALLTRIM(TRB->TIPODOC) $ "RE0|DE0|RE8|DE8"
					cMovto := OemToAnsi(STR0032)
				ElseIf TRB->TES <= "500" .And. ALLTRIM(TRB->TIPODOC) $ "RE6|DE6"
					cMovto := OemToAnsi(STR0034)
				ElseIf TRB->TES <= "500" .And. ALLTRIM(TRB->TIPODOC) $ "RE8|DE8"
					cMovto := OemToAnsi(STR0032)
				ElseIf TRB->TES <= "500" .And. ALLTRIM(TRB->TIPODOC) $ "REM|NF|FAC"
					cMovto := OemToAnsi(STR0031)
				Else
					cMovto := OemToAnsi(STR0033)
				EndIf
				@ Li,205+nIncCol PSAY cMovto
	
				Li++
	
				nEntradas += Iif(TES <= "500",QUANT,0)
	         	nSalidas  += Iif(TES >  "500",QUANT,0)
	         	nCEntrada += Iif(TES <= "500",Round(CUSTOTAL,aTamT[2]),0)
	         	nCSalidas += Iif(TES >  "500",Round(CUSTOTAL,aTamT[2]),0)
	
			EndIf
	     	dbSelectArea("TRB")
			dbSkip()
		EndDo
	EndIf	
		
	If (nEntradas + nSalidas) > 0

		Li++
		@ li,000 PSAY STR0021      //"T O T A I S  :"
      	@ Li,038+nIncCol PSAY nEntradas    Picture cPicD1Qt
      	@ Li,062+nIncCol PSAY nCEntrada    Picture cPicD1Cust
      	@ Li,108+nIncCol PSAY nSalidas     Picture cPicD2Qt
      	@ Li,132+nIncCol PSAY nCSalidas    Picture cPicD2Cust
      	@ Li,157+nIncCol PSAY aSalAtu[1]   Picture cPicB2Qt
	  	If aSalAtu[1] == 0.00
			aSalAtu[mv_par10+1] := aSalAtu[mv_par10+1] * aSalAtu[1]
		EndIf
      	@ Li,182+nIncCol PSAY aSalAtu[mv_par10+1] Picture cPicD1Cust
      	Li++
      	@ Li,135+nIncCol PSAY STR0022   //"QTD. NA SEGUNDA UM: "
      	@ Li,157+nIncCol PSAY aSalAtu[7] Picture cPicB2Qt2
      	Li++

      	@Li ,000 PSAY __PrtThinLine()
      	Li++
	   	If lQuery
			SB1->(DbSkip())
		EndIf   
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
      	//³ Verifica se deve ou nao listar os produtos s/movimento ³
      	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
      	If mv_par07 == 1
			If !lQuery
	        	If Li > 58
	            	cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
	         	EndIf
				dbSelectArea("TRB")
	   	
	         	@ Li,000 PSAY STR0023   //"NAO HOUVE MOVIMENTACAO PARA ESTE PRODUTO"
	         	Li++
      			@Li ,000 PSAY __PrtThinLine()
	         	Li++
			Else
            	If TRB->PRODUTO <> SB1->B1_COD .And. SB1->B1_FILIAL==xFilial("SB1");
               		.And. &cCond1 <= &cCond2 .And. !SB1->(eof()) .And.;
					(SB1->B1_TIPO >= mv_par03 .AND. SB1->B1_TIPO <= mv_par04)
			      	If Li > 58
         				cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
				   	EndIf
			      	MTR901CAB(@aSalAtu,cAlias,cPicB2Cust,nTam,nIncCol)
   		      		@ Li,000 PSAY STR0023   //"NAO HOUVE MOVIMENTACAO PARA ESTE PRODUTO"
		         	Li++
      				@ Li ,000 PSAY __PrtThinLine()
		         	Li++
				EndIf
				SB1->(DbSkip())
			Endif
      	EndIf
	EndIf
EndDo

If li != 80
   roda(cbcont,cbtxt,Tamanho)
EndIf

dbSelectArea("SB1")
DbClearFilter()
dbSetOrder(1)
dbSelectArea("SB2")
dbSetOrder(1)
dbSelectArea("SD1")
If mv_par11 != 1
   RetIndex("SD1")
   Ferase(cTrbSD1+OrdBagExt())
EndIf
dbSetOrder(1)
dbSelectArea("SD2")
If mv_par11 != 1
   RetIndex("SD2")
   Ferase(cTrbSD1+OrdBagExt())
EndIf
dbSetOrder(1)
dbSelectArea("SD3")
If mv_par11 != 1
   RetIndex("SD3")
   Ferase(cTrbSD1+OrdBagExt())
EndIf
dbSetOrder(1)

dbSelectArea("TRB")
dbCloseArea()

If !lQuery
    Ferase(cNomeArq+GetDbExtension())	// Elimina arquivos de Trabalho
    Ferase(cNomeArq+OrdBagExt())		// Elimina arquivos de Trabalho
EndIf

dbSelectArea("TRA")
dbCloseArea()
Ferase(cNomArqA+GetDbExtension())	// Elimina arquivos de Trabalho
Ferase(cNomArqA+OrdBagExt())		// Elimina arquivos de Trabalho
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MTR901val³ Autor ³ Paulo Boschetti       ³ Data ³ 22.12.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mtr901val()

If mv_par09 > 0 .And. mv_par09 < 3
   Return .T.
Else
   Return .T.
EndIf

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MTR901Cab³ Autor ³ Marcos Bregantim      ³ Data ³ 28.07.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cabecalho com dados do produto                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Mtr901Cab(ExpA1,ExpC1,ExpC2,ExpN1,ExpN2)                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1: Array com saldos do produto                         ³±±
±±³          ³ ExpC1: alias arq. trb.                                     ³±±
±±³          ³ ExpC2: picture (nao usado)                                 ³±±
±±³          ³ ExpN1: tam.usado em campos numericos                       ³±±
±±³          ³ ExpN2: incremento na coluna devido tam.do campo documento  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Mtr901Cab(aSalAtu,cAlias,cPicB2Cust,nTam,nIncCol)

LOCAL nCusMed := 0
LOCAL cPicD1Cust:= PesqPict("SD1","D1_CUSTO"+IIF(mv_par10 == 1 ,"",Str(mv_par10,1)),nTam,mv_par10)
LOCAL aSalMov := {}
LOCAL cSeek           
LOCAL cProduto
LOCAL i:=0
LOCAL aSalAlmox:={},aArea:={}
aSalAtu := {0,0,0,0,0,0,0}

If !lQuery
	SB1->( MsSeek(xFilial("SB1")+TRB->PRODUTO) )
EndIf
cProduto	:=	SB1->B1_COD

//Grava o Acumulado
If lCusUnif
	aArea:=GetArea()
	aSalAtu  := { 0,0,0,0,0,0,0 }
	dbSelectArea("SB2")
	dbSetOrder(1)
	dbSeek(cSeek:=xFilial("SB2")+cProduto)
	While !Eof() .And. B2_FILIAL+B2_COD == cSeek
		aSalAlmox := CalcEst(cProduto,SB2->B2_LOCAL,mv_par05,,,( lCusRep .And. mv_par13==2 ))
		For i:=1 to Len(aSalAtu)
			aSalAtu[i] += aSalAlmox[i]
		Next i
		dbSkip()
	EndDo
	RestArea(aArea)
Else
	aSalAtu   := CalcEst(cPRODUTO,mv_par08,mv_par05,,,( lCusRep .And. mv_par13==2 ))
EndIf	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Custo de Reposicao do Produto        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCusRep .And. mv_par13==2
	aSalAtu := {aSalAtu[1],aSalAtu[18],aSalAtu[19],aSalAtu[20],aSalAtu[21],aSalAtu[22],aSalAtu[07]}
EndIf

dbSelectArea("TRA")
RecLock("TRA",.T.)
Replace FILIAL    With xFilial("SB9")
Replace COD       With SB1->B1_COD
Replace LOCAL     With mv_par08
Replace QINI      With aSalAtu[1]
Replace QISEGUM   With ConvUm(COD,QINI,0,2)
Replace VINI      With aSalAtu[2]
If mv_par10 > 1
   Replace VINI   With Round(xMoeda(TRA->VINI,1,mv_par10,mv_par05,nDecs+1),nDecs)
EndIf
MsUnLock()

dbSelectArea("TRB")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Saldo Inicial do Produto             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SB2->( dbSeek(xFilial("SB2")+cProduto) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Custo Medio do Produto               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aSalAtu[1] > 0
   nCusmed := Round(aSalAtu[mv_par10+1]/aSalAtu[1],nDecs)
ElseIf aSalAtu[1] == 0 .And. aSalAtu[mv_par10+1] == 0
   nCusMed := 0
Else
   nCusmed := &(Eval(bBloco,"SB2->B2_CM",mv_par10))
EndIf

@ Li,000 PSAY SB1->B1_COD
@ Li,019 PSAY SubStr(SB1->B1_DESC,1,30)
@ Li,056 PSAY STR0024+SB1->B1_UM		//"UM : "
@ Li,068 PSAY STR0025+SB1->B1_TIPO		//"TIPO : "
@ Li,083 PSAY STR0026+SB1->B1_GRUPO		//"GRUPO : "
If lCusRep
	@ Li,115+nIncCol PSAY STR0069          	//"Custo Reposição : "
Else
	@ Li,115+nIncCol PSAY STR0027          	//"Custo Medio : "
EndIf	

@ Li,132+nIncCol PSAY nCusMed		         Picture cPicD1Cust
@ Li,157+nIncCol PSAY aSalAtu[1]    	     Picture PesqPictQt("B2_QATU" ,nTam)
@ Li,182+nIncCol PSAY aSalAtu[mv_par10+1] 	 Picture cPicD1Cust

Li++
@ Li,000 PSAY STR0028+SUBS(SB1->B1_POSIPI,1,4)+"."+SUBS(SB1->B1_POSIPI,5,4)+"."+SUBS(SB1->B1_POSIPI,9,2)    //"Posicao IPI : "
@ Li,035 PSAY STR0029+SB2->B2_LOCALIZ     //"Localizacao : "
Li += 2
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MTR901Dev³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 17/07/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Avalia se item pertence a uma nota de devolu‡ao             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR901                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTR901Dev()
LOCAL cAlias:=Alias(),lRet:=.F.

If cAlias == "SD1"
   dbSelectArea("SF1")
   dbSeek(cFilial+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA)
   If SF1->F1_TIPO == "D"
      lRet:=.T.
   EndIf
ElseIf cAlias == "SD2"
   dbSelectArea("SF2")
   dbSeek(cFilial+SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA)
   If SF2->F2_TIPO == "D"
      lRet:=.T.
   EndIf
EndIf
dbSelectArea(cAlias)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTR901VAlm ³ Autor ³ Lucas                 ³ Data ³19/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida Almoxarifado do KARDEX com relacao a custo unificado ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR901                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTR901VAlm
LOCAL lRet:=.T.
LOCAL cConteudo:=&(ReadVar())
LOCAL nOpc:=2
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se utiliza custo unificado por Empresa/Filial       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL lCusUnif := IIf(FindFunction("A330CusFil"),A330CusFil(),GetMV("MV_CUSFIL",.F.))
If lCusUnif .And. cConteudo != "**"
	nOpc := Aviso(STR0035,STR0036,{STR0037,STR0038})	//"Aten‡„o"###"Ao alterar o almoxarifado o custo medio unificado sera desconsiderado."###"Confirma"###"Abandona"
	If nOpc == 2
		lRet:=.F.
	EndIf
EndIf
Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTR901S12 ³ Autor ³ Nereu Humberto Junior ³ Data ³ 18.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cabecalho com dados do produto                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTR901S12(aSalAtu,oSection1,oSection2,lQuery)

LOCAL nCusMed := 0
LOCAL aSalMov := {}
LOCAL cSeek           
LOCAL cProduto
LOCAL i:=0
LOCAL aSalAlmox:={},aArea:={}
aSalAtu := {0,0,0,0,0,0,0}

If !lQuery
	SB1->( MsSeek(xFilial("SB1")+TRB->PRODUTO) )
EndIf
cProduto	:=	SB1->B1_COD

//Grava o Acumulado
If lCusUnif
	aArea:=GetArea()
	aSalAtu  := { 0,0,0,0,0,0,0 }
	dbSelectArea("SB2")
	dbSetOrder(1)
	dbSeek(cSeek:=xFilial("SB2")+cProduto)
	While !Eof() .And. B2_FILIAL+B2_COD == cSeek
		aSalAlmox := CalcEst(cProduto,SB2->B2_LOCAL,mv_par05,,,( lCusRep .And. mv_par13==2 ))
		For i:=1 to Len(aSalAtu)
			aSalAtu[i] += aSalAlmox[i]
		Next i
		dbSkip()
	EndDo
	RestArea(aArea)
Else
	aSalAtu   := CalcEst(cPRODUTO,mv_par08,mv_par05,,,( lCusRep .And. mv_par13==2 ))
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Custo de Reposicao do Produto        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCusRep .And. mv_par13==2
	aSalAtu := {aSalAtu[1],aSalAtu[18],aSalAtu[19],aSalAtu[20],aSalAtu[21],aSalAtu[22],aSalAtu[07]}
EndIf

dbSelectArea("TRB")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Saldo Inicial do Produto             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SB2->( dbSeek(xFilial("SB2")+cProduto) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Custo Medio do Produto               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aSalAtu[1] > 0
   nCusmed := Round(aSalAtu[mv_par10+1]/aSalAtu[1],nDecs)
ElseIf aSalAtu[1] == 0 .And. aSalAtu[mv_par10+1] == 0
   nCusMed := 0
Else
   nCusmed := &(Eval(bBloco,"SB2->B2_CM",mv_par10))
EndIf

oSection1:Init()
oSection2:Init()

If lCusRep
	oSection1:Cell("nCusMed"):SetTitle(UPPER(STR0069))
EndIf

oSection1:Cell("nCusMed"):SetValue(nCusMed)			
oSection1:Cell("nQtdSal"):SetValue(aSalAtu[1])
oSection1:Cell("nVlrSal"):SetValue(aSalAtu[mv_par10+1])

oSection1:PrintLine()
oSection2:PrintLine()

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTR901CUnf ³ Autor ³Nereu Humberto Junior  ³ Data ³18/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ajusta grupo de perguntas p/ Custo Unificado                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR901                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTR901CUnf(lCusUnif)
Local aSvAlias:=GetArea()        
Local nTamSX1 :=Len(SX1->X1_GRUPO)
dbSelectArea("SX1")
If dbSeek(PADR("MTR901",nTamSX1)+"08",.F.)
	If !("MTR901VAlm" $ X1_VALID)
		RecLock("SX1",.F.)
		If Empty(X1_VALID)
			Replace X1_VALID With "MTR901VAlm"
		Else
			Replace X1_VALID With X1_VALID+".And.MTR901VAlm"
		EndIf
		MsUnlock()
	EndIf
	If lCusUnif .And. X1_CNT01 != "**"
		RecLock("SX1",.F.)
		Replace X1_CNT01 With "**"
		MsUnlock()
	EndIf
EndIf
RestArea(aSvAlias)
Return(NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTR901NFExp ³ Autor ³Lucas				 ³ Data ³18/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega arquivo TRB com resultado da Query                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR901                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTR901FillTrb(aCampos)

Local cNomeArq:=CriaTrab(aCampos)

If Select("TRB") > 0		
	TRB->(DbCloseArea())
EndIf
		
dbUseArea( .T., __cRDDNTTS, cNomeArq, "TRB", if(.F. .Or. .F., !.F., NIL), .F. )
If mv_par11 == 1
   IndRegua("TRB",cNomeArq,'PRODUTO+Dtos(EMISSAO)+NUMSEQ+TES',,,STR0011)  //"Selecionando Registros..."
Else
   IndRegua("TRB",cNomeArq,'PRODUTO+SEQCALC+TES',,,STR0011)  //"Selecionando Registros..."
Endif
		
DbSelectArea("TRC")
DbGoTop()
While !Eof()
	DbSelectArea("TRB")
	Reclock("TRB",.T.)
	Replace 	PRODUTO 	With	TRC->PRODUTO
	Replace 	EMISSAO 	With	TRC->EMISSAO
	Replace 	SEQCALC 	With	TRC->SEQCALC
	Replace 	NUMSEQ 		With	TRC->NUMSEQ
	Replace 	TES 		With	TRC->TES
	Replace 	CF 			With	TRC->CF
	Replace 	TIPODOC 	With	If(AllTrim(TRC->TIPODOC) $ "FT|NF","FAC",TRC->TIPODOC)
	Replace 	NUMDOC 		With	TRC->NUMDOC
	Replace 	SERIE 		With	TRC->SERIE
	Replace 	QUANT 		With	TRC->QUANT
	Replace 	CUSTOTAL 	With	TRC->CUSTOTAL
	Replace 	CLIFOR	 	With	TRC->CLIFOR
	Replace 	LOJA		With	TRC->LOJA
	MsUnLock()
	DbSelectArea("TRC")
	DbSkip()
End	
Return 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ AjustaSX1³ Autor ³ Microsiga S/A         ³ Data ³28/12/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta o dicionario de dados SX1                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR901			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()

Local aAreaAnt := GetArea()
Local cPerg    := "MTR901"
Local aHelpPor := aHelpEng := aHelpSpa := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona o Help da nova pergunta 17                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHelpPor := {'Selecione o tipo de custo a ser',;
		     'impresso no relatorio.'			,;
		     '- Custo Medio' ,;
			 '- Custo de Reposição (Somente Argentina)'}
aHelpSpa := {'Seleccione el tipo de costo que',;
			 ' se imprimira en el informe (Sólo Argentina)',;
			 '- Costo Medio' ,'- Costo de Reposicion'}
aHelpEng := {'Select the type of expense that',;
	         ' will be printed in the report.'	,;
			 ' Average cost ',;
			 '- Replacement cost (Only Argentina)'}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona no grupo a nova pergunta 17                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PutSx1(cPerg,;					//-- 01 - X1_GRUPO
    '13',;						//-- 02 - X1_ORDEM
    'Qual Custo Imprimir?',;	//-- 03 - X1_PERGUNT
    '¿Qué Costo imprimir?',;	//-- 04 - X1_PERSPA
    'What Cost Print?',;		//-- 05 - X1_PERENG
    'mv_chd',;					//-- 06 - X1_VARIAVL
    'N',;						//-- 07 - X1_TIPO
    1,;							//-- 08 - X1_TAMANHO
    0,;							//-- 09 - X1_DECIMAL
    1,;							//-- 10 - X1_PRESEL
    'C',;						//-- 11 - X1_GSC
    '',;						//-- 12 - X1_VALID
    '',;						//-- 13 - X1_F3
    '',;						//-- 14 - X1_GRPSXG
    '',;						//-- 15 - X1_PYME
    'mv_par13',;				//-- 16 - X1_VAR01
    'Medio',;					//-- 17 - X1_DEF01
    'Medio',;					//-- 18 - X1_DEFSPA1
    'Average',;					//-- 19 - X1_DEFENG1
    '',;						//-- 20 - X1_CNT01
    'Reposição',;				//-- 21 - X1_DEF02
    'Reposicion',;				//-- 22 - X1_DEFSPA2
    'Replacement',;				//-- 23 - X1_DEFENG2
    '',;						//-- 24 - X1_DEF03
    '',;						//-- 25 - X1_DEFSPA3
    '',;						//-- 26 - X1_DEFENG3
    '',;						//-- 27 - X1_DEF04
    '',;						//-- 28 - X1_DEFSPA4
    '',;						//-- 29 - X1_DEFENG4
    '',;						//-- 30 - X1_DEF05
    '',;						//-- 31 - X1_DEFSPA5
    '',;						//-- 32 - X1_DEFENG5
    ,;							//-- 33 - HelpPor
    ,;							//-- 35 - HelpEng
    ,;							//-- 34 - HelpSpa
    '')							//-- 36 - X1_HELP 
    PutSX1Help("P.MTR90117.",aHelpPor,aHelpEng,aHelpSpa,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona o Help A910CUSRP                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHelpPor := {"ATENÇÂO (Somente Argentina)." ,;
 			 "O Custo de Reposição esta ",;
			 "desativado, o relatorio sera impresso",;
			 "pelo custo medio." }
aHelpSpa := {"Atención(Sólo Argentina).",;
		     "El Costo de Reposicion esta ",;
			 "desactivado, el informe se",;
			 "imprimira por el costo promedio. "}
aHelpEng := {"Attention(Only Argentina).",;
		     "The replacement cost is off,",;
			 "the report will be printed by the",;
			 "average cost." }
PutSX1Help("P"+"A911CUSRP",aHelpPor,aHelpEng,aHelpSpa,.T.)

aHelpPor := { "Verifique a configuração do"			,"parametro MV_CUSREP e se existe na "	,"base de dados todos os campos"	,"utilizados para o custo de reposição."}
aHelpSpa := { "Verifique la configuracion del"		,"parametro MV_CUSREP y si existen en"	,"la base de datos todos los campos","utilizados para el costo de reposicion."}
aHelpEng := { "Check the configuration parameter"	,"MV_CUSREP and is in the database"		,"all the fields used for the"		,"replacement cost."}
PutSX1Help("S"+"A911CUSRP",aHelpPor,aHelpEng,aHelpSpa,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta o Help da pergunta 7                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHelpPor := {'Considera a lista de produtos sem',;
		     'movimentos do cadastro de movimentações',;
		     'internas (SD3).',;
		     'Ao selecionar "Sim", os produtos serão',;
		     'exibidos no relatório independente do',;
		     'armazém selecionado.'}
aHelpSpa := {'Considera que la lista de productos sin',;
		     'movimientos en el registro interno (SD3).',;
		     'Al seleccionar "Sí", los productos que',;
		     'figuran en el informe seleccionaron',;
		     'de forma independiente desde el almacén.'}
aHelpEng := {'Considers the list of products with no',;
		     'movements on internal register (SD3).',;
		     'By selecting "Yes", the products will',;
		     'appear in the report independently',;
		     'selected from the warehouse.'}
PutSX1Help("P.MTR90107.",aHelpPor,aHelpEng,aHelpSpa,.T.)

RestArea(aAreaAnt)

Return
