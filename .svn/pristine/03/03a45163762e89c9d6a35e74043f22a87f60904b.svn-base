#INCLUDE "PROTHEUS.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "TOPCONN.CH"
#include "MATR991.CH"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATR991   ºAutor  ³Ivan Haponczuk      º Data ³  14/12/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Certificado de IVA Bimestral y Anual                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5 - Colombia                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador º Data   º BOPS º  Motivo da Alteracao                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÃÍÍÍÍÍÍÍÍÃÍÍÍÍÍÍÅÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºLaura Medinaº15/03/13ºTFVWHCº Se agregaron y modificaron etiquetas.    º±±
±±ºLaura Medinaº26/03/13ºTFVWHCº Se agrego leyenda al pie del certificado.º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */

Function Matr991(alDados)

	Private opReport	:= NIL
	Private apDados		:= aClone(alDados)

	If TRepInUse()	//verifica se relatorios personalizaveis esta disponivel
		opReport:=GeraReport()
		opReport:PrintDialog()
	Endif

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GeraReportºAutor  ³Ivan Haponczuk      º Data ³  14/12/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Cria o objeto do relatorio e o configura.                  º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function GeraReport()

	Local olReport	:= NIL

	olReport:= TReport():New("Matr991",STR0001,,{|opReport|PrintReport(opReport)},"")
	olReport:lHeaderVisible		:= .F.	// Não imprime cabeçalho do protheus
	olReport:lFooterVisible		:= .F.	// Não imprime rodapé do protheus
	olReport:lParamPage			:= .F.	// Não imprime pagina de parametros
	olReport:oPage:nPaperSize	:= 9	// Impressão em papel A4
	olReport:NFONTBODY			:= 12	// TAMANHO DA FONTE

Return olReport

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³PrintReportºAutor  ³Ivan Haponczuk     º Data ³  14/12/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime o relatorio a partir do array.                     º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function PrintReport()

 	Local nlI       := 0
	Local nlX       := 0
	Local nlLin     := 0
	Local clFornec  := 0
	Local nlBase    := 0
	Local nlAlq     := 0
	Local nlValRet  := 0
	Local nlTotBase := 0
	Local nlTotRet  := 0 
	Local nlTotIVA  := 0
	Local alItens   := {}
	Local nSinal	:= 1  
	Local nlValIVA  := 0

	For nlX:=1 To Len(apDados)

		alItens := {} 

		opReport:PrintText("",0100,0800)
		opReport:PrintText(STR0001,0090,0250) //CERTIFICADO DE RETENCION POR IVA

		opReport:Line(0140,0250,0140,1045)          

		opReport:PrintText(STR0019,0150,0250) //RETENEDOR
		opReport:PrintText(STR0020,0150,1250) //FECHA DE EXPEDICION 
		opReport:PrintText(dtoc(ddatabase),0150,1750) //FECHA DE EXPEDICION

		opReport:PrintText(SM0->M0_NOMECOM,0220,0250)

		opReport:PrintText(AllTrim(SM0->M0_ENDENT)+", "+SM0->M0_CIDENT,0280,0250)

		opReport:PrintText(STR0006+" "+SM0->M0_CGC,0340,0250) //NIT:

		opReport:PrintText(STR0002+" "+DTOC(MV_PAR02)+" "+STR0003+" "+DTOC(MV_PAR03),0460,0080) //Periodo del - al
		opReport:PrintText(STR0004+" "+apDados[nlX,1]                                ,0460,1200) //CERTIFICADO No.:

		dbSelectArea("SA2")
		dbSetOrder(1)
		SA2->(dbGoTop())
		SA2->(dbSeek(xFilial("SA2")+PADR(apDados[nlX,3],TamSx3("A2_COD")[1])+PADR(apDados[nlX,4],TamSx3("A2_LOJA")[1])))

		opReport:PrintText(STR0005+" "+SA2->A2_NOME                 	,0580,0080) //RETENIDO :
		opReport:PrintText(STR0006+" "+SA2->A2_CGC                  	,0580,1500) //NIT:
		opReport:PrintText(STR0007+" "+SA2->A2_END+", "+SA2->A2_MUN	,0640,0080) //DIRECCION:

		opReport:PrintText(STR0008,0760,0080) //Concepto
		opReport:PrintText(STR0009,0760,0840) //Valor Base
		opReport:PrintText(STR0010,0760,1190) //Base
		opReport:PrintText(STR0011,0760,1635) //% Aplicado
		opReport:PrintText(STR0012,0760,1950) //Total IVA Retenido

		opReport:Line(0820,0080,0820,0650)
		opReport:Line(0820,0675,0820,1135)
		opReport:Line(0820,1160,0820,1600)
		opReport:Line(0820,1625,0820,1925)
		opReport:Line(0820,1950,0820,2410)

		nlLin := 850

		nlBase   := 0
		nlValRet := 0
		nlValIVA := 0

		clFornec := apDados[nlX,3]
		clLoja   := apDados[nlX,4]
		nlAlq    := apDados[nlX,9]		
		Do While clFornec == apDados[nlX,3] .and. clLoja == apDados[nlX,4]
			If nlAlq == apDados[nlX,9]
				nlBase   += apDados[nlX,8]
				nlValRet += apDados[nlX,12]	
				nlValIVA += apDados[nlX,11] //IVA Operacion	
			Else
				aAdd(alItens,{nlBase,nlAlq,nlValRet})
				nlBase   := apDados[nlX,8]
				nlAlq    := apDados[nlX,9]
				nlValRet := apDados[nlX,12]	  
				nlValIVA := apDados[nlX,11] //IVA Operacion	
			EndIf
			nlX++
			If nlX > Len(apDados)
				Exit
			EndIf
		EndDo
		nlX--

		aAdd(alItens,{nlBase,nlAlq,nlValRet,nlValIVA}) //IVA Operacion	

		nlTotBase := 0
		nlTotIVA  := 0 
		nlTotRet  := 0 
        If cPaisLoc<> "COL"
        	nSinal:= -1
		EndIf
		For nlI := 1 To Len(alItens)		
			opReport:PrintText(STR0013                                           		,nlLin,0080) //RETENCION FUENTE IVA
			opReport:PrintText(Transform((alItens[nlI,1]* nSinal)	,"@E 999,999,999.99")	,nlLin,0760)
			opReport:PrintText(Transform((alItens[nlI,4]* nSinal)	,"@E 999,999,999.99")	,nlLin,1230)  //IVA Operacion	 
			opReport:PrintText(Transform(alItens[nlI,2]			,"@E %999.9999")        ,nlLin,1660)
			opReport:PrintText(Transform((alItens[nlI,3]* nSinal)	,"@E 999,999,999.99")	,nlLin,2050)
			nlTotBase += alItens[nlI,1]
			nlTotRet  += alItens[nlI,3]
			nlTotIVA  += alItens[nlI,4]
			nlLin += 60
		Next nlI

		nlLin += 10
		opReport:Line(nlLin,0675,nlLin,1135)
		opReport:Line(nlLin,1160,nlLin,1600)
		opReport:Line(nlLin,1950,nlLin,2410)

		nlLin += 20
		opReport:PrintText(STR0014                                       	,nlLin,0080) //TOTAL
		opReport:PrintText(Transform((nlTotBase*nSinal)	,"@E 999,999,999.99")	,nlLin,0760)
		opReport:PrintText(Transform((nlTotIVA*nSinal)	,"@E 999,999,999.99")	,nlLin,1230)
		opReport:PrintText(Transform((nlTotRet*nSinal)	,"@E 999,999,999.99") 	,nlLin,2050)

		nlLin += 120
		opReport:PrintText(STR0015,nlLin,0200) //SON:

		nlLin += 80
		nlLinhas := MLCOUNT(AllTrim(Extenso(nlTotRet)), 70, 3, .T.)

		If nlLinhas == 0
			nlLinhas := 1
		EndIf

		For nlI=1 TO nlLinhas 
			opReport:PrintText(MEMOLINE(AllTrim(Extenso(nlTotRet)), 70, nlI, 3, .T.),nlLin,0200)
			nlLin += 60
		Next nlI

		dbSelectArea("SFB")
		dbSetOrder(1)
		SFB->(dbGoTop())
		SFB->(dbSeek(xFilial("SFB")+"RV0"))

		nlLin += 60
		nlLinhas := MLCOUNT(SFB->FB_CERTIF, 80, 3, .T.)
		If nlLinhas == 0
			nlLinhas := 1
		ElseIf nlLinhas > 3
			nlLinhas := 3
		EndIf
		For nlI=1 TO nlLinhas 
			opReport:PrintText(MEMOLINE(SFB->FB_CERTIF, 80, nlI, 3, .T.),nlLin,0100)
			nlLin += 60
		Next nlI

		nlLin += 60
		opReport:PrintText(STR0021,nlLin,0100) //CIUDAD DONDE SE PRACTICO LA RETENCION
			
		nlLin += 60
		opReport:PrintText(AllTrim(SM0->M0_CIDENT)+", "+MesExtenso(Month(dDataBase))+" "+AllTrim(Str(Day(dDataBase)))+" "+STR0018+" "+AllTrim(Str(Year(dDataBase))),nlLin,0100) //de

		nlLin += 420
		opReport:Line(nlLin,0200,nlLin,0820)
		nlLin += 20
		opReport:PrintText(STR0016,nlLin,0360) //Firma y Sello
		nlLin += 180
		opReport:PrintText(MEMOLINE(STR0017, 090, 1, 2, .T.),nlLin,0100) //ESTA CERTIFICACION SE EXPICE SIN FIRMA AUTOGRAFA DE ACUERDO A LO DISPUESTO EN EL ARTICULO 
		nlLin += 60
		opReport:PrintText(MEMOLINE(STR0017, 090, 2, 2, .T.),nlLin,0100) //10 DEL DECRETO 836 DEL 26 DE MARZO DE 1991.
        
		nlLin += 180
		opReport:PrintText(MEMOLINE(STR0022, 090, 1, 2, .T.),nlLin,0100) //Se expide este certificado para dar cumplimiento al Decreto 2300 Art. 23 de Dic1996 y según 
		nlLin += 60
		opReport:PrintText(MEMOLINE(STR0022, 090, 2, 2, .T.),nlLin,0100) //especificacion del Decreto 380 Art. 7 del  27/02/1996.


		opReport:EndPage()

	Next nlX

Return