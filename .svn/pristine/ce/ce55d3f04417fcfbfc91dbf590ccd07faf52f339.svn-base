#INCLUDE "MATR681.CH"
#INCLUDE "Protheus.ch"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MATR681  ³ Autor ³ Marco Bianchi         ³ Data ³ 13/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Informe de Pedidos de Venda/Remitos/Notas Fiscais          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAFAT - Localizacoes - R4                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MATR681()

Local oReport
Private cRetTitle := GetDescRem()

If FindFunction("TRepInUse") .And. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	MATR681R3()
EndIf

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³ Marco Bianchi         ³ Data ³14/06/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local oReport
Local oRelat1
Local oRelat2
Local oRelat3

Local cAliasQry := ""
Local cDoc	    := ""
Local cItem	    := ""
Local dEmissao  := ctod("  /  /  ")
Local nQuant    := 0

Local cCliente  := ""
Local cLoja	    := ""
Local cCod 	    := ""
Local cUM 	    := ""
Local cGrupo    := ""

Local cSerie    := ""
Local cEspecie  := ""
Local nTotal    := 0
Local nTamData  := Len(DTOC(MsDate()))

Private cPerg   := "MTR681"

#IFNDEF TOP
	cAliasQry := "SC5"			// Baseado no Pedido de Vendas
#ELSE	
	cAliasQry := GetNextAlias()
#ENDIF


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MATR681",STR0014,"MTR681", {|oReport| ReportPrint(oReport,oRelat1,oRelat2,oRelat3,cAliasQry)},STR0014)
oReport:SetLandscape() 
oReport:SetTotalInLine(.F.)

Pergunte(oReport:uParam,.F.)


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 1 : PV/RM/FT                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oRelat1 := TRSection():New(oReport,STR0025,{"SC5","SC6","SD2"},{STR0017,STR0018},/*Campos do SX3*/,/*Campos do SIX*/)
oRelat1:SetTotalInLine(.F.)
oRelat1:SetLineBreak()

// Informacoes do Pedido de Venda
TRCell():New(oRelat1,"C5_EMISSAOE"	,/*Tabela*/	,RetTitle("C5_EMISSAO"	),PesqPict("SC5","C5_EMISSAO"),nTamData,/*lPixel*/,{|| (cAliasQry)->C5_EMISSAO })
TRCell():New(oRelat1,"C5_CLIENTEE"	,/*Tabela*/	,RetTitle("C5_CLIENTE"	),PesqPict("SC5","C5_CLIENTE"),TamSX3("C5_CLIENTE")[1],/*lPixel*/,{|| (cAliasQry)->C5_CLIENTE })
TRCell():New(oRelat1,"C5_LOJACLIE"	,/*Tabela*/	,RetTitle("C5_LOJACLI"	),PesqPict("SC5","C5_LOJACLI"),TamSX3("C5_LOJACLI")[1],/*lPixel*/,{|| (cAliasQry)->C5_LOJACLI })
TRCell():New(oRelat1,"C5_CLIENTEC"	,/*Tabela*/	,RetTitle("C5_CLIENTE"	),PesqPict("SC5","C5_CLIENTE"),TamSX3("C5_CLIENTE")[1],/*lPixel*/,{|| (cAliasQry)->C5_CLIENTE })
TRCell():New(oRelat1,"C5_LOJACLIC"	,/*Tabela*/	,RetTitle("C5_LOJACLI"	),PesqPict("SC5","C5_LOJACLI"),TamSX3("C5_LOJACLI")[1],/*lPixel*/,{|| (cAliasQry)->C5_LOJACLI })
TRCell():New(oRelat1,"C5_EMISSAOC"	,/*Tabela*/	,RetTitle("C5_EMISSAO"	),PesqPict("SC5","C5_EMISSAO"),nTamData,/*lPixel*/,{|| (cAliasQry)->C5_EMISSAO })
TRCell():New(oRelat1,"C6_NUM"		,"SC6"		,RetTitle("C6_NUM"		),PesqPict("SC6","C6_NUM"		),TamSX3("C6_NUM"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"C6_ITEM"		,"SC6"		,Substr(RetTitle("C6_ITEM"),1,2),PesqPict("SC6","C6_ITEM"		),TamSX3("C6_ITEM"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"C6_PRODUTO"	,"SC6"		,RetTitle("C6_PRODUTO"	),PesqPict("SC6","C6_PRODUTO"	),TamSX3("C6_PRODUTO"	)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"C6_UM"		,"SC6"		,RetTitle("C6_UM"		),PesqPict("SC6","C6_UM"		),TamSX3("C6_UM"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"B1_GRUPO"		,"SB1"		,RetTitle("B1_GRUPO"	),PesqPict("SB1","B1_GRUPO"		),TamSX3("B1_GRUPO"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"C6_QTDVEN"	,"SC6"		,RetTitle("C6_QTDVEN"	),PesqPict("SC6","C6_QTDVEN"	),TamSX3("C6_QTDVEN"	)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat1,"C6_VALOR"		,"SC6"		,RetTitle("C6_VALOR"	),PesqPict("SC6","C6_VALOR"		),TamSX3("C6_VALOR"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat1,"C6_ENTREG"	,"SC6"		,RetTitle("C6_ENTREG"	),PesqPict("SC6","C6_ENTREG"	),nTamData,/*lPixel*/,/*{|| code-block de impressao }*/)
// Informacoes do Remito
If cPaisLoc == "VEN"
	TRCell():New(oRelat1,"DOC"		,"SD2",STR0024					 ,PesqPict("SD2","D2_DOC"		),TamSX3("D2_DOC"	)[1],/*lPixel*/,{|| cDoc 		})//Fatura
Else
	TRCell():New(oRelat1,"DOC"		,"SD2",cRetTitle				 ,PesqPict("SD2","D2_DOC"		),TamSX3("D2_DOC"	)[1],/*lPixel*/,{|| cDoc 		})//Remito
EndIf
TRCell():New(oRelat1,"ITEM"		,"SD2",Substr(RetTitle("D2_ITEM"),1,2),PesqPict("SD2","D2_ITEM"		),TamSX3("D2_ITEM"	)[1],/*lPixel*/,{|| cItem 		})
TRCell():New(oRelat1,"EMISSAO"	,"SD2",RetTitle("D2_EMISSAO"	),PesqPict("SD2","D2_EMISSAO"	),nTamData				,/*lPixel*/,{|| dEmissao 	})
TRCell():New(oRelat1,"QUANT"	,"SD2",RetTitle("D2_QUANT"		),PesqPict("SD2","D2_QUANT"		),TamSX3("D2_QUANT"	)[1],/*lPixel*/,{|| nQuant 		},,,"RIGHT")
// Informacoes da Fatura
TRCell():New(oRelat1,"D2_SERIE"		,"SD2",Substr(RetTitle("D2_SERIE"),1,3),PesqPict("SD2","D2_SERIE"		),TamSX3("D2_SERIE"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"D2_DOC"		,"SD2",STR0024					 ,PesqPict("SD2","D2_DOC"		),TamSX3("D2_DOC"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"D2_ITEM"		,"SD2",Substr(RetTitle("D2_ITEM"),1,2),PesqPict("SD2","D2_ITEM"		),TamSX3("D2_ITEM"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"D2_EMISSAO"	,"SD2",RetTitle("D2_EMISSAO"	),PesqPict("SD2","D2_EMISSAO"	),nTamData					,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat1,"D2_QUANT"		,"SD2",RetTitle("D2_QUANT"		),PesqPict("SD2","D2_QUANT"		),TamSX3("D2_QUANT"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat1,"D2_TOTAL"		,"SD2",RetTitle("D2_TOTAL"		),PesqPict("SD2","D2_TOTAL"		),TamSX3("D2_TOTAL"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 2 : RM/PV/FT                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oRelat2 := TRSection():New(oReport,STR0026,{"SC5","SC6","SD2"},{STR0017,STR0018},/*Campos do SX3*/,/*Campos do SIX*/)
oRelat2:SetTotalInLine(.F.)
oRelat2:SetLineBreak()

// Informacoes do Remito
TRCell():New(oRelat2,"D2_EMISSAOE"	,/*Tabela*/,RetTitle("D2_EMISSAO")	,PesqPict("SD2","D2_EMISSAO"),nTamData,/*lPixel*/,{||dEmissao})
TRCell():New(oRelat2,"D2_CLIENTEE"	,/*Tabela*/,RetTitle("D2_CLIENTE")	,PesqPict("SD2","D2_CLIENTE"),TamSX3("D2_CLIENTE"	)[1],/*lPixel*/,{||cCliente})
TRCell():New(oRelat2,"D2_LOJAE"		,/*Tabela*/,RetTitle("D2_LOJA"   )	,PesqPict("SD2","D2_LOJA"   ),TamSX3("D2_LOJA"		)[1],/*lPixel*/,{||cLoja	})
TRCell():New(oRelat2,"D2_CLIENTEC"	,/*Tabela*/,RetTitle("D2_CLIENTE")	,PesqPict("SD2","D2_CLIENTE"),TamSX3("D2_CLIENTE"	)[1],/*lPixel*/,{||cCliente})
TRCell():New(oRelat2,"D2_LOJAC"	  	,/*Tabela*/,RetTitle("D2_LOJA"   )	,PesqPict("SD2","D2_LOJA"   ),TamSX3("D2_LOJA"		)[1],/*lPixel*/,{||cLoja	})
TRCell():New(oRelat2,"D2_EMISSAOC"	,/*Tabela*/,RetTitle("D2_EMISSAO")	,PesqPict("SD2","D2_EMISSAO"),nTamData					,/*lPixel*/,{||dEmissao})
If cPaisLoc == "VEN"
	TRCell():New(oRelat2,"D2_DOC"  		,/*Tabela*/,STR0024					,PesqPict("SD2","D2_DOC"	),TamSX3("D2_DOC"		)[1],/*lPixel*/,{|| cDoc 	})//Fatura
Else
	TRCell():New(oRelat2,"D2_DOC"  		,/*Tabela*/,cRetTitle				,PesqPict("SD2","D2_DOC"	),TamSX3("D2_DOC"		)[1],/*lPixel*/,{|| cDoc 	})//Remito
EndIf
TRCell():New(oRelat2,"D2_ITEM"		,/*Tabela*/,Substr(RetTitle("D2_ITEM"),1,2),PesqPict("SD2","D2_ITEM"	),TamSX3("D2_ITEM"		)[1],/*lPixel*/,{|| cItem 	})
TRCell():New(oRelat2,"D2_COD"		,/*Tabela*/,RetTitle("D2_COD"	)	,PesqPict("SD2","D2_COD"	),TamSX3("D2_COD"		)[1],/*lPixel*/,{|| cCod 	})
TRCell():New(oRelat2,"D2_UM"		,/*Tabela*/,RetTitle("D2_UM"	)	,PesqPict("SD2","D2_UM"		),TamSX3("D2_UM"		)[1],/*lPixel*/,{|| cUM 	})
TRCell():New(oRelat2,"D2_GRUPO"		,/*Tabela*/,RetTitle("D2_GRUPO"	)	,PesqPict("SD2","D2_GRUPO"	),TamSX3("D2_GRUPO"		)[1],/*lPixel*/,{|| cGrupo })
TRCell():New(oRelat2,"D2_QUANT"		,/*Tabela*/,RetTitle("D2_QUANT"	)	,PesqPict("SD2","D2_QUANT"	),TamSX3("D2_QUANT"		)[1],/*lPixel*/,{|| nQuant },,,"RIGHT")

// Dados do Pedido de Vendas
TRCell():New(oRelat2,"C6_NUM"		,"SC6",RetTitle("C6_NUM"	),PesqPict("SC6","C6_NUM"		),TamSX3("C6_NUM"	)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat2,"C6_ITEM"		,"SC6",Substr(RetTitle("C6_ITEM"),1,2),PesqPict("SC6","C6_ITEM"		),TamSX3("C6_ITEM"	)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat2,"C5_EMISSAO"	,"SC5",RetTitle("C5_EMISSAO"),PesqPict("SC5","C5_EMISSAO"	),nTamData				,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat2,"C6_QTDVEN"	,"SC6",RetTitle("C6_QTDVEN"	),PesqPict("SC6","C6_QTDVEN"	),TamSX3("C6_QTDVEN")[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat2,"C6_VALOR"		,"SC6",RetTitle("C6_VALOR"	),PesqPict("SC6","C6_VALOR"		),TamSX3("C6_VALOR"	)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat2,"C6_ENTREG"	,"SC6",RetTitle("C6_ENTREG"	),PesqPict("SC6","C6_ENTREG"	),nTamData				,/*lPixel*/,/*{|| code-block de impressao }*/)

// Informacoes da Fatura
TRCell():New(oRelat2,"SERIE"	,"SD2",Substr(RetTitle("D2_SERIE"),1,3),PesqPict("SD2","D2_SERIE"	  ),TamSX3("D2_SERIE"	)[1],/*lPixel*/,{|| SD2->D2_SERIE 		})
TRCell():New(oRelat2,"DOC"		,"SD2",STR0024				 ,PesqPict("SD2","D2_DOC"	  ),TamSX3("D2_DOC"		)[1],/*lPixel*/,{|| SD2->D2_DOC 		})
TRCell():New(oRelat2,"ITEM"		,"SD2",Substr(RetTitle("D2_ITEM"),1,2),PesqPict("SD2","D2_ITEM"	  ),TamSX3("D2_ITEM"	)[1],/*lPixel*/,{|| SD2->D2_ITEM 		})
TRCell():New(oRelat2,"EMISSAO"	,"SD2",RetTitle("D2_EMISSAO"),PesqPict("SD2","D2_EMISSAO"),nTamData				,/*lPixel*/,{|| SD2->D2_EMISSAO	})
TRCell():New(oRelat2,"QUANT"	,"SD2",RetTitle("D2_QUANT"	),PesqPict("SD2","D2_QUANT"	  ),TamSX3("D2_QUANT"	)[1],/*lPixel*/,{|| SD2->D2_QUANT 		},,,"RIGHT")
TRCell():New(oRelat2,"TOTAL"	,"SD2",RetTitle("D2_TOTAL"	),PesqPict("SD2","D2_TOTAL"	  ),TamSX3("D2_TOTAL"	)[1],/*lPixel*/,{|| SD2->D2_TOTAL 		},,,"RIGHT")


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 3 : FT/RM/PV                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oRelat3 := TRSection():New(oReport,STR0027,{"SC5","SC6","SD2"},{STR0017,STR0018},/*Campos do SX3*/,/*Campos do SIX*/)
oRelat3:SetTotalInLine(.F.)
oRelat3:SetLineBreak()

// Informacoes da Fatura
TRCell():New(oRelat3,"D2_EMISSAOE"	,/*Tabela*/,RetTitle("D2_EMISSAO"	),PesqPict("SD2","D2_EMISSAO"	),nTamData,/*lPixel*/,{||dEmissao})
TRCell():New(oRelat3,"D2_CLIENTEE"	,/*Tabela*/,RetTitle("D2_CLIENTE"	),PesqPict("SD2","D2_CLIENTE"	),TamSX3("D2_CLIENTE"	)[1],/*lPixel*/,{||cCliente})
TRCell():New(oRelat3,"D2_LOJAE"		,/*Tabela*/,RetTitle("D2_LOJA"   	),PesqPict("SD2","D2_LOJA"   	),TamSX3("D2_LOJA"		)[1],/*lPixel*/,{||cLoja	})
TRCell():New(oRelat3,"D2_CLIENTEC"	,/*Tabela*/,RetTitle("D2_CLIENTE"	),PesqPict("SD2","D2_CLIENTE"	),TamSX3("D2_CLIENTE"	)[1],/*lPixel*/,{||cCliente})
TRCell():New(oRelat3,"D2_LOJAC"	  	,/*Tabela*/,RetTitle("D2_LOJA"   	),PesqPict("SD2","D2_LOJA"   	),TamSX3("D2_LOJA"		)[1],/*lPixel*/,{||cLoja	})
TRCell():New(oRelat3,"D2_EMISSAOC"	,/*Tabela*/,RetTitle("D2_EMISSAO"	),PesqPict("SD2","D2_EMISSAO"	),nTamData					,/*lPixel*/,{||dEmissao})
TRCell():New(oRelat3,"D2_SERIE"		,/*Tabela*/,Substr(RetTitle("D2_SERIE"),1,3),PesqPict("SD2","D2_SERIE"		),TamSX3("D2_SERIE"		)[1],/*lPixel*/,{|| cSerie 		})
TRCell():New(oRelat3,"D2_DOC"		,/*Tabela*/,STR0024					 ,PesqPict("SD2","D2_DOC"		),TamSX3("D2_DOC"		)[1],/*lPixel*/,{|| cDoc 		},,,,,,.T.)
TRCell():New(oRelat3,"D2_ESPECIE"	,/*Tabela*/,RetTitle("D2_ESPECIE"	),PesqPict("SD2","D2_ESPECIE"	),TamSX3("D2_ESPECIE"	)[1],/*lPixel*/,{|| cEspecie	})
TRCell():New(oRelat3,"D2_ITEM"		,/*Tabela*/,Substr(RetTitle("D2_ITEM"),1,2),PesqPict("SD2","D2_ITEM"		),TamSX3("D2_ITEM"		)[1],/*lPixel*/,{|| cItem 		})
TRCell():New(oRelat3,"D2_COD"		,/*Tabela*/,RetTitle("D2_COD"		),PesqPict("SD2","D2_COD"		),TamSX3("D2_COD"		)[1],/*lPixel*/,{|| cCod 		})
TRCell():New(oRelat3,"D2_UM"		,/*Tabela*/,RetTitle("D2_UM"		),PesqPict("SD2","D2_UM"		),TamSX3("D2_UM"		)[1],/*lPixel*/,{|| cUM 		})
TRCell():New(oRelat3,"D2_GRUPO"		,/*Tabela*/,RetTitle("D2_GRUPO"		),PesqPict("SD2","D2_GRUPO"		),TamSX3("D2_GRUPO"		)[1],/*lPixel*/,{|| cGrupo 		})
TRCell():New(oRelat3,"D2_QUANT"		,/*Tabela*/,RetTitle("D2_QUANT"		),PesqPict("SD2","D2_QUANT"		),TamSX3("D2_QUANT"		)[1],/*lPixel*/,{|| nQuant 		},,,"RIGHT")
TRCell():New(oRelat3,"D2_TOTAL"		,/*Tabela*/,RetTitle("D2_TOTAL"		),PesqPict("SD2","D2_TOTAL"		),TamSX3("D2_TOTAL"		)[1],/*lPixel*/,{|| nTotal 		},,,"RIGHT")
// Informacoes do Remito
If cPaisLoc == "VEN"
	TRCell():New(oRelat3,"DOC"			,"SD2",STR0024					 ,PesqPict("SD2","D2_DOC"		),TamSX3("D2_DOC"		)[1],/*lPixel*/,{|| SD2->D2_DOC 		},,,,,,.T.)//Fatura
Else
	TRCell():New(oRelat3,"DOC"			,"SD2",cRetTitle				 ,PesqPict("SD2","D2_DOC"		),TamSX3("D2_DOC"		)[1],/*lPixel*/,{|| SD2->D2_DOC 		},,,,,,.T.)//Remito
EndIf
TRCell():New(oRelat3,"ITEM"			,"SD2",Substr(RetTitle("D2_ITEM"),1,2),PesqPict("SD2","D2_ITEM"		),TamSX3("D2_ITEM"		)[1],/*lPixel*/,{|| SD2->D2_ITEM 		})
TRCell():New(oRelat3,"EMISSAO"		,"SD2",RetTitle("D2_EMISSAO"	),PesqPict("SD2","D2_EMISSAO"	),nTamData					,/*lPixel*/,{|| SD2->D2_EMISSAO	})
TRCell():New(oRelat3,"QUANT"		,"SD2",RetTitle("D2_QUANT"		),PesqPict("SD2","D2_QUANT"		),TamSX3("D2_QUANT"		)[1],/*lPixel*/,{|| SD2->D2_QUANT 		},,,"RIGHT")

// Dados do Pedido de Vendas
TRCell():New(oRelat3,"C6_NUM"		,"SC6",RetTitle("C6_NUM"		),PesqPict("SC6","C6_NUM"		),TamSX3("C6_NUM"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat3,"C6_ITEM"		,"SC6",Substr(RetTitle("C6_ITEM"),1,2),PesqPict("SC6","C6_ITEM"		),TamSX3("C6_ITEM"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat3,"C5_EMISSAO"	,"SC5",RetTitle("C5_EMISSAO"	),PesqPict("SC5","C5_EMISSAO"	),nTamData					,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oRelat3,"C6_QTDVEN"	,"SC6",RetTitle("C6_QTDVEN"		),PesqPict("SC6","C6_QTDVEN"	),TamSX3("C6_QTDVEN"	)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat3,"C6_VALOR"		,"SC6",RetTitle("C6_VALOR"		),PesqPict("SC6","C6_VALOR"		),TamSX3("C6_VALOR"		)[1],/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oRelat3,"C6_ENTREG"	,"SC6",RetTitle("C6_ENTREG"		),PesqPict("SC6","C6_ENTREG"	),nTamData					,/*lPixel*/,/*{|| code-block de impressao }*/)



Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³ Marco Bianchi         ³ Data ³20/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,oRelat1,oRelat2,oRelat3,cAliasQry)

#IFNDEF TOP
	If mv_par01 == 1
		cAliasQry := "SC5"			// Baseado no Pedido de Vendas
	Else
		cAliasQry := "SD2"			// Baseado no Remito ou Fatura
	EndIf	
#ENDIF


If mv_par01 = 1        // PV/RM/FT
	ImpPRF(oReport,oRelat1,cAliasQry)
ElseIf mv_PAR01 = 2    // RM/PV/FT
  	ImpRPF(oReport,oRelat2,cAliasQry)
Else				    // FT/RM/PV
	ImpFRP(oReport,oRelat3,cAliasQry)
EndIf

Return



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ImpPRF   ³ Autor ³ Marco Bianchi         ³ Data ³ 20/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao por Pedido/Remito/Fatura		                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681 - R4                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpPRF(oReport,oRelat1,cAliasQry)
       

Local cChPed    := ""
Local lImpr		:= .T.
Local cChFat	:= ""
Local cWhere	:= ""	
Local dEmiss    := CTOD("  /  /  ")
Local cPedido   := ""
Local cCli  	:= ""
Local cLojaCli 	:= ""
Local cOrder    := ""

Local cDoc		:= ""
Local cItem		:= ""
Local dEmissao	:= CTOD("  /  /  ")
Local nQuant	:= 0


#IFNDEF TOP
	Local cCondicao := ""
#ENDIF

oReport:Section(1):Cell("DOC" 		):SetBlock({|| cDoc		})
oReport:Section(1):Cell("ITEM" 		):SetBlock({|| cItem	})
oReport:Section(1):Cell("EMISSAO"	):SetBlock({|| dEmissao	})
oReport:Section(1):Cell("QUANT" 	):SetBlock({|| nQuant	})
cDoc	:= ""
cItem	:= ""
dEmissao:= ctod("  /  /  ")
nQuant	:= 0

oReport:Section(2):disable()
oReport:Section(3):disable()
                                                                                                                        
If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
	oReport:Section(1):Cell("C5_EMISSAOC"):Disable()
	oReport:Section(1):Cell("C5_CLIENTEC"):Disable()
	oReport:Section(1):Cell("C5_LOJACLIC"):Disable()
Else										// Ordem: por Cliente
	oReport:Section(1):Cell("C5_EMISSAOE"):Disable()
	oReport:Section(1):Cell("C5_CLIENTEE"):Disable()
	oReport:Section(1):Cell("C5_LOJACLIE"):Disable()
EndIf

oReport:SetTitle(STR0013 + STR0015 + "/" + cRetTitle + "/" + STR0024) //"Relacao de pedidos de venda/remisiones/faturas

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Transforma parametros Range em expressao SQL                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MakeSqlExpr(oReport:uParam)
#IFDEF TOP  
	
	dbSelectArea("SC5")
	If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
		dbSetOrder(2)   
		cOrder := "%C5_EMISSAO,C5_CLIENTE,C5_LOJACLI,C5_NUM%"
	Else										// Ordem: por Cliente
		dbSetOrder(3)
		cOrder := "%C5_CLIENTE,C5_LOJACLI,C5_EMISSAO,C5_NUM%"
	EndIf	

	cWhere := "%"
	If !Empty(mv_par02)
		cWhere +=" AND C5_CLIENTE >= '" + %Exp:mv_par02% + "'"
	Endif
	If !Empty(mv_par03)
		cWhere+=" AND C5_CLIENTE <='" + %Exp:mv_par03% + "'"
	Endif
	If !Empty(mv_par08)
	    cWhere+=" AND C5_EMISSAO >= '" + %Exp:DTOS(mv_par08)% + "'"
	Endif
	If !Empty(mv_par09)
		cWhere+=" AND C5_EMISSAO <= '" + %Exp:DTOS(mv_par09)% + "'"
	Endif        
	cWhere += "%"

	oReport:Section(1):BeginQuery()	
	BeginSql Alias cAliasQry

	SELECT C5_FILIAL,C5_NUM,C5_EMISSAO,C5_CLIENTE,C5_LOJACLI
	FROM %Table:SC5% SC5
	WHERE C5_FILIAL = %xFilial:SC5% AND SC5.%notdel%
	%Exp:cWhere%
	ORDER BY %Exp:cOrder%
	EndSql       
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
		
#ELSE

	dbSelectArea("SC5")
	If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
		dbSetOrder(2)   
		cOrder:="C5_FILIAL+DTOS(C5_EMISSAO)+C5_CLIENTE+C5_LOJACLI+C5_NUM"
	Else										// Ordem: por Cliente
		dbSetOrder(3)   
   	    cOrder:= "C5_FILIAL+C5_CLIENTE+DTOS(C5_EMISSAO)+C5_NUM"
	EndIf	
		
	cCondicao:="C5_FILIAL=='"+xFilial("SC5")+"'" 
	If !Empty(mv_par02)
		cCondicao+=" .AND. C5_CLIENTE >='"+mv_par02+"'"
	Endif
	If !Empty(mv_par03)
		cCondicao+=" .AND. C5_CLIENTE <='"+mv_par03+"'"
	Endif
	If !Empty(mv_par08)
	    cCondicao+=" .AND. DTOS(C5_EMISSAO)>='"+DTOS(mv_par08)+"'"
	Endif
	If !Empty(mv_par09)
		cCondicao+=" .AND. DTOS(C5_EMISSAO)<='"+DTOS(mv_par09)+"'"
	Endif       
	cArqTrab := CriaTrab(Nil,.F.)
	IndRegua("SC5",cArqTrab,cOrder,,cCondicao,OemToAnsi(STR0019))  //"Selecionando pedidos"
	
#ENDIF
	

SC6->(DbSetOrder(1))
dbSelectArea(cAliasQry)
dbGoTop()
oReport:Section(1):Init()
oReport:SetMeter((cAliasQry)->(LastRec()))       
                       
dEmiss		:= CTOD("  /  /  ")
cCli 		:= ""
cLojaCli	:= ""
While !oReport:Cancel() .And. !(cAliasQry)->(Eof())
	
	DbSelectArea("SC6")
	cChPed:=xFilial("SC6")+(cAliasQry)->C5_NUM
	
	// Pesquisa SC6: Itens do Pedido de Vendas
	If DbSeek(cChPed)
		lImpr   := .T.
		cPedido := ""
		
		While !Eof() .and. C6_FILIAL+C6_NUM==cChPed
			If(lImpr:=(SC6->C6_PRODUTO>=mv_par04))
				If !Empty(mv_par05)
					lImpr:=(SC6->C6_PRODUTO<=mv_par05)
				Endif
			Endif
			If lImpr
				If !Empty(mv_par06) .Or. !Empty(mv_par07)
					If (SB1->(MsSeek(xFilial("SB1")+SC6->C6_PRODUTO)))
						If (lImpr:=SB1->B1_GRUPO>=mv_par06)
							If !Empty(mv_par07)
								lImpr:=SB1->B1_GRUPO<=mv_par07
							Endif
						Endif
					Endif
				Endif
			Endif
			If lImpr
				cChRem:=xFilial("SD2")+SC6->C6_NUM+SC6->C6_ITEM
				// Pesquisa Remito (SD2) a partir do Pedido de Vendas
				DbSelectArea("SD2")		// Itens da Nota de Saida
				dbSetOrder(8)			// Filial, Pedido de Vendas, Item do Pedido
				
				If DbSeek(cChRem)
					nRem:=0
					While !Eof() .and. D2_FILIAL+D2_PEDIDO+D2_ITEMPV==cChRem  //impressao das remisiones
						If D2_ITEMPV==SC6->C6_ITEM  .and. IsRemito(1,"SD2->D2_TIPODOC") .and. D2_COD==SC6->C6_PRODUTO
							
							cChFat		:= xFilial("SD2")+SD2->D2_CLIENTE+SD2->D2_LOJA+SD2->D2_SERIE+SD2->D2_DOC+SD2->D2_ITEM
							nReg 		:= Recno()
							cDoc 		:= ""
							cItem 		:= ""
							dEmissao 	:= CTOD("  /  /  ")
							nQuant		:= 0
							cDoc 		:= D2_DOC
							cItem 		:= D2_ITEM
							dEmissao 	:= D2_EMISSAO
							nQuant		:= D2_QUANT
							
							// Pesquisa Fatura (SD2) a partir do Remito (SD2)
							DbSelectArea("SD2")			// Itens da Nota de Saida
							DbSetOrder(9)				// Filial, Cliente, Loja, Serie Remito, Doc Remito, Item Remito
							If DbSeek(cChFat)
								
								// Informacoes do Pedido
								If oReport:Section(1):GetOrder() == 1			// Ordem: por Emissao
									If dEmiss <> (cAliasQry)->C5_EMISSAO
										oReport:Section(1):Cell("C5_EMISSAOE"	):Show()
									Else
										oReport:Section(1):Cell("C5_EMISSAOE"	):Hide()
									EndIf
									If  cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI;
										.And. cPedido == SC6->C6_NUM
										oReport:Section(1):Cell("C5_CLIENTEE"	):Hide()
										oReport:Section(1):Cell("C5_LOJACLIE"	):Hide()
										oReport:Section(1):Cell("C6_NUM"		):Hide()
									Else
										oReport:Section(1):Cell("C5_CLIENTEE"	):Show()
										oReport:Section(1):Cell("C5_LOJACLIE"	):Show()
										oReport:Section(1):Cell("C6_NUM"		):Show()
									EndIf
								Else        									// Ordem: por Cliente
									If cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI
										oReport:Section(1):Cell("C5_CLIENTEC"	):Hide()
										oReport:Section(1):Cell("C5_LOJACLIC"	):Hide()
									Else
										oReport:Section(1):Cell("C5_CLIENTEC"	):Show()
										oReport:Section(1):Cell("C5_LOJACLIC"	):Show()
									EndIf
									If dEmiss == (cAliasQry)->C5_EMISSAO .And. cPedido == SC6->C6_NUM
										oReport:Section(1):Cell("C6_NUM"		):Hide()
										oReport:Section(1):Cell("C5_EMISSAOC"	):Hide()
									Else
										oReport:Section(1):Cell("C6_NUM"		):Show()
										oReport:Section(1):Cell("C5_EMISSAOC"	):Show()
									EndIf
								EndIf
								
								oReport:Section(1):Cell("C6_ITEM"		):Show()
								oReport:Section(1):Cell("C6_PRODUTO"	):Show()
								oReport:Section(1):Cell("C6_UM"	   		):Show()
								oReport:Section(1):Cell("B1_GRUPO"		):Show()
								oReport:Section(1):Cell("C6_QTDVEN"		):Show()
								oReport:Section(1):Cell("C6_VALOR"		):Show()
								oReport:Section(1):Cell("C6_ENTREG"		):Show()
								// Informacoes do remito
								oReport:Section(1):Cell("DOC" 			):Show()
								oReport:Section(1):Cell("ITEM"			):Show()
								oReport:Section(1):Cell("EMISSAO"		):Show()
								oReport:Section(1):Cell("QUANT"			):Show()
								// Informacoes da Fatura
								oReport:Section(1):Cell("D2_SERIE"		):Show()
								oReport:Section(1):Cell("D2_DOC"		):Show()
								oReport:Section(1):Cell("D2_ITEM"		):Show()
								oReport:Section(1):Cell("D2_EMISSAO"	):Show()
								oReport:Section(1):Cell("D2_QUANT"		):Show()
								oReport:Section(1):Cell("D2_TOTAL"		):Show()
								
								lFirst := .T.
								While !Eof() .and. (D2_FILIAL+D2_CLIENTE+D2_LOJA+D2_SERIREM+D2_REMITO+D2_ITEMREM)==cChFat
									If !IsRemito(1,"SD2->D2_TIPODOC")
										
										// Imprime Pedido/Remite/Fatura
										If !lFirst
										
											// Informacoes do Pedido
											If oReport:Section(1):GetOrder() == 1			// Ordem: por Emissao
											
												If dEmiss <> (cAliasQry)->C5_EMISSAO
													oReport:Section(1):Cell("C5_EMISSAOE"	):Show()
												Else
													oReport:Section(1):Cell("C5_EMISSAOE"	):Hide()
												EndIf
												If  cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI;
													.And. cPedido == SC6->C6_NUM
													oReport:Section(1):Cell("C5_CLIENTEE"	):Hide()
													oReport:Section(1):Cell("C5_LOJACLIE"	):Hide()
													oReport:Section(1):Cell("C6_NUM"		):Hide()
												Else
													oReport:Section(1):Cell("C5_CLIENTEE"	):Show()
													oReport:Section(1):Cell("C5_LOJACLIE"	):Show()
													oReport:Section(1):Cell("C6_NUM"		):Show()
												EndIf
											Else											// Ordem: por Cliente
												If cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI
													oReport:Section(1):Cell("C5_CLIENTEC"	):Hide()
													oReport:Section(1):Cell("C5_LOJACLIC"	):Hide()
												Else
													oReport:Section(1):Cell("C5_CLIENTEC"	):Show()
													oReport:Section(1):Cell("C5_LOJACLIC"	):Show()
												EndIf
												If dEmiss == (cAliasQry)->C5_EMISSAO .And. cPedido == SC6->C6_NUM
													oReport:Section(1):Cell("C6_NUM"		):Hide()
													oReport:Section(1):Cell("C5_EMISSAOC"	):Hide()
												Else
													oReport:Section(1):Cell("C6_NUM"		):Show()
													oReport:Section(1):Cell("C5_EMISSAOC"	):Show()
												EndIf
											EndIf
											
											// Informacoes do Pedido
											oReport:Section(1):Cell("C6_ITEM"		):Hide()
											oReport:Section(1):Cell("C6_PRODUTO"	):Hide()
											oReport:Section(1):Cell("C6_UM"	   		):Hide()
											oReport:Section(1):Cell("B1_GRUPO"		):Hide()
											oReport:Section(1):Cell("C6_QTDVEN"		):Hide()
											oReport:Section(1):Cell("C6_VALOR"		):Hide()
											oReport:Section(1):Cell("C6_ENTREG"		):Hide()
											// Informacoes do remito
											oReport:Section(1):Cell("DOC" 			):Hide()
											oReport:Section(1):Cell("ITEM"			):Hide()
											oReport:Section(1):Cell("EMISSAO"		):Hide()
											oReport:Section(1):Cell("QUANT"			):Hide()
										EndIf
										
										oReport:Section(1):PrintLine()
										lFirst := .F.
										
									Endif
									DbSkip()
								Enddo
								
							Else
								
								// Imprime Pedido e Remite (Fatura nao encontrada a partir do Remito)
								If oReport:Section(1):GetOrder() == 1			// Ordem: por Emissao
									// Informacoes do Pedido
									If dEmiss <> (cAliasQry)->C5_EMISSAO
										oReport:Section(1):Cell("C5_EMISSAOE"	):Show()
									Else
										oReport:Section(1):Cell("C5_EMISSAOE"	):Hide()
									EndIf
									
									If  cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI;
										.And. cPedido == SC6->C6_NUM
										oReport:Section(1):Cell("C5_CLIENTEE"	):Hide()
										oReport:Section(1):Cell("C5_LOJACLIE"	):Hide()
										oReport:Section(1):Cell("C6_NUM"		):Hide()
									Else
										oReport:Section(1):Cell("C5_CLIENTEE"	):Show()
										oReport:Section(1):Cell("C5_LOJACLIE"	):Show()
										oReport:Section(1):Cell("C6_NUM"		):Show()
									EndIf
								Else        									// Ordem: por Cliente
									If cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI
										oReport:Section(1):Cell("C5_CLIENTEC"	):Hide()
										oReport:Section(1):Cell("C5_LOJACLIC"	):Hide()
									Else
										oReport:Section(1):Cell("C5_CLIENTEC"	):Show()
										oReport:Section(1):Cell("C5_LOJACLIC"	):Show()
									EndIf
									If dEmiss == (cAliasQry)->C5_EMISSAO .And. cPedido == SC6->C6_NUM
										oReport:Section(1):Cell("C6_NUM"		):Hide()
										oReport:Section(1):Cell("C5_EMISSAOC"	):Hide()
									Else
										oReport:Section(1):Cell("C6_NUM"		):Show()
										oReport:Section(1):Cell("C5_EMISSAOC"	):Show()
									EndIf
								EndIf
								
								oReport:Section(1):Cell("C6_ITEM"	 	):Show()
								oReport:Section(1):Cell("C6_PRODUTO"	):Show()
								oReport:Section(1):Cell("C6_UM"	   		):Show()
								oReport:Section(1):Cell("B1_GRUPO"		):Show()
								oReport:Section(1):Cell("C6_QTDVEN"		):Show()
								oReport:Section(1):Cell("C6_VALOR"		):Show()
								oReport:Section(1):Cell("C6_ENTREG"		):Show()
								// Informacoes do remito
								oReport:Section(1):Cell("DOC"			):Show()
								oReport:Section(1):Cell("ITEM"			):Show()
								oReport:Section(1):Cell("EMISSAO"		):Show()
								oReport:Section(1):Cell("QUANT"			):Show()
								// Informacoes da Fatura
								oReport:Section(1):Cell("D2_SERIE"		):Hide()
								oReport:Section(1):Cell("D2_DOC"		):Hide()
								oReport:Section(1):Cell("D2_ITEM"		):Hide()
								oReport:Section(1):Cell("D2_EMISSAO"	):Hide()
								oReport:Section(1):Cell("D2_QUANT"		):Hide()
								oReport:Section(1):Cell("D2_TOTAL"		):Hide()
								
								oReport:Section(1):PrintLine()
								
							Endif
							
							DbSelectArea("SD2")
							DbGoTo(nReg)
							DbSetOrder(8)
							
						Endif
						DbSkip()
					Enddo
					
				Else
					
					// Imprime Pedido
					// Informacoes do Pedido
					If oReport:Section(1):GetOrder() == 1			// Ordem: por Emissao
						If dEmiss <> (cAliasQry)->C5_EMISSAO
							oReport:Section(1):Cell("C5_EMISSAOE"	):Show()
						Else
							oReport:Section(1):Cell("C5_EMISSAOE"	):Hide()
						EndIf
						
						If  cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI;
							.And. cPedido == SC6->C6_NUM
							oReport:Section(1):Cell("C5_CLIENTEE"	):Hide()
							oReport:Section(1):Cell("C5_LOJACLIE"	):Hide()
							oReport:Section(1):Cell("C6_NUM"		):Hide()
						Else
							oReport:Section(1):Cell("C5_CLIENTEE"	):Show()
							oReport:Section(1):Cell("C5_LOJACLIE"	):Show()
							oReport:Section(1):Cell("C6_NUM"		):Show()
						EndIf
					Else
						If cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI
							oReport:Section(1):Cell("C5_CLIENTEC"	):Hide()
							oReport:Section(1):Cell("C5_LOJACLIC"	):Hide()
						Else
							oReport:Section(1):Cell("C5_CLIENTEC"	):Show()
							oReport:Section(1):Cell("C5_LOJACLIC"	):Show()
						EndIf
						If dEmiss == (cAliasQry)->C5_EMISSAO .And. cPedido == SC6->C6_NUM
							oReport:Section(1):Cell("C6_NUM"		):Hide()
							oReport:Section(1):Cell("C5_EMISSAOC"	):Hide()
						Else
							oReport:Section(1):Cell("C6_NUM"		):Show()
							oReport:Section(1):Cell("C5_EMISSAOC"	):Show()
						EndIf
					EndIf
					
					oReport:Section(1):Cell("C6_ITEM"		):Show()
					oReport:Section(1):Cell("C6_PRODUTO"	):Show()
					oReport:Section(1):Cell("C6_UM"	   		):Show()
					oReport:Section(1):Cell("B1_GRUPO"		):Show()
					oReport:Section(1):Cell("C6_QTDVEN"		):Show()
					oReport:Section(1):Cell("C6_VALOR"		):Show()
					oReport:Section(1):Cell("C6_ENTREG"		):Show()
					// Informacoes do remito
					oReport:Section(1):Cell("DOC"			):Hide()
					oReport:Section(1):Cell("ITEM"			):Hide()
					oReport:Section(1):Cell("EMISSAO"		):Hide()
					oReport:Section(1):Cell("QUANT"			):Hide()
					// Informacoes da Fatura
					oReport:Section(1):Cell("D2_SERIE"		):Hide()
					oReport:Section(1):Cell("D2_DOC"		):Hide()
					oReport:Section(1):Cell("D2_ITEM"		):Hide()
					oReport:Section(1):Cell("D2_EMISSAO"	):Hide()
					oReport:Section(1):Cell("D2_QUANT"		):Hide()
					oReport:Section(1):Cell("D2_TOTAL"		):Hide()
					
					oReport:Section(1):PrintLine()
					
				Endif
				
				
				// Pesquisa Fatura (SD2) a partir do Pedido de Vendas
				cChFat:=xFilial("SD2")+SC6->C6_NUM+SC6->C6_ITEM
				DbSelectArea("SD2")
				dbSetOrder(8)
				If DbSeek(cChFat)
					// Informacoes do Pedido
					If oReport:Section(1):GetOrder() == 1			// Ordem: por Emissao
						If dEmiss <> (cAliasQry)->C5_EMISSAO
							oReport:Section(1):Cell("C5_EMISSAOE"	):Show()
						Else
							oReport:Section(1):Cell("C5_EMISSAOE"	):Hide()
						EndIf
						If  cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI;
							.And. cPedido == SC6->C6_NUM
							oReport:Section(1):Cell("C5_CLIENTEE"	):Hide()
							oReport:Section(1):Cell("C5_LOJACLIE"	):Hide()
							oReport:Section(1):Cell("C6_NUM"		):Hide()
						Else
							oReport:Section(1):Cell("C5_CLIENTEE"	):Show()
							oReport:Section(1):Cell("C5_LOJACLIE"	):Show()
							oReport:Section(1):Cell("C6_NUM"		):Show()
						EndIf
					Else
						If cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI
							oReport:Section(1):Cell("C5_CLIENTEC"	):Hide()
							oReport:Section(1):Cell("C5_LOJACLIC"	):Hide()
						Else
							oReport:Section(1):Cell("C5_CLIENTEC"	):Show()
							oReport:Section(1):Cell("C5_LOJACLIC"	):Show()
						EndIf
						If dEmiss == (cAliasQry)->C5_EMISSAO .And. cPedido == SC6->C6_NUM
							oReport:Section(1):Cell("C6_NUM"		):Hide()
							oReport:Section(1):Cell("C5_EMISSAOC"	):Hide()
						Else
							oReport:Section(1):Cell("C6_NUM"		):Show()
							oReport:Section(1):Cell("C5_EMISSAOC"	):Show()
						EndIf
					EndIf
					
					oReport:Section(1):Cell("C6_ITEM"		):Show()
					oReport:Section(1):Cell("C6_PRODUTO"	):Show()
					oReport:Section(1):Cell("C6_UM"	   		):Show()
					oReport:Section(1):Cell("B1_GRUPO"		):Show()
					oReport:Section(1):Cell("C6_QTDVEN"		):Show()
					oReport:Section(1):Cell("C6_VALOR"		):Show()
					oReport:Section(1):Cell("C6_ENTREG"		):Show()
					// Informacoes do remito
					oReport:Section(1):Cell("DOC"			):Hide()
					oReport:Section(1):Cell("ITEM"			):Hide()
					oReport:Section(1):Cell("EMISSAO"		):Hide()
					oReport:Section(1):Cell("QUANT"			):Hide()
					// Informacoes da Fatura
					oReport:Section(1):Cell("D2_SERIE"		):Show()
					oReport:Section(1):Cell("D2_DOC"		):Show()
					oReport:Section(1):Cell("D2_ITEM"		):Show()
					oReport:Section(1):Cell("D2_EMISSAO"	):Show()
					oReport:Section(1):Cell("D2_QUANT"		):Show()
					oReport:Section(1):Cell("D2_TOTAL"		):Show()
					
					lFirst := .T.
					While !Eof() .and. D2_FILIAL+D2_PEDIDO+D2_ITEMPV==cChFat
						If Empty(D2_REMITO) .AND. !IsRemito(1,"SD2->D2_TIPODOC") .and. D2_COD == SC6->C6_PRODUTO
							If !lFirst
								// Informacoes do Pedido
								If oReport:Section(1):GetOrder() == 1			// Ordem: por Emissao
									If dEmiss <> (cAliasQry)->C5_EMISSAO
										oReport:Section(1):Cell("C5_EMISSAOE"	):Show()
									Else
										oReport:Section(1):Cell("C5_EMISSAOE"	):Hide()
									EndIf
									
									If  cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI;
										.And. cPedido == SC6->C6_NUM
										oReport:Section(1):Cell("C5_CLIENTEE"	):Hide()
										oReport:Section(1):Cell("C5_LOJACLIE"	):Hide()
										oReport:Section(1):Cell("C6_NUM"		):Hide()
									Else
										oReport:Section(1):Cell("C5_CLIENTEE"	):Show()
										oReport:Section(1):Cell("C5_LOJACLIE"	):Show()
										oReport:Section(1):Cell("C6_NUM"		):Show()
									EndIf
								Else        									// Ordem: por Cliente
									If cCli == (cAliasQry)->C5_CLIENTE .And. cLojaCli == (cAliasQry)->C5_LOJACLI
										oReport:Section(1):Cell("C5_CLIENTEC"	):Hide()
										oReport:Section(1):Cell("C5_LOJACLIC"	):Hide()
									Else
										oReport:Section(1):Cell("C5_CLIENTEC"	):Show()
										oReport:Section(1):Cell("C5_LOJACLIC"	):Show()
									EndIf
									If dEmiss == (cAliasQry)->C5_EMISSAO .And. cPedido == SC6->C6_NUM
										oReport:Section(1):Cell("C6_NUM"		):Hide()
										oReport:Section(1):Cell("C5_EMISSAOC"	):Hide()
									Else
										oReport:Section(1):Cell("C6_NUM"		):Show()
										oReport:Section(1):Cell("C5_EMISSAOC"	):Show()
									EndIf
								EndIf
								
								oReport:Section(1):Cell("C6_ITEM"		):Hide()
								oReport:Section(1):Cell("C6_PRODUTO"	):Hide()
								oReport:Section(1):Cell("C6_UM"	   		):Hide()
								oReport:Section(1):Cell("B1_GRUPO"		):Hide()
								oReport:Section(1):Cell("C6_QTDVEN"		):Hide()
								oReport:Section(1):Cell("C6_VALOR"		):Hide()
								oReport:Section(1):Cell("C6_ENTREG"		):Hide()
							EndIf
							
							oReport:Section(1):PrintLine()
							lFirst := .F.
							
						Endif
						DbSkip()
					Enddo
					DbSelectArea("SC6")
				Endif
				
			Endif
			cCli 	:= SC6->C6_CLI
			cLojaCli:= SC6->C6_LOJA
			cPedido := SC6->C6_NUM
			dEmiss 	:= (cAliasQry)->C5_EMISSAO
			DbselectArea("SC6")
			DbSkip()
		Enddo
		
	Endif
	
	cCli 	:= (cAliasQry)->C5_CLIENTE
	cPedido	:= (cAliasQry)->C5_NUM
	dEmiss	:= (cAliasQry)->C5_EMISSAO
	dbSelectArea(cAliasQry)
	oReport:IncMeter()
	DbSkip()
	
EndDo

oReport:Section(1):Finish()
oReport:Section(1):SetPageBreak(.T.)
	
#IFNDEF TOP
	RetIndex("SC5")
	cArqTrab+=OrdBagExt()
	If File(cArqTrab)
	   Ferase(cArqTrab)
	EndIf
#ENDIF


Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ImpRPF   ³ Autor ³ Marco Bianchi         ³ Data ³ 20/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao por Remito/Pedido/Fatura		                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681 - R4                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpRPF(oReport,oRelat2,cAliasQry)

Local cChPed    := ""
Local cWhere	:= ""	
Local dEmiss    := CTOD("  /  /  ")
Local cCli  	:= ""
Local cLojaCli 	:= ""
Local cOrder    := ""
Local cCliente	:= ""
Local cLoja		:= ""
Local dEmissao	:= CTOD("  /  /  ")
Local cDoc 		:= ""
Local cItem		:= ""
Local cCod 		:= ""
Local cUM 		:= ""
Local cGrupo	:= ""
Local nQuant	:= 0

#IFNDEF TOP
	Local cCondicao := ""
#ENDIF

MakeSqlExpr(oReport:uParam)

oReport:SetTitle(STR0013 + cRetTitle + "/" +  STR0015 + "/" + STR0024) //"Relacao de remisiones/pedidos de venda/faturas

oReport:Section(2):Cell("D2_EMISSAOE"	):SetBlock({|| dEmissao	})
oReport:Section(2):Cell("D2_CLIENTEE"	):SetBlock({|| cCliente	})
oReport:Section(2):Cell("D2_LOJAE"		):SetBlock({|| cLoja	})
oReport:Section(2):Cell("D2_CLIENTEC"	):SetBlock({|| cCliente	})
oReport:Section(2):Cell("D2_LOJAC"	  	):SetBlock({|| cLoja	})
oReport:Section(2):Cell("D2_EMISSAOC"	):SetBlock({|| dEmissao	})
oReport:Section(2):Cell("D2_DOC"	 	):SetBlock({|| cDoc		})
oReport:Section(2):Cell("D2_ITEM" 		):SetBlock({|| cItem	})
oReport:Section(2):Cell("D2_COD"		):SetBlock({|| cCod		})
oReport:Section(2):Cell("D2_UM"			):SetBlock({|| cUM		})
oReport:Section(2):Cell("D2_GRUPO"		):SetBlock({|| cGrupo	})
oReport:Section(2):Cell("D2_QUANT"		):SetBlock({|| nQuant	})

cDoc	:= ""
cItem	:= ""
dEmissao:= ctod("  /  /  ")
nQuant  := 0
cCliente:= ""
cLoja	:= ""
cCod 	:= ""
cUM 	:= ""
cGrupo  := ""

If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
	oReport:Section(2):Cell("D2_EMISSAOC"):Disable()
	oReport:Section(2):Cell("D2_CLIENTEC"):Disable()
	oReport:Section(2):Cell("D2_LOJAC"):Disable()
Else										// Ordem: por Cliente
	oReport:Section(2):Cell("D2_EMISSAOE"):Disable()
	oReport:Section(2):Cell("D2_CLIENTEE"):Disable()
	oReport:Section(2):Cell("D2_LOJAE"):Disable()
EndIf

oReport:Section(1):disable()
oReport:Section(3):disable()
 
#IFDEF TOP

	dbSelectArea("SD2")
	If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
		dbSetOrder(5)   
		cOrder := "%D2_EMISSAO,D2_CLIENTE,D2_LOJA,D2_DOC%"
	Else										// Ordem: por Cliente
		dbSetOrder(9)
		cOrder := "%D2_CLIENTE,D2_LOJA,D2_EMISSAO,D2_DOC%"
	EndIf	

	cWhere := "%"
	If !Empty(mv_par02)
		cWhere+=" AND D2_CLIENTE  >= '" + %Exp:mv_par02% + "'"
	Endif
	If !Empty(mv_par03)
		cWhere+=" AND D2_CLIENTE <= '" + %Exp:mv_par03% + "'"
	Endif
	If !Empty(mv_par04)
		cWhere+=" AND D2_COD >= '" + %Exp:mv_par04% + "'"
	Endif
	If !Empty(mv_par05)
		cWhere+=" AND D2_COD <= '" + %Exp:mv_par05% + "'"
	Endif
	If !Empty(mv_par06)
		cWhere+=" AND D2_GRUPO >= '" + %Exp:mv_par06% + "'"
	Endif
	If !Empty(mv_par07)
		cWhere+=" AND D2_GRUPO <= '" + %Exp:mv_par07% + "'"
	Endif
	If !Empty(mv_par08)
	    cWhere+=" AND D2_EMISSAO >= '" + %Exp:DTOS(mv_par08)% + "'"
	Endif
	If !Empty(mv_par09)
		cWhere+=" AND D2_EMISSAO <= '" + %Exp:DTOS(mv_par09)% + "'"
	Endif
	cWhere+=" AND ("+IsRemito(3,'D2_TIPODOC')+ ")"
	cWhere += "%"

	oReport:Section(2):BeginQuery()	
	BeginSql Alias cAliasQry
	SELECT D2_FILIAL,D2_SERIE,D2_DOC,D2_ITEM,D2_EMISSAO,D2_QUANT,D2_TOTAL,D2_ESPECIE
	,D2_COD,D2_UM,D2_GRUPO,D2_REMITO,D2_ITEMREM,D2_SERIREM
	,D2_PEDIDO,D2_ITEMPV,D2_CLIENTE,D2_LOJA
	,R_E_C_N_O_ RECNO 
	FROM %Table:SD2% SD2
	WHERE D2_FILIAL = %xFilial:SD2% AND SD2.%notdel%
	%Exp:cWhere%
	ORDER BY %Exp:cOrder%
	EndSql       
	oReport:Section(2):EndQuery(/*Array com os parametros do tipo Range*/)

#ELSE

	dbSelectArea("SD2")
	If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
		dbSetOrder(5)   
		cOrder:="D2_FILIAL+DTOS(D2_EMISSAO)+D2_CLIENTE+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
	Else										// Ordem: por Cliente
		dbSetOrder(9)   
		cOrder:="D2_FILIAL+D2_CLIENTE+DTOS(D2_EMISSAO)+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
	EndIf

	cCondicao:="D2_FILIAL=='"+xFilial("SD2")+"'"
	If !Empty(mv_par02)
		cCondicao+=" .AND. D2_CLIENTE >='"+mv_par02+"'"
	Endif
	If !Empty(mv_par03)
		cCondicao+=" .AND. D2_CLIENTE <='"+mv_par03+"'"
	Endif
	If !Empty(mv_par04)
		cCondicao+=" .AND. D2_COD>='"+mv_par04+"'"
	Endif
	If !Empty(mv_par05)
		cCondicao+=" .AND. D2_COD<='"+mv_par05+"'"
	Endif
	If !Empty(mv_par06)
		cCondicao+=" .AND. D2_GRUPO>='"+mv_par06+"'"
	Endif
	If !Empty(mv_par07)
		cCondicao+=" .AND. D2_GRUPO<='"+mv_par07+"'"
	Endif
	If !Empty(mv_par08)
		cCondicao+=" .AND. DTOS(D2_EMISSAO)>='"+DTOS(mv_par08)+"'"
	Endif
	If !Empty(mv_par09)
		cCondicao+=" .AND. DTOS(D2_EMISSAO)<='"+DTOS(mv_par09)+"'"
	Endif
	cCondicao += ".And. "+IsRemito(2,'SD2->D2_TIPODOC')
	cArqTrab := CriaTrab(Nil,.F.)
	IndRegua("SD2",cArqTrab,cOrder,,cCondicao,OemToAnsi(STR0020+cRetTitle))  //"Selecionando remisiones"
	nIndex := RetIndex("SD2")
	SD2->( dbSetIndex( cArqTrab + OrdBagExt() ) )
	dbSetOrder(nIndex+1)

#ENDIF

SC6->(DbSetOrder(1))
SC5->(DbSetOrder(1))

dbSelectArea(cAliasQry)
dbGoTop()
oReport:Section(2):Init()
oReport:SetMeter((cAliasQry)->(LastRec()))

dEmiss   	:= CTOD("  /  /  ")
cCli 		:= ""
cLojaCli    := ""
cChave		:= ""
While !oReport:Cancel() .And. !(cAliasQry)->(Eof())
	
	If oReport:Section(1):GetOrder() == 1							// Ordem: por Emissao
		If dEmiss <> (cAliasQry)->D2_EMISSAO
			oReport:Section(2):Cell("D2_EMISSAOE"	):Show()
		Else
			oReport:Section(2):Cell("D2_EMISSAOE"	):Hide()
		EndIf
		
		If cChave <> (cAliasQry)->D2_CLIENTE+(cAliasQry)->D2_DOC
			oReport:Section(2):Cell("D2_CLIENTEE"	):Show()
			oReport:Section(2):Cell("D2_LOJAE"		):Show()
			oReport:Section(2):Cell("D2_DOC"		):Show()
		Else
			oReport:Section(2):Cell("D2_CLIENTEE"	):Hide()
			oReport:Section(2):Cell("D2_LOJAE"		):Hide()
			oReport:Section(2):Cell("D2_DOC"		):Hide()
		EndIf
	Else 															// Ordem: por Cliente
		If cCli <> (cAliasQry)->D2_CLIENTE .Or. cLojaCli <> (cAliasQry)->D2_LOJA
			oReport:Section(2):Cell("D2_CLIENTEC"	):Show()
			oReport:Section(2):Cell("D2_LOJAC"		):Show()
		Else
			oReport:Section(2):Cell("D2_CLIENTEC"	):Hide()
			oReport:Section(2):Cell("D2_LOJAC"		):Hide()
		EndIf
		
		
		If cChave <> DTOS((cAliasQry)->D2_EMISSAO)+(cAliasQry)->D2_DOC
			oReport:Section(2):Cell("D2_EMISSAOC"	):Show()
			oReport:Section(2):Cell("D2_DOC"		):Show()
		Else
			oReport:Section(2):Cell("D2_EMISSAOC"	):Hide()
			oReport:Section(2):Cell("D2_DOC"		):Hide()
		EndIf
	EndIf
	
	
	oReport:Section(2):Cell("D2_ITEM"		):Show()
	oReport:Section(2):Cell("D2_COD"		):Show()
	oReport:Section(2):Cell("D2_UM"			):Show()
	oReport:Section(2):Cell("D2_GRUPO"		):Show()
	oReport:Section(2):Cell("D2_QUANT"		):Show()
	// Dados do Pedido de Vendas
	oReport:Section(2):Cell("C6_NUM"		):Show()
	oReport:Section(2):Cell("C6_ITEM"		):Show()
	oreport:Section(2):Cell("C5_EMISSAO"	):Show()
	oReport:Section(2):Cell("C6_QTDVEN"		):Show()
	oReport:Section(2):Cell("C6_VALOR"		):Show()
	oReport:Section(2):Cell("C6_ENTREG"		):Show()
	// Informacoes da Fatura
	oReport:Section(2):Cell("SERIE"			):Show()
	oReport:Section(2):Cell("DOC"			):Show()
	oReport:Section(2):Cell("ITEM"			):Show()
	oReport:Section(2):Cell("EMISSAO"		):Show()
	oReport:Section(2):Cell("QUANT"			):Show()
	oReport:Section(2):Cell("TOTAL"			):Show()
	
	cCliente	:= (cAliasQry)->D2_CLIENTE
	cLoja		:= (cAliasQry)->D2_LOJA
	dEmissao	:= (cAliasQry)->D2_EMISSAO
	cDoc 		:= (cAliasQry)->D2_DOC
	cItem 		:= (cAliasQry)->D2_ITEM
	cCod 		:= (cAliasQry)->D2_COD
	cUM 		:= (cAliasQry)->D2_UM
	cGrupo		:= (cAliasQry)->D2_GRUPO
	nQuant		:= (cAliasQry)->D2_QUANT
	
	
	// Pesquisa Pedido de Vendas a partir do Remito (SD2)
	cChPed:=xFilial("SC6")+(cAliasQry)->D2_PEDIDO+(cAliasQry)->D2_ITEMPV
	DbSelectArea("SC6")
	If DbSeek(cChPed)
		
		SC5->(DbSeek(xFilial("SC5")+(cAliasQry)->D2_PEDIDO))
		
		#IFNDEF TOP
			dbSelectArea("SD2")
			dbSetOrder(nIndex+1)
			dbGoto(nReg)
		#ENDIF
		
	Else
		
		// Imprime apenas Remito
		// Dados do Pedido de Vendas
		oReport:Section(2):Cell("C6_NUM"		):Hide()
		oReport:Section(2):Cell("C6_ITEM"		):Hide()
		oreport:Section(2):Cell("C5_EMISSAO"	):Hide()
		oReport:Section(2):Cell("C6_QTDVEN"		):Hide()
		oReport:Section(2):Cell("C6_VALOR"		):Hide()
		oReport:Section(2):Cell("C6_ENTREG"		):Hide()
		
	Endif
	
	cChIFat:=xFilial("SD2")+(cAliasQry)->D2_CLIENTE+(cAliasQry)->D2_LOJA+(cAliasQry)->D2_SERIE+(cAliasQry)->D2_DOC+(cAliasQry)->D2_ITEM
	dbSelectArea("SD2")
	Dbsetorder(9)
	nReg := Recno()
	If DbSeek(cChIFat)
		// Imprime Remito, Pedido e Fatura
		While (D2_FILIAL+D2_CLIENTE+D2_LOJA+D2_SERIREM+D2_REMITO+D2_ITEMREM)==cChIFat
			oReport:Section(2):PrintLine()
			dbSelectArea("SD2")
			DbSkip()
		Enddo
	Else
		
		// Imprime Remito e Pedido
		// Informacoes da Fatura
		oReport:Section(2):Cell("SERIE"		):Hide()
		oReport:Section(2):Cell("DOC"		):Hide()
		oReport:Section(2):Cell("ITEM"		):Hide()
		oReport:Section(2):Cell("EMISSAO"	):Hide()
		oReport:Section(2):Cell("QUANT"		):Hide()
		oReport:Section(2):Cell("TOTAL"		):Hide()
		
		oReport:Section(2):PrintLine()
		
	Endif
	
	cCli 	:= (cAliasQry)->D2_CLIENTE
	cLojaCli:= (cAliasQry)->D2_LOJA
	dEmiss 	:= (cAliasQry)->D2_EMISSAO
	If oReport:Section(1):GetOrder() == 1								// Ordem: por Emissao
		cChave   := (cAliasQry)->D2_CLIENTE + (cAliasQry)->D2_DOC
	Else																// Ordem por Cliente
		cChave   := DTOS((cAliasQry)->D2_EMISSAO) + (cAliasQry)->D2_DOC
	EndIf
	
	DbSelectArea(cAliasQry)
	oReport:IncMeter()
	DbSkip()
	
Enddo

oReport:Section(2):Finish()
oReport:Section(2):SetPageBreak(.T.)

RetIndex("SC5")
RetIndex("SC6")

#IFNDEF TOP
	RetIndex("SD2")
	cArqTrab+=OrdBagExt()
	If File(cArqTrab)
		Ferase(cArqTrab)
	EndIf
#ENDIF


Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ImpFRP   ³ Autor ³ Marco Bianchi         ³ Data ³ 20/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao por Fatura/Remito/Pedido		                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681 - R4                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpFRP(oReport,oRelat3,cAliasQry)

Local cWhere	:= ""	
Local dEmiss    := CTOD("  /  /  ")
Local cCli  	:= ""
Local cLojaCli 	:= ""
Local cOrder    := ""
Local cCliente	:= ""
Local cLoja		:= ""
Local dEmissao	:= CTOD("  /  /  ")
Local cDoc 		:= ""
Local cSerie	:= ""
Local cEspecie	:= ""
Local cItem		:= ""
Local cCod 		:= ""
Local cUM 		:= ""
Local cGrupo	:= ""
Local nQuant	:= 0
Local nTotal	:= 0

#IFNDEF TOP
	Local cCondicao := ""
#ENDIF


oReport:Section(3):Cell("D2_EMISSAOE"	):SetBlock({|| dEmissao		})
oReport:Section(3):Cell("D2_CLIENTEE"	):SetBlock({|| cCliente		})
oReport:Section(3):Cell("D2_LOJAE"		):SetBlock({|| cLoja		})
oReport:Section(3):Cell("D2_CLIENTEC"	):SetBlock({|| cCliente		})
oReport:Section(3):Cell("D2_LOJAC"	  	):SetBlock({|| cLoja		})
oReport:Section(3):Cell("D2_EMISSAOC"	):SetBlock({|| dEmissao		})
oReport:Section(3):Cell("D2_SERIE"		):SetBlock({|| cSerie		})
oReport:Section(3):Cell("D2_DOC"		):SetBlock({|| cDoc		    })
oReport:Section(3):Cell("D2_ESPECIE"	):SetBlock({|| cEspecie		})
oReport:Section(3):Cell("D2_ITEM"		):SetBlock({|| cItem		})
oReport:Section(3):Cell("D2_COD"	  	):SetBlock({|| cCod		    })
oReport:Section(3):Cell("D2_UM"			):SetBlock({|| cUM		    })
oReport:Section(3):Cell("D2_GRUPO"		):SetBlock({|| cGrupo		})
oReport:Section(3):Cell("D2_QUANT"		):SetBlock({|| nQuant		})
oReport:Section(3):Cell("D2_TOTAL"		):SetBlock({|| nTotal		})

cDoc	 := ""
cItem	 := ""
dEmissao := ctod("  /  /  ")
nQuant   := 0
cCliente := ""
cLoja	 := ""
cCod 	 := ""
cUM 	 := ""
cGrupo   := ""
cSerie   := ""
cEspecie := ""
nTotal   := 0

If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
	oReport:Section(3):Cell("D2_EMISSAOC"):Disable()
	oReport:Section(3):Cell("D2_CLIENTEC"):Disable()
	oReport:Section(3):Cell("D2_LOJAC"):Disable()
Else										// Ordem: por Cliente
	oReport:Section(3):Cell("D2_EMISSAOE"):Disable()
	oReport:Section(3):Cell("D2_CLIENTEE"):Disable()
	oReport:Section(3):Cell("D2_LOJAE"):Disable()
EndIf
oReport:Section(1):disable()
oReport:Section(2):disable()

oReport:SetTitle(STR0013 + STR0016 + "/" + cRetTitle + "/" + STR0015) //"Relacao de faturas/remisiones/pedidos de venda

	
MakeSqlExpr(oReport:uParam)
#IFDEF TOP

	dbSelectArea("SD2")
	If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
		dbSetOrder(5)   
		cOrder := "%D2_EMISSAO,D2_CLIENTE,D2_LOJA,D2_SERIE,D2_DOC,D2_ITEM%"
	Else										// Ordem: por Cliente
		dbSetOrder(9)
		cOrder := "%D2_CLIENTE,D2_LOJA,D2_EMISSAO,D2_SERIE,D2_DOC,D2_ITEM%"
	EndIf	

	cWhere := "%"
	If !Empty(mv_par02)
		cWhere+=" AND D2_CLIENTE  >= '" + %Exp:mv_par02% + "'"
	Endif
	If !Empty(mv_par03)
		cWhere+=" AND D2_CLIENTE <= '" + %Exp:mv_par03% + "'"
	Endif
	If !Empty(mv_par04)
		cWhere+=" AND D2_COD >= '" + %Exp:mv_par04% + "'"
	Endif
	If !Empty(mv_par05)
		cWhere+=" AND D2_COD <= '" + %Exp:mv_par05% + "'"
	Endif
	If !Empty(mv_par06)
		cWhere+=" AND D2_GRUPO >= '" + %Exp:mv_par06% + "'"
	Endif
	If !Empty(mv_par07)
		cWhere+=" AND D2_GRUPO <= '" + %Exp:mv_par07% + "'"
	Endif
	If !Empty(mv_par08)
	    cWhere+=" AND D2_EMISSAO >= '" + %Exp:DTOS(mv_par08)% + "'"
	Endif
	If !Empty(mv_par09)
		cWhere+=" AND D2_EMISSAO <= '" + %Exp:DTOS(mv_par09)% + "'"
	Endif
	cWhere+=" AND NOT ("+IsRemito(3,'D2_TIPODOC')+ ")"
	cWhere += "%"

	oReport:Section(3):BeginQuery()	
	BeginSql Alias cAliasQry
	SELECT D2_FILIAL,D2_SERIE,D2_DOC,D2_ITEM,D2_EMISSAO,D2_QUANT,D2_TOTAL,D2_ESPECIE
	,D2_COD,D2_UM,D2_GRUPO,D2_REMITO,D2_ITEMREM,D2_SERIREM
	,D2_PEDIDO,D2_ITEMPV,D2_CLIENTE,D2_LOJA
	,R_E_C_N_O_ RECNO 
	FROM %Table:SD2% SD2
	WHERE D2_FILIAL = %xFilial:SD2% AND SD2.%notdel%
	%Exp:cWhere%
	ORDER BY %Exp:cOrder%
	EndSql       
	oReport:Section(3):EndQuery(/*Array com os parametros do tipo Range*/)

#ELSE

	dbSelectArea("SD2")
	If oReport:Section(1):GetOrder() == 1		// Ordem: por Emissao
		dbSetOrder(5)   
		cOrder:="D2_FILIAL+DTOS(D2_EMISSAO)+D2_CLIENTE+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
	Else										// Ordem: por Cliente
		dbSetOrder(9)   
		cOrder:="D2_FILIAL+D2_CLIENTE+DTOS(D2_EMISSAO)+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
	EndIf

	cCondicao:="D2_FILIAL=='"+xFilial("SD2")+"'" 
	If !Empty(mv_par02)
		cCondicao+=" .AND. D2_CLIENTE >='"+mv_par02+"'"
	Endif
	If !Empty(mv_par03)
		cCondicao+=" .AND. D2_CLIENTE <='"+mv_par03+"'"
	Endif
	If !Empty(mv_par04)
		cCondicao+=" .AND. D2_COD>='"+mv_par04+"'"
	Endif
	If !Empty(mv_par05)
		cCondicao+=" .AND. D2_COD<='"+mv_par05+"'"
	Endif
	If !Empty(mv_par06)
		cCondicao+=" .AND. D2_GRUPO>='"+mv_par06+"'"
	Endif
	If !Empty(mv_par07)
		cCondicao+=" .AND. D2_GRUPO<='"+mv_par07+"'"
	Endif
	If !Empty(mv_par08)
	    cCondicao+=" .AND. DTOS(D2_EMISSAO)>='"+DTOS(mv_par08)+"'"
	Endif
	If !Empty(mv_par09)
		cCondicao+=" .AND. DTOS(D2_EMISSAO)<='"+DTOS(mv_par09)+"'"
	Endif
	cCondicao += ".And. !("+IsRemito(2,'SD2->D2_TIPODOC')+")"			
	cArqTrab := CriaTrab(Nil,.F.)
	IndRegua("SD2",cArqTrab,cOrder,,cCondicao,OemToAnsi(STR0021+cRetTitle))  //"Selecionando Faturas"
	nIndex := RetIndex("SD2")
	SD2->( dbSetIndex( cArqTrab + OrdBagExt() ) )
	dbSetOrder(nIndex+1)

#ENDIF

SC6->(DbSetOrder(1))
SC5->(DbSetOrder(1))

dbSelectArea(cAliasQry)
dbGoTop()
oReport:Section(3):Init()
oReport:SetMeter((cAliasQry)->(LastRec()))

dEmiss  := CTOD("  /  /  ")
cCli 	:= ""
cLojaCli:= ""
cChave	:= ""
While !oReport:Cancel() .And. !(cAliasQry)->(Eof())
	
	If oReport:Section(1):GetOrder() == 1								// Ordem: por Emissao
		If dEmiss <> (cAliasQry)->D2_EMISSAO
			oReport:Section(3):Cell("D2_EMISSAOE"	):Show()
		Else
			oReport:Section(3):Cell("D2_EMISSAOE"	):Hide()
		EndIf
		If cChave <> (cAliasQry)->D2_CLIENTE+(cAliasQry)->D2_SERIE+(cAliasQry)->D2_DOC
			oReport:Section(3):Cell("D2_CLIENTEE"	):Show()
			oReport:Section(3):Cell("D2_LOJAE"		):Show()
			oReport:Section(3):Cell("D2_SERIE"		):Show()
			oReport:Section(3):Cell("D2_DOC"		):Show()
			oReport:Section(3):Cell("D2_ESPECIE"	):Show()
		Else
			oReport:Section(3):Cell("D2_CLIENTEE"	):Hide()
			oReport:Section(3):Cell("D2_LOJAE"		):Hide()
			oReport:Section(3):Cell("D2_SERIE"		):Hide()
			oReport:Section(3):Cell("D2_DOC"		):Hide()
			oReport:Section(3):Cell("D2_ESPECIE"	):Hide()
		EndIf
	Else 																// Ordem: por Cliente
		If cCli <> (cAliasQry)->D2_CLIENTE .Or. cLojaCli <> (cAliasQry)->D2_LOJA
			oReport:Section(3):Cell("D2_CLIENTEC"	):Show()
			oReport:Section(3):Cell("D2_LOJAC"		):Show()
		Else
			oReport:Section(3):Cell("D2_CLIENTEC"	):Hide()
			oReport:Section(3):Cell("D2_LOJAC"		):Hide()
		EndIf
		If cChave <> DTOS((cAliasQry)->D2_EMISSAO)+(cAliasQry)->D2_SERIE+(cAliasQry)->D2_DOC
			oReport:Section(3):Cell("D2_EMISSAOC"	):Show()
			oReport:Section(3):Cell("D2_SERIE"		):Show()
			oReport:Section(3):Cell("D2_DOC"		):Show()
			oReport:Section(3):Cell("D2_ESPECIE"	):Show()
		Else
			oReport:Section(3):Cell("D2_EMISSAOC"	):Hide()
			oReport:Section(3):Cell("D2_SERIE"		):Hide()
			oReport:Section(3):Cell("D2_DOC"		):Hide()
			oReport:Section(3):Cell("D2_ESPECIE"	):Hide()
		EndIf
	EndIf
	
	oReport:Section(3):Cell("D2_ITEM"		):Show()
	oReport:Section(3):Cell("D2_COD"		):Show()
	oReport:Section(3):Cell("D2_UM"			):Show()
	oReport:Section(3):Cell("D2_GRUPO"		):Show()
	oReport:Section(3):Cell("D2_QUANT"		):Show()
	oReport:Section(3):Cell("D2_TOTAL"		):Show()
	
	// Informacoes do Remito
	oReport:Section(3):Cell("DOC"			):Show()
	oReport:Section(3):Cell("ITEM"			):Show()
	oReport:Section(3):Cell("EMISSAO"		):Show()
	oReport:Section(3):Cell("QUANT"			):Show()
	
	// Dados do Pedido de Vendas
	oReport:Section(3):Cell("C6_NUM"		):Show()
	oReport:Section(3):Cell("C6_ITEM"		):Show()
	oreport:Section(3):Cell("C5_EMISSAO"	):Show()
	oReport:Section(3):Cell("C6_QTDVEN"		):Show()
	oReport:Section(3):Cell("C6_VALOR"		):Show()
	oReport:Section(3):Cell("C6_ENTREG"		):Show()
	
	cCliente	:= (cAliasQry)->D2_CLIENTE
	cLoja		:= (cAliasQry)->D2_LOJA
	dEmissao	:= (cAliasQry)->D2_EMISSAO
	cSerie 		:= (cAliasQry)->D2_SERIE
	cDoc 		:= (cAliasQry)->D2_DOC
	cEspecie	:= (cAliasQry)->D2_ESPECIE
	cItem 		:= (cAliasQry)->D2_ITEM
	cCod 		:= (cAliasQry)->D2_COD
	cUM 		:= (cAliasQry)->D2_UM
	cGrupo		:= (cAliasQry)->D2_GRUPO
	nQuant		:= (cAliasQry)->D2_QUANT
	nTotal		:= (cAliasQry)->D2_TOTAL
	
	#IFDEF TOP
		SD2->(dbSetOrder(3))
		SD2->(DBSeek(xFilial("SD2")+(cAliasQry)->D2_REMITO+(cAliasQry)->D2_SERIREM+(cAliasQry)->D2_CLIENTE+(cAliasQry)->D2_LOJA+(cAliasQry)->D2_COD+(cAliasQry)->D2_ITEMREM))
	#ELSE
		dbSelectArea("SD2")
		DbSetOrder(3)
		nReg := Recno()
		DBSeek(xFilial("SD2")+(cAliasQry)->D2_REMITO+(cAliasQry)->D2_SERIREM+(cAliasQry)->D2_CLIENTE+(cAliasQry)->D2_LOJA+(cAliasQry)->D2_COD+(cAliasQry)->D2_ITEMREM)
		
	#ENDIF
	
	If !Empty((cAliasQry)->D2_PEDIDO)
		SC6->(DBSeek(xFilial("SC6")+(cAliasQry)->D2_PEDIDO+(cAliasQry)->D2_ITEMPV))
		SC5->(DBSeek(xFilial("SC5")+(cAliasQry)->D2_PEDIDO))
	Else
		SC6->(DBSeek(xFilial("SC6")+SD2->D2_PEDIDO+SD2->D2_ITEMPV))
		SC5->(DBSeek(xFilial("SC5")+SD2->D2_PEDIDO))
	EndIf	
	
	
	oReport:Section(3):PrintLine()
	
	#IFNDEF TOP
		dbSelectArea("SD2")
		dbSetOrder(nIndex+1)
		dbGoto(nReg)
	#ENDIF	
	
	
	dEmiss  := (cAliasQry)->D2_EMISSAO
	cCli	:= (cAliasQry)->D2_CLIENTE
	cLojaCli:= (cAliasQry)->D2_LOJA
	If oReport:Section(1):GetOrder() == 1								// Ordem: por Emissao
		cChave   := (cAliasQry)->D2_CLIENTE + (cAliasQry)->D2_SERIE +(cAliasQry)->D2_DOC + (cAliasQry)->D2_ESPECIE
	Else																// Ordem por Cliente
		cChave   := DTOS((cAliasQry)->D2_EMISSAO) + (cAliasQry)->D2_SERIE +(cAliasQry)->D2_DOC + (cAliasQry)->D2_ESPECIE
	EndIf
	
	
	DbSelectArea(cAliasQry)
	oReport:IncMeter()
	DbSkip()
	
Enddo

oReport:Section(3):Finish()
oReport:Section(3):SetPageBreak(.T.)

RetIndex("SC5")
RetIndex("SC6")

#IFNDEF TOP
	RetIndex("SD2")
	cArqTrab+=OrdBagExt()
	If File(cArqTrab)
		Ferase(cArqTrab)
	EndIf
#ENDIF
                      

Return




/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATR681  ³ Autor ³ Marcello Gabriel      ³ Data ³ 08.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Informe de Pedidos de Venda/Remitos/Notas Fiscais          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR681(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFAT - Localizacoes                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Matr681R3()

Private cRetTitle := GetDescRem()

lEnd    := .F.
cDesc1  := OemToAnsi(STR0001)+OemToAnsi(STR0002)+"/"+cRetTitle+"/"+OemToAnsi(STR0003) //"Relacao de pedidos de venda/remisiones/faturas 
cDesc2  := ""
cDesc3  := ""
cString := "SC6"
cabec1  := ""
cabec2  := ""
aLinha  := {}
aOrd    := {OemToAnsi(STR0004),OemToAnsi(STR0005)} //"Data de Emissao"###"Cliente"
aReturn := {OemToAnsi(STR0007),1,OemToAnsi(STR0008),1,2,1,"",1}  //"Zebrado"###"Administracao"
cPerg   := "MTR681"
nJuros  := 0
nLastKey:= 0
nomeprog:= "MATR681"
limite  := 220
tamanho := "G"
Titulo  := ""
cbcont	:= 00
cbtxt	:= space(10)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros ³
//³ mv_par01       // ¨Basado por ?      ³
//³ mv_par02       // ¨De proveedor?     ³
//³ mv_par03       // ¨Hasta proveedor?  ³
//³ mv_par04       // ¨De producto ?     ³
//³ mv_par05       // ¨Hasta producto?   ³
//³ mv_par06       // ¨De grupo ?        ³
//³ mv_par07       // ¨Hasta grupo ?     ³
//³ mv_par08       // ¨De Fecha Emision? ³
//³ mv_par09       // ¨Hasta Fech.Emis.? ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

MR681Tit()  //montagem do Titulo
wnrel :="MATR681"            //Nome Default do relatorio em Disco
wnrel :=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.F.,tamanho,,.F.)
MR681Tit()  //montagem do Titulo de acordo com a escolha nos parametros
Titulo:= Padc(Titulo,60)
If nLastKey != 27
	SetDefault(aReturn,cString)
   If nLastKey != 27
      Do Case
         Case MV_PAR01==1   //baseado no pedido de venda
               RptStatus({|lEnd| R681PV(@lEnd)})
         Case MV_PAR01==2   //baseado na remision
               RptStatus({|lEnd| R681Rem(@lEnd)})
         Case MV_PAR01==3   //baseado na fatura
               RptStatus({|lEnd| R681Fat(@lEnd)})
      EndCase
   Endif
Endif   
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ M681CAB  ³ Autor ³ Marcello Gabriel      ³ Data ³ 15.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao do cabecalho do Relatorio                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function M681CAB()
Local cAux,nAux
Local nTamQ  	:= 14
Local nTamV  	:= 20
Local nTamQ2	:= 14 
Local nTamV2 	:= 20 
Local nTamProd 	:= TamSX3("C6_PRODUTO")[1]

If cPaisLoc == "MEX"
	If MV_PAR01 == 2 .AND. nTamProd == 30
		nTamQ  := 06
	ElseIf MV_PAR01 == 3 .AND. nTamProd == 30
		nTamQ  := 09
		nTamV  := 14
		nTamQ2 := 08
		nTamV2 := 12			
	Else
		nTamQ  := 12
		nTamV  := 16
		nTamQ2 := 12 
		nTamV2 := 16 
	EndIf
EndIf

Do Case
   Case MV_PAR01==1 //baseado no pedido  
        Cabec1:=STR0006  //"Dados do Pedido de Venda"
        Cabec2:=Padr(RetTitle("C5_EMISSAO"),10)+" "
        cAux:=Padr(RetTitle("C5_CLIENTE"),TamSX3("C5_CLIENTE")[1]+TamSX3("C5_LOJACLI")[1])+"  "
        If aReturn[8]==1
           Cabec2+=" "+cAux
        Else   
           Cabec2:=cAux+" "+Cabec2
        Endif    
        Cabec2+=Padr(RetTitle("C5_NUM"),TamSX3("C5_NUM")[1])+" "+;
                Padr(RetTitle("C6_ITEM"),TamSX3("C6_ITEM")[1])+" "+;
                Padr(RetTitle("C6_PRODUTO"),TamSX3("C6_PRODUTO")[1])+" "+;
                Padr(RetTitle("C6_UM"),TamSX3("C6_UM")[1])+" "+;
                Padr(RetTitle("B1_GRUPO"),TamSX3("B1_GRUPO")[1])+" "+;
                Padl(Trim(RetTitle("C6_QTDVEN")),nTamQ)+" "+;
                Padl(Trim(RetTitle("C6_VALOR")),nTamV)+" "+; 
                Padr(RetTitle("C6_ENTREG"),10)+"  "
        //Montagem do cabecalho da remision
        nAux:=Len(Cabec2)-Len(Cabec1)
        Cabec1+=Space(nAux)
        Cabec1+="Dados "+cRetTitle
        Cabec2+=Padr(RetTitle("D2_DOC"),TamSX3("D2_DOC")[1])+" "+;
                Padr(RetTitle("D2_ITEM"),TamSX3("D2_ITEM")[1])+" "+;
                Padr(RetTitle("D2_EMISSAO"),10)+" "+;
                Padl(Trim(RetTitle("D2_QUANT")),nTamQ)+"  "
        //Montagem do cabecalho da fatura
        nAux:=Len(Cabec2)-Len(Cabec1)
        Cabec1+=Space(nAux)
        Cabec1+=STR0010 //"Dados da Fatura"
        Cabec2+=Padr(RetTitle("D2_SERIE"),TamSX3("D2_SERIE")[1])+" "+;
                Padr(RetTitle("D2_DOC"),TamSX3("D2_DOC")[1])+" "+; 
                Padr(RetTitle("D2_ITEM"),TamSX3("D2_ITEM")[1])+" "+;
                Padr(RetTitle("D2_EMISSAO"),10)+" "+;
                Padl(Trim(RetTitle("D2_QUANT")),nTamQ-10)+" "+;
                Padl(Trim(RetTitle("D2_TOTAL")),nTamV)+" "
   Case MV_PAR01==2 //baseado na remision
        Cabec1:="Dados "+cRetTitle
        Cabec2:=Padr(RetTitle("D2_EMISSAO"),10)+" "
        cAux:=Padr(RetTitle("D2_CLIENTE"),TamSX3("D2_CLIENTE")[1]+TamSX3("D2_LOJA")[1])+"  "
        If aReturn[8]==1
           Cabec2+=" "+cAux
        Else   
            Cabec2:=cAux+" "+Cabec2
        Endif    
        Cabec2+=Padr(RetTitle("D2_DOC"),TamSX3("D2_DOC")[1])+" "+;
                Padr(RetTitle("D2_ITEM"),TamSX3("D2_ITEM")[1])+" "+;
                Padr(RetTitle("D2_COD"),TamSX3("D2_COD")[1])+" "+;
                Padr(RetTitle("D2_UM"),TamSX3("D2_UM")[1])+" "+;
                Padr(RetTitle("D2_GRUPO"),TamSX3("D2_GRUPO")[1])+" "+;
                Padl(Trim(RetTitle("D2_QUANT")),nTamQ)+"  " 
        //Montagem do cabecalho do pedido
        nAux:=Len(Cabec2)-Len(Cabec1)
        Cabec1+=Space(nAux)
        Cabec1+=STR0006
        Cabec2+=Padr(RetTitle("C6_NUM"),TamSX3("C6_NUM")[1])+" "+;
                Padr(RetTitle("C6_ITEM"),TamSX3("C6_ITEM")[1])+" "+;
                Padr(RetTitle("C5_EMISSAO"),10)+" "+;
                Padl(Trim(RetTitle("C6_QTDVEN")),nTamQ)+" "+;
                Padl(Trim(RetTitle("C6_VALOR")),nTamV)+" "+; 
                Padr(RetTitle("C6_ENTREG"),10)+"  "
        //Montagem do cabecalho da fatura
        nAux:=Len(Cabec2)-Len(Cabec1)
        Cabec1+=Space(nAux)
        Cabec1+=STR0010
        Cabec2+=Padr(RetTitle("D2_SERIE"),TamSX3("D2_SERIE")[1])+" "+;
                Padr(RetTitle("D2_DOC"),TamSX3("D2_DOC")[1])+" "+;
                Padr(RetTitle("D2_ITEM"),TamSX3("D2_ITEM")[1])+" "+;
                Padr(RetTitle("D2_EMISSAO"),10)+" "+;
                Padl(Trim(RetTitle("D2_QUANT")),nTamQ)+" "+;
                Padl(Trim(RetTitle("D2_TOTAL")),nTamV)+" "
   Case MV_PAR01==3 //baseado no fatura
        Cabec1:=STR0010 //dados da fatura
        Cabec2:=Padr(RetTitle("D2_EMISSAO"),10)+" "
        cAux:=Padr(RetTitle("D2_CLIENTE"),TamSX3("D2_CLIENTE")[1]+TamSX3("D2_LOJA")[1])+"  "
        If aReturn[8]==1
           Cabec2+=" "+cAux
        Else   
            Cabec2:=cAux+" "+Cabec2
        Endif    
        Cabec2+=Padr(RetTitle("D2_SERIE"),TamSX3("D2_SERIE")[1])+" "+;
                Padr(RetTitle("D2_DOC"),TamSX3("D2_DOC")[1])+" "+; 
                Padr(RetTitle("D2_ESPECIE"),TamSX3("D2_ESPECIE")[1])+" "+; 
                Padr(RetTitle("D2_ITEM"),TamSX3("D2_ITEM")[1])+" "+;
                Padr(RetTitle("D2_COD"),TamSX3("D2_COD")[1])+" "+;
                Padr(RetTitle("D2_UM"),TamSX3("D2_UM")[1])+" "+;
                Padr(RetTitle("D2_GRUPO"),TamSX3("D2_GRUPO")[1])+" "+;
                Padl(Trim(RetTitle("D2_QUANT")),nTamQ)+" " +;
                Padl(Trim(RetTitle("D2_TOTAL")),nTamV)+"  "
        //Montagem do cabecalho da remision
        nAux:=Len(Cabec2)-Len(Cabec1)
        Cabec1+=Space(nAux)
        Cabec1+="Dados "+cRetTitle
        Cabec2+=Padr(RetTitle("D2_DOC"),TamSX3("D2_DOC")[1])+" "+;
                Padr(RetTitle("D2_ITEM"),TamSX3("D2_ITEM")[1])+" "+;
                Padr(RetTitle("D2_EMISSAO"),10)+" "+;
                Padl(Trim(RetTitle("D2_QUANT")),nTamQ)+"  "
        //Montagem do cabecalho do pedido
        nAux:=Len(Cabec2)-Len(Cabec1)
        Cabec1+=Space(nAux)
        Cabec1+=STR0006
	  	Cabec2+=Padr(RetTitle("C6_NUM"),TamSX3("C6_NUM")[1])+" "+;
	   			Padr(RetTitle("C6_ITEM"),TamSX3("C6_ITEM")[1])+" "+;
	      		Padr(RetTitle("C5_EMISSAO"),10)+" "+;
				Padl(Trim(RetTitle("C6_QTDVEN")),nTamQ2)+" "+;
	   			Padl(Trim(RetTitle("C6_VALOR")),nTamV2)+" "+; 
	      		Padr(RetTitle("C6_ENTREG"),11)+"  "

				
EndCase
Cabec(titulo,cabec1,cabec2,nomeprog,tamanho)
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ M681DPED ³ Autor ³ Marcello Gabriel      ³ Data ³ 15.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao do item do pedido                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function M681DPED(nLinha,nColuna,lImpPV,cAlias)
Local nCol:=0
Local nTamQ := 14
Local nTamV := 20
Local nTamProd 	:= TamSX3("C6_PRODUTO")[1]

If cPaisLoc == "MEX"
	nTamQ := 12
	nTamV := 16
EndIf

If	lImpPV==Nil
   lImpPV:=.T.
Endif
If MV_PAR01==1 //impressao baseda no pedido
	If aReturn[8]<>1 //nao ordenada por emissao
 		If lImpPV               
			@nLinha,nColuna PSay Padr((cAlias)->C5_EMISSAO,10)
		  	nColuna:=PCol()+1
	  	Else
	   	nColuna+=11 
	  	Endif
	Endif
 	If aReturn[8]<>2 //nao ordenada por cliente
  		If lImpPV
    		@nLinha,nColuna PSay (cAlias)->C5_CLIENTE Picture PesqPict("SC5","C5_CLIENTE")
      	@nLinha,PCol()+1 PSay (cAlias)->C5_LOJACLI Picture PesqPict("SC5","C5_LOJACLI")
       	nColuna:=PCol()+1
      Else
      	nColuna+=Len((cAlias)->C5_CLIENTE)+Len((cAlias)->C5_LOJACLI)+2
      Endif       
	Endif
Endif
IF lImpPV
	If MV_PAR01 == 3 .AND. nTamProd == 30	
		@nLinha,nColuna+2 PSay (cAlias)->C5_NUM Picture PesqPict("SC5","C5_NUM")
	Else	
		@nLinha,nColuna PSay (cAlias)->C5_NUM Picture PesqPict("SC5","C5_NUM")
	EndIf
	nColuna:=PCol()+1
Else
	nColuna+=TamSX3("C5_NUM")[1]+1
Endif
lImpPV:=.F.
@nLinha,nColuna PSay SC6->C6_ITEM Picture PesqPict("SC6","C6_ITEM")
If MV_PAR01==1  //impressao baseada no pedido
   SB1->(MsSeek(xFilial("SB1")+SC6->C6_PRODUTO))		
   @nLinha,PCol()+1 PSay SC6->C6_PRODUTO Picture PesqPict("SC6","C6_PRODUTO")
   @nLinha,PCol()+1 PSay SC6->C6_UM Picture PesqPict("SC6","C6_UM")
   @nLinha,Pcol()+1 PSay SB1->B1_GRUPO Picture PesqPict("SB1","B1_GRUPO")
Else
	@nLinha,Pcol()+1 PSay Padr((cAlias)->C5_EMISSAO,10)   
Endif          
If MV_PAR01 == 2 .AND. nTamProd == 30
	@nLinha,PCol()-5 PSay SC6->C6_QTDVEN Picture PesqPict("SC6","C6_QTDVEN",nTamQ)
	@nLinha,PCol()+3 PSay SC6->C6_VALOR Picture PesqPict("SC6","C6_VALOR",nTamV)
	@nLinha,PCol()+3 PSay Padr(SC6->C6_ENTREG,10)
ElseIf MV_PAR01 == 3 .AND. nTamProd == 30
	@nLinha,PCol()-3 PSay SC6->C6_QTDVEN Picture PesqPict("SC6","C6_QTDVEN",nTamQ)
	@nLinha,PCol()-4 PSay SC6->C6_VALOR Picture PesqPict("SC6","C6_VALOR",nTamV)
	@nLinha,PCol()+2 PSay Padr(SC6->C6_ENTREG,10)       
Else
	@nLinha,PCol()+1 PSay SC6->C6_QTDVEN Picture PesqPict("SC6","C6_QTDVEN",nTamQ)
	@nLinha,PCol()+1 PSay SC6->C6_VALOR Picture PesqPict("SC6","C6_VALOR",nTamV)
	@nLinha,PCol()+1 PSay Padr(SC6->C6_ENTREG,10)
EndIf
nCol:=PCol()
Return(nCol)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ M681DREM ³ Autor ³ Marcello Gabriel      ³ Data ³ 15.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao do item do pedido                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function M681DREM(nLinha,nColuna,lImpRm,cAlias)
Local nCol:=0
Local nTamQ := 14
Local nTamProd 	:= TamSX3("C6_PRODUTO")[1]

If cPaisLoc == "MEX"
	nTamQ := 12
EndIf

If lImpRM==Nil
   lImpRM:=.T.
Endif
If	MV_PAR01==2 //impressao baseda na remision
	If aReturn[8]<>1 //nao ordenada por emissao
 		If lImpRM               
		 	@nLinha,nColuna PSay Padr((cAlias)->D2_EMISSAO,10)
		  	nColuna:=PCol()+1
	  	Else
	   	nColuna+=11 
		Endif
	Endif
 	If aReturn[8]<>2 //nao ordenada por cliente
  		If lImpRM
    		@nLinha,nColuna PSay (cAlias)->D2_CLIENTE Picture PesqPict("SD2","D2_CLIENTE")
      	@nLinha,PCol()+1 PSay (cAlias)->D2_LOJA Picture PesqPict("SD2","D2_LOJA")
       	nColuna:=PCol()+1
    	Else
     		nColuna+=Len((cAlias)->D2_CLIENTE)+Len((cAlias)->D2_LOJA)+2
     	Endif       
 	Endif
Endif
IF lImpRM	
	@nLinha,nColuna PSay (cAlias)->D2_DOC Picture PesqPict("SD2","D2_DOC")
	nColuna:=PCol()+1
Else
	nColuna+=TamSX3("D2_DOC")[1]+1
Endif

lImpRM:=.F.
@nLinha,nColuna PSay (cAlias)->D2_ITEM Picture PesqPict("SD2","D2_ITEM")
If MV_PAR01==2  //impressao baseada na remision
   @nLinha,PCol()+1 PSay (cAlias)->D2_COD Picture PesqPict("SD2","D2_COD")
   @nLinha,PCol()+1 PSay (cAlias)->D2_UM Picture PesqPict("SD2","D2_UM")
   @nLinha,PCol()+1 PSay (cAlias)->D2_GRUPO Picture PesqPict("SD2","D2_GRUPO")
Else   
   	@nLinha,PCol()+1 PSay Padr((cAlias)->D2_EMISSAO,10)
Endif
If MV_PAR01 == 2 .AND. nTamProd == 30
	@nLinha,PCol()-4 PSay (cAlias)->D2_QUANT Picture PesqPict("SD2","D2_QUANT",nTamQ)    
ElseIf MV_PAR01 == 3 .AND. nTamProd == 30
	@nLinha,PCol()-2 PSay (cAlias)->D2_QUANT Picture PesqPict("SD2","D2_QUANT",nTamQ)	
Else	
	@nLinha,PCol()+1 PSay (cAlias)->D2_QUANT Picture PesqPict("SD2","D2_QUANT",nTamQ)
EndIf
nCol:=Pcol()

Return(nCol)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ M681DFAT ³ Autor ³ Marcello Gabriel      ³ Data ³ 15.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao do item do pedido                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function M681DFAT(nLinha,nColuna,lImpFT,cAlias)
Local nCol:=0
Local nTamQ := 14
Local nTamV := 20
Local nTamProd 	:= TamSX3("C6_PRODUTO")[1]


If cPaisLoc == "MEX"
	nTamQ := 12
	nTamV := 16
EndIf

If lImpFT==Nil
   lImpFT:=.T.
Endif
If MV_PAR01==3 //impressao baseda na fatura
	If aReturn[8]<>1 //nao ordenada por emissao
 		If lImpFT
		 	@nLinha,nColuna PSay Padr((cAlias)->D2_EMISSAO,10)
		  	nColuna:=PCol()+1
	  	Else
	   	nColuna+=11 
	  	Endif
	Endif
 	If aReturn[8]<>2 //nao ordenada por cliente
  		If lImpFT
    		@nLinha,nColuna PSay (cAlias)->D2_CLIENTE Picture PesqPict("SD2","D2_CLIENTE")
      	@nLinha,PCol()+1 PSay (cAlias)->D2_LOJA Picture PesqPict("SD2","D2_LOJA")
       	nColuna:=PCol()+1
      Else
      	nColuna+=Len((cAlias)->D2_CLIENTE)+Len((cAlias)->D2_LOJA)+2
      Endif       
  	Endif
Endif
IF lImpFT	
	If MV_PAR01 == 1 .AND. nTamProd == 30
   		@nLinha,nColuna+15 PSay (cAlias)->D2_SERIE Picture PesqPict("SD2","D2_SERIE")   //parametro 1 ncoluna
    Else 
    	@nLinha,nColuna PSay (cAlias)->D2_SERIE Picture PesqPict("SD2","D2_SERIE")   //parametro 1 ncoluna
    EndIf
	@nLinha,PCol()+1 PSay (cAlias)->D2_DOC Picture PesqPict("SD2","D2_DOC")
	If MV_PAR01==3 //baseado na fatura
  	   @nLinha,PCol()+1 Psay (cAlias)->D2_ESPECIE Picture PesqPict("SD2","D2_ESPECIE")
  	Endif   
	nColuna:=PCol()+1
Else
	nColuna+=TamSX3("D2_DOC")[1]+TamSX3("D2_SERIE")[1]+2
	If MV_PAR01==3 //baseado na fatura
  	   nColuna+=TamSX3("D2_ESPECIE")[1]+1
  	Endif   
Endif
lImpFT:=.F.
@nLinha,nColuna PSay (cAlias)->D2_ITEM Picture PesqPict("SD2","D2_ITEM")
If MV_PAR01==3  //impressao baseada na fatura
   @nLinha,PCol()+1 PSay (cAlias)->D2_COD Picture PesqPict("SD2","D2_COD")
   @nLinha,PCol()+1 PSay (cAlias)->D2_UM Picture PesqPict("SD2","D2_UM")
   @nLinha,PCol()+1 PSay (cAlias)->D2_GRUPO Picture PesqPict("SD2","D2_GRUPO")
Else   
	@nLinha,PCol()+1 PSay Padr((cAlias)->D2_EMISSAO,10)
Endif    
If MV_PAR01 == 1 .AND. nTamProd == 30
	@nLinha,PCol()-5 PSay (cAlias)->D2_QUANT Picture PesqPict("SD2","D2_QUANT",nTamQ)
	@nLinha,PCol()-4   PSay (cAlias)->D2_TOTAL Picture PesqPict("SD2","D2_TOTAL",nTamV)
ElseIf MV_PAR01 == 3 .AND. nTamProd == 30
	@nLinha,PCol()-4 PSay (cAlias)->D2_QUANT Picture PesqPict("SD2","D2_QUANT",nTamQ) 
	@nLinha,PCol()   PSay (cAlias)->D2_TOTAL Picture PesqPict("SD2","D2_TOTAL",nTamV)
Else
	@nLinha,PCol()+1 PSay (cAlias)->D2_QUANT Picture PesqPict("SD2","D2_QUANT",nTamQ)  
	@nLinha,PCol()   PSay (cAlias)->D2_TOTAL Picture PesqPict("SD2","D2_TOTAL",nTamV)
EndIf
nCol:=Pcol()
Return(nCol)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ R681PV   ³ Autor ³ Marcello Gabriel      ³ Data ³ 08.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R681PV(lEnd)
Local cChave,cCond,cOrdem,cArqTrab,lRet,nLin,xZero,cOrd,nFat,nRem,lImpPed,lImpr
Local cChPed,cChRem,cChFat,nColPed,nColIRem,nColIPed,nColIFat
Local cAliasSC5,cQuery
Local nOrdSC5,nOrdSC6,nOrdSD2,nOrdSB1

nOrdSB1:=SB1->(IndexOrd())
SD2->(DbSetOrder(1))
nOrdSD2:=SD2->(IndexOrd())
SD2->(DbSetOrder(8))
nOrdSC6:=SC6->(IndexOrd())
SC6->(DbSetOrder(1))

DbSelectArea("SC5")
nOrdSC5:=IndexOrd()
lRet:=.F.
lImpPed:=.T.

#IFDEF TOP
cAliasSC5:="C5TMP"
If Select(cAliasSC5)<>0
   DbSelectArea(cAliasSC5)
   DbCloseArea()
Endif   
cQuery:="SELECT C5_FILIAL,C5_NUM,C5_EMISSAO,C5_CLIENTE,C5_LOJACLI"
cQuery+=",R_E_C_N_O_ RECNO FROM "+RetSqlName("SC5")+" "+cAliasSC5+" "
cQuery+="WHERE C5_FILIAL='"+xFilial("SC5")+"'"
If !Empty(mv_par02)
	cQuery+=" AND C5_CLIENTE >='"+mv_par02+"'"
Endif
If !Empty(mv_par03)
	cQuery+=" AND C5_CLIENTE <='"+mv_par03+"'"
Endif
If !Empty(mv_par08)
    cQuery+=" AND C5_EMISSAO>='"+DTOS(mv_par08)+"'"
Endif
If !Empty(mv_par09)
	cQuery+=" AND C5_EMISSAO<='"+DTOS(mv_par09)+"'"
Endif        
cQuery+=" AND D_E_L_E_T_<>'*' ORDER BY " 
Do Case
   Case aReturn[8]==1      // emissao
        cQuery+="C5_EMISSAO,C5_CLIENTE,C5_LOJACLI,C5_NUM"
        cOrdem:= "Padr("+(cAliasSC5)+"->C5_EMISSAO,10)"
        xZero:="//"
   Case aReturn[8]==2      // cliente
        cQuery+= "C5_CLIENTE,C5_EMISSAO,C5_NUM"
        cOrdem:= cAliasSC5+"->C5_CLIENTE+' '+"+cAliasSC5+"->C5_LOJACLI"
        xZero:=""
   OtherWise
EndCase
cQuery:=ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliasSC5,.F.,.T.)
TCSetField(cAliasSC5,"C5_EMISSAO","D",8,0)
#ELSE
cAliasSC5:="SC5"
Do Case
   Case aReturn[8] = 1      // emissao
        cChave:="C5_FILIAL+DTOS(C5_EMISSAO)+C5_CLIENTE+C5_LOJACLI+C5_NUM"
        cOrdem:= "Padr(SC5->C5_EMISSAO,10)"
        xZero:="//"
   Case aReturn[8] = 2      // cliente
        cChave:= "C5_FILIAL+C5_CLIENTE+DTOS(C5_EMISSAO)+C5_NUM"
        cOrdem:= "SC5->C5_CLIENTE+' '+SC5->C5_LOJACLI"
        xZero:=""
   OtherWise
EndCase
cCond:="C5_FILIAL=='"+xFilial("SC5")+"'" 
If !Empty(mv_par02)
	cCond+=" .AND. C5_CLIENTE >='"+mv_par02+"'"
Endif
If !Empty(mv_par03)
	cCond+=" .AND. C5_CLIENTE <='"+mv_par03+"'"
Endif
If !Empty(mv_par08)
    cCond+=" .AND. DTOS(C5_EMISSAO)>='"+DTOS(mv_par08)+"'"
Endif
If !Empty(mv_par09)
	cCond+=" .AND. DTOS(C5_EMISSAO)<='"+DTOS(mv_par09)+"'"
Endif       
cArqTrab := CriaTrab(Nil,.F.)
IndRegua("SC5",cArqTrab,cChave,,cCond,OemToAnsi(STR0009))  //"Selecionando pedidos"
#ENDIF
DbGoTop()
SetRegua(RecCount())
nLin:=80
nColPed:=0
cOrd:=xZero
m_pag:=1  
lImpr:=.F.
Do While !Eof() .and. !lEnd
	DbSelectArea("SC6")
	cChPed:=xFilial("SC6")+(cAliasSC5)->C5_NUM
	If DbSeek(cChPed)
		lImpr:=.T.
		While !Eof() .and. C6_FILIAL+C6_NUM==cChPed
			If(lImpr:=(SC6->C6_PRODUTO>=mv_par04))
				If !Empty(mv_par05)
					lImpr:=(SC6->C6_PRODUTO<=mv_par05)
				Endif
			Endif
			If lImpr
				If !Empty(mv_par06) .Or. !Empty(mv_par07)
					If (SB1->(MsSeek(xFilial("SB1")+SC6->C6_PRODUTO)))
						If (lImpr:=SB1->B1_GRUPO>=mv_par06)
							If !Empty(mv_par07)
								lImpr:=SB1->B1_GRUPO<=mv_par07
							Endif
						Endif
					Endif
				Endif
			Endif
			If lImpr
				If cOrd<>&cOrdem .Or. nLin>55
					nLin++
					If nLin>55
						M681CAB()
						nLin:=9
					Endif
					cOrd:=&cOrdem
					@nLin,000 PSay cOrd
					nColPed:=PCol()+2
					nColIPed:=PCol()+2
					lImpPed:=.T.
				Endif
				nRem:=(nFat:=0)
				nColIRem:=M681DPed(nLin,nColIPed,@lImpPed,cAliasSC5)+2
				cChRem:=xFilial("SD2")+SC6->C6_NUM+SC6->C6_ITEM
				DbSelectArea("SD2")
				dbSetOrder(8)			// Filial, Pedido de Vendas, Item do Pedido
				If DbSeek(cChRem)
					nRem:=0
					While !Eof() .and. D2_FILIAL+D2_PEDIDO+D2_ITEMPV==cChRem  //impressao das remisiones
						If D2_ITEMPV==SC6->C6_ITEM  .and. IsRemito(1,"SD2->D2_TIPODOC")  .and. D2_COD==SC6->C6_PRODUTO
							If nLin>55
								M681CAB()
								nLin:=9
								@nLin,000 PSay &cOrdem
								nColIRem:=M681DPed(nLin,nColIPed,@lImpPed,cAliasSC5)+2
							Endif    
							nColIFat:=M681DRem(nLin,nColIRem,.T.,"SD2")+2
							nRem++
							
							cRemito := SD2->D2_DOC
      						cItemRem := SD2->D2_ITEM 
							cChFat:=xFilial("SD2")+SD2->D2_CLIENTE+SD2->D2_LOJA+SD2->D2_SERIE+cRemito+cItemRem
							nReg := Recno()
							DbSelectArea("SD2")
							DbSetOrder(9)
							If DbSeek(cChFat)
								nFat:=0
								While !Eof() .and. (D2_FILIAL+D2_CLIENTE+D2_LOJA+D2_SERIREM+D2_REMITO+D2_ITEMREM)==cChFat
									If !IsRemito(1,"SD2->D2_TIPODOC")
										If nLin>55
											M681CAB()
											nLin:=9
											@nLin,000 PSay &cOrdem
											M681DPed(nLin,nColIPed,.T.,cAliasSC5)
										Endif
									 //	M681DRem(nLin,nColIrem,.T.,"SD2")
										M681DFat(nLin,nColIFat,.T.,"SD2")
										nLin++
										nFat++
									Endif
									DbSkip()
								Enddo
								If nFat==0
									nLin++
								Endif
							Else
							//	nColIFat:=M681DRem(nLin,nColIRem,.T.,"SD2")+2
								nLin++
							Endif
							DbSelectArea("SD2")
							DbGoTo(nReg)
							DbSetOrder(8)
						Endif
						DbSkip()
					Enddo
					If nRem==0
						//nColIFat:=M681DRem(nLin,nColIRem,.T.,"SD2")+2
						nColIFat:=147
					Endif
				Else
					//nColIFat:=M681DRem(nLin,nColIRem,.T.,"SD2")+2
					nColIFat:=147
				Endif
				cChFat:=xFilial("SD2")+SC6->C6_NUM+SC6->C6_ITEM
				nFat:=0    
				
				DbSelectArea("SD2")
				dbSetOrder(8)
				If DbSeek(cChFat)
					While !Eof() .and. D2_FILIAL+D2_PEDIDO+D2_ITEMPV==cChFat
						If Empty(D2_REMITO) .AND. !IsRemito(1,"SD2->D2_TIPODOC") .and. D2_COD == SC6->C6_PRODUTO
							If nLin>55
								M681CAB()
								nLin:=9
								@nLin,000 PSay &cOrdem
								M681DPed(nLin,nColIPed,.T.,cAliasSC5)
							Endif
							M681DFat(nLin,nColIFat,.T.,"SD2")
							nLin++
							nFat++
						Endif
						DbSkip()
					Enddo
					DbSelectArea("SC6")
				Endif
				If nFat==0 .and. nRem==0
					nLin++
				Endif
			Endif
			DbselectArea("SC6")
			DbSkip()
		Enddo
		
		DbSelectArea(cAliasSC5)
	Else
		nLin++
	Endif
	IncRegua()
	If lEnd
		DbGoBottom()
		@++nLin,000 Psay STR0011 //"Impressao cancelada pelo usuario."
	Endif
	DbSkip()
	lImpPed:=.T.
Enddo
If nLin>9 .and. nLin<80
	roda(cbcont,cbtxt,tamanho)
Endif
If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	Ourspool(wnrel)
Endif
MS_FLUSH()
SB1->(DbSetOrder(nOrdSB1))
SD2->(DbSetOrder(nOrdSD2))
SC6->(DbSetOrder(nOrdSC6))
#IFDEF TOP
DbSelectArea(cAliasSC5)
DbCloseArea()
#ELSE
RetIndex("SC5")
SC5->(DbSetOrder(nOrdSC5))
cArqTrab+=OrdBagExt()
If File(cArqTrab)
   Ferase(cArqTrab)
EndIf
#ENDIF
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ R681Rem  ³ Autor ³ Marcello Gabriel      ³ Data ³ 08.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R681Rem(lEnd)
Local cChave,cCond,cOrdem,cArqTrab,lRet,nLin,xZero,cOrd,nFat,cRem,lImpRem
Local cChPed,cChIFat,cChFat,nColRem,nColIPed
Local nOrdSC6,OrdSD2,nOrdSC5
Local cQuery,cAliasREM,cRemito,cItemRem

nOrdSC5:=SC5->(IndexOrd())
SC5->(DbSetOrder(1))
nOrdSC6:=SC6->(IndexOrd())
SC6->(DbSetOrder(1))
DbSelectArea("SD2")
nOrdSD2:=SD2->(IndexOrd())
lRet:=.F.
lImpRem:=.T.

#IFDEF TOP
cAliasREM:="REMTMP"
If Select(cAliasREM)<>0
   DbSelectArea(cAliasREM)
   DbCloseArea()
Endif   

cQuery:="SELECT D2_FILIAL,D2_SERIE,D2_DOC,D2_ITEM,D2_EMISSAO,D2_QUANT,D2_TOTAL,D2_ESPECIE"
cQuery+=",D2_COD,D2_UM,D2_GRUPO,D2_REMITO,D2_ITEMREM"
cQuery+=",D2_PEDIDO,D2_ITEMPV,D2_CLIENTE,D2_LOJA"
cQuery+=",R_E_C_N_O_ RECNO FROM "+RetSqlName("SD2")+" "+cAliasREM+" "
cQuery+="WHERE D2_FILIAL='"+xFilial("SD2")+"'"

If !Empty(mv_par02)
	cQuery+=" AND D2_CLIENTE >='"+mv_par02+"'"
Endif
If !Empty(mv_par03)
	cQuery+=" AND D2_CLIENTE <='"+mv_par03+"'"
Endif
If !Empty(mv_par04)
	cQuery+=" AND D2_COD>='"+mv_par04+"'"
Endif
If !Empty(mv_par05)
	cQuery+=" AND D2_COD<='"+mv_par05+"'"
Endif
If !Empty(mv_par06)
	cQuery+=" AND D2_GRUPO>='"+mv_par06+"'"
Endif
If !Empty(mv_par07)
	cQuery+=" AND D2_GRUPO<='"+mv_par07+"'"
Endif
If !Empty(mv_par08)
    cQuery+=" AND D2_EMISSAO>='"+DTOS(mv_par08)+"'"
Endif
If !Empty(mv_par09)
	cQuery+=" AND D2_EMISSAO<='"+DTOS(mv_par09)+"'"
Endif
cQuery+=" AND ("+IsRemito(3,'D2_TIPODOC')+ ")"
cQuery+=" AND D_E_L_E_T_<>'*' ORDER BY " 

Do Case
   Case aReturn[8] = 1      // emissao
        cQuery+="D2_EMISSAO,D2_CLIENTE,D2_LOJA,D2_SERIE,D2_DOC,D2_ITEM"
        cOrdem:= "Padr("+cAliasREM+"->D2_EMISSAO,10)"
        xZero:="//"
   Case aReturn[8] = 2      // cliente
        cQuery+="D2_CLIENTE,D2_LOJA,D2_EMISSAO,D2_SERIE,D2_DOC,D2_ITEM"
        cOrdem:= cAliasRem+"->D2_CLIENTE+' '+"+cAliasREM+"->D2_LOJA"
        xZero:=""
   OtherWise
EndCase

cQuery:=ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliasREM,.F.,.T.)
TCSetField(cAliasREM,"D2_EMISSAO","D",8,0)
#ELSE
cAliasREM:="SD2"
Do Case
   Case aReturn[8] = 1      // emissao
        cChave:="D2_FILIAL+DTOS(D2_EMISSAO)+D2_CLIENTE+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
        cOrdem:= "Padr(SD2->D2_EMISSAO,10)"
        xZero:="//"
   Case aReturn[8] = 2      // cliente
        cChave:="D2_FILIAL+D2_CLIENTE+DTOS(D2_EMISSAO)+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
        cOrdem:= "SD2->D2_CLIENTE+' '+SD2->D2_LOJA"
        xZero:=""
   OtherWise
EndCase

cCond:="D2_FILIAL=='"+xFilial("SD2")+"'" 
If !Empty(mv_par02)
	cCond+=" .AND. D2_CLIENTE >='"+mv_par02+"'"
Endif
If !Empty(mv_par03)
	cCond+=" .AND. D2_CLIENTE <='"+mv_par03+"'"
Endif
If !Empty(mv_par04)
	cCond+=" .AND. D2_COD>='"+mv_par04+"'"
Endif
If !Empty(mv_par05)
	cCond+=" .AND. D2_COD<='"+mv_par05+"'"
Endif
If !Empty(mv_par06)
	cCond+=" .AND. D2_GRUPO>='"+mv_par06+"'"
Endif
If !Empty(mv_par07)
	cCond+=" .AND. D2_GRUPO<='"+mv_par07+"'"
Endif
If !Empty(mv_par08)
    cCond+=" .AND. DTOS(D2_EMISSAO)>='"+DTOS(mv_par08)+"'"
Endif
If !Empty(mv_par09)
	cCond+=" .AND. DTOS(D2_EMISSAO)<='"+DTOS(mv_par09)+"'"
Endif
cCond += ".And. "+IsRemito(2,'SD2->D2_TIPODOC')
cArqTrab := CriaTrab(Nil,.F.)
IndRegua("SD2",cArqTrab,cChave,,cCond,OemToAnsi(STR0012+cRetTitle))  //"Selecionando remisiones"
nIndex := RetIndex("SD2")
SD2->( dbSetIndex( cArqTrab + OrdBagExt() ) )
dbSetOrder(nIndex+1)

#ENDIF
dbgotop()
SetRegua(RecCount())
nLin   := 80
nColRem:= 0
cOrd   := xZero
m_pag  := 1
cRem   := ""

Do While !Eof()
	If cOrd<>&cOrdem .or. nLin>55
 		nLin++
	  	If nLin>55
		  	M681CAB()
		   nLin:=9
	   Endif
		@nLin,000 PSay &cOrdem
		nColRem:=PCol()+2
		cOrd:=&cOrdem
		lImpRem:=.T.
		cRem:=""
	Endif
	If cRem<>D2_DOC
		lImpRem:=.T.
	 	cRem:=D2_DOC
	Endif
	nColIPed:=M681DRem(nLin,nColRem,@lImpRem,cAliasREM)+2
	nColIFat:=nColIPed
	cChPed:=xFilial("SC6")+(cAliasREM)->D2_PEDIDO+(cAliasREM)->D2_ITEMPV
 	nFat:=0
	DbSelectArea("SC6")
	If DbSeek(cChPed)
 		SC5->(DbSeek(xFilial("SC5")+(cAliasREM)->D2_PEDIDO))
   		nColIFat:=M681DPed(nLin,nColIPed,.T.,"SC5")+2
     	If nFat==0
      		cRemito := (cAliasREM)->D2_DOC
      		cItemRem := (cAliasREM)->D2_ITEM 
			cChIFat:=xFilial("SD2")+(cAliasREM)->D2_CLIENTE+(cAliasREM)->D2_LOJA+(cAliasREM)->D2_SERIE+cRemito+cItemRem
       		If Select("SD2") == 0
				ChkFile("__SD2",.F.,"SD2")
			Else
				dbSelectArea("SD2")
			Endif
			Dbsetorder(9)
         	nReg := Recno()
         	If DbSeek(cChIFat)
				While (D2_FILIAL+D2_CLIENTE+D2_LOJA+D2_SERIREM+D2_REMITO+D2_ITEMREM)==cChIFat
					If nLin>55
						M681CAB()
						nLin:=9
						@nLin,000 PSay &cOrdem
						nColIFat:=M681DRem(nLin,nColRem,.T.,cAliasREM)+2
					Endif
					M681DFat(nLin++,nColIFat,.T.,"SD2")
					nFat++
               DbSkip()
          	Enddo
         Endif
         #IFNDEF TOP
         dbSetOrder(nIndex+1)
         dbGoto(nReg)
         #ENDIF
       Endif   
    Endif   
    If nFat==0
       nLin++
    Endif   
    DbSelectArea(cAliasREM)
	IncRegua()
	If lEnd
	   DbGoBottom()
	   @++nLin,000 Psay STR0011 //"Impressao cancelada pelo usuario."
	Endif
	DbSkip()
Enddo

If nLin>9 .and. nLin<80
	roda(cbcont,cbtxt,tamanho)
Endif
If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	Ourspool(wnrel)
Endif
MS_FLUSH()
SD2->(DbSetOrder(nOrdSD2))
SC6->(DbSetOrder(nOrdSC6))
SC5->(DbSetOrder(nOrdSC5))
#IFDEF TOP
DbSelectArea(cAliasREM)
DbCloseArea()
#ELSE
RetIndex("SD2")
SD2->(DbSetOrder(nOrdSD2))
cArqTrab+=OrdBagExt()
If File(cArqTrab)
   Ferase(cArqTrab)
EndIf
#ENDIF
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ R681Fat  ³ Autor ³ Marcello Gabriel      ³ Data ³ 08.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R681Fat(lEnd)
Local cChave,cCond,cOrdem,cArqTrab,lRet,nLin,xZero,cOrd,nFat,cFat,lImpFat
Local cChPed,cChIFat,cChFat,nColIRem,nColIPed,nColFat
Local nOrdSC6,nOrdSD2,nOrdSC5
Local cAliasSD2,cQuery,cRemito,cItemRem,cSerieRem,cCliRem,	cLojaRem,cCodRem

nOrdSC6:=SC6->(IndexOrd())
SC6->(DbSetOrder(1))
nOrdSC5:=SC5->(IndexOrd())
SC5->(DbSetOrder(1))
DbSelectArea("SD2")
nOrdSD2:=IndexOrd()
lRet:=.F.
lImpFat:=.T.

#IFDEF TOP
cAliasSD2:="D2TMP"
If Select(cAliasSD2)<>0
   DbSelectArea(cAliasSD2)
   DbCloseArea()
Endif   
cQuery:="SELECT D2_FILIAL,D2_SERIE,D2_DOC,D2_ITEM,D2_EMISSAO,D2_QUANT,D2_TOTAL,D2_ESPECIE"
cQuery+=",D2_COD,D2_UM,D2_GRUPO,D2_REMITO,D2_ITEMREM,D2_SERIREM"
cQuery+=",D2_PEDIDO,D2_ITEMPV,D2_CLIENTE,D2_LOJA"
cQuery+=",R_E_C_N_O_ RECNO FROM "+RetSqlName("SD2")+" "+cAliasSD2+" "
cQuery+="WHERE D2_FILIAL='"+xFilial("SD2")+"'"
If !Empty(mv_par02)
	cQuery+=" AND D2_CLIENTE >='"+mv_par02+"'"
Endif
If !Empty(mv_par03)
	cQuery+=" AND D2_CLIENTE <='"+mv_par03+"'"
Endif
If !Empty(mv_par04)
	cQuery+=" AND D2_COD>='"+mv_par04+"'"
Endif
If !Empty(mv_par05)
	cQuery+=" AND D2_COD<='"+mv_par05+"'"
Endif
If !Empty(mv_par06)
	cQuery+=" AND D2_GRUPO>='"+mv_par06+"'"
Endif
If !Empty(mv_par07)
	cQuery+=" AND D2_GRUPO<='"+mv_par07+"'"
Endif
If !Empty(mv_par08)
    cQuery+=" AND D2_EMISSAO>='"+DTOS(mv_par08)+"'"
Endif
If !Empty(mv_par09)
	cQuery+=" AND D2_EMISSAO<='"+DTOS(mv_par09)+"'"
Endif
cQuery+=" AND NOT ("+IsRemito(3,'D2_TIPODOC')+ ")"
cQuery+=" AND D_E_L_E_T_<>'*' ORDER BY " 
Do Case
   Case aReturn[8] = 1      // emissao
        cQuery+="D2_EMISSAO,D2_CLIENTE,D2_LOJA,D2_SERIE,D2_DOC,D2_ITEM"
        cOrdem:= "Padr("+cAliasSD2+"->D2_EMISSAO,10)"
        xZero:="//"
   Case aReturn[8] = 2      // cliente
        cQuery+="D2_CLIENTE,D2_LOJA,D2_EMISSAO,D2_SERIE,D2_DOC,D2_ITEM"
        cOrdem:= cAliasSD2+"->D2_CLIENTE+' '+"+cAliasSD2+"->D2_LOJA"
        xZero:=""
   OtherWise
EndCase
cQuery:=ChangeQuery(cQuery)
dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery),cAliasSD2, .F., .T.)
TCSetField(cAliasSD2,"D2_EMISSAO","D",8,0)
#ELSE
cAliasSD2:="SD2"
Do Case
   Case aReturn[8] = 1      // emissao
        cChave:="D2_FILIAL+DTOS(D2_EMISSAO)+D2_CLIENTE+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
        cOrdem:= "Padr(SD2->D2_EMISSAO,10)"
        xZero:="//"
   Case aReturn[8] = 2      // cliente
        cChave:="D2_FILIAL+D2_CLIENTE+DTOS(D2_EMISSAO)+D2_LOJA+D2_SERIE+D2_DOC+D2_ITEM"
        cOrdem:= "SD2->D2_CLIENTE+' '+SD2->D2_LOJA"
        xZero:=""
   OtherWise
EndCase
cCond:="D2_FILIAL=='"+xFilial("SD2")+"'" 
If !Empty(mv_par02)
	cCond+=" .AND. D2_CLIENTE >='"+mv_par02+"'"
Endif
If !Empty(mv_par03)
	cCond+=" .AND. D2_CLIENTE <='"+mv_par03+"'"
Endif
If !Empty(mv_par04)
	cCond+=" .AND. D2_COD>='"+mv_par04+"'"
Endif
If !Empty(mv_par05)
	cCond+=" .AND. D2_COD<='"+mv_par05+"'"
Endif
If !Empty(mv_par06)
	cCond+=" .AND. D2_GRUPO>='"+mv_par06+"'"
Endif
If !Empty(mv_par07)
	cCond+=" .AND. D2_GRUPO<='"+mv_par07+"'"
Endif
If !Empty(mv_par08)
    cCond+=" .AND. DTOS(D2_EMISSAO)>='"+DTOS(mv_par08)+"'"
Endif
If !Empty(mv_par09)
	cCond+=" .AND. DTOS(D2_EMISSAO)<='"+DTOS(mv_par09)+"'"
Endif  
cCond += ".And. !("+IsRemito(2,'SD2->D2_TIPODOC')+")"			

cArqTrab := CriaTrab(Nil,.F.)
IndRegua("SD2",cArqTrab,cChave,,cCond,OemToAnsi(STR0012+" "+STR0003))  //"Selecionando faturas"
nIndex := RetIndex("SD2")
SD2->( dbSetIndex( cArqTrab + OrdBagExt() ) )
dbSetOrder(nIndex+1)

#ENDIF
DbGoTop()
SetRegua(RecCount())
nLin   := 80
nColFat:= 0
cOrd   := xZero
m_pag  := 1
cFat   := ""
Do While !Eof()
	If cOrd<>&cOrdem .Or. nLin>55
		nLin++
  		If nLin>55
		  	M681CAB()
		   nLin:=9
		   cOrd:=xZero
	   Endif
		@nLin,000 PSay &cOrdem
		nColFat:=PCol()+2
		cOrd:=&cOrdem
		lImpFat:=.T.
		cFat:=""
	Endif
	If D2_DOC<>cFat
	   lImpFat:=.T.
	   cFat:=D2_DOC
	Endif
	nColIRem:=M681DFat(nLin,nColFat,@lImpFat,cAliasSD2)+2
	#IFDEF TOP
		SD2->(DbSetOrder(3))
		SD2->(DBSeek(xFilial("SD2")+(cAliasSD2)->D2_REMITO+(cAliasSD2)->D2_SERIREM+(cAliasSD2)->D2_CLIENTE + (cAliasSD2)->D2_LOJA + (cAliasSD2)->D2_COD + (cAliasSD2)->D2_ITEMREM))
		nColIPed:=M681DRem(nLin,nColIRem,.T.,"SD2")+2
		If !Empty((cAliasSD2)->D2_PEDIDO)
			SC6->(DBSeek(xFilial("SC6")+(cAliasSD2)->D2_PEDIDO+(cAliasSD2)->D2_ITEMPV))
			SC5->(DBSeek(xFilial("SC5")+(cAliasSD2)->D2_PEDIDO))
		Else
			SC6->(DBSeek(xFilial("SC6")+SD2->D2_PEDIDO+SD2->D2_ITEMPV))
			SC5->(DBSeek(xFilial("SC5")+SD2->D2_PEDIDO))
		EndIf	
	#ELSE
  		cRemito 	:= (cAliasSD2)->D2_REMITO
	  	cItemRem 	:= (cAliasSD2)->D2_ITEMREM
  		cSerieRem   := (cAliasSD2)->D2_SERIREM
	  	cCliRem		:= (cAliasSD2)->D2_CLIENTE
  		cLojaRem	:= (cAliasSD2)->D2_LOJA   
	  	cCodRem		:= (cAliasSD2)->D2_COD
     	If Select("SD2") == 0
			ChkFile("__SD2",.F.,"SD2")
		Else
			dbSelectArea("SD2")
		Endif
		DbSetOrder(3)        
		nReg := Recno()
		DBSeek(xFilial("SD2")+cRemito+cSerieRem+cCliRem+cLojaRem+cCodRem+cItemRem)
		nColIPed:=M681DRem(nLin,nColIRem,.T.,"SD2")+2
	 	dbSetOrder(nIndex+1)
	 	
		If !Empty((cAliasSD2)->D2_PEDIDO)
			SC6->(DBSeek(xFilial("SC6")+(cAliasSD2)->D2_PEDIDO+(cAliasSD2)->D2_ITEMPV))
			SC5->(DBSeek(xFilial("SC5")+(cAliasSD2)->D2_PEDIDO))
		Else
			SC6->(DBSeek(xFilial("SC6")+SD2->D2_PEDIDO+SD2->D2_ITEMPV))
			SC5->(DBSeek(xFilial("SC5")+SD2->D2_PEDIDO))
		EndIf	
		dbGoto(nReg)
	#ENDIF
   M681DPed(nLin,nColIPed,.T.,"SC5")
   DbSelectArea(cAliasSD2)
	nLin++
	IncRegua()
	If lEnd
	   DbGoBottom()
	   @++nLin,000 Psay STR0011 //"Impressao cancelada pelo usuario."
	Endif
	DbSkip()
Enddo
If nLin>9 .and. nLin<80
	roda(cbcont,cbtxt,tamanho)
Endif
If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	Ourspool(wnrel)
Endif
MS_FLUSH()

SC6->(DbSetOrder(nOrdSC6))
SC5->(DbSetOrder(nOrdSC5))
#IFDEF TOP
DbSelectArea(cAliasSD2)
DbCloseArea()
#ELSE
RetIndex("SD2")
SD2->(DbSetOrder(nOrdSD2))
cArqTrab+=OrdBagExt()
If File(cArqTrab)
   Ferase(cArqTrab)
EndIf
#ENDIF
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ MR681TIT ³ Autor ³ Marcello Gabriel      ³ Data ³ 09.10.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Montagem do titulo do relatorio (antes e depois das pergs) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR681                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MR681Tit()
If MV_PAR01 = 1        // PV/RM/FT
	Titulo := OemToAnsi(STR0001)+OemToAnsi(STR0002)+"/"+cRetTitle+"/"+OemToAnsi(STR0003) //"Relacao de pedidos de venda/remisiones/faturas 
ElseIf MV_PAR01 = 2    // RM/PV/FT
	Titulo := OemToAnsi(STR0001)+cRetTitle+"/"+OemToAnsi(STR0002)+"/"+OemToAnsi(STR0003) //"Relacao de remisiones/pedidos de venda/faturas
ElseIf MV_PAR01 = 3    // FT/RM/PV
	Titulo := OemToAnsi(STR0001)+OemToAnsi(STR0003)+"/"+cRetTitle+"/"+OemToAnsi(STR0002)  //"Relacao de faturas/remisiones/pedidos de venda
EndIf
Return .t.
