#INCLUDE "MATR765.CH"  
#INCLUDE "FIVEWIN.CH"
             

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MATR765  ³ Autor ³ Marco Bianchi         ³ Data ³ 17/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Curva ABC de Consumo de Vendas por Cliente.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAFAT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MATR765()

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	MATR765R3()
EndIf

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³ Marco Bianchi         ³ Data ³ 17/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local oReport
Local oRegiao
Local oCliente
Local oProd
Local oValores

#IFDEF TOP
	Local cAliasSJ3 := GetNextAlias()
#ELSE	
	Local cAliasSJ3 := "SJ3"
#ENDIF	

Private aValores := {0,0,0,0,0,0,0,0,0,0,0,0}
Private nG	  	 := 0


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao dos fontes      ³
//³ SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
//| cliente, assim verificando a necessidade de uma atualizacao     |
//| nestes fontes. NAO REMOVER !!!							        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !(FindFunction("SIGACUS_V") .And. SIGACUS_V() >= 20050512)
	Final("Atualizar patch do programa SIGACUS.PRW !!!")
EndIf
If !(FindFunction("SIGACUSA_V") .And. SIGACUSA_V() >= 20050512)
	Final("Atualizar patch do programa SIGACUSA.PRX !!!")
EndIf
If !(FindFunction("SIGACUSB_V") .And. SIGACUSB_V() >= 20060920)
	Final("Atualizar patch do programa SIGACUSB.PRX !!!")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MATR765",STR0027,"MTR765", {|oReport| ReportPrint(oReport,cAliasSJ3,oRegiao,oCliente,oProd,oValores)},STR0028 + " " + STR0029 + " " + STR0030)	// "Curva ABC de Estoque - Ordem"###"Relatório para mostrar as quantidades de vendas (consumos) de "###"produtos no estoque por regiao e por cliente."###"Este relatorio deverá ser impresso no formulário de 132 colunas."
oReport:SetLandscape() 
oReport:SetTotalInLine(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grupo de Perguntas                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³                                                                        ³
//³                      Definicao das Secoes                              ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 01 - Regiao                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oRegiao := TRSection():New(oReport,STR0056,{"SJ3","TRB"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)	// "Regiao"
oRegiao:SetTotalInLine(.F.)
oReport:Section(1):SetHeaderSection(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 02 - Cliente                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oCliente := TRSection():New(oRegiao,STR0057,{},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)			// "Cliente"
oCliente:SetTotalInLine(.F.)
oReport:Section(1):Section(1):SetHeaderSection(.T.)
oReport:Section(1):Section(1):SetHeaderBreak()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 03 - Produto                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oProd := TRSection():New(oCliente,STR0058,{},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)			// "Produto"
oProd:SetTotalInLine(.F.)
oReport:Section(1):Section(1):Section(1):SetHeaderPage()


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 04 - Valores Mensais                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oValores := TRSection():New(oProd,STR0059,{"TRB","SJ3","SB1","SA1"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)			// "Valores Mensais"
oValores:SetTotalInLine(.F.)
oReport:Section(1):Section(1):Section(1):Section(1):SetHeaderPage()


oReport:Section(1):SetEditCell(.F.)

oReport:Section(1):Section(1):SetEdit(.F.)
oReport:Section(1):Section(1):Section(1):SetEdit(.F.)
oReport:Section(1):Section(1):Section(1):Section(1):SetEdit(.F.)


Return(oReport)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Eduardo Riera          ³ Data ³04.05.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,cAliasSJ3,oRegiao,oCliente,oProd,oValores)


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFNDEF TOP
	Local cCondicao := ""
#ENDIF

Local i
Local nI := 0
Local cMesRela, cMesInic
Local nDecPrec, aCampos, cArqTrab, cKey, cIndex
Local cProduto, aRegQtde, cRegiaoC, nPrecoUn, aAcumVal
Local nQtdCons, nNoMeses, dDatCons, cAnoAuxi, nConsMed, nTtGeral
Local nElement, nPorcPrd, nAcPorce, nQtdes, cClasse
Local nContaIt, aTam:=0
Local cInd
Local cDescProd:=""
Local nTotCli  :=0
Local nTotAcum := 0
Local nMes
Local aNomeMes := {STR0042,STR0043,STR0044,STR0045,STR0046,STR0047,STR0048,STR0049,STR0050,STR0051,STR0052,STR0053}		//"Jan"###"Fev"###"Mar"###"Abr"###"Mai"###"Jun"###"Jul"###"Ago"###"Set"###"Out"###"Nov"###"Dez"
Local aCabec   := {"","","","","","","","","","","",""}
Local nTamSXG  := TamSXG("001")			// Grupo de Cliente
Local nTamSXG2 := TamSXG("002")			// Grupo de Loja

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa o titulo do campo J3_CLIENTE (cliente/concessionaria).  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek( "J3_CLIENTE" )
dbSetOrder(1)
oReport:SetTitle(oReport:Title() + " - " + AllTrim(X3Titulo()) + STR0031 +  " - " + GetMv("MV_MOEDA" + STR(mv_par13,1)) )	// "Curva ABC de Estoque - Ordem de "###" / % de Participacao"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o mes/ano para o cabecalho.                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cMesRela := StrZero(mv_par03,2)
cAnoRela := StrZero(mv_par04,4)
For nI := 11 To 0 Step -1
	nMes := Val(StrZero(mv_par03,2)) - nI
	If nMes < 1
		nMes := 12 + nMes
		cAno := Str(Val(StrZero(mv_par04,4))-1,4)
	Else
		cAno := StrZero(mv_par04,4)
	Endif
	aCabec[12-nI] := aNomeMes[nMes] + "/" + cAno
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³                                                                        ³
//³                     Definicao das Celulas                              ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 01 - Regiao                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRCell():New(oRegiao,"REGIAO"		,"TRB"		,RetTitle("J3_REGVEND"	),/*Picture*/,11,/*lPixel*/,{|| STR0032 + TRB->REGIAO 					 		})	// "Regiao: "
TRCell():New(oRegiao,"X5_DESCRI"	,"SX5"		,/*Titulo*/				 ,/*Picture*/,50,/*lPixel*/,{|| AllTrim(SX5->X5_DESCRI)						})	// Descricao da Regiao
TRCell():New(oRegiao,"NPORCPRD"		,/*Tabela*/	,STR0033				 ,/*Picture*/,21,/*lPixel*/,{|| STR0033 + Transform(nPorcPrd,"@E 999.99") + "%"})	// "Participacao: "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 02 - Cliente                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRCell():New(oCliente,"CLIENTE"		,"TRB"		,RetTitle("J3_CLIENTE"	),/*Picture*/					,11						 	,/*lPixel*/,{|| TRB->CLIENTE 	})	// Codigo do Cliente
TRCell():New(oCliente,"NOMECLI"		,"SA1"		,RetTitle("A1_NOME"		),/*Picture*/					,50							,/*lPixel*/,{|| SA1->A1_NOME	})	// Nome do Cliente
TRCell():New(oCliente,"NTOTCLI"		,/*Tabela*/	,STR0054				 ,PesqPict("SJ3","J3_VALR01")	,TamSX3("J3_VALR01"	)[1]	,/*lPixel*/,{|| nTotCli			})	// Valor do Consumo Medio por Cliente (C.M.M * Vl.Unit.)
TRCell():New(oCliente,"NPORCPRD"	,"TRB"		,STR0055				 ,"@E 999.99"			 		,06						 	,/*lPixel*/,{|| nPorcPrd		})	// "%Participacao" % de participacao do cliente em relacao ao total da regiao


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 03 - Produtos                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRCell():New(oProd,"PRODUTO"	,"TRB"		,RetTitle("J3_PRODUTO")		,PesqPict("SJ3","J3_PRODUTO"	),TamSX3("J3_PRODUTO"	)[1]	,/*lPixel*/,{|| TRB->PRODUTO								})	// Codigo do Produto
TRCell():New(oProd,"CCLASSE"	,/*Tabela*/	,STR0037				 	,PesqPict("SJ3","J3_FATVEN"		),TamSX3("J3_FATVEN"	)[1]	,/*lPixel*/,{|| cClasse   	 								})	// Classe
TRCell():New(oProd,"CDESCPROD"	,"SB1"		,RetTitle("B1_DESC"	)		,PesqPict("SB1","B1_DESC"		),TamSX3("B1_DESC"		)[1]	,/*lPixel*/,{|| cDescProd 									})	// Descricao do Produto
TRCell():New(oProd,"VAL_UNIT"	,"TRB"		,STR0034				 	,PesqPict("SJ3","J3_VALR01"		),TamSX3("J3_VALR01"	)[1]	,/*lPixel*/,{|| xMoeda(TRB->VAL_UNIT,1,mv_par13,ddatabase)	})	// "Valor Unitario"
TRCell():New(oProd,"PROCPROD"	,"TRB"		,STR0035				 	,"@E 999.99"			 	 	 ,06							,/*lPixel*/,{|| nPorcPrd									})	// "% Part"
TRCell():New(oProd,"NACPORCE"	,/*Tabela*/	,STR0036				 	,"@E 999.99"			 	 	 ,06							,/*lPixel*/,{|| nAcPorce 									})	// "% Acum"
            
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao 04 - Valores Mensais                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRCell():New(oValores,"NCOL01"		,/*Tabela*/	,aCabec[01]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[01] 	})
TRCell():New(oValores,"NCOL02"		,/*Tabela*/	,aCabec[02]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[02] 	})
TRCell():New(oValores,"NCOL03"		,/*Tabela*/	,aCabec[03]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[03] 	})
TRCell():New(oValores,"NCOL04"		,/*Tabela*/	,aCabec[04]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[04] 	})
TRCell():New(oValores,"NCOL05"		,/*Tabela*/	,aCabec[05]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[05]	})
TRCell():New(oValores,"NCOL06"		,/*Tabela*/	,aCabec[06]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[06] 	})
TRCell():New(oValores,"NCOL07"		,/*Tabela*/	,aCabec[07]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[07] 	})
TRCell():New(oValores,"NCOL08"		,/*Tabela*/	,aCabec[08]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[08] 	})
TRCell():New(oValores,"NCOL09"		,/*Tabela*/	,aCabec[09]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[09] 	})
TRCell():New(oValores,"NCOL10"		,/*Tabela*/	,aCabec[10]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[10] 	})
TRCell():New(oValores,"NCOL11"		,/*Tabela*/	,aCabec[11]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[11] 	})
TRCell():New(oValores,"NCOL12"		,/*Tabela*/	,aCabec[12]	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| aValores[12] 	})
TRCell():New(oValores,"QTDECM"		,"TRB"		,STR0038	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| TRB->QTDECM 	})	// "C.M.M."
TRCell():New(oValores,"NTOTACUM"	,/*Tabela*/	,STR0039	,PesqPict("SJ3","J3_QUAR01"),TamSX3("J3_QUAR01")[1],/*lPixel*/,{|| nTotAcum 		})	// "Tot.Cons."

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao das Quebras: por Regiao e por Cliente                        ³
//³ Estas quebras sao validadas sempre na execucao de um PrintLine         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oBreak1 := TRBreak():New(oRegiao	,oRegiao:Cell("REGIAO"		),"TOTAL DA REGIAO " 	,.F.)
oBreak2 := TRBreak():New(oCliente	,oCliente:Cell("CLIENTE"	),"TOTAL DO CLIENTE " 	,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Transforma parametros Range em expressao SQL                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MakeSqlExpr(oReport:uParam)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Os 12 meses estao contidos em 1 ano ou 2 anos.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par03 == 12
	cAnoAnte := StrZero(mv_par04,4)
	cMesInic := "01"
Else
	cAnoAnte := StrZero((mv_par04-1),4)
	cMesInic := StrZero((mv_par03+1),2)
Endif
If mv_par08 == 2
	cTipPrec := "S"      		// preco standard
ElseIf mv_par08 == 3
	cTipPrec := "C"      		// preco de tabela informado no cad. de cliente
ElseIf mv_par08 == 4
	Pergunte("MR765B",.F.)    	// preco de tabela informado pelo usuario
	Pergunte("MR765B",.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01 // Nro. Tabela                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTipPrec := mv_par01
	Pergunte("MTR765",.F.)
Else
	cTipPrec := "M"      		// preco medio
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa o numero de casas decimais do valor e qtde.         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTam:=TamSX3("J3_VALR01")
nDecPrec:=aTam[2]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define estrutura e gera o arquivo de trabalho                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTam:=TamSX3("J3_REGVEND")
aCampos := {{ "REGIAO"  , "C",aTam[1],aTam[2]							},;
{ "PRODUTO" , "C", 15						, 0 						},;
{ "CLIENTE" , "C", nTamSXG[1] 				, 0 						},;
{ "LOJACLI" , "C", nTamSXG2[1]				, 0 						},;
{ "VALCHAV" , "C", 16						, 0 						},;
{ "VAL_UNIT", "N", 16						, nDecPrec 					},;
{ "VALCMED" , "N", 16						, nDecPrec 					},;
{ "MESCONS" , "N", 02						, 0							},;
{ "QTDE01"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE02"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE03"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE04"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE05"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE06"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE07"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE08"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE09"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE10"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE11"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDE12"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	},;
{ "QTDECM"  , "N", TamSx3("J3_QUAR01")[1]	, TamSx3("J3_QUAR01")[2]	}}

cArqTrab := CriaTrab(aCampos,.T.)
Use &cArqTrab Alias "TRB" New
cKey := "REGIAO + CLIENTE + LOJACLI + PRODUTO"
IndRegua("TRB",cArqTrab,cKey,,,STR0041)		//"Selecionando Registros..."


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtros                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SJ3")		// Acumulados Mensais
dbSetOrder(3)      		// Filial,Regial,Cliente,Loja,Produto,Ano

#IFDEF TOP
	oReport:Section(1):BeginQuery()
	BeginSql Alias cAliasSJ3
	SELECT *
	FROM %Table:SJ3% SJ3
	WHERE J3_FILIAL = %xFilial:SJ3%  AND
	J3_REGVEND >= %Exp:mv_par01% AND
	J3_REGVEND <= %Exp:mv_par02% AND
	J3_CLIENTE >= %Exp:mv_par09% AND
	J3_CLIENTE <= %Exp:mv_par10% AND
	(J3_ANO = %Exp:StrZero(mv_par04,4)% OR J3_ANO = %Exp:cAnoAnte%) AND
	SJ3.%notdel%
	ORDER BY J3_FILIAL,J3_REGVEND,J3_PRODUTO,J3_ANO,J3_FATVEN
	EndSql
	oReport:Section(1):EndQuery()
#ELSE
	cIndex := CriaTrab(Nil,.F.)
	cKey   := "J3_FILIAL + J3_REGVEND + J3_PRODUTO + J3_ANO + J3_FATVEN"
	cCondicao +=" J3_REGVEND >= '"+mv_par01+"' .And. J3_REGVEND <= '"+mv_par02+"' .And. "
	cCondicao +=" J3_CLIENTE >= '"+mv_par09+"' .And. J3_CLIENTE <= '"+mv_par10+"' .And. "
	cCondicao +=" 	(J3_ANO == '"+StrZero(mv_par04,4)+"' .Or. J3_ANO == '"+cAnoAnte	+"') "
	oReport:Section(1):SetFilter(cCondicao,cKey)
	dbGoTop()
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para movimentacao do cursor.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cAliasSJ3)
oReport:SetMeter(RecCount())		// Total de Elementos da regua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo de trabalho com dados do SJ3 - acum. mensais.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRegQtde := {}
While !oReport:Cancel() .And. (cAliasSJ3)->(!Eof()) .And. (cAliasSJ3)->J3_FILIAL == xFilial("SJ3")

	
	cRegiaoC := (cAliasSJ3)->J3_REGVEND
	While !oReport:Cancel() .And. (cAliasSJ3)->J3_REGVEND == cRegiaoC .And. (cAliasSJ3)->J3_FILIAL == xFilial("SJ3");
		.And. (cAliasSJ3)->(!Eof())
		
		cCliente := (cAliasSJ3)->J3_CLIENTE
		cLojaCli := (cAliasSJ3)->J3_LOJA
		dbSelectArea("SA1")
		dbSeek( xFilial("SA1") + cCliente + cLojaCli )
		Aadd( aRegQtde, { cRegiaoC, cCliente, cLojaCli, 0 } )
		While (cAliasSJ3)->J3_CLIENTE == cCliente .And. (cAliasSJ3)->J3_LOJA == cLojaCli .And. ;
			(cAliasSJ3)->J3_REGVEND == cRegiaoC .And. (cAliasSJ3)->J3_FILIAL == xFilial("SJ3") ;
			.And. (cAliasSJ3)->(!Eof())
			
			cProduto := (cAliasSJ3)->J3_PRODUTO
			dbSelectArea("SB1")
			dbSeek( xFilial("SB1") + cProduto )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atribui preco unitario conforme escolha do usuario (parametro). ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cTipPrec == "S"       // preco standard
				nPrecoUn := xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),1,mv_par13,ddatabase)
			ElseIf cTipPrec == "M" .Or. cTipPrec == "1" .Or. (cTipPrec > "7" .And. cTipPrec <= "9")
				nPrecoUn := xMoeda(SB1->B1_PRV1,1,mv_par13,ddatabase)
			ElseIf cTipPrec == "C"   // tabela informada no cad. de cliente
				If Empty(SA1->A1_TABELA) .Or. SA1->A1_TABELA == "1" 
					nPrecoUn := xMoeda(SB1->B1_PRV1,1,mv_par13,ddatabase)
				Else                                                   
					nPrecoUn := MaTabPrVen(SA1->A1_TABELA,cProduto,0,SA1->A1_COD,SA1->A1_LOJA,mv_par13,ddatabase)					
				Endif
			Else
				dbSelectArea("SA1")
				dbSeek( xFilial("SA1") + (cAliasSJ3)->J3_CLIENTE + (cAliasSJ3)->J3_LOJA )
				nPrecoUn := MaTabPrVen(cTipPrec,cProduto,0,SA1->A1_COD,SA1->A1_LOJA,mv_par13,ddatabase)									
			Endif
			
			aAcumVal := { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0}
			nQtdCons := 0   // qtde. consumida do produto no periodo
			nNoMeses := 0   // nro. de meses do produto para calculo
			
			If cAnoAnte == cAnoRela
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Os 12 meses estao em 1 unico ano.                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				While (cAliasSJ3)->J3_PRODUTO==cProduto .And. (cAliasSJ3)->J3_CLIENTE == cCliente ;
					.And. (cAliasSJ3)->J3_LOJA==cLojaCli .And. (cAliasSJ3)->J3_REGVEND == cRegiaoC ;
					.And. (cAliasSJ3)->J3_FILIAL == xFilial("SJ3") .And. (cAliasSJ3)->(!Eof())
					
					For i := 1 To 12
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica data de inicio de consumo, informado no SB1.   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dDatCons := Ctod("01/" + StrZero(i,2) + "/" + cAnoRela,"ddmmyy")
						dDatCons := LastDay(dDatCons)
						dbSelectArea(cAliasSJ3)
						If dDatCons >= RetFldProd(SB1->B1_COD,"B1_CONINI")
							cInd   := "J3_QUAR"+StrZero(i,2)
							nQtdes := &cInd
							aAcumVal[i] += nQtdes
							nQtdCons += nQtdes
							nNoMeses++
						Endif
					Next
					dbSelectArea(cAliasSJ3)
					dbSkip()
					oReport:IncMeter()
				End
				
			Else
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Os 12 meses estao em 2 anos consecutivos.         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cAnoRela == (cAliasSJ3)->J3_ANO
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ O primeiro ano nao possui informacoes, somente o 2o. ano. ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cMesAuxi := "01"
					cAnoAuxi := cAnoRela
					i := 13 - Val(cMesRela)
					
					For nMes := 1 To (i-1)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica data de inicio de consumo, informado no SB1.   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dDatCons := Ctod("01/" + StrZero((nMes+Val(cMesRela)),2) + "/" + cAnoAnte,"ddmmyy")
						dDatCons := LastDay(dDatCons)
						If dDatCons >= RetFldProd(SB1->B1_COD,"B1_CONINI")
							nNoMeses++
						Endif
					Next
				Else
					cMesAuxi := cMesInic
					cAnoAuxi := cAnoAnte
					i := 1
				Endif
				
				While (cAliasSJ3)->J3_PRODUTO==cProduto .And. (cAliasSJ3)->J3_CLIENTE == cCliente ;
					.And. (cAliasSJ3)->J3_LOJA==cLojaCli .And. (cAliasSJ3)->J3_REGVEND == cRegiaoC ;
					.And. (cAliasSJ3)->J3_FILIAL == xFilial("SJ3") .And. (cAliasSJ3)->(!Eof())
					
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica data de inicio de consumo, informado no SB1.   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dDatCons := Ctod("01/" + cMesAuxi + "/" + cAnoAuxi,"ddmmyy")
					dDatCons := LastDay(dDatCons)
					dbSelectArea(cAliasSJ3)
					If dDatCons >= RetFldProd(SB1->B1_COD,"B1_CONINI")
						cInd   := "J3_QUAR"+cMesAuxi
						nQtdes := &cInd
						aAcumVal[i] += nQtdes
						nQtdCons += nQtdes
						nNoMeses++
					Endif
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Posiciona no proximo mes.        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cMesAuxi := StrZero((Val(cMesAuxi)+1),2)
					i++
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se e' o proximo ano, ou ja finalizou o periodo.  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Val(cMesAuxi) >= 13 .Or. i >= 13
						dbSelectArea(cAliasSJ3)
						dbSkip()
						oReport:IncMeter()
						
						If (cAliasSJ3)->J3_PRODUTO == cProduto .And. (cAliasSJ3)->J3_CLIENTE == cCliente .And. ;
							(cAliasSJ3)->J3_LOJA == cLojaCli .And. (cAliasSJ3)->J3_REGVEND == cRegiaoC .And. ;
							(cAliasSJ3)->J3_FILIAL == xFilial("SJ3") .And. (cAliasSJ3)->(!Eof()) .And. ;
							((cAliasSJ3)->J3_ANO == cAnoRela .Or. (cAliasSJ3)->J3_ANO == cAnoAnte)
							
							If cAnoAuxi == (cAliasSJ3)->J3_ANO
								If cAnoRela == cAnoAuxi
									cMesAuxi := "01"
									cAnoAuxi := cAnoRela
									i := 13 - Val(cMesRela)
								Else
									cMesAuxi := cMesInic
									i := 1
									nNoMeses := 0
								Endif
							Else
								cMesAuxi := "01"
								cAnoAuxi := (cAliasSJ3)->J3_ANO
							Endif
						Else
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Se finalizou os dados no ano anterior, acumular no  ³
							//³ nro. de meses os meses restante do ano final.       ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If cAnoAuxi == cAnoAnte .And. Year(RetFldProd(SB1->B1_COD,"B1_CONINI")) <= Val(cAnoAuxi)
								nNoMeses += Val(cMesRela)
							Endif
						Endif
					Endif
				End
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava somente se houver dados no periodo.           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nQtdCons != 0 .Or. nNoMeses > 0
				nConsMed := nQtdCons / Iif(nNoMeses==0,1,nNoMeses)
				aRegQtde[Len(aRegQtde)][4] += Iif(nConsMed<=0,0,(nConsMed*nPrecoUn))
				
				dbSelectArea("TRB")
				dbSeek( cRegiaoC + cCliente + cLojaCli + cProduto )
				
				If Eof()
					RecLock("TRB",.T.)
					Replace REGIAO   With cRegiaoC, PRODUTO With cProduto, ;
					VAL_UNIT With nPrecoUn
					Replace CLIENTE  With cCliente, LOJACLI With cLojaCli
				Else
					RecLock("TRB")
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava o valor do consumo em ordem decrescente de valor,   ³
				//³ para ordenacao pelo campo.                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nConsPrd := 10000000000000 - Iif(nConsMed > 0,(nConsMed * nPrecoUn),0.01)
				Replace VALCHAV With Right(StrZero(nConsPrd,17,nDecPrec),16)
				Replace VALCMED With (nConsMed * nPrecoUn), ;
				MESCONS With nNoMeses, QTDECM With nConsMed
				For i := 1 To 12
					FieldPut(FieldPos("QTDE"+StrZero(i,2)),aAcumVal[i])
				Next
				MsUnLock()
				dbSelectArea("SJ3")
			Endif
		End     // Cliente
	End        // Regiao
End           // Eof()


nTtGeral	:= 0
AEval( aRegQtde, { | x | nTtGeral += x[4] } )
dbSelectArea("TRB")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos cabecalhos                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If oReport:Section(1):GetOrder() == 1     // por % de participacao
	DbClearFilter()
	If File(cArqTrab+OrdBagExt())
		dbClearInd()
		RetIndex("SJ3")
		dbSelectArea("TRB")
		Ferase(cArqTrab+OrdBagExt())
	Endif
	cKey := "REGIAO + CLIENTE + LOJACLI + VALCHAV"
	IndRegua("TRB",cArqTrab,cKey,,,STR0041)		//"Selecionando Registros..."
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Metodo TrPosition()                                                     ³
//³                                                                        ³
//³Posiciona em um registro de uma outra tabela. O posicionamento será     ³
//³realizado antes da impressao de cada linha do relatório.                ³
//³                                                                        ³
//³                                                                        ³
//³ExpO1 : Objeto Report da Secao                                          ³
//³ExpC2 : Alias da Tabela                                                 ³
//³ExpX3 : Ordem ou NickName de pesquisa                                   ³
//³ExpX4 : String ou Bloco de código para pesquisa. A string será macroexe-³
//³        cutada.                                                         ³
//³                                                                        ³				
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRPosition():New(oReport:Section(1):Section(1)	,"SA1",1,{|| xFilial("SA1")+TRB->CLIENTE	})
TRPosition():New(oReport:Section(1)				,"SX5",1,{|| xFilial("SX5")+"61"+cRegiaoC	})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para movimentacao do cursor.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TRB")
oReport:SetMeter(RecCount())		// Total de Elementos da regua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do relatorio.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TRB")
dbGoTop()
cQuebraReg	:= ""
oReport:Section(1):Init()
oReport:Section(1):Section(1):Init()  
oReport:Section(1):Section(1):Section(1):Init()  
oReport:Section(1):Section(1):Section(1):Section(1):Init()  
cQuebraReg := ""
While !oReport:Cancel() .And. TRB->(!Eof())
	
	cRegiaoC := TRB->REGIAO
	nTotRegi := 0
	AEval( aRegQtde, { | x | Iif(x[1]==cRegiaoC,nTotRegi += x[4],) } )
	nPorcPrd := Iif(nTtGeral==0,1,(nTotRegi / nTtGeral))*100
	
	If cQuebraReg <> TRB->REGIAO
		oReport:FatLine()
		oReport:SkipLine(2)
		oReport:Section(1):PrintLine()
		cQuebraReg := TRB->REGIAO
	EndIf	
		
	cCliente := TRB->CLIENTE
	cLojaCli := TRB->LOJACLI
	nAcPorce := 0
	nElement := AScan( aRegQtde, { | x | x[1]+x[2]+x[3] == cRegiaoC+cCliente+cLojaCli } )
    nTotCli  := 0
    AEval( aRegQtde, { | x | If(x[1]+x[2]+x[3] == cRegiaoC+cCliente+cLojaCli,nTotCli += x[4],) } )
	nPorcPrd := Iif(nTotRegi==0,1,(nTotCli / nTotRegi))*100
	oReport:Section(1):Section(1):printLine()
		
	nContaIt := 1
	While TRB->CLIENTE == cCliente .And. TRB->LOJACLI == cLojaCli .And. ;
			TRB->REGIAO == cRegiaoC .And. TRB->(!Eof())
			
		If nContaIt <= mv_par11
			cProduto := TRB->PRODUTO
			SB1 -> ( dbSeek( xFilial("SB1") + cProduto ) )
			If mv_par12 == 1
				cDescProd := SB1->B1_DESC
			Else
				dbSelectArea("SA7")
				dbSetOrder(2)
				If dbSeek(xFilial()+cProduto+cCliente+cLojaCli)
					cDescProd := SA7->A7_DESCCLI
				Else
					cDescProd :=  SB1->B1_DESC
				Endif
			Endif
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula classificacao ABC dos produtos.               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nAcPorce <= mv_par05
				cClasse := "A"
			ElseIf nAcPorce <= (mv_par05 + mv_par06)
				cClasse := "B"
			Else
				cClasse := "C"
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula % do produto sobre o total do cliente.        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nPorcPrd := Round(TRB->VALCMED / Iif(nTotCli<=0,0,nTotCli),5) * 100
				
			If nPorcPrd >= 0
				nAcPorce += nPorcPrd
				If nAcPorce > 100
					nDiferen := 100 - nAcPorce
					nPorcPrd += nDiferen
					nAcPorce := 100
				Endif
			Endif
				
			nTotAcum := 0
			aValores := {0,0,0,0,0,0,0,0,0,0,0,0}
			For ni = 1 To 12
				cInd := "TRB->QTDE"+StrZero(ni,2)
				nTotAcum += &cInd
				aValores[ni] := &cInd
				nG := ni
			Next
				
			oReport:Section(1):section(1):section(1):PrintLine()
			oReport:Section(1):Section(1):Section(1):Section(1):PrintLine()
			nContaIt++
				
			dbSelectArea("TRB")
			TRB -> ( dbSkip() )
			oReport:IncMeter()
		Else
			dbSelectArea("TRB")
			dbSeek( cRegiaoC + Replicate("z",15), .T. )
			If cRegiaoC == TRB->REGIAO
				dbSkip()
			Endif
		Endif
	End
End

oReport:FatLine()
oReport:PrintText(STR0040 + Transform(nTtGeral,Tm(nTtGeral,16,2)))	// "Total Geral:"


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza Secoes                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:Section(1):Section(1):Section(1):Section(1):Finish()
oReport:Section(1):Section(1):Section(1):Finish()
oReport:Section(1):Section(1):Finish()			
oReport:Section(1):Finish()	

dbSelectArea("TRB")
dbCloseArea()

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATR765R3³ Autor ³ Elizabeth A. Eguni    ³ Data ³ 25/09/95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relatorio de Curva ABC de Consumo de Vendas por Cliente.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR765R3(void)                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Patricia Sal.³30/12/99³XXXXXX³Acerto Arq. de Trab.(Fornec. com 20 p. e ³±±
±±³              ³        ³      ³Loja com 4 pos.)                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATR765R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL CbTxt
LOCAL cDesc1 := OemToAnsi(STR0001)	//"Relat¢rio para mostrar as quantidades de vendas (consumos) de "
LOCAL cDesc2 := OemToAnsi(STR0002)	//"produtos no estoque por regiao e por cliente."
LOCAL cDesc3 := OemToAnsi(STR0003)	//"Este relatorio dever  ser impresso no formul rio de 132 colunas."
LOCAL CbCont,cabec1,cabec2,wnrel
LOCAL tamanho:= "G"
LOCAL limite := 220
LOCAL cString:= "SJ3"
LOCAL nTipo  := 0
LOCAL nOrdem,i
LOCAL titulo := OemToAnsi(STR0004)	//"Curva ABC de Estoque - Ordem de "
LOCAL lContinua := .T., lFirst := .T.
PRIVATE nTamSXG  := TamSXG("001")  // Grupo de Cliente
PRIVATE nTamSXG2 := TamSXG("002") // Grupo de Loja
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa o titulo do campo J3_CLIENTE (cliente/concessionaria).  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek( "J3_CLIENTE" )
dbSetOrder(1)
cTitClie := AllTrim(X3Titulo())
titulo   += cTitClie + STR0005	//" / % de Participacao"

PRIVATE aReturn := { STR0006, 1,STR0007, 1, 2, 1, "",1 }			//"Zebrado"###"Administracao"
PRIVATE nomeprog:= "MATR765"
PRIVATE aLinha  := { }, nLastKey := 0
PRIVATE cPerg   := "MTR765"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao dos fontes      ³
//³ SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
//| cliente, assim verificando a necessidade de uma atualizacao     |
//| nestes fontes. NAO REMOVER !!!							        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !(FindFunction("SIGACUS_V") .And. SIGACUS_V() >= 20050512)
	Final("Atualizar patch do programa SIGACUS.PRW !!!")
EndIf
If !(FindFunction("SIGACUSA_V") .And. SIGACUSA_V() >= 20050512)
	Final("Atualizar patch do programa SIGACUSA.PRX !!!")
EndIf
If !(FindFunction("SIGACUSB_V") .And. SIGACUSB_V() >= 20060920)
	Final("Atualizar patch do programa SIGACUSB.PRX !!!")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt    := SPACE(10)
cbcont   := 0
li       := 80
m_pag    := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("MTR765",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // Regiao de                                        ³
//³ mv_par02 // Regiao ate                                       ³
//³ mv_par03 // Mes                                              ³
//³ mv_par04 // Ano                                              ³
//³ mv_par05 // % Prod. A                                        ³
//³ mv_par06 // % Prod. B                                        ³
//³ mv_par07 // % Prod. C                                        ³
//³ mv_par08 // Preco p/Calc.: Medio/Standard/Tab.Cliente/Tabela ³
//³ mv_par09 // Cliente de                                       ³
//³ mv_par10 // Cliente ate                                      ³
//³ mv_par11 // Nro. de itens impressos por cliente.             ³
//³ mv_par12 // Quanto a Descricao  Produto  Prod x Cli.         ³
//³ mv_par13 // Qual moeda                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := "MATR765"            //Nome Default do relatorio em Disco

wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",.T.,Tamanho)

If nLastKey == 27
	DbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	DbClearFilter()
	Return
Endif

RptStatus({|lEnd| C765Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ C765IMP  ³ Autor ³ Rosane Luciane Chene  ³ Data ³ 09.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR765 		                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function C765Imp(lEnd,WnRel,cString)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL CbTxt
LOCAL cDesc1 := OemToAnsi(STR0001)	//"Relat¢rio para mostrar as quantidades de vendas (consumos) de "
LOCAL cDesc2 := OemToAnsi(STR0002)	//"produtos no estoque por regiao e por cliente."
LOCAL cDesc3 := OemToAnsi(STR0003)	//"Este relatorio dever  ser impresso no formul rio de 132 colunas."
LOCAL CbCont,cabec1,cabec2
LOCAL tamanho:= "G"
LOCAL limite := 220
LOCAL nTipo  := 0
LOCAL nOrdem,i
LOCAL nTotRegs, nMult := 1, nPosAnt := 4, nPosAtu := 4, nPosCnt := 0
LOCAL cSav20, cSav7

LOCAL titulo := OemToAnsi(STR0004)	//"Curva ABC de Estoque - Ordem de "
LOCAL lContinua := .T., lFirst := .T.

LOCAL cTitClie, cMesRela, cMesInic
LOCAL nDecPrec, aCampos, cArqTrab, cKey, cIndex, cCondicao
LOCAL cProduto, aRegQtde, cRegiaoC, nPrecoUn, aAcumVal
LOCAL nQtdCons, nNoMeses, dDatCons, cAnoAuxi, nConsMed, nTtGeral
LOCAL nElement, nPorcPrd, nAcPorce, nQtdes, cClasse, nColunas
LOCAL cDescReg, cTabRegi := "61"
LOCAL nItemRel, nContaIt, aTam:=0
Local cNTabela, cInd
LOCAL cDescProd:=""
Local nTotCli  :=0
Local nDecimal := 2
Local nMes
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa o titulo do campo J3_CLIENTE (cliente/concessionaria).  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek( "J3_CLIENTE" )
dbSetOrder(1)
cTitClie := AllTrim(X3Titulo())
titulo   += cTitClie + STR0005 + " - " + GetMv("MV_MOEDA" + STR(Mv_Par13,1))//" / % de Participacao"
nOrdem   := aReturn[8]
nTipo    := 15
cRegInic := mv_par01
cRegiFim := mv_par02
cMesRela := StrZero(mv_par03,2)
cAnoRela := StrZero(mv_par04,4)
cClieIni := mv_par09
cClieFim := mv_par10
nItemRel := mv_par11
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt    := SPACE(10)
cbcont   := 0
li       := 80
m_pag    := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Os 12 meses estao contidos em 1 ano ou 2 anos.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par03 == 12
	cAnoAnte := StrZero(mv_par04,4)
	cMesInic := "01"
Else
	cAnoAnte := StrZero((mv_par04-1),4)
	cMesInic := StrZero((mv_par03+1),2)
Endif
If mv_par08 == 2
	cTipPrec := "S"      && preco standard
ElseIf mv_par08 == 3
	cTipPrec := "C"      && preco de tabela informado no cad. de cliente
ElseIf mv_par08 == 4
	Pergunte("MR765B",.F.)    && preco de tabela informado pelo usuario
	Pergunte("MR765B",.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01 // Nro. Tabela                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTipPrec := mv_par01
	Pergunte("MTR765",.F.)
Else
	cTipPrec := "M"      && preco medio
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa o numero de casas decimais do valor e qtde.         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek( "J3_VALR01" )
nDecPrec := SX3->X3_DECIMAL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define estrutura e gera o arquivo de trabalho                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTam:=TamSX3("J3_REGVEND")
aCampos := {{ "REGIAO"  , "C",aTam[1],aTam[2]},;
{ "PRODUTO" , "C", 15, 0 },;
{ "CLIENTE" , "C", nTamSXG[1] , 0 },;
{ "LOJACLI" , "C", nTamSXG2[1], 0 },;
{ "VALCHAV" , "C", 16, 0 },;
{ "VAL_UNIT", "N", 16, nDecPrec },;
{ "VALCMED" , "N", 16, nDecPrec },;
{ "MESCONS" , "N", 02, 0 },;
{ "QTDE01"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2] },;
{ "QTDE02"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE03"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE04"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE05"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE06"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE07"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE08"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE09"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE10"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE11"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDE12"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]},;
{ "QTDECM"  , "N", TamSx3("J3_QUAR01")[1], TamSx3("J3_QUAR01")[2]}}

cArqTrab := CriaTrab(aCampos,.T.)
Use &cArqTrab Alias "TRB" New

cKey := "REGIAO + CLIENTE + LOJACLI + PRODUTO"
IndRegua("TRB",cArqTrab,cKey,,,STR0008)		//"Selecionando Registros..."

dbSelectArea("SJ3")
dbSetOrder(3)      && J3_FILIAL+J3_REGVEND+J3_CLIENTE+J3_LOJA+J3_PRODUTO+J3_ANO
dbSeek( xFilial("SJ3") + cRegInic , .T. )
#IFDEF TOP
	cIndex := CriaTrab(Nil,.F.)
	cKey   := IndexKey()
	cCondicao := 'J3_FILIAL == "' + xFilial("SJ3") + '" .And. J3_REGVEND >= "' + cRegInic
	cCondicao += '" .And. J3_REGVEND <= "' + cRegiFim + '" .And. J3_ANO <= "' + cAnoRela
	cCondicao += '" .And. J3_ANO >= "' + cAnoAnte + '" .And. J3_CLIENTE >= "' + cClieIni
	cCondicao += '" .And. J3_CLIENTE <= "' + cClieFim + '"'
	IndRegua("SJ3",cIndex,cKey,,cCondicao,STR0008)			//"Selecionando Registros..."
	nIndex := RetIndex("SJ3")
	dbSelectArea("SJ3")
	dbSetOrder(nIndex+1)
	dbGoTop()
	//#ELSE
	//	cCondicao := "J3_FILIAL==xFilial('SJ3') .And. J3_REGVEND>=cRegInic "
	//	cCondicao += ".And. J3_REGVEND<=cRegiFim .And. (J3_ANO==cAnoRela "
	//	cCondicao += ".Or.  J3_ANO==cAnoAnte) .And. J3_CLIENTE>=cClieIni "
	//	cCondicao += ".And. J3_CLIENTE<=cClieFim"
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para movimentacao do cursor.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SJ3")
SetRegua(RecCount())		// Total de Elementos da regua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo de trabalho com dados do SJ3 - acum. mensais.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRegQtde := {}
While SJ3->J3_FILIAL == xFilial("SJ3") .And. SJ3->(!Eof()) .And. ;
	SJ3->J3_REGVEND <= cRegiFim
	
	cRegiaoC := SJ3->J3_REGVEND
	
	While SJ3->J3_REGVEND == cRegiaoC .And. SJ3->J3_FILIAL == xFilial("SJ3") ;
		.And. SJ3->(!Eof())
		
		If SJ3->J3_CLIENTE < cClieIni .Or. SJ3->J3_CLIENTE > cClieFim
			dbSelectArea("SJ3")
			dbSkip()
			Loop
		Endif
		
		cCliente := SJ3->J3_CLIENTE
		cLojaCli := SJ3->J3_LOJA
		
		dbSelectArea("SA1")
		dbSeek( xFilial("SA1") + cCliente + cLojaCli )
		Aadd( aRegQtde, { cRegiaoC, cCliente, cLojaCli, 0 } )
		While SJ3->J3_CLIENTE == cCliente .And. SJ3->J3_LOJA == cLojaCli .And. ;
			SJ3->J3_REGVEND == cRegiaoC .And. SJ3->J3_FILIAL == xFilial("SJ3") ;
			.And. SJ3->(!Eof())
			
			If SJ3->J3_ANO != cAnoRela .And. SJ3->J3_ANO != cAnoAnte
				dbSelectArea("SJ3")
				dbSkip()
				Loop
			Endif
			
			cProduto := SJ3->J3_PRODUTO
			dbSelectArea("SB1")
			dbSeek( xFilial("SB1") + cProduto )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atribui preco unitario conforme escolha do usuario (parametro). ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cTipPrec == "S"       && preco standard
				nPrecoUn := xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),1,mv_par13,ddatabase)
			ElseIf cTipPrec == "M" .Or. cTipPrec == "1" .Or. (cTipPrec > "7" .And. cTipPrec <= "9")
				nPrecoUn := xMoeda(SB1->B1_PRV1,1,mv_par13,ddatabase)
			ElseIf cTipPrec == "C"   && tabela informada no cad. de cliente
				If Empty(SA1->A1_TABELA) .Or. SA1->A1_TABELA == "1" 
					nPrecoUn := xMoeda(SB1->B1_PRV1,1,mv_par13,ddatabase)
				Else                                                   
					nPrecoUn := MaTabPrVen(SA1->A1_TABELA,cProduto,0,SA1->A1_COD,SA1->A1_LOJA,mv_par13,ddatabase)					
				Endif
			Else
				dbSelectArea("SA1")
				dbSeek( xFilial("SA1") + SJ3->J3_CLIENTE + SJ3->J3_LOJA )
				nPrecoUn := MaTabPrVen(cTipPrec,cProduto,0,SA1->A1_COD,SA1->A1_LOJA,mv_par13,ddatabase)									
			Endif
			
			aAcumVal := { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0}
			nQtdCons := 0   && qtde. consumida do produto no periodo
			nNoMeses := 0   && nro. de meses do produto para calculo
			
			If cAnoAnte == cAnoRela
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Os 12 meses estao em 1 unico ano.                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				While SJ3->J3_PRODUTO==cProduto .And. SJ3->J3_CLIENTE == cCliente ;
					.And. SJ3->J3_LOJA==cLojaCli .And. SJ3->J3_REGVEND == cRegiaoC ;
					.And. SJ3->J3_FILIAL == xFilial("SJ3") .And. SJ3->(!Eof())
					
					If SJ3->J3_ANO != cAnoRela .And. SJ3->J3_ANO != cAnoAnte
						dbSelectArea("SJ3")
						dbSkip()
						Loop
					Endif
					For i := 1 To 12
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica data de inicio de consumo, informado no SB1.   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dDatCons := Ctod("01/" + StrZero(i,2) + "/" + cAnoRela,"ddmmyy")
						dDatCons := LastDay(dDatCons)
						If dDatCons >= RetFldProd(SB1->B1_COD,"B1_CONINI")
							cInd   := "SJ3->J3_QUAR"+StrZero(i,2)
							nQtdes := &cInd
							aAcumVal[i] += nQtdes
							nQtdCons += nQtdes
							nNoMeses++
						Endif
					Next
					dbSelectArea("SJ3")
					dbSkip()
				End
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Movimentacao do cursor.                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				IncRegua()
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Os 12 meses estao em 2 anos consecutivos.         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cAnoRela == SJ3->J3_ANO
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ O primeiro ano nao possui informacoes, somente o 2o. ano. ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cMesAuxi := "01"
					cAnoAuxi := cAnoRela
					i := 13 - Val(cMesRela)
					
					For nMes := 1 To (i-1)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica data de inicio de consumo, informado no SB1.   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dDatCons := Ctod("01/" + StrZero((nMes+Val(cMesRela)),2) + "/" + cAnoAnte,"ddmmyy")
						dDatCons := LastDay(dDatCons)
						If dDatCons >= RetFldProd(SB1->B1_COD,"B1_CONINI")
							nNoMeses++
						Endif
					Next
				Else
					cMesAuxi := cMesInic
					cAnoAuxi := cAnoAnte
					i := 1
				Endif
				
				While SJ3->J3_PRODUTO==cProduto .And. SJ3->J3_CLIENTE == cCliente ;
					.And. SJ3->J3_LOJA==cLojaCli .And. SJ3->J3_REGVEND == cRegiaoC ;
					.And. SJ3->J3_FILIAL == xFilial("SJ3") .And. SJ3->(!Eof())
					
					If SJ3->J3_ANO != cAnoRela .And. SJ3->J3_ANO != cAnoAnte
						dbSelectArea("SJ3")
						dbSkip()
						Loop
					Endif
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica data de inicio de consumo, informado no SB1.   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dDatCons := Ctod("01/" + cMesAuxi + "/" + cAnoAuxi,"ddmmyy")
					dDatCons := LastDay(dDatCons)
					If dDatCons >= RetFldProd(SB1->B1_COD,"B1_CONINI")
						cInd   := "SJ3->J3_QUAR"+cMesAuxi
						nQtdes := &cInd
						aAcumVal[i] += nQtdes
						nQtdCons += nQtdes
						nNoMeses++
					Endif
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Posiciona no proximo mes.        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cMesAuxi := StrZero((Val(cMesAuxi)+1),2)
					i++
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se e' o proximo ano, ou ja finalizou o periodo.  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Val(cMesAuxi) >= 13 .Or. i >= 13
						dbSelectArea("SJ3")
						dbSkip()
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Movimentacao do cursor.                             ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						IncRegua()
						If SJ3->J3_PRODUTO == cProduto .And. SJ3->J3_CLIENTE == cCliente .And. ;
							SJ3->J3_LOJA == cLojaCli .And. SJ3->J3_REGVEND == cRegiaoC .And. ;
							SJ3->J3_FILIAL == xFilial("SJ3") .And. SJ3->(!Eof()) .And. ;
							(SJ3->J3_ANO == cAnoRela .Or. SJ3->J3_ANO == cAnoAnte)
							
							If cAnoAuxi == SJ3->J3_ANO
								If cAnoRela == cAnoAuxi
									cMesAuxi := "01"
									cAnoAuxi := cAnoRela
									i := 13 - Val(cMesRela)
								Else
									cMesAuxi := cMesInic
									i := 1
									nNoMeses := 0
								Endif
							Else
								cMesAuxi := "01"
								cAnoAuxi := SJ3->J3_ANO
							Endif
						Else
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Se finalizou os dados no ano anterior, acumular no  ³
							//³ nro. de meses os meses restante do ano final.       ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If cAnoAuxi == cAnoAnte .And. Year(RetFldProd(SB1->B1_COD,"B1_CONINI")) <= Val(cAnoAuxi)
								nNoMeses += Val(cMesRela)
							Endif
						Endif
					Endif
				End
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava somente se houver dados no periodo.           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nQtdCons != 0 .Or. nNoMeses > 0
				nConsMed := nQtdCons / Iif(nNoMeses==0,1,nNoMeses)
				//				aRegQtde[Len(aRegQtde)][4] += nConsMed * nPrecoUn
				aRegQtde[Len(aRegQtde)][4] += Iif(nConsMed<=0,0,(nConsMed*nPrecoUn))
				
				dbSelectArea("TRB")
				dbSeek( cRegiaoC + cCliente + cLojaCli + cProduto )
				
				If Eof()
					RecLock("TRB",.T.)
					Replace REGIAO   With cRegiaoC, PRODUTO With cProduto, ;
					VAL_UNIT With nPrecoUn
					Replace CLIENTE  With cCliente, LOJACLI With cLojaCli
				Else
					RecLock("TRB")
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava o valor do consumo em ordem decrescente de valor,   ³
				//³ para ordenacao pelo campo.                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nConsPrd := 10000000000000 - Iif(nConsMed > 0,(nConsMed * nPrecoUn),0.01)
				Replace VALCHAV With Right(StrZero(nConsPrd,17,nDecPrec),16)
				Replace VALCMED With (nConsMed * nPrecoUn), ;
				MESCONS With nNoMeses, QTDECM With nConsMed
				For i := 1 To 12
					FieldPut(FieldPos("QTDE"+StrZero(i,2)),aAcumVal[i])
				Next
				MsUnLock()
				dbSelectArea("SJ3")
			Endif
		End     && Cliente
	End        && Regiao
End           && Eof()

nTtGeral	:= 0
AEval( aRegQtde, { | x | nTtGeral += x[4] } )

dbSelectArea("SJ3")
RetIndex("SJ3")
DbClearFilter()

dbSelectArea("TRB")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos cabecalhos                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem == 1     && por % de participacao
	DbClearFilter()
	If File(cArqTrab+OrdBagExt())
		dbClearInd()
		RetIndex("SJ3")
		dbSelectArea("TRB")
		Ferase(cArqTrab+OrdBagExt())
	Endif
	cKey := "REGIAO + CLIENTE + LOJACLI + VALCHAV"
	IndRegua("TRB",cArqTrab,cKey,,,STR0008)		//"Selecionando Registros..."
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera cabecalho 1 conforme paramentros informados.            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cabec1 := STR0009 	//"Numero da Peca  C Denominacao                      Valor Unitario % Part % Acum"
cabec2 := Space(32)
cabec2 := MR765Cab1( cabec2, cMesRela, cAnoRela, cAnoAnte )
//cabec1:="Numero da Peca  C Denominacao                      Valor Unitario % Part % Acum    Mes 01    Mes 02    Mes 03    Mes 04    Mes 05    Mes 06    Mes 07    Mes 08    Mes 09    Mes 10    Mes 11    Mes 12    C.M.M.  Tot.Cons."
//         123456789012345 1 123456789012345678901234567890 9.999.999.999,99 999,99 999,99 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 99.999.999
//         01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//         0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22

/*
123456789012345 1 123456789012345678901234567890 9.999.999.999,99 999,99 999,99 
                                9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.999.99 9.999.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99 9.999.999.99
01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22
*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para movimentacao do cursor.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TRB")
SetRegua(RecCount())		// Total de Elementos da regua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do relatorio.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TRB")
dbGoTop()
While TRB->(!Eof())
	
	If lEnd
		@PROW()+1,001 Psay STR0010	//"CANCELADO PELO OPERADOR"
		lContinua := .F.
		Exit
	Endif
	
	cRegiaoC := TRB->REGIAO
	nTotRegi := 0
	AEval( aRegQtde, { | x | Iif(x[1]==cRegiaoC,nTotRegi += x[4],) } )
	
	If li > 59
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	Endif
	nPorcPrd := Iif(nTtGeral==0,1,(nTotRegi / nTtGeral))*100
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tabela 61 - nome da regiao.                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cDescReg := AllTrim(Tabela(cTabRegi,cRegiaoC,.F.))
	nColunas := 18 + Len(cDescReg)
	@ li,00 Psay STR0011 + Substr(cRegiaoC,1,4) + " - " 		//"Regiao : "
	@ li,15 Psay cDescReg + " - "
	@ li,nColunas Psay nPorcPrd Picture "@E 999.99"
	@ li,nColunas+7 Psay "%" 
	li+=2
	While TRB->REGIAO==cRegiaoC .And. TRB->(!Eof()) .And. lContinua
		
		If lEnd
			@PROW()+1,001 Psay STR0010	//"CANCELADO PELO OPERADOR"
			lContinua := .F.
			Exit
		Endif
		
		cCliente := TRB->CLIENTE
		cLojaCli := TRB->LOJACLI
		dbSelectArea("SA1")
		dbSeek( xFilial("SA1") + cCliente + cLojaCli )
		nAcPorce	:= 0
		
		If li > 59
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
			nPorcPrd := Iif(nTtGeral==0,1,(nTotRegi / nTtGeral))*100
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tabela 61 - nome da regiao.                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cDescReg := AllTrim(Tabela(cTabRegi,cRegiaoC,.F.))
			nColunas := 18 + Len(cDescReg)
			@ li,00 Psay STR0011 + Substr(cRegiaoC,1,4) + " - " 		//"Regiao : "
			@ li,15 Psay cDescReg + " - "
			@ li,nColunas Psay nPorcPrd Picture "@E 999.99"
			@ li,nColunas+7 Psay "%"
			li+=2
		Endif
		
		nElement := AScan( aRegQtde, { | x | x[1]+x[2]+x[3] == cRegiaoC+cCliente+cLojaCli } )
      nTotCli  := 0
      AEval( aRegQtde, { | x | If(x[1]+x[2]+x[3] == cRegiaoC+cCliente+cLojaCli,nTotCli += x[4],) } )
		If nTotCli > 999999999
			nDecimal := 0
			Else
				nDecimal := 2
		EndIf
		
		nPorcPrd := Iif(nTotRegi==0,1,(aRegQtde[nElement][4] / nTotRegi))*100
		//		nColunas := Len(cTitClie) + 16 + Len(Alltrim(SA1->A1_NOME)) + 3
		@ li,00 Psay cTitClie + " : " + cCliente + "  " + cLojaCli + " - " + Alltrim(SA1->A1_NOME) + " - " + ;
		STR0025 + " " + Trans(nTotCli,Tm(nTotCli,15,nDecimal)) + " " + Transform(nPorcPrd, "@E 999.99") + " %"
		//		@ li,nColunas Psay nPorcPrd Picture "@E 999.99"
		//		@ li,nColunas+7 Psay "%"
		li++
		
		nContaIt := 1
		While TRB->CLIENTE == cCliente .And. TRB->LOJACLI == cLojaCli .And. ;
			TRB->REGIAO == cRegiaoC .And. TRB->(!Eof()) .And. lContinua
			
			If lEnd
				@PROW()+1,001 Psay STR0010	//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Exit
			Endif
			
			If li > 59
				cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				nPorcPrd := Iif(nTtGeral==0,1,(nTotRegi / nTtGeral))*100
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Tabela 61 - nome da regiao.                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cDescReg := AllTrim(Tabela(cTabRegi,cRegiaoC,.F.))
				nColunas := 18 + Len(cDescReg)
				@ li,00 Psay STR0011 + Substr(cRegiaoC,1,4) + " - " 		//"Regiao : "
				@ li,15 Psay cDescReg + " - "
				@ li,nColunas Psay nPorcPrd Picture "@E 999.99"
				@ li,nColunas+7 Psay "%"
				li+=2
				
				nPorcPrd := Iif(nTotRegi==0,1,(aRegQtde[nElement][4] / nTotRegi))*100
				nColunas := Len(cTitClie) + 16 + Len(Alltrim(SA1->A1_NOME)) + 3
				@ li,00 Psay cTitClie + " : " + cCliente + "  " + cLojaCli + " - " + AllTrim(SA1->A1_NOME) + " - "
				@ li,nColunas Psay nPorcPrd Picture "@E 999.99"
				@ li,nColunas+7 Psay "%"
				li++
			EndIf
			
			If nContaIt <= nItemRel
				cProduto := TRB->PRODUTO
				SB1 -> ( dbSeek( xFilial("SB1") + cProduto ) )
				If mv_par12 == 1
					cDescProd := SB1->B1_DESC
				Else
					dbSelectArea("SA7")
					dbSetOrder(2)
					If dbSeek(xFilial()+cProduto+cCliente+cLojaCli)
						cDescProd := SA7->A7_DESCCLI
					Else
						cDescProd :=  SB1->B1_DESC
					Endif
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula classificacao ABC dos produtos.               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nAcPorce <= mv_par05
					cClasse := "A"
				ElseIf nAcPorce <= (mv_par05 + mv_par06)
					cClasse := "B"
				Else
					cClasse := "C"
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula % do produto sobre o total do cliente.        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nTotClie := aRegQtde[nElement][4]
				nPorcPrd := Round(TRB->VALCMED / Iif(nTotClie<=0,0,nTotClie),5) * 100
				
				@ li, 00 Psay TRB->PRODUTO Picture "@!S15"
				@ li, 16 Psay cClasse Picture "!"
				@ li, 18 Psay cDescProd Picture "@!S30"
				@ li, 49 Psay xMoeda(TRB->VAL_UNIT,1,mv_par13,ddatabase) Picture Tm(xMoeda(TRB->VAL_UNIT,1,mv_par13,ddatabase),16,nDecimal)
				If nPorcPrd >= 0
					nAcPorce += nPorcPrd
					If nAcPorce > 100
						nDiferen := 100 - nAcPorce
						nPorcPrd += nDiferen
						nAcPorce := 100
					Endif
					@ li, 66 Psay nPorcPrd Picture "@E 999.99"
					@ li, 73 Psay nAcPorce Picture "@E 999.99"
				Endif
				
				nColunas := 32
				nTotAcum := 0
				li++
				For i = 1 To 12
					cInd := "TRB->QTDE"+StrZero(i,2)
					nTotAcum += &cInd
					@ li,nColunas Psay &cInd Picture Tm(&cInd,12,nDecimal) //PesqPict("SJ3","J3_QUAR01",09)
					nColunas += 13
				Next
				@ li,nColunas Psay TRB->QTDECM Picture Tm(TRB->QTDECM,12,nDecimal) //PesqPict("SJ3","J3_QUAR01",09)
				@ li,PCol() + 1 Psay nTotAcum    Picture Tm(nTotacum,12,nDecimal) //PesqPict("SJ3","J3_QUAR01",10)
				
				li += 2
				nContaIt++
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Movimentacao do cursor.                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				IncRegua()
				
				dbSelectArea("TRB")
				TRB -> ( dbSkip() )
			Else
				dbSelectArea("TRB")
				dbSeek( cRegiaoC + Replicate("z",15), .T. )
				If cRegiaoC == TRB->REGIAO
					dbSkip()
				Endif
			Endif
		End
		li++
	End
	@ li,00 Psay Replicate("-",220)
	li++
End

If li != 80
	If nTtGeral > 999999999
		nDecimal := 0
	Else
		nDecimal := 2
	EndIf
	@ li, 00 Psay STR0026
	@ li, 49 Psay nTtGeral Picture Tm(nTtGeral,16,nDecimal)
	Roda(CbCont," ",tamanho)
Endif

dbSelectArea("TRB")
dbCloseArea()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a Integridade dos dados                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MsUnlockAll()
dbSelectArea("SJ3")
DbClearFilter()
RetIndex("SJ3")
dbSetOrder(1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deleta arquivo de trabalho.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF File(cArqTrab+GetDBExtension())
	FErase(cArqTrab+GetDBExtension())
Endif
IF File( cArqTrab+OrdBagExt() )
	FErase( cArqTrab+OrdBagExt() )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se em disco, desvia para Spool                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aReturn[5] = 1    // Se Saida para disco, ativa SPOOL
	Set Printer TO
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MR765Cab1³ Autor ³ Elizabeth A. Eguni    ³ Data ³ 25/09/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera cabecalho 1 para o relatorio.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := MR765Cab1( ExpC1 )                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 - cabecalho 1                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR765                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MR765Cab1( cabec1, cMesRela, cAnoRela, cAnoAnte )
LOCAL nMes, cAno,n
LOCAL aNomeMes := {STR0012,STR0013,STR0014,STR0015,STR0016,STR0017,STR0018,STR0019,STR0020,STR0021,STR0022,STR0023}		//"Jan"###"Fev"###"Mar"###"Abr"###"Mai"###"Jun"###"Jul"###"Ago"###"Set"###"Out"###"Nov"###"Dez"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o mes/ano para o cabecalho.                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For n := 11 To 0 Step -1
	nMes := Val(cMesRela) - n
	If nMes < 1
		nMes := 12 + nMes
		cAno := cAnoAnte
	Else
		cAno := cAnoRela
	Endif
	cabec1 += Right( Space(12) + aNomeMes[nMes] + "/" + cAno, 12 ) + " "
Next
cabec1 += PadL(STR0024,25)	//"    C.M.M.  Tot.Cons."
//cabec1:="Numero da Peca  Clas. Denominacao                       Valor Unitario  % Part  % Acum      Mes 01    Mes 02    Mes 03    Mes 04    Mes 05    Mes 06    Mes 07    Mes 08    Mes 09    Mes 10    Mes 11    Mes 12      C.M.M."
//cabec1:="Numero da Peca  C Denominacao                      Valor Unitario % Part % Acum    Mes 01    Mes 02    Mes 03    Mes 04    Mes 05    Mes 06    Mes 07    Mes 08    Mes 09    Mes 10    Mes 11    Mes 12    C.M.M.  Tot.Cons."
//         123456789012345 1 123456789012345678901234567890 9.999.999.999,99 999,99 999,99 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 9.999.999 99.999.999
//         01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//         0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22

Return cabec1
