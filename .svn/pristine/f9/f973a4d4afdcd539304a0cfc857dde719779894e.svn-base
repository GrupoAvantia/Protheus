#INCLUDE "rwmake.ch" 
#INCLUDE "FATR320.ch"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ FINR320  ³ Autor ³ Marco Bianchi         ³ Data ³ 09/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ WorkArea - Release 4.                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAFAT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function FATR320(aParam)

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef(aParam)
	oReport:PrintDialog()
Else
	FATR320R3()
EndIf

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³ Marco Bianchi         ³ Data ³ 09/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef(aParam)

Local oReport

#IFDEF TOP
	Local cAliasSA3 := cAliasAD7 := GetNextAlias()
	Local cAliasAD8 := GetNextAlias()
#ELSE 
	Local cAliasSA3 := "SA3"
	Local cAliasAD7 := "AD7"
	Local cAliasAD8 := "AD8"
#ENDIF	


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("FATR320",STR0019,"FTR320P9R1", {|oReport| ReportPrint(oReport,cAliasSA3,cAliasAD7,aParam,cAliasAD8)},STR0020 + " " + STR0021)	// "Workarea"###"Este programa tem como objetivo imprimir relatorio "###"de acordo com os parametros informados pelo usuario."
oReport:SetLandscape() 
oReport:SetTotalInLine(.F.)

Pergunte("FTR320P9R1",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica as Perguntas Seleciondas                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( aParam == Nil )
	Pergunte(oReport:uParam,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Vendedor - Setion(1)                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oVendedor := TRSection():New(oReport,STR0023,{"SA3"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)		// "Workarea"
oVendedor:SetTotalInLine(.F.)
TRCell():New(oVendedor,"A3_COD"		,"SA3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oVendedor,"A3_NOME"	,"SA3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Agenda - Setion(1):Section(1)                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oAgenda := TRSection():New(oVendedor,STR0024,{"AD7","SUS","SU5"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)		// "Workarea"
oAgenda:SetTotalInLine(.F.)
TRCell():New(oAgenda,"AD7_TOPICO"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_DATA"		,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_HORA1"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_HORA2"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"CMEMO"		,"AD7",STR0022		,/*Picture*/,40			,/*lPixel*/,{|| MSMM((cAliasAD7)->AD7_CODMEM) })	// "Comentario"
TRCell():New(oAgenda,"AD7_NROPOR"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_CODCLI"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_LOJA"		,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"A1_NOME"		,"SA1",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_PROSPE"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"AD7_LOJPRO"	,"AD7",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"US_NOME"		,"SUS",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oAgenda,"U5_CONTAT"	,"SU5",/*Titulo*/	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz a quebra de linha para impressao de campos MEMO                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:Section(1):Section(1):Cell("CMEMO"):SetLineBreak()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tarefas - Section(2)                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oTarefa := TRSection():New(oReport,STR0025,{"AD8","SA1"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)		// "Workarea"
oTarefa:SetTotalInLine(.F.)
TRCell():New(oTarefa,"AD8_TOPICO"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"AD8_DTINI"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"AD8_DTFIM"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"AD8_STATUS"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"AD8_PRIOR"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"AD8_PERC"		,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"CMEMO"		,"AD8",STR0022		,/*Picture*/,40		   		,/*lPixel*/,{|| MSMM((cAliasAD8)->AD8_CODMEM) })		// "Comentario"
TRCell():New(oTarefa,"AD8_CODCLI"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"AD8_LOJCLI"	,"AD8",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oTarefa,"A1_NOME"		,"SA1",/*Titulo*/	,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz a quebra de linha para impressao de campos MEMO                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:Section(2):Cell("CMEMO"):SetLineBreak()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Busca descricao de campos do tipo ComboBox                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:Section(2):Cell("AD8_STATUS"):SetCBox("1=Nao Iniciada;2=Em Andamento;3=Completada;4=Suspensa;5=Encerrada")
oReport:Section(2):Cell("AD8_PRIOR"):SetCBox("1=Baixa;2=Normal;3=Alta")

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³ Marco Bianchi         ³ Data ³ 09/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³04/04/2007³Norbert Waage  ³Bops 122912 - Correcao da sintaxe das con-  ³±±
±±³          ³               ³dicoes em SQL e CodeBase.                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,cAliasSA3,cAliasAD7,aParam,cAliasAD8)

Local lQuery    := .F.
Local cRepr     := ""
Local dDataIni  := CTOD("  /  /  ")
Local dDataFim  := CTOD("  /  /  ")
Local dDtTaskIni:= CTOD("  /  /  ")
Local dDtTaskFim:= CTOD("  /  /  ")
Local nListTar  := 0
Local nSaltaPag := 0
Local nLoop 	:= 0

#IFNDEF TOP
	Local cCondicao := ""
#ELSE
	Local cOrder    := ""	
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Transforma parametros Range em expressao SQL                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MakeSqlExpr(oReport:uParam)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega variaveis para filtro                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( aParam <> Nil )
	For nLoop := 1 To Len( aParam )
		lImpParam:= .F.
		cTipo	:= aParam[ nLoop, 1 ]
		Do Case
		Case cTipo == "REPRID_FROM"
			cRepr  := aParam[nLoop,2]
		Case cTipo == "REPRID_TO"
			cRepr := aParam[nLoop,2]
		Case cTipo == "DATE_TO"
			dDataIni  := aParam[nLoop,2]
			dDtTaskIni:= aParam[nLoop,2]
		Case cTipo == "DATE_FROM"
			dDataFim  := aParam[nLoop,2]
			dDtTaskFim:= aParam[nLoop,2]				
		Case cTipo == "LIST_TASK"
			nListTar := aParam[nLoop,2]
		Case cTipo == "TASK_FROM"
			nTaskFrom:= aParam[nLoop,2]
		Case cTipo == "EJECT_PAGE"
			nSaltaPag := aParam[nLoop,2]
		EndCase
	Next nLoop

	Do Case
	Case nTaskFrom == 1
		dDtTaskIni := FirstDay(dDataIni)
		dDtTaskFim := LastDay(dDataIni)
	Case nTaskFrom == 2
		aSemana := LimSemana( dDataIni )
		dDtTaskIni := aSemana[1]
		dDtTaskFim := aSemana[2]
	EndCase
	
Else
	cRepr     := MV_PAR01    // Representante(s)
	dDataIni  := MV_PAR02    // Data De
	dDataFim  := MV_PAR03    // Data Ate
	dDtTaskIni:= MV_PAR02    // Data De
	dDtTaskFim:= MV_PAR03    // Data Ate
	nListTar  := MV_PAR04    // Listar Tarefas (Pendentes, Concluidas, Todas)
	nSaltaPag := MV_PAR05    // Salta Pagina por Representante
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatório                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP

	dbSelectArea("AD7")		// Agenda (CRM)
	dbSetOrder(1)			// Vendedor,Data,Hora

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query do relatório da secao 1                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lQuery := .T.
	cOrder := "%" + AD7->(IndexKey()) + "%"
	oReport:Section(1):BeginQuery()	
	BeginSql Alias cAliasSA3
	SELECT *
	FROM %Table:SA3% SA3,%Table:AD7% AD7
	WHERE A3_FILIAL = %xFilial:SA3% AND 
		SA3.%NotDel% AND 
		
		AD7_FILIAL = %xFilial:AD7% AND 
		AD7_DATA >= %Exp:DTOS(dDataIni)% AND AD7_DATA <= %Exp:DTOS(dDataFim)% AND 
		AD7_VEND = A3_COD AND 
		AD7.%NotDel%
	ORDER BY %Exp:SqlOrder(cOrder)%
	EndSql 
	oReport:Section(1):EndQuery({MV_PAR01})
	
	dbSelectArea(cAliasSA3)
	dbGoTop()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Usa Query da Secao Principal nas Secoes Filhas                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):Section(1):SetParentQuery()
	
#ELSE

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Utilizar a funcao MakeAdvlExpr, somente quando for utilizar o range de parametros para ambiente CDX ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeAdvplExpr("FTR320P9R1") 

	dbSelectArea("SA3")
	dbSetOrder(1)	     
	cCondicao := "A3_FILIAL=='" + xFilial("SA3") + "'"
	
	If !Empty(mv_par01)
		cCondicao += " .AND. " + mv_par01
	EndIf

	oReport:Section(1):SetFilter(cCondicao,IndexKey())
	
#ENDIF		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Define regra de saida do loop quando mutilizado metodo Print            ³
//³Altera texto dos totalizadores de acordo com ordem selecionada          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	oReport:Section(1):Section(1):SetParentFilter({|x| (cAliasAD7)->AD7_FILIAL+(cAliasAD7)->AD7_VEND == x}, {||xFilial("AD7")+(cAliasSA3)->A3_COD} )
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Metodo TrPosition()                                                     ³
//³                                                                        ³
//³Posiciona em um registro de uma outra tabela. O posicionamento será     ³
//³realizado antes da impressao de cada linha do relatório.                ³
//³                                                                        ³
//³                                                                        ³
//³ExpO1 : Objeto Report da Secao                                          ³
//³ExpC2 : Alias da Tabela                                                 ³
//³ExpX3 : Ordem ou NickName de pesquisa                                   ³
//³ExpX4 : String ou Bloco de código para pesquisa. A string será macroexe-³
//³        cutada.                                                         ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRPosition():New(oReport:Section(1):Section(1),"SA1",1,{|| xFilial("SA1")+(cAliasAD7)->AD7_CODCLI+(cAliasAD7)->AD7_LOJA })
TRPosition():New(oReport:Section(2),"SA1",1,{|| xFilial("SA1")+(cAliasAD8)->AD8_CODCLI+(cAliasAD8)->AD8_LOJCLI })

TRPosition():New(oReport:Section(1):Section(1),"SU5",1,{|| xFilial("SU5")+(cAliasAD7)->AD7_CONTAT })
TRPosition():New(oReport:Section(1):Section(1),"SUS",1,{|| xFilial("SUS")+(cAliasAD7)->AD7_PROSPE+(cAliasAD7)->AD7_LOJPRO })

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da impressao do fluxo do relatório                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetMeter((cAliasSA3)->(LastRec()))
While !oReport:Cancel() .And. !(cAliasSA3)->(Eof())
	
	cVendedor := (cAliasSA3)->A3_COD
	cCodUsr   := (cAliasSA3)->A3_CODUSR

	If !lQuery
		dbSelectArea(cAliasAD7)
		dbSetOrder(1)
		dbSeek(xFilial(cAliasAD7)+(cAliasSA3)->A3_COD  )
	EndIf	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Impressao das Agendas do Representante                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):Init()
	oReport:Section(1):Section(1):Init()
	oReport:Section(1):PrintLine()

	dbSelectArea(cAliasAD7)
	While !oReport:Cancel() .And. !(cAliasAD7)->(Eof()) .And. (cAliasAD7)->AD7_VEND == cVendedor
		oReport:section(1):Section(1):PrintLine()
		dbSelectArea(cAliasAD7)
		dbSkip()
	EndDo
	oReport:Section(1):Section(1):Finish()
	oReport:Section(1):Finish()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Filtro das Tarefas do Representante                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("AD8")		// Tarefas (CRM)
	dbSetOrder(2)			// Codigo do Usuario, Data inicial
	#IFDEF TOP
		cOrder := "%" + AD8->(IndexKey()) + "%"
		cWhere := "%"
		Do Case
		Case nListTar == 1
			cWhere += "AD8_STATUS<>'5' AND AD8_STATUS<>'3' AND "
		Case nListTar == 2
			cWhere += "( AD8_STATUS='5' OR AD8_STATUS='3' ) AND "
		EndCase
		cWhere += "%"
	
		oReport:Section(2):BeginQuery()	
		BeginSql Alias cAliasAD8
		SELECT *	
		FROM %Table:AD8% AD8
		WHERE 
			AD8_FILIAL = %xFilial:AD8% AND
			AD8_CODUSR = %Exp:cCodUsr%     AND 
			AD8_DTINI >= %Exp:DToS( dDtTaskIni )% AND 
			AD8_DTFIM <= %Exp:DToS( dDtTaskFim )% AND 
			%Exp:cWhere%
			AD8.%Notdel%
		ORDER BY %Exp:SqlOrder(cOrder)%
		EndSql 
		oReport:Section(2):EndQuery(/*Array com os parametros do tipo Range*/)
	
	#ELSE 

		cCondicao := ""
		cCondicao := "AD8_FILIAL='" + xFilial("AD8")+ "' .AND. "
		cCondicao += "AD8_CODUSR = '"+ cCodUsr + "' .AND. "
		cCondicao += "DTOS(AD8_DTINI) >= '"+ DToS( dDtTaskIni ) + "' .AND. "
		cCondicao += "DTOS(AD8_DTFIM) <= '"+ DToS( dDtTaskFim ) + "'"

		Do Case
		Case nListTar == 1
			cCondicao += " .AND. AD8_STATUS<>'5' .AND. AD8_STATUS<>'3' "
		Case nListTar == 2
			cCondicao += " .AND. ( AD8_STATUS='5' .OR. AD8_STATUS='3' )"
		EndCase
		oReport:Section(2):SetFilter(cCondicao,IndexKey())

	#ENDIF	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Impressao das Tarefas do representante                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(2):Print()
	
    If nSaltaPag == 1
    	oReport:Section(1):SetPageBreak()
    EndIf
	
	dbSelectArea(cAliasSA3)
	If !lQuery
		dbSkip()
	EndIf	
	oReport:IncMeter()
EndDo

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Fatr320   º Autor ³ Kleber Dias Gomes  º Data ³  05/10/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ WorkArea.                                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6 IDE                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fatr320R3(aParam)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cDesc1         := STR0001 //"Este programa tem como objetivo imprimir relatorio "
Local cDesc2         := STR0002 //"de acordo com os parametros informados pelo usuario."
Local cDesc3         := STR0003 //"Workarea"
Local cPict          := ""
Local cString        := "SA3"
Local titulo         := STR0003
Local Li             := 80
Local wnrel          := "FATR320"
Local imprime        := .T.
Local aOrd           := {}
Local nomeprog       := "FATR320"

Private lEnd         := .F.
Private lAbortPrint  := .F.
Private limite       := 220
Private tamanho      := "G"

Private nTipo        := 18
Private aReturn      := { STR0004, 1, STR0005, 2, 2, 1, "", 1} //"Zebrado"###"Administracao"
Private nLastKey     := 0
Private cPerg        := "FTR320"
Private cbtxt        := Space(10)
Private cbcont       := 00
Private CONTFL       := 01
Private m_pag        := 01

dbSelectArea("SA3")
dbSetOrder(1)

If ( aParam <> Nil )
	cPerg := ""
Else
	Pergunte(cPerg,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a interface padrao com o usuario...                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

wnrel := SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.F.,Tamanho,,.T.)

If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Return
Endif

nTipo := If(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento. RPTSTATUS monta janela com a regua de processamento. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

RptStatus({|| RunReport(wnRel,cString,NomeProg,Titulo,Li,aParam) },Titulo)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³RUNREPORT º Autor ³ Kleber Dias Gomes  º Data ³  05/10/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Funcao auxiliar chamada pela RPTSTATUS. A funcao RPTSTATUS º±±
±±º          ³ monta a janela com a regua de processamento.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Programa principal                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function RunReport(wnRel,cString,NomeProg,Titulo,Li,aParam)

//Local nOrdem

Local cCabec1 := STR0006 //"AGENDA - REPRESENTANTE NOME"
Local cCabec2 := STR0007 //"ASSUNTO                        DATA       H.INICIO H.FIM COMENTARIO                               OPORTUNIDADE CLIENTE LOJA NOME                 PROSPECT LOJA NOME                 CONTATO NOME"

//"TAREFA - REPRESENTANTE NOME"
//"ASSUNTO                        DT.INICIO  DT.TERMINO STATUS       PRIORIDADE %COMPLETO COMENTARIO                               CLIENTE LOJA NOME"

//          1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17         18        19        20        21       22
//01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//AGENDA REPRESENTANTE NOME
//       XXXXXX        XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
//ASSUNTO                        DATA       H.INICIO H.FIM COMENTARIO                               OPORTUNIDADE CLIENTE LOJA NOME                 PROSPECT LOJA NOME                 CONTATO NOME
//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX/XX/XXXX XXXXX    XXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXX       XXXXXX  XX   XXXXXXXXXXXXXXXXXXXX XXXXXX   XX   XXXXXXXXXXXXXXXXXXXX XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

//TAREFA - REPRESENTANTE NOME
//         XXXXXX        XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
//ASSUNTO                        DT.INICIO  DT.TERMINO STATUS       PRIORIDADE %COMPLETO COMENTARIO                               CLIENTE LOJA NOME
//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX/XX/XXXX XX/XX/XXXX XXXXXXXXXXXX XXXXXX        999,99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXX  XX   XXXXXXXXXXXXXXXXXXXX

Local aStruAD8  := {}
Local aItStatus := {STR0008,STR0009,STR0010,STR0011,STR0012} //"Não Iniciada"###"Em Andamento"###"Completada"###"Suspensa"###"Encerrada"###
Local aPrior    := {STR0013,STR0014,STR0015} //"Baixa"###"Normal"###"Alta"	
#IFNDEF TOP
	Local aStatus   := {}
#ENDIF
Local cAliasAD8 := "AD8"
Local cLinha    := 0
Local cMemo     := ""
Local cQuebra   := ""
Local cQuery    := ""
Local cReprDe   := ""
Local cReprAte  := ""
Local dDataIni
Local dDataFim
Local dDtTaskIni
Local dDtTaskFim
Local nCabec    := 1
Local nMemCount := 0
Local nLoop     := 0
Local nListTar  := 0
Local nTaskFrom := 3
Local nSaltaPag := 2
Local nStru     := 0
Local lCabec    := .T.
Local lImpParam := Nil

If ( aParam <> Nil )
	For nLoop := 1 To Len( aParam )
		lImpParam:= .F.
		cTipo	:= aParam[ nLoop, 1 ]
		Do Case
		Case cTipo == "REPRID_FROM"
			cReprDe  := aParam[nLoop,2]
		Case cTipo == "REPRID_TO"
			cReprAte := aParam[nLoop,2]
		Case cTipo == "DATE_TO"
			dDataIni  := aParam[nLoop,2]
			dDtTaskIni:= aParam[nLoop,2]
		Case cTipo == "DATE_FROM"
			dDataFim  := aParam[nLoop,2]
			dDtTaskFim:= aParam[nLoop,2]				
		Case cTipo == "LIST_TASK"
			nListTar := aParam[nLoop,2]
		Case cTipo == "TASK_FROM"
			nTaskFrom:= aParam[nLoop,2]
		Case cTipo == "EJECT_PAGE"
			nSaltaPag := aParam[nLoop,2]
		EndCase
	Next nLoop

	Do Case
	Case nTaskFrom == 1
		dDtTaskIni := FirstDay(dDataIni)
		dDtTaskFim := LastDay(dDataIni)
	Case nTaskFrom == 2
		aSemana := LimSemana( dDataIni )
		dDtTaskIni := aSemana[1]
		dDtTaskFim := aSemana[2]
	EndCase
Else
	cReprDe   := MV_PAR01    // Representante De
	cReprAte  := MV_PAR02    // Representante Ate
	dDataIni  := MV_PAR03    // Data De
	dDataFim  := MV_PAR04    // Data Ate
	dDtTaskIni:= MV_PAR03    // Data De
	dDtTaskFim:= MV_PAR04    // Data Ate
	nListTar  := MV_PAR05    // Listar Tarefas (Pendentes, Concluidas, Todas)
	nSaltaPag := MV_PAR06    // Salta Pagina por Representante
EndIf

dbSelectArea("SA3")
dbSetOrder(1)
dbSeek(xFilial("SA3")+cReprDe,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ SETREGUA -> Indica quantos registros serao processados para a regua ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetRegua(RecCount())

While ( !Eof() .And. SA3->A3_COD >= cReprDe .And. SA3->A3_COD <= cReprAte )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o cancelamento pelo usuario...                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lAbortPrint
		@Li,00 PSAY STR0016 //"*** CANCELADO PELO OPERADOR ***"
		Exit
	Endif

	dbSelectArea("AD7")
	dbSetOrder(1)
	dbSeek(xFilial("AD7")+SA3->A3_COD+Dtos(dDataIni),.T.)
	While ( !Eof() .And. xFilial("AD7") == AD7->AD7_FILIAL .And.;
			AD7->AD7_VEND == SA3->A3_COD .And.;
			AD7->AD7_DATA >= dDataIni .And.;
			AD7->AD7_DATA <= dDataFim )
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do cabecalho do relatorio. . .                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Li > 60 .Or. ( nSaltaPag == 1 .And. SA3->A3_COD != cQuebra )
			Li := Cabec(Titulo,cCabec1,cCabec2,NomeProg,Tamanho,nTipo,,lImpParam)
			Li++
			nCabec := 1
			lCabec := .T.
			cQuebra:= SA3->A3_COD
		EndIf
	
		If ( nSaltaPag == 2 .And. SA3->A3_COD != cQuebra )
			lCabec := .T.
			cQuebra:= SA3->A3_COD
		EndIf
									
		cCabec1        := STR0006 //"AGENDA - REPRESENTANTE NOME"
		cCabec2        := STR0007 //"ASSUNTO                        DATA       H.INICIO H.FIM COMENTARIO                               OPORTUNIDADE CLIENTE LOJA NOME                 PROSPECT LOJA NOME                 CONTATO NOME"
		cMemo     := MSMM(AD7_CODMEM)
		nMemCount := MlCount( cMemo )
		If Li > 60
			Li := Cabec(Titulo,cCabec1,cCabec2,NomeProg,Tamanho,nTipo,,lImpParam)
			Li++
			nCabec := 1
			lCabec := .T.
		Endif
		If nCabec == 2
			@li,000 PSAY __PrtThinLine()
			Li++
			@li,000 PSAY cCabec1
			Li++
			@li,000 PSAY cCabec2
			Li++
			@li,000 PSAY __PrtThinLine()
			Li++
			nCabec := 1
			lCabec := .T.
		EndIf
		If lCabec
			@Li,09  PSAY SA3->A3_COD
			@Li,23  PSAY SA3->A3_NOME
			lCabec := .F.
			Li++
		EndIf
		@Li,00  PSAY AD7_TOPICO
		@Li,31  PSAY AD7_DATA
		@Li,42  PSAY AD7_HORA1
		@Li,51  PSAY AD7_HORA2
		@Li,57  PSAY If(!Empty(nMemCount),MemoLine(cMemo,40,1),"")
		@Li,98  PSAY AD7_NROPOR
		@Li,111 PSAY AD7_CODCLI
		@Li,119 PSAY AD7_LOJA
		SA1->(dbSetOrder(1))
		If SA1->(dbSeek(xFilial("SA1")+AD7->AD7_CODCLI+AD7->AD7_LOJA))
			@Li,124 PSAY PadR(SA1->A1_NOME,20)
		EndIf
		@Li,145 PSAY AD7_PROSPE
		@Li,154 PSAY AD7_LOJPRO
		SUS->(dbSetOrder(1))
		If SUS->(dbSeek(xFilial("SUS")+AD7->AD7_PROSPE+AD7->AD7_LOJPRO))
			@Li,159 PSAY PadR(SUS->US_NOME,20)
		EndIf
		@Li,180 PSAY AD7_CONTAT
		SU5->(dbSetOrder(1))
		If SU5->(dbSeek(xFilial("SU5")+AD7->AD7_CONTAT))
			@Li,188 PSAY PadR(SU5->U5_CONTAT,30)
		EndIf

		nMemCount := MlCount(cMemo,40)
		If nMemCount > 1
			For nLoop := 2 To nMemCount
				cLinha := MemoLine( cMemo, 40, nLoop )
				Li++
				@Li,57 PSAY cLinha
				If Li > 60
					Li := Cabec(Titulo,cCabec1,cCabec2,NomeProg,Tamanho,nTipo,,lImpParam)
					Li++
				Endif
			Next nLoop
		EndIf
		Li++
		dbSelectArea("AD7")
		dbSkip()
	EndDo

	dbSelectArea("AD8")
	dbSetOrder(2)
	#IFDEF TOP
		cAliasAD8 := GetNextAlias()

		aStruAD8 := AD8->(dbStruct())

		cQuery := ""
		cQuery += "SELECT * FROM " + RetSqlName( "AD8" ) + " "
		cQuery += "WHERE "
		cQuery += "AD8_FILIAL='" + xFilial( "AD8" )   + "' AND "
		cQuery += "AD8_CODUSR='" + SA3->A3_CODUSR     + "' AND "
		cQuery += "AD8_DTINI>='" + DToS( dDtTaskIni ) + "' AND "
		cQuery += "AD8_DTINI<='" + DToS( dDtTaskFim ) + "' AND "
		Do Case
		Case nListTar == 1
			cQuery += "AD8_STATUS<>'5' AND AD8_STATUS<>'3' AND "
		Case nListTar == 2
			cQuery += "( AD8_STATUS='5' OR AD8_STATUS='3' ) AND "
		EndCase
		cQuery += "D_E_L_E_T_<>'*'"
		cQuery += " ORDER BY "+SqlOrder(AD8->(IndexKey()))
		cQuery := ChangeQuery( cQuery )
		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAD8, .T., .T. )

		For nStru := 1 To Len(aStruAD8)
			If aStruAD8[nStru,2] <> "C" .And. !Empty( ( cAliasAD8 )->( FieldPos( aStruAD8[nStru,1] ) ) )
				TcSetField(cAliasAD8,aStruAD8[nStru,1],aStruAD8[nStru,2],aStruAD8[nStru,3],aStruAD8[nStru,4])
			EndIf
		Next nStru
	#ELSE
		cAliasAD8 := "AD8
		aStatus := {}
		AAdd( aStatus, { || AD8->AD8_STATUS != "3" .And. AD8->AD8_STATUS != "5" } )
		AAdd( aStatus, { || AD8->AD8_STATUS == "3" .Or.  AD8->AD8_STATUS == "5" } )
		AAdd( aStatus, { || .T. } )
		dbSelectArea(cAliasAD8)
		dbGoTop()
		dbSeek(xFilial("AD8")+SA3->A3_CODUSR+DtoS(dDtTaskIni),.T.)
	#ENDIF

	While ( !Eof() .And. xFilial("AD8") == (cAliasAD8)->AD8_FILIAL .And.;
			(cAliasAD8)->AD8_CODUSR == SA3->A3_CODUSR .And. (cAliasAD8)->AD8_DTINI >= dDtTaskIni .And.;
			(cAliasAD8)->AD8_DTINI <= dDtTaskFim )
		#IFNDEF TOP
			If Eval(aStatus[ nListTar ])
		#ENDIF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Impressao do cabecalho do relatorio. . .                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Li > 60 .Or. ( nSaltaPag == 1 .And. SA3->A3_COD != cQuebra )
				Li := Cabec(Titulo,cCabec1,cCabec2,NomeProg,Tamanho,nTipo,,lImpParam)
				Li++
				nCabec := 1
				lCabec := .T.
				cQuebra:= SA3->A3_COD
			EndIf
		
			If ( nSaltaPag == 2 .And. SA3->A3_COD != cQuebra )
				lCabec := .T.
				cQuebra:= SA3->A3_COD
			EndIf		
				
			cCabec1   := STR0017 //"TAREFA - REPRESENTANTE NOME"
			cCabec2   := STR0018 //"ASSUNTO                        DT.INICIO  DT.TERMINO STATUS       PRIORIDADE %COMPLETO COMENTARIO                               CLIENTE LOJA NOME"
			cMemo     := MSMM((cAliasAD8)->AD8_CODMEM)
			nMemCount := MlCount( cMemo )
			If Li > 60
				Li := Cabec(Titulo,cCabec1,cCabec2,NomeProg,Tamanho,nTipo,,lImpParam)
				Li++
				nCabec := 2
				lCabec := .T.
			Endif
			If nCabec == 1
				@li,000 PSAY __PrtThinLine()
				Li++
				@li,000 PSAY cCabec1
				Li++
				@li,000 PSAY cCabec2
				Li++
				@li,000 PSAY __PrtThinLine()
				Li++
				nCabec := 2
				lCabec := .T.
			EndIf
			If lCabec
				@Li,09  PSAY SA3->A3_COD
				@Li,23  PSAY SA3->A3_NOME
				lCabec := .F.
				Li++
			EndIf
			@Li,00  PSAY (cAliasAD8)->AD8_TOPICO
			@Li,31  PSAY (cAliasAD8)->AD8_DTINI
			@Li,42  PSAY (cAliasAD8)->AD8_DTFIM
			@Li,53  PSAY aItStatus[Val((cAliasAD8)->AD8_STATUS)]
			@Li,66  PSAY aPrior[Val((cAliasAD8)->AD8_PRIOR)]
			@Li,77  PSAY Transform((cAliasAD8)->AD8_PERC,PesqPict("AD8","AD8_PERC"))
			@Li,87  PSAY If(!Empty(nMemCount),MemoLine(cMemo,40,1),"")
			@Li,128 PSAY (cAliasAD8)->AD8_CODCLI
			@Li,136 PSAY (cAliasAD8)->AD8_LOJCLI
			SA1->(dbSetOrder(1))
			If SA1->(dbSeek(xFilial("SA1")+(cAliasAD8)->AD8_CODCLI+(cAliasAD8)->AD8_LOJCLI))
				@Li,141 PSAY PadR(SA1->A1_NOME,40)
			EndIf
			nMemCount := MlCount(cMemo,40)
			If nMemCount > 1
				For nLoop := 2 To nMemCount
					cLinha := MemoLine( cMemo, 40, nLoop )
					Li++
					@Li,87 PSAY cLinha
					If Li > 60
						Li := Cabec(Titulo,cCabec1,cCabec2,NomeProg,Tamanho,nTipo,,lImpParam)
						Li++
					Endif
				Next nLoop
			EndIf
			Li++
			#IFNDEF TOP
			EndIf
			#ENDIF
		dbSelectArea(cAliasAD8)	
		dbSkip()
	EndDo

	#IFDEF TOP
		dbSelectArea(cAliasAD8)
		dbCloseArea()
		dbSelectArea("AD8")
	#ENDIF

	dbSelectArea("SA3")
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza a execucao do relatorio...                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SET DEVICE TO SCREEN

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se impressao em disco, chama o gerenciador de impressao...          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aReturn[5]==1
	dbCommitAll()
	SET PRINTER TO
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return
