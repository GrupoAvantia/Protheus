#INCLUDE "CNTR020.CH"
#INCLUDE "PROTHEUS.CH" 

#DEFINE DEF_ADITI "1" //Aditivo                                   
#DEFINE DEF_REAJU "2" //Reajuste
#DEFINE DEF_REALI "3" //Realinhamento
#DEFINE DEF_READQ "4" //Readequacao
#DEFINE DEF_PARAL "5" //Paralisacao
#DEFINE DEF_REINI "6" //Reinicio
#DEFINE DEF_CLAUS "7" //Alteracao de Clausula

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CNTR020  ³ Autor ³ Marcos V. Ferreira    ³ Data ³ 10.03.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Revisao de Contratos                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CNTR020R3()
Local cMens := ""
Local nOpca := 0

Private cTitRel := OemToAnsi(STR0001) //Revisoes
Private lEnd 	:= .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajustar perguntas do SX1									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1('CNR020')          

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Contrato de:                                 ³
//³ mv_par02     // Contrato ate:                                ³
//³ mv_par03     // Revisao de                                   ³
//³ mv_par04     // Revisao ate                                  ³
//³ mv_par05     // Vigencia de:                                 ³
//³ mv_par06     // Vigencia ate:                                ³
//³ mv_par07     // Fornecedor de:                               ³
//³ mv_par08     // Fornecedor ate:                              ³
//³ mv_par09     // Tipo de Contrato ?                           ³
//³ mv_par10     // Cliente    de:                               ³
//³ mv_par11     // Cliente    ate:                              ³
//| mv_par12     // Imprimir valores - Moeda do ctr/Outra moeda  ³
//| mv_par13     // Moeda: 										 ³
//| mv_par14     // Data de Referencia:							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("CNR020",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tela de configuracao do Relatorio							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg FROM  96,9 TO 310,592 TITLE OemToAnsi(STR0001) PIXEL    //"Revisoes de Contratos"
@ 18, 6 TO 66, 287 LABEL "" OF oDlg  PIXEL
@ 29, 15 SAY OemToAnsi(STR0002) SIZE 268, 8 OF oDlg PIXEL    //"Este relatorio ira emitir uma relacao de revisôes de contratos e suas respectivas"
@ 38, 15 SAY OemToAnsi(STR0003) SIZE 268, 8 OF oDlg PIXEL    //"planilhas e crongramas referentes aos contrato selecionados."
DEFINE SBUTTON FROM 80, 190 TYPE 5 ACTION Pergunte("CNR020",.T.) ENABLE OF oDlg
DEFINE SBUTTON FROM 80, 223 TYPE 1 ACTION (oDlg:End(),nOpca:=1) ENABLE OF oDlg
DEFINE SBUTTON FROM 80, 255 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg CENTER

If mv_par12 == 2
	cTitRel += STR0024 +AllTrim(GetMv("MV_SIMB" +Ltrim(Str(mv_par13)))) +")"
EndIf

If nOpca == 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento do Relatorio									 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RptStatus({|lEnd| CNR020Imp(@lEnd)})

EndIf	

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CNR020IMP³ Autor ³ Marcos V. Ferreira    ³ Data ³ 10.03.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CNR020Imp(lEnd)
Local cAliasTOP := CriaTrab(Nil, .F.)
Local cQuery	:= ""
Local cSeek		:= ""
Local aStrucCN9 := {}
Local cDirSpool := GetMv("MV_RELT")
Local lImpJust  := .F.
Local nTotLinha := 0
Local nX		:= 0                                                 
Local X,y
Local cTipContra
Local aTipContr:={}
Local cTipoCtr :=""                      
Local cEspec   :=""
Local cNumContra:=""
Local nPosContr:=0
Local lImpData:=.F.
Local cCodTipo:=""
Local lCronJaImp   :=.T.
Local lCronCtbJaImp:=.T.
Local lPlanJaImp   :=.T.
Local nTotPag      :=0
Local nPagAtu      :=1
Local lFiltForn    :=!Empty(mv_par07) .Or. (!Empty(mv_par08) .And. UPPER(mv_par08) != REPLICATE("Z",TamSx3("CNC_CODIGO")[1]))
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera Picture dos Campos									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cPtVIGE	:= PesqPict("CN9","CN9_VIGE")
Local cPtMINCAU := PesqPict("CN9","CN9_MINCAU")
Local cPtVLINI  := PesqPict("CN9","CN9_VLINI")
Local cPtVLATU  := PesqPict("CN9","CN9_VLATU")
Local cPtSALDO  := PesqPict("CN9","CN9_SALDO")
Local cPtData   := PesqPict("CN9","CN9_DTREV")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa Fontes											 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local oFont06	:= TFont():New("Arial",06,08,,.T.,,,,.T.,.F.)
Local oFont10	:= TFont():New("Arial",10,10,,.F.,,,,.T.,.F.)
Local oFont10n	:= TFont():New("Arial",10,10,,.T.,,,,.T.,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas pelo Objeto TMSPrinter					     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nlin	:= 2900
Private nPagina := 0
Private oPrint   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis Gerais                                    			  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cImpCtbCro:=.T. // Imprimi Cronograma Contabil
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa Objeto TMSPrinter								           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPrint	:= TMSPrinter():New(OemToAnsi(STR0001)) //"Revisoes"
oPrint	:SetPortrait()	//Retrato
oPrint	:Setup() 		//Configurar Impressao       


DbselectArea("CN1")
Dbseek(xFilial()+MV_PAR09)
If Found()
   cTipContra:=CN1->CN1_ESPCTR //TIPO DE CONTRAT COMPRA/VENDA
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Query do Contrato											           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStrucCN9 := CN9->(dbStruct())
cQuery := "SELECT DISTINCT CN9_NUMERO, CN9_REVISA, CN9_DTINIC, CN9_SITUAC, CN9_DTFIM, CN9_DTPROP, "
cQuery += "CN9_DTASSI, CN9_DTENCE, CN9_CONDPG, CN9_DTREV, CN9_DTREIN,CN9_DTFIMP, CN9_TPCTO, CN9_DTULST, CN9_VIGE, CN9_INDICE, "
cQuery += "CN9_FLGCAU, CN9_MINCAU, CN9_FLGREJ, CN9_REVATU, CN9_VLINI, CN9_VLATU, CN9_SALDO, "
cQuery += "CN9_CODOBJ, CN9_CODCLA, CN9_CODJUS, CN9_UNVIGE, CN0_CODIGO, CN0_TIPO, CN0_DESCRI, CN0_ESPEC, CN9_CLIENT, CN9_LOJACL "
cQuery += "FROM " + RetSqlName("CN9") + " CN9  "
If lFiltForn
	cQuery += "LEFT JOIN " + RetSqlName("CNC") + " CNC ON CN9.CN9_NUMERO = CNC.CNC_NUMERO " 
EndIf
cQuery += "LEFT JOIN " + RetSqlName("CN0") + " CN0 ON CN9.CN9_TIPREV = CN0.CN0_CODIGO "
cQuery += "WHERE "
cQuery += "CN9_FILIAL = '"		+	xFilial("CN9")	+ "' AND "
cQuery += "CN9_NUMERO >= '"		+	mv_par01		+ "' AND "
cQuery += "CN9_NUMERO <= '"		+	mv_par02		+ "' AND "
cQuery += "CN9_REVISA >= '"		+	mv_par03		+ "' AND "
cQuery += "CN9_REVISA <= '"		+	mv_par04		+ "' AND "
cQuery += "CN9_DTINIC >= '"		+	dtos(mv_par05)	+ "' AND "
cQuery += "CN9_DTFIM  <= '"		+	dtos(mv_par06)	+ "' AND "
If lFiltForn
	cQuery += "CNC_FILIAL = '"		+	xFilial("CNC")	+ "' AND "
	cQuery += "CNC_CODIGO >= '"		+	mv_par07		+ "' AND "
	cQuery += "CNC_CODIGO <= '"		+	mv_par08		+ "' AND "
	cQuery += "CNC.D_E_L_E_T_ = '' AND "       
Else
	cQuery += "CN9_CLIENT >= '"    +	mv_par10		+ "' AND "
	cQuery += "CN9_CLIENT <= '"    +	mv_par11		+ "' AND "
EndIf
If !Empty(mv_par09)
	cQuery += "CN9_TPCTO = '"		   + 	mv_par09		+ "' AND "
EndIf
cQuery += "CN9.D_E_L_E_T_ = '' "
cQuery += "ORDER BY CN9_NUMERO, CN9_REVISA "

cQuery := ChangeQuery(cQuery)
MsAguarde({|| dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliasTOP,.F.,.T.)},OemToAnsi(STR0005)) //Processando

For nX := 1 to Len(aStrucCN9)
	If aStrucCN9[nX,2] != 'C' .And. FieldPos(aStrucCN9[nx,1]) > 0
		TCSetField(cAliasTop,aStrucCN9[nX,1], aStrucCN9[nX,2],aStrucCN9[nX,3],aStrucCN9[nX,4])
	EndIf	
Next nX

dbSelectArea(cAliasTop)
SetRegua(LastRec())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criando array com todos os tipos de revisão   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cNumContra:=(cAliasTop)->CN9_NUMERO
Do While !Eof()                                              
    DbselectArea("CN0")                        
    DbSetOrder(1)
    DbSeek(xfilial()+(cAliasTop)->CN0_CODIGO)
    cCodTipo +=If(Len(ALLTRIM(CN0->CN0_CODIGO))>0,CN0->CN0_CODIGO,"000")+","
    cTipoCtr +=If(Len(ALLTRIM(CN0->CN0_TIPO))>0,CN0->CN0_TIPO,"0")
    cEspec   +=If(Len(ALLTRIM(CN0->CN0_ESPEC))>0,CN0->CN0_ESPEC,"0")
    DbselectArea(cAliasTop)
    (cAliasTop)->(dbSkip())
    if cNumContra<>(cAliasTop)->CN9_NUMERO 
       aAdd(aTipContr,{{cNumContra,cTipoCtr,cEspec,cCodTipo}})
       cNumContra:=(cAliasTop)->CN9_NUMERO
       nTotPag:=0
    Endif                                                    
Enddo

(cAliasTop)->(dbGoTop())

Do While !Eof()
	If lEnd
		CNRBox(0050,2350,OemToAnsi(STR0004),,.T.,1) //-- CANCELADO PELO OPERADOR
		Exit
	EndIf

	IncProc()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa variaveis		            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nLin	 := 2900	//-- Forca a Quebra da pagina por Contrato
	lImpJust := .F.		//-- Imprimir Justificativas	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressao da Revisao				  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//-- Cabecalho da Revisao
	CNRBox(0050,2350,OemToAnsi(STR0009)+(cAliasTop)->CN9_REVISA,,.T.,1)
	//-- Numero do Contrato
	CNRBox(0050,0800,RetTitle("CN9_NUMERO"),(cAliasTop)->CN9_NUMERO,.F.)
	//-- Revisao
	CNRBox(0805,1200,RetTitle("CN9_REVISA"),(cAliasTop)->CN9_REVISA,.F.)
    //-- Tipo da Revisao
	CNRBox(1205,2350,RetTitle("CN0_TIPO"),(cAliasTop)->CN0_TIPO+" - "+(cAliasTop)->CN0_DESCRI,.T.)
	//-- Situacao
	CNRBox(0050,0800,RetTitle("CN9_SITUAC"),CNR010Sit((cAliasTop)->CN9_SITUAC),.F.)
	//-- Tipo do Documento
	CNRBox(0805,1600,RetTitle("CN9_TPCTO"),(cAliasTop)->CN9_TPCTO + " - " + Posicione("CN1",1,xFilial("CN1")+(cAliasTop)->CN9_TPCTO,"CN1_DESCRI"),.F.)
	//-- Data Inicio
	CNRBox(1605,2000,RetTitle("CN9_DTINIC"),dtoc((cAliasTop)->CN9_DTINIC),.F.)
	//-- Data Final
	CNRBox(2005,2350,RetTitle("CN9_DTFIM"),dtoc((cAliasTop)->CN9_DTFIM),.T.)

	If !Empty((cAliasTop)->CN9_CLIENT)
		//-- Cliente
		CNRBox(0050,0800,RetTitle("CN9_CLIENT"),(cAliasTop)->CN9_CLIENT,.F.)
		//-- Loja
		CNRBox(0805,1300,RetTitle("CN9_LOJACL"),(cAliasTop)->CN9_LOJACL,.F.)
		//-- Descricao
		CNRBox(1305,2350,STR0023,Posicione("SA1",1,xFilial("SA1")+(cAliasTop)->CN9_CLIENT+(cAliasTop)->CN9_LOJACL,"A1_NOME"),.T.)//"Razão Social"
	EndIf

	//-- Valor Inicial
	CNRBox(0050,0800,RetTitle("CN9_VLINI"),TransForm((cAliasTop)->(C020xMoeda(.F.,CN9_NUMERO,CN9_REVISA,"CN9_VLINI")),cPtVLINI),.F.)
	//-- Valor Atual
	CNRBox(0805,1200,RetTitle("CN9_VLATU"),TransForm((cAliasTop)->(C020xMoeda(.F.,CN9_NUMERO,CN9_REVISA,"CN9_VLATU")),cPtVLATU),.F.)
   
   nPosContr := aScan(aTipContr,{|x| x[1][1] ==(cAliasTop)->CN9_NUMERO })

	For x=1 to Len(Alltrim(aTipContr[nPosContr][1][2]))
      If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "56" 
         lImpData:=.T. //imprime data de revisão e  data prevista
      Endif
	Next x
   IF !lImpData
       //-- Saldo
     	CNRBox(1205,2350,RetTitle("CN9_SALDO"),TransForm((cAliasTop)->(C020xMoeda(.F.,CN9_NUMERO,CN9_REVISA,"CN9_SALDO")),cPtSALDO),.T.)
   Else
       //-- Saldo
     	CNRBox(1205,1600,RetTitle("CN9_SALDO"),TransForm((cAliasTop)->(C020xMoeda(.F.,CN9_NUMERO,CN9_REVISA,"CN9_SALDO")),cPtSALDO),.F.)
     	//-- Data Revisão
     	CNRBox(1605,2000,RetTitle("CN9_DTREV"),TransForm((cAliasTop)->CN9_DTREV,cPtData),.F.)
     	//-- Data Reinicio
     	CNRBox(2005,2350,RetTitle("CN9_DTFIMP"),TransForm((cAliasTop)->CN9_DTFIMP,cPtData),.T.)
     	lImpData:=.F.
   Endif 	
	                                                    
	nLin += 50 //-- Salta Linha
	
	For x=nPagAtu to Len(Alltrim(aTipContr[nPosContr][1][2]))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aditivos                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
     If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "1" 
        If Substr(Alltrim(aTipContr[nPosContr][1][3]),x,1) $ "1"      //ADITIVO DE QUANTIDADE
           	CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lPlanJaImp,.F.,.F.,.F.)
           	lPlanJaImp:=.F.
  	     ElseIf Substr(Alltrim(aTipContr[nPosContr][1][3]),x,1) $ "3" // ADITIVO DE PRAZO 
            CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,.F.,lCronJaImp,.F.,lCronCtbJaImp) 
            lCronJaImp   :=.F.
            lCronCtbJaImp:=.F. 
/*            IF lCronCtbJaImp                                                       
               CNR010CrnCtb((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA)
               lCronCtbJaImp:=.F.
            Endif */
         Else
            CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lPlanJaImp,lCronJaImp,.F.,lCronCtbJaImp) 
            lCronJaImp   :=.F.                                                  
            lPlanJaImp   :=.F.
            lCronCtbJaImp:=.F.
   	  	Endif
			//-- Restaura AliasTop
			dbSelectArea(cAliasTop)
			//-- Imprimir Justificativas
			lImpJust := .T. 
	  EndIf
   
     //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
     //³ REAJUSTE                                           ³
     //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                          
     If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "2" 
	     CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lPlanJaImp,lCronJaImp,.F.,lCronCtbJaImp)
	     lPlanJaImp   :=.F.
        lCronJaImp   :=.F.
        lCronCtbJaImp:=.F.
        
/*        IF lCronCtbJaImp
            CNR010CrnCtb((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lCronCtbJaImp)
            lCronCtbJaImp:=.F.
         Endif*/
  		  //-- Imprimir Justificativas				
		  lImpJust := .T.                                                         
     EndIf

	  //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  //³ REALINHAMENTO                                      ³
	  //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                          
     If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "3" 
	      CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lPlanJaImp,lCronJaImp,.F.,lCronCtbJaImp)
	      lPlanJaImp   :=.F.
         lCronJaImp   :=.F. 
			lCronCtbJaImp:=.F.

/*         IF lCronCtbJaImp
            CNR010CrnCtb((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lCronCtbJaImp)
            lCronCtbJaImp:=.F.
         Endif*/
  		  //-- Imprimir Justificativas				
		  lImpJust := .T.                                                         
	  EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Readequacao            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                          
		If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "4" 
			CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lPlanJaImp,lCronJaImp,.F.,lCronCtbJaImp)
			lPlanJaImp   :=.F.
			lCronJaImp   :=.F. 
			lCronCtbJaImp:=.F.
/*			If lCronCtbJaImp
				CNR010CrnCtb((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lCronCtbJaImp)
				lCronCtbJaImp:=.F.
			Endif*/
			//-- Imprimir Justificativas				
			lImpJust := .T.
		EndIf

	  //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  //³ Reinicio               ³
	  //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                          
     If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "6" 
	   	CNR010Pla((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA,lPlanJaImp,lCronJaImp,.F.,lCronCtbJaImp)
   	   lPlanJaImp   :=.F.
         lCronJaImp   :=.F.                                                                                         
         lCronCtbJaImp:=.F.
		   //-- Imprimir Justificativas				
		   lImpJust := .T.                                                         
	  EndIf

	  //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  //³ Contabil               ³
	  //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                          
		If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "8" 
			If lCronCtbJaImp
				CNR010CrnCtb((cAliasTop)->CN9_NUMERO,(cAliasTop)->CN9_REVISA)
				lCronCtbJaImp := .F.
			EndIf
		EndIf

	  //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  //³ Alteracao de Clausulas ou Paralisacao ³
	  //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ    
     If Substr(Alltrim(aTipContr[nPosContr][1][2]),x,1) $ "57" 
        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  //³ Imprime Texto de Clausulas do Contrato					       ³
		  //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		  nTotLinha := MlCount(MSMM((cAliasTop)->CN9_CODCLA))
		  CNRBox(0050,2350,OemToAnsi(STR0006),,.T.,1) //-- Cabecalho CLAUSULAS
		  For nX := 1 To nTotLinha
		    	CNRBox(0100,Nil,MemoLine(MSMM((cAliasTop)->CN9_CODCLA),,nX),,.T.,4,oFont10)
		  Next nX
        If nTotLinha == 0
		   	CNRBox(0100,0100,OemToAnsi(STR0008),,.T.,4) //-- EM BRANCO
		  EndIf
		  nLin += 50 //-- Salta linha
		  //-- Imprimir Justificativas
		  lImpJust := .T.
     EndIf
     nTotPag++
     If(nTotPag)>1
        Exit
     Endif
   Next x
   nPagAtu++
   nTotPag      := 0
   lPlanJaImp   :=.T.
   lCronJaImp   :=.T.
   lCronCtbJaImp:=.T.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Texto de Justificativas do Contrato				   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lImpJust .And. !Empty((cAliasTop)->CN9_REVISA)
      nLin += 50 //-- Salta linha
	   nTotLinha := MlCount(MSMM((cAliasTop)->CN9_CODJUS))
	   CNRBox(0050,2350,OemToAnsi(STR0007),,.T.,1) //-- Cabecalho JUSTIFICATIVAS
	   For nX := 1 To nTotLinha
	   	 CNRBox(0100,Nil,MemoLine(MSMM((cAliasTop)->CN9_CODJUS),,nX),,.T.,4,oFont10)
		Next nXt
	   If nTotLinha == 0
		  	CNRBox(0100,0100,OemToAnsi(STR0008),,.T.,4) //-- EM BRANCO
	   EndIf
	   nLin += 50 //-- Salta linha
	 EndIf
	(cAliasTop)->(dbSkip())
EndDo

//-- Encerra arquivo de Trabalho
dbSelectArea(cAliasTop)
dbCloseArea()

//-- Grava Imagem em Disco
If !Empty(cDirSpool)
	oPrint:SaveAllAsJPEG(cDirSpool+"CNTR020",875,1170,140)
EndIF	

//-- Visualiza antes de Imprimir
oPrint:Preview()  

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ AjustaSX1³ Autor ³ Marcos V. Ferreira    ³ Data ³10/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta as perguntas do SX1                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020  		                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1(cPerg)
Local aAreaAnt := GetArea()
Local aHelpPor := {}
Local aHelpEng := {}
Local aHelpSpa := {}
Local nTamSX1  := Len(SX1->X1_GRUPO)
Local nTamCli  := TamSx3('A1_COD')[1]
Local nTamFor  := TamSx3('CNC_CODIGO')[1]

If cPerg == "CNR020"

	SX1->( dbSetOrder( 1 ) )
	
	If !SX1->( dbSeek( cPerg ) ) 
		//---------------------------------------MV_PAR01--------------------------------------------------
		aHelpPor := {"Numero inicial do Contrato"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"01","Contrato de:","","","mv_ch1","C",15,0,0,"G","","CN9001","","S","mv_par01","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	
		//---------------------------------------MV_PAR02--------------------------------------------------
		aHelpPor := {"Numero final do Contrato"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"02","Contrato ate:","","","mv_ch2","C",15,0,0,"G","","CN9001","","S","mv_par02","","","","ZZZZZZZZZZZZZZZ","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR03--------------------------------------------------
		aHelpPor := {"Numero inicial da Revisao"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"03","Revisao de:","","","mv_ch3","C",3,0,0,"G","","","","S","mv_par03","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR04--------------------------------------------------
		aHelpPor := {"Numero final da Revisao"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"04","Revisao ate:","","","mv_ch4","C",3,0,0,"G","","","","S","mv_par04","","","","ZZZ","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR05--------------------------------------------------
		aHelpPor := {"Data de inicio da Vigencia"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"05","Vigencia de:","","","mv_ch5","D",08,0,0,"G","","","","S","mv_par05","","","","01/01/06","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR06--------------------------------------------------
		aHelpPor := {"Data de termino da Vigencia"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"06","Vigencia ate:","","","mv_ch6","D",08,0,0,"G","","","","S","mv_par06","","","","31/12/49","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR07--------------------------------------------------
		aHelpPor := {"Codigo inicial do Fornecedor"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"07","Fornecedor de:","","","mv_ch7","C",6,0,0,"G","","CNC","CNR020ClFr('CNR020','1')","S","mv_par07","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR08--------------------------------------------------
		aHelpPor := {"Codigo final do Fornecedor"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"08","Fornecedor ate:","","","mv_ch8","C",6,0,0,"G","CNR020ClFr('CNR020','1')","CNC","","S","mv_par08","","","","ZZZZZZ","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR09--------------------------------------------------
		aHelpPor := {"Codigo do Tipo de Contrato"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"09","Tipo de Contrato ?","","","mv_ch9","C",3,0,0,"G","","CN1","","S","mv_par09","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

	EndIf

	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"10"))
		//---------------------------------------MV_PAR10--------------------------------------------------
		aHelpPor := {"Codigo inicial do Cliente"}
		aHelpSpa := {"Codigo inicio del Cliente"}
		aHelpEng := {"Client initial Code"}

		
		PutSX1(cPerg,"10","Cliente de:","","","mv_cha","C",nTamCli,0,0,"G","CNR020ClFr('CNR020','2')","SA1GCT","001","S","mv_par10","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	 EndIf
	 If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"11"))
		//---------------------------------------MV_PAR11--------------------------------------------------
		aHelpPor := {"Codigo final do Cliente"}
		aHelpSpa := {"Codigo final del Cliente"}
		aHelpEng := {"Client final Code"}
		
		PutSX1(cPerg,"11","Cliente ate:","","","mv_chb","C",nTamCli,0,0,"G","CNR020ClFr('CNR020','2')","SA1GCT","001","S","mv_par11","","","",REPLICATE("Z",nTamCli),"","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	EndIf   

	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"12")) 	
		//---------------------------------------MV_PAR12--------------------------------------------------
		aHelpPor := {"Define se a impressão dos valores","no relatório será realizada na","moeda do contrato ou em outra moeda."}
		aHelpEng := {"Define if the values will be printed","using contract currency or other currency."}
		aHelpSpa := {"Define si la impresión de los valores","en el informe será realizada en la ","moneda del contrato o en otra moneda."}
		
		PutSX1(cPerg,"12","Imprimir valores na moeda","Imprimir valores en moneda","Print values in currency","mv_chc","N",1,0,0,"C","","","","S","mv_par12","Do contrato","Del contrato","Of Contract","","Informada","Informada","Informed","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	EndIf
	
	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"13"))
		//---------------------------------------MV_PAR13--------------------------------------------------
		aHelpPor := {"Indica a moeda para impressão","dos valores. A pergunta anterior",'deve estar como "Informada".'}
		aHelpEng := {"Indica la moneda para impresión","de los valores. La pregunta anterior",'debe estar como "Informada".'}
		aHelpSpa := {"Indicates the values currency ","printing. The previous question must",'be set as "Informed".'}
		
		PutSX1(cPerg,"13","Moeda","Moneda","Currency","mv_chd","N",1,0,0,"C","","","","S","mv_par13","Moeda 1","Moneda 1","Currency 1","","Moeda 2","Moneda 2","Currency 2","Moeda 3","Moneda 3","Currency 3","Moeda 4","Moneda 4","Currency 4","Moeda 5","Moneda 5","Currency 5",aHelpPor,aHelpEng,aHelpSpa)
	EndIf
	
	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"14")) 	
		//---------------------------------------MV_PAR14--------------------------------------------------
		aHelpPor := {"Data de referência para conversão","dos valores entre moedas."}
		aHelpEng := {"Fecha de referencia para conversión","de los valores entre monedas."}
		aHelpSpa := {"Reference date for values","convertion between currency."}
		
		PutSX1(cPerg,"14","Data de referencia","Fecha de referencia","Reference date","mv_che","D",8,0,0,"G","","","","S","mv_par14","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inclui validação no parâmetro MV_PAR07 - Fornecedor de ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SX1->( dbSeek(PadR(cPerg,nTamSX1)+"07",.T.) ) 
		If  SX1->X1_VALID != "CNR020ClFr('CNR020','1')"
			RecLock("SX1",.F.)
			SX1->X1_VALID := "CNR020ClFr('CNR020','1')"
			MsUnlock()
		EndIf  

		If  SX1->X1_TAMANHO!= nTamFor
			RecLock("SX1",.F.)
			SX1->X1_TAMANHO := nTamFor
			MsUnlock()
		EndIf            
	EndIf	                                              
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inclui validação e tamanho no parâmetro MV_PAR08 - Fornecedor para ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SX1->( dbSeek(PadR(cPerg,nTamSX1)+"08",.T.) ) 
		If  SX1->X1_VALID != "CNR020ClFr('CNR020','1')"
			RecLock("SX1",.F.)
			SX1->X1_VALID := "CNR020ClFr('CNR020','1')"
			MsUnlock()
		EndIf  
		
		If  SX1->X1_TAMANHO!= nTamFor
			RecLock("SX1",.F.)
			SX1->X1_TAMANHO := nTamFor
			MsUnlock()
		EndIf          
	EndIf

Else
	SX1->( dbSetOrder( 1 ) )
	
	If !SX1->( dbSeek( cPerg ) ) 
		//---------------------------------------MV_PAR01--------------------------------------------------
		aHelpPor := {"Numero do Contrato"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"01","Contrato ?","","","mv_ch1","C",15,0,0,"R","","CN9001","","S","mv_par01","","","","CN9_NUMERO","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR02--------------------------------------------------
		aHelpPor := {"Código da Revisao"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"02","Revisao ?","","","mv_ch2","C",3,0,0,"R","","","","S","mv_par02","","","","CN9_REVISA","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
			
		//---------------------------------------MV_PAR03--------------------------------------------------
		aHelpPor := {"Data de inicio da Vigencia"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"03","Vigencia de:","","","mv_ch3","D",08,0,0,"G","","","","S","mv_par03","","","","01/01/06","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR04--------------------------------------------------
		aHelpPor := {"Data de termino da Vigencia"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"04","Vigencia ate:","","","mv_ch4","D",08,0,0,"G","","","","S","mv_par04","","","","31/12/49","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR05--------------------------------------------------
		aHelpPor := {"Codigo do Fornecedor"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"05","Fornecedor ?","","","mv_ch5","C",6,0,0,"R","","CNC","","S","mv_par05","","","","CNC_CODIGO","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
			
		//---------------------------------------MV_PAR06--------------------------------------------------
		aHelpPor := {"Codigo do Tipo de Contrato"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"06","Tipo de Contrato ?","","","mv_ch6","C",3,0,0,"R","","CN1","","S","mv_par06","","","","CN9_SITUAC","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

	EndIf                                                                                  
	
	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"07"))
		//---------------------------------------MV_PAR07--------------------------------------------------
		aHelpPor := {"Codigo do Cliente"}
		aHelpSpa := {"Codigo del Cliente"}
		aHelpEng := {"Client Code"}

		PutSX1(cPerg,"07","Cliente:"    ,"","","mv_ch7","C",nTamCli,0,0,"R","CNR020ClFr('CNR021','2')","SA1GCT","001","S","mv_par07","","","","CN9_CLIENT","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)	
	EndIf
	
	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"08")) 	
		//---------------------------------------MV_PAR08--------------------------------------------------
		aHelpPor := {"Define se a impressão dos valores","no relatório será realizada na","moeda do contrato ou em outra moeda."}
		aHelpEng := {"Define if the values will be printed","using contract currency or other currency."}
		aHelpSpa := {"Define si la impresión de los valores","en el informe será realizada en la ","moneda del contrato o en otra moneda."}
		
		PutSX1(cPerg,"08","Imprimir valores na moeda","Imprimir valores en moneda","Print values in currency","mv_ch8","N",1,0,0,"C","","","","S","mv_par08","Do contrato","Del contrato","Of Contract","","Informada","Informada","Informed","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	EndIf
	
	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"09")) 	
		//---------------------------------------MV_PAR09--------------------------------------------------
		aHelpPor := {"Indica a moeda para impressão","dos valores. A pergunta anterior",'deve estar como "Informada".'}
		aHelpEng := {"Indica la moneda para impresión","de los valores. La pregunta anterior",'debe estar como "Informada".'}
		aHelpSpa := {"Indicates the values currency ","printing. The previous question must",'be set as "Informed".'}
		
		PutSX1(cPerg,"09","Moeda","Moneda","Currency","mv_ch9","N",1,0,0,"C","","","","S","mv_par09","Moeda 1","Moneda 1","Currency 1","","Moeda 2","Moneda 2","Currency 2","Moeda 3","Moneda 3","Currency 3","Moeda 4","Moneda 4","Currency 4","Moeda 5","Moneda 5","Currency 5",aHelpPor,aHelpEng,aHelpSpa)
	EndIf
	
	If !SX1->(dbSeek(PADR(cPerg,nTamSX1)+"10")) 	
		//---------------------------------------MV_PAR10--------------------------------------------------
		aHelpPor := {"Data de referência para conversão","dos valores entre moedas."}
		aHelpEng := {"Fecha de referencia para conversión","de los valores entre monedas."}
		aHelpSpa := {"Reference date for values","convertion between currency."}
		
		PutSX1(cPerg,"10","Data de referencia","Fecha de referencia","Reference date","mv_cha","D",8,0,0,"G","","","","S","mv_par10","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inclui validação no parâmetro MV_PAR05 - Fornecedor de ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SX1->( dbSeek(PadR(cPerg,nTamSX1)+"05",.T.) ) 
		If  SX1->X1_VALID != "CNR020ClFr('CNR021','1')"
			RecLock("SX1",.F.)
			SX1->X1_VALID := "CNR020ClFr('CNR021','1')"
			MsUnlock()
		EndIf               
	EndIf	

EndIf

RestArea(aAreaAnt)
Return

/*----------Versao TReport-------------*/
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CNTR020  ³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de revisoes dos contratos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CNTR020() 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajustar perguntas do SXB									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If FindFunction("CliConsPad")
	CliConsPad() 
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a atualizacao do ambiente                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  FindFunction("TRepInUse") .And. TRepInUse()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ajustar perguntas do SX1									 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AjustaSX1('CNR021')

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // Contrato:                                    ³
	//³ mv_par02     // Revisao de                                   ³
	//³ mv_par03     // Vigencia de:                                 ³
	//³ mv_par04     // Vigencia ate:                                ³
	//³ mv_par05     // Fornecedor de:                               ³
	//³ mv_par06     // Tipo de Contrato ?                           ³
	//| mv_par07	 // Cliente ?                                    ³
	//| mv_par08     // Imprimir valores - Moeda do ctr/Outra moeda  ³
	//| mv_par09     // Moeda: 										 ³
	//| mv_par10     // Data de Referencia:							 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte("CNR021",.F.)
	
	oReport := ReportDef()
	
	oReport:PrintDialog()
	
Else 		

	Return CNTR020R3() // Executa versão anterior do fonte	
	
EndIf

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina padrao de configuracao das sections do relatorio    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()
Local oReport
Local oSectCab// Sessao Cabecalho
Local oSectPla// Sessao Planilhas
Local oSectItm// Sessao Itens de Planilhas
Local oSectCro// Sessao Cronograma
Local oSectCtb// Sessao Cronograma Contabil

Local nCNBTamPrd:= TamSX3("CNB_PRODUT")[1]

oReport := TReport():New("CNTR021",OemToAnsi(STR0001),"CNR021",{|oReport| CR020IMP(oReport)},OemToAnsi(STR0011))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cabecalho do contrato                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectCab := TRSection():New(oReport,OemToAnsi(STR0010),{"CN9","SE4","CN1"})
TRCell():New(oSectCab,"CN9_NUMERO","CN9",,,TamSx3("CN9_NUMERO")[1])
TRCell():New(oSectCab,"CN9_REVISA","CN9",,,TamSx3("CN9_REVISA")[1])
TRCell():New(oSectCab,"CN0_DESCRI","CN0",OemToAnsi(STR0019),,iIf(TamSx3("CN0_DESCRI")[1]>15,15,TamSx3("CN0_DESCRI")[1]))
TRCell():New(oSectCab,"CN9_SITUAC","CN9",,,TamSx3("CN9_SITUAC"	)[1],,{|| QA_CBox("CN9_SITUAC",AllTrim(QRYCN9->CN9_SITUAC))})
TRCell():New(oSectCab,"CN9_DTFIM","CN9",,,TamSx3("CN9_DTFIM")[1])
TRCell():New(oSectCab,"CN1_DESCRI","CN1","Tipo",,iIf(TamSx3("CN1_DESCRI")[1]>10,10,TamSx3("CN1_DESCRI")[1]))
TRCell():New(oSectCab,"CN9_VLATU","CN9",,,TamSx3("CN9_VLATU")[1],,{|| QRYCN9->(C020xMoeda(.T.,CN9_NUMERO,CN9_REVISA,"CN9_VLATU"))})
TRCell():New(oSectCab,"CN9_SALDO","CN9",,,TamSx3("CN9_SALDO")[1],,{|| QRYCN9->(C020xMoeda(.T.,CN9_NUMERO,CN9_REVISA,"CN9_SALDO"))}) 

oSectCab:Cell("CN0_DESCRI"):SetLineBreak()  
oSectCab:Cell("CN1_DESCRI"):SetLineBreak()  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cabecalho das Planilhas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectPla := TRSection():New(oSectCab,OemToAnsi(STR0012),{"CNA","CNL"})
TRCell():New(oSectPla,"CNA_NUMERO","CNA")
TRCell():New(oSectPla,"CNL_DESCRI","CNL","Tipo")
TRCell():New(oSectPla,"CNA_FORNEC","CNA")
TRCell():New(oSectPla,"CNA_LJFORN","CNA")
TRCell():New(oSectPla,"CNA_CLIENT","CNA")
TRCell():New(oSectPla,"CNA_LOJACL","CNA")
TRCell():New(oSectPla,"CNA_VLTOT","CNA",,,,,{|| QRYCNA->(C020xMoeda(.T.,CNA_CONTRA,CNA_REVISA,"CNA_VLTOT"))})
TRCell():New(oSectPla,"CNA_SALDO","CNA",,,,,{|| QRYCNA->(C020xMoeda(.T.,CNA_CONTRA,CNA_REVISA,"CNA_SALDO"))})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Itens das planilhas                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectItm := TRSection():New(oSectPla,OemToAnsi(STR0013),{"CNB"})
TRCell():New(oSectItm,"CNB_ITEM","CNB")
TRCell():New(oSectItm,"CNB_PRODUT","CNB",,,nCNBTamPrd)
TRCell():New(oSectItm,"CNB_QUANT","CNB")
TRCell():New(oSectItm,"CNB_VLUNIT","CNB",,,,,{|| QRYCNB->(C020xMoeda(.T.,CNB_CONTRA,CNB_REVISA,"CNB_VLUNIT"))})
TRCell():New(oSectItm,"CNB_VLTOT","CNB",,,,,{|| QRYCNB->(C020xMoeda(.T.,CNB_CONTRA,CNB_REVISA,"CNB_VLTOT"))})
TRCell():New(oSectItm,"CNB_DESC","CNB")
TRCell():New(oSectItm,"CNB_VLDESC","CNB",,,,,{|| QRYCNB->(C020xMoeda(.T.,CNB_CONTRA,CNB_REVISA,"CNB_VLDESC"))})
TRCell():New(oSectItm,"CNB_QTDMED","CNB")
TRCell():New(oSectItm,"CNB_SLDMED","CNB")

oSectItm:Cell("CNB_PRODUT"):SetLineBreak()  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cronogramas                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectCro := TRSection():New(oSectPla,OemToAnsi(STR0014),{"CNF"})
TRCell():New(oSectCro,"CNF_NUMERO","CNF")
TRCell():New(oSectCro,"CNF_PARCEL","CNF")
TRCell():New(oSectCro,"CNF_COMPET","CNF")
TRCell():New(oSectCro,"CNF_VLPREV","CNF",,,,,{|| QRYCNF->(C020xMoeda(.T.,CNF_CONTRA,CNF_REVISA,"CNF_VLPREV"))})
TRCell():New(oSectCro,"CNF_VLREAL","CNF",,,,,{|| QRYCNF->(C020xMoeda(.T.,CNF_CONTRA,CNF_REVISA,"CNF_VLREAL"))})
TRCell():New(oSectCro,"CNF_SALDO","CNF",,,,,{|| QRYCNF->(C020xMoeda(.T.,CNF_CONTRA,CNF_REVISA,"CNF_SALDO"))})
TRCell():New(oSectCro,"CNF_DTVENC","CNF")
TRCell():New(oSectCro,"CNF_PRUMED","CNF")
TRCell():New(oSectCro,"CNF_DTREAL","CNF")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cronograma Contabil                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectCtb := TRSection():New(oSectPla,OemToAnsi(STR0020),{"CNW"})
TRCell():New(oSectCtb,"CNW_NUMERO","CNW")
TRCell():New(oSectCtb,"CNW_PARCEL","CNW")
TRCell():New(oSectCtb,"CNW_DTPREV","CNW")
TRCell():New(oSectCtb,"CNW_VLPREV","CNW",,,,,{|| QRYCNW->(C020xMoeda(.T.,CNW_CONTRA,CNW_REVISA,"CNW_VLPREV"))})
TRCell():New(oSectCtb,"CNW_HIST","CNW")
TRCell():New(oSectCtb,"CNW_CC","CNW")
TRCell():New(oSectCtb,"CNW_DTLANC","CNW")
TRCell():New(oSectCtb,"CNW_ITEMCT","CNW")
TRCell():New(oSectCtb,"CNW_CLVL","CNW")

Return oReport

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CR020Imp ³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa impressao do relatorio                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oExp01 - Objeto do relatorio                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CR020Imp(oReport)
Local oSectCab := oReport:Section(1)//Cabecalho do contrato
Local oSectPla := oReport:Section(1):Section(1)//Cabecalho da planilha
Local oSectItm := oReport:Section(1):Section(1):Section(1)//Itens de Planilhas
Local oSectCro := oReport:Section(1):Section(1):Section(2)//Cronogramas
Local oSectCtb := oReport:Section(1):Section(1):Section(3)//Cronogramas Contabeis
Local nTotLinha
Local cTpRev//Tipo da revisao
Local cEsRev//Especie da revisao
Local lPlanil//Exibe planilha
Local lCronog//Exibe Cronograma
Local lCroCtb//Exibe Cronograma Contabil
Local nX
Local cCronAt
Local lFstCNA  := .T.
Local lMedeve  := .F.
Local lCtb     := .F.

If mv_par08 == 2
	oReport:SetTitle(oReport:Title() +STR0024 +AllTrim(GetMv("MV_SIMB" +Ltrim(Str(mv_par09)))) +")")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Configura perguntas do tipo Range                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ    
MakeSqlExpr(PadR("CNR021",10))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Configura filtro do cabecalho                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
CR020Cab(oSectCab)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtra planilhas e itens                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CR020Pla(oSectPla)
CR020Itm(oSectItm)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtra cronogramas do contrato                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CR020Cro(oSectCro)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtra cronogramas contabeis do contrato                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CR020Ctb(oSectCtb)

oSectCab:Init()
While !QRYCN9->(Eof())
	If oReport:Cancel()
		Exit
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica tipo do contrato                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	dbSelectArea("CN1")
	dbSetOrder(1)
	If dbSeek(xFilial("CN1")+QRYCN9->CN9_TPCTO)
		lCtb    := .F.//(CN1->CN1_CROCTB = '1')//Contrato com cronograma contabil
		lMedeve := (CN1->CN1_MEDEVE = '1')//Contrato com medicao eventual
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime cabecalho do contrato                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSectCab:PrintLine()
	oReport:SkipLine()
	oReport:SkipLine()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Armazena tipo e especie de revisao                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   	
	cTpRev := CN0->CN0_TIPO
	cEsRev := CN0->CN0_ESPEC

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se imprime planilha/cabecalho             ³
	//³ Planilha imprime quando:                           ³
	//³ 1 - Aditivo de quantidade                          ³
	//³ 2 - Aditivo de quantidade/prazo                    ³
	//³ 3 - Reajuste                                       ³
	//³ 4 - Realinhamento                                  ³
	//³ 5 - Readequacao                                    ³
	//³ Planilha imprime quando:                           ³
	//³ 1 - Aditivo de quantidade                          ³
	//³ 2 - Aditivo de prazo                               ³
	//³ 3 - Aditivo de quantidade/prazo                    ³
	//³ 4 - Reajuste                                       ³
	//³ 5 - Realinhamento                                  ³
	//³ 6 - Reinicio                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   		
	lPlanil := ((cTpRev == DEF_ADITI .And. cEsRev $ "14") .OR. (cTpRev == DEF_REAJU) .OR. (cTpRev == DEF_REALI) .OR. (cTpRev == DEF_READQ))
	lCronog := !lMedeve .And. ((cTpRev == DEF_ADITI .And. cEsRev $ "134") .OR. (cTpRev == DEF_REAJU) .OR. (cTpRev == DEF_REALI) .OR. (cTpRev == DEF_REINI))
	lCroCtb := lCtb .And. ((cTpRev == DEF_ADITI .And. cEsRev $ "134") .OR. (cTpRev == DEF_REAJU) .OR. (cTpRev == DEF_REALI) .OR. (cTpRev == DEF_REINI))
	lFstCNA := .T.
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Texto de Justificativa da revisao   			   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	   
	nTotLinha := MlCount(MSMM(QRYCN9->CN9_CODJUS))
	oReport:SkipLine()
	oReport:SkipLine()
	CR010Box(oReport,STR0017)
	For nX := 1 To nTotLinha
		oReport:PrintText(MemoLine(MSMM(QRYCN9->CN9_CODJUS),,nX))
		oReport:SkipLine()
	Next nX
   If nTotLinha == 0
		oReport:PrintText(OemToAnsi(STR0020))//"EM BRANCO"
   EndIf
   oReport:SkipLine()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se a revisao e do tipo "Alteracao de Clausula"    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If cTpRev == DEF_CLAUS
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime Texto de Alteracao de clausula      			      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotLinha := MlCount(MSMM(QRYCN9->CN9_CODCLA))
		oReport:SkipLine()
		oReport:SkipLine()
		CR010Box(oReport,STR0018)
		For nX := 1 To nTotLinha
			oReport:PrintText(MemoLine(MSMM(QRYCN9->CN9_CODCLA),,nX))
			oReport:SkipLine()
		Next nX
	   If nTotLinha == 0
			oReport:PrintText(OemToAnsi(STR0020))
	   EndIf
	   oReport:SkipLine()
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Os filtros das secoes filhas sao criados dentro do looop   ³
	//³ pois e necessario conhecer o tipo de revisao antes de      ³
	//³ filtrar os itens filhos                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If lPlanil

		oSectPla:Init()
		oSectPla:ExecSQL()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Percorre planilhas                                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		While !QRYCNA->(Eof())
//			CR010Box(oReport,OemToAnsi(STR0016)+QRYCN9->CN9_NUMERO+If(!Empty(QRYCN9->CN9_REVISA),"/"+QRYCN9->CN9_REVISA,""))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime cabecalho da planilha                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lFstCNA
				oSectPla:PrintHeader()
			EndIf
			lFstCNA:=.F.
			If !Empty(QRYCNA->CNA_CLIENT)
				oSectPla:Cell("CNA_FORNEC"):Disable()
				oSectPla:Cell("CNA_LJFORN"):Disable() 
				oSectPla:Cell("CNA_CLIENT"):Enable()
				oSectPla:Cell("CNA_LOJACL"):Enable()   
			Else   
				oSectPla:Cell("CNA_CLIENT"):Disable()
				oSectPla:Cell("CNA_LOJACL"):Disable()			
				oSectPla:Cell("CNA_FORNEC"):Enable()
				oSectPla:Cell("CNA_LJFORN"):Enable() 			
			EndIf
			oSectPla:PrintLine()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime itens de planilha                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oSectItm:Print()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se imprime cronograma                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lCronog
				oReport:SkipLine()
//				CR010Box(oReport,OemToAnsi(STR0015)+QRYCN9->CN9_NUMERO+If(!Empty(QRYCN9->CN9_REVISA),"/"+QRYCN9->CN9_REVISA,""))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Imprime cronograma                                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	 			oSectCro:Print()
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se imprime cronograma contabil                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lCroCtb
				oReport:SkipLine()
//				CR010Box(oReport,OemToAnsi(STR0022)+QRYCN9->CN9_NUMERO+If(!Empty(QRYCN9->CN9_REVISA),"/"+QRYCN9->CN9_REVISA,""))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Imprime cronograma                                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	 			oSectCtb:Print()
			EndIf

			QRYCNA->(dbSkip())
			oReport:SkipLine()
			oReport:SkipLine()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Finaliza secao das planilhas                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSectPla:Finish()
		//QRYCNA->(dbCloseArea())
	ElseIf lCronog//Imprime apenas cronogramas
		oSectCro:Init()
		oSectCro:ExecSQL()

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Mantem controle do cronograma atual                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCronAt := QRYCNF->CNF_NUMERO
//		CR010Box(oReport,OemToAnsi(STR0015)+QRYCN9->CN9_NUMERO+If(!Empty(QRYCN9->CN9_REVISA),"/"+QRYCN9->CN9_REVISA,""))

		While !QRYCNF->(Eof())
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se mudou cronograma, em caso positivo imprime     ³
			//³ cabecalho                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cCronAt != QRYCNF->CNF_NUMERO
//				CR010Box(oReport,OemToAnsi(STR0015)+QRYCN9->CN9_NUMERO+If(!Empty(QRYCN9->CN9_REVISA),"/"+QRYCN9->CN9_REVISA,""))
				oSectCro:PrintHeader()
				cCronAt := QRYCNF->CNF_NUMERO
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime cronograma                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oSectCro:PrintLine()
			QRYCNF->(dbSkip())
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Finaliza sessao dos cronogramas                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSectCro:Finish()
	EndIf
	
	QRYCN9->(dbSkip())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Forca quebra de pagina em caso de novo contrato            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 	If !QRYCN9->(Eof())
		oReport:EndPage()
	EndIf
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza sessao do cabecalho                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectCab:Finish()
Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CR020Cab ³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Filtra contratos de acordo com os parametros               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oExp01 - Section do cabecalho de contrato                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CR020Cab(oSectCab)
Local cPart   := "%%" 

If !Empty(MV_PAR06)
	cPart    := "%AND CN9_TPCTO = '"+MV_PAR06+"'%"
EndIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera query de filtro dos contratos                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectCab:BeginQuery()
If Empty(MV_PAR05)
	BeginSql alias "QRYCN9"
		SELECT CN9_NUMERO, CN9_REVISA, CN9_DTINIC, CN9_SITUAC, CN9_DTFIM, CN9_DTPROP,CN9_DTASSI, CN9_DTENCE, CN9_CONDPG, CN9_TPCTO, CN9_DTULST, CN9_VIGE, CN9_INDICE,CN9_FLGCAU, CN9_MINCAU, CN9_FLGREJ, CN9_REVATU, CN9_VLINI, CN9_VLATU, CN9_SALDO,CN9_CODOBJ, CN9_CODCLA, CN9_CODJUS, CN9_UNVIGE, CN0_TIPO, CN0_DESCRI, CN0_ESPEC, CN9_TIPREV, CN9_CLIENT, CN9_LOJACL
		FROM %table:CN9% CN9 ,%table:CN0% CN0 WHERE
		CN9_FILIAL = %xFilial:CN9% AND
		CN0_FILIAL = %xFilial:CN0% AND
		CN0.CN0_CODIGO = CN9.CN9_TIPREV AND
		CN9_DTINIC >= %exp:dtos(MV_PAR03)% AND
		CN9_DTFIM  <= %exp:dtos(MV_PAR04)% AND 
		CN9.%notDel% %exp:cPart%                   
		
		
		ORDER BY CN9_NUMERO, CN9_REVISA
	EndSql

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Informa parametros de range                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSectCab:EndQuery({MV_PAR01,MV_PAR02,MV_PAR07})
Else
	BeginSql alias "QRYCN9"
		SELECT CN9_NUMERO, CN9_REVISA, CN9_DTINIC, CN9_SITUAC, CN9_DTFIM, CN9_DTPROP,CN9_DTASSI, CN9_DTENCE, CN9_CONDPG, CN9_TPCTO, CN9_DTULST, CN9_VIGE, CN9_INDICE,CN9_FLGCAU, CN9_MINCAU, CN9_FLGREJ, CN9_REVATU, CN9_VLINI, CN9_VLATU, CN9_SALDO,CN9_CODOBJ, CN9_CODCLA, CN9_CODJUS, CN9_UNVIGE, CN0_TIPO, CN0_DESCRI, CN0_ESPEC, CN9_TIPREV
		FROM %table:CN9% CN9 ,%table:CN0% CN0 ,%table:CNC% CNC WHERE
		CN9_FILIAL = %xFilial:CN9% AND
		CN0_FILIAL = %xFilial:CN0% AND
		CNC_FILIAL = %xFilial:CNC% AND
		CN9.CN9_NUMERO = CNC.CNC_NUMERO AND
		CN0.CN0_CODIGO = CN9.CN9_TIPREV AND
		CN9_DTINIC >= %exp:dtos(MV_PAR03)% AND
		CN9_DTFIM  <= %exp:dtos(MV_PAR04)% AND 
		CN9.%notDel% %exp:cPart%
		ORDER BY CN9_NUMERO, CN9_REVISA
	EndSql

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Informa parametros de range                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSectCab:EndQuery({MV_PAR01,MV_PAR02,MV_PAR05})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Configura posicionamento das tabelas filhas        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRPosition():New(oSectCab,"SE4",1,"xFilial() + QRYCN9->CN9_CONDPG")//Cond. Pagto
TRPosition():New(oSectCab,"CN1",1,"xFilial() + QRYCN9->CN9_TPCTO")//Tipo de Contrato
TRPosition():New(oSectCab,"CN0",1,"xFilial() + QRYCN9->CN9_TIPREV")//Tipo de Contrato
Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CR020Pla ³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Filtra planilhas do contrato atual                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oExp01 - Section do cabecalho de planilha                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CR020Pla(oSectPla)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera query de filtro das planilhas                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
BEGIN REPORT QUERY oSectPla

BeginSql alias "QRYCNA"
	SELECT CNA_NUMERO,CNA_FORNEC,CNA_LJFORN,CNA_CRONOG,CNA_DTINI,CNA_VLTOT,CNA_SALDO,CNA_TIPPLA,CNA_DTFIM,CNA_FLREAJ, CNA_CONTRA, CNA_REVISA, CNA_CLIENT, CNA_LOJACL
	FROM %table:CNA% CNA WHERE
	CNA_FILIAL = %xfilial:CNA% AND
	CNA_CONTRA = %report_param:QRYCN9->CN9_NUMERO% AND
	CNA_REVISA = %report_param:QRYCN9->CN9_REVISA% AND
	CNA.%notDel%
	ORDER BY CNA_CONTRA,CNA_REVISA,CNA_NUMERO
EndSql

END REPORT QUERY oSectPla

TRPosition():New(oSectPla,"CNL",1,"xFilial() + QRYCNA->CNA_TIPPLA")//Tipos de Tabelas
Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CR020Itm ³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Filtra itens da planilha atual                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oExp01 - Section do itens da planilha                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CR020Itm(oSectItm)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera query de filtro dos itens das planilhas       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
BEGIN REPORT QUERY oSectItm
BeginSql alias "QRYCNB"
	SELECT CNB_ITEM, CNB_PRODUT, CNB_QUANT, CNB_VLUNIT, CNB_VLTOT, CNB_VLTOT, CNB_DESC, CNB_VLDESC, CNB_QTDMED, CNB_SLDMED, CNB_CONTRA, CNB_REVISA, CNB_NUMERO
	FROM %table:CNB% CNB WHERE
	CNB_FILIAL = %xfilial:CNB% AND
	CNB_CONTRA = %report_param:QRYCNA->CNA_CONTRA% AND
	CNB_REVISA = %report_param:QRYCNA->CNA_REVISA% AND
	CNB_NUMERO = %report_param:QRYCNA->CNA_NUMERO% AND
	CNB.%notDel%
	ORDER BY CNB_CONTRA,CNB_REVISA,CNB_NUMERO,CNB_ITEM
EndSql
END REPORT QUERY oSectItm
Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CR020Cro ³ Autor ³ Marcelo Custodio      ³ Data ³ 25.05.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Filtra cronogramas do contrato ou da planilha              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oExp01 - Section do cronograma                             ³±±
±±³          ³ lExp02 - Filtra por planilha ou contrato                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CR020Cro(oSectCro,lPlanil)
Local cPart
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inclui filtro por planilha, quando houver impressao³
//³ de planilhas e cronogramas                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lPlanil
	cPart := "%AND (CNF_NUMERO = '"+ QRYCNA->CNA_CRONOG +"')%"
else
	cPart := "%%"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera query dos cronogramas                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
BEGIN REPORT QUERY oSectCro
BeginSQL alias "QRYCNF"
	SELECT CNF.*
	FROM %table:CNF% CNF WHERE
	CNF_FILIAL = %xFilial:CNF% AND
	CNF_CONTRA = %report_param:QRYCN9->CN9_NUMERO% AND
	CNF_REVISA = %report_param:QRYCN9->CN9_REVISA% AND
	CNF.%notDel% %report_param:cPart%
	ORDER BY CNF_CONTRA,CNF_REVISA,CNF_NUMERO,CNF_PARCEL
EndSql
END REPORT QUERY oSectCro
Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CR020Ctb ³ Autor ³ Marcelo Custodio      ³ Data ³ 28.02.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Filtra cronogramas contabeis do contrato ou da planilha    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oExp01 - Section do cronograma                             ³±±
±±³          ³ lExp02 - Filtra por planilha ou contrato                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CR020Ctb(oSectCtb,lPlanil)
Local cPart
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inclui filtro por planilha, quando houver impressao³
//³ de planilhas e cronogramas                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lPlanil
	cPart := "%AND (CNW_NUMERO = '"+ QRYCNA->CNA_CRONCT +"')%"
else
	cPart := "%%"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera query dos cronogramas con                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
BEGIN REPORT QUERY oSectCtb
BeginSQL alias "QRYCNW"
	SELECT CNW.*
	FROM %table:CNW% CNW WHERE
	CNW_FILIAL = %xFilial:CNW% AND
	CNW_CONTRA = %report_param:QRYCN9->CN9_NUMERO% AND
	CNW_REVISA = %report_param:QRYCN9->CN9_REVISA% AND
	CNW.%notDel% %report_param:cPart%
	ORDER BY CNW_CONTRA,CNW_REVISA,CNW_NUMERO,CNW_PARCEL
EndSql
END REPORT QUERY oSectCtb

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CNR020ClFr³ Autor ³ Aline Sebrian         ³ Data ³ 12.04.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida os parametro do Cliente e Fornecedor no relatorio.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³CNR020ClFr()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³CNR020ClFr()                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CNR020ClFr(cCnr,cTipo)
Local aSaveArea	:= GetArea()

If cCnr=="CNR020"
	If cTipo=="1"    
		If !Empty(mv_par07) .Or. (!Empty(mv_par08) .And. UPPER(mv_par08) != REPLICATE("Z",TamSx3("CNC_CODIGO")[1]))
			mv_par10	:= Space(TamSx3("A1_COD")[1])
			mv_par11	:= REPLICATE("Z",TamSx3("A1_COD")[1])	 
		EndIf
	Else
		If !Empty(mv_par10) .Or. (!Empty(mv_par11) .And. UPPER(mv_par11) != REPLICATE("Z",TamSx3("A1_COD")[1]))
			mv_par07	:= Space(TamSx3("CNC_CODIGO")[1])
			mv_par08	:= REPLICATE("Z",TamSx3("CNC_CODIGO")[1])    
		EndIf
	EndIf   
Else
	If cTipo=="1"  
		If !Empty(MV_PAR05)
			MV_PAR07	:= Space(TamSx3("A1_COD")[1]) 
		EndIf
	Else
		If !Empty(MV_PAR07)
			MV_PAR05	:= Space(TamSx3("CNC_CODIGO")[1])
		EndIf
	EndIf              
EndIf
	    
RestArea(aSaveArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³C020xMoedaºAutor  ³Microsiga           º Data ³  11/06/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Realiza a conversao entre moedas de um determinado valor.  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ lTReport: indica se a impressao e em TReport.			  º±±
±±º			 ³ cContra: numero do contrato que esta sendo impresso.		  º±±
±±º			 ³ cRev: revisao do contrato que esta sendo impresso.		  º±±
±±º			 ³ cCampo: nome do campo a ser convertido.					  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno	 ³ nRet: valor apos conversao								  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNTR020													  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function C020xMoeda(lTReport,cContra,cRev,cCampo)
Local dDataRef  := If(lTReport,mv_par10,mv_par14)
Local nMoedaOri	:= Posicione("CN9",1,xFilial("CN9")+cContra+cRev,"CN9_MOEDA")
Local nMoedaDes := nMoedaOri
Local nRet 		:= &(cCampo)

If Empty(dDataRef)
	dDataRef := dDataBase
EndIf

If lTReport
	If mv_par08 == 2
		nMoedaDes := mv_par09
	EndIf
ElseIf mv_par12 == 2
	nMoedaDes := mv_par13
EndIf

nRet := Round(xMoeda(nRet,nMoedaOri,nMoedaDes,dDataRef,6),TamSX3(cCampo)[2])

Return nRet
