#INCLUDE "MATR103.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR103  ³ Autor ³Alexandre Inacio Lemes ³ Data ³19/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Relatorio de Notas de Transferencia entre Filiais.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR103(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Matr103()

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef()
	oReport:PrintDialog()
Else
	MATR103R3()
EndIf
                                               
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ ReportDef³Autor  ³Alexandre Inacio Lemes ³Data  ³19/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Relatorio de Notas de Transferencia entre Filiais.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oExpO1: Objeto do relatorio                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local cTitle  := STR0001 //"Relação de Notas Transferencia entre Filiais"
Local oReport 
Local oFornece 
Local oSectSF1 
Local oSectSD1 
Local oCliente 
Local oSectSF2 
Local oSectSD2         
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                                   ³
//³ mv_par01 Da Filial                                                     ³ 
//³ mv_par02 Ate a Filial                                                  ³ 
//³ mv_par03 Imprime Itens?   Sim(1) / Nao(2)                              ³
//³ mv_par04 Imprimir:   Entradas(1) / Saidas(2) / Ambos(3)                ³
//³ mv_par05 Emissao de                                                    ³
//³ mv_par06 Emissao ate                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR103",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MTR103",cTitle,"MTR103", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003) //"Este programa tem como objetivo imprimir relatorio de acordo com os parametros informados pelo usuario."
oReport:SetLandscape() 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oFornece:= TRSection():New(oReport,STR0017,{"SF1","SA2"},/*aOrdem*/) //"Relação de Notas Transferência entre Filiais"

TRCell():New(oFornece,"F1_FORNECE","SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oFornece,"F1_LOJA"   ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oFornece,"A2_NOME"   ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oFornece:SetTotalInLine(.F.)
oFornece:SetTotalText(STR0010) // "TOTAL ENTRADAS ->>> "

oSectSF1:= TRSection():New(oFornece,STR0018,{"SF1"},/*aOrdem*/) //"Relação de Notas Transferência entre Filiais"
oSectSF1:SetHeaderPage()

TRCell():New(oSectSF1,"F1_EMISSAO","SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_DOC"    ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_SERIE"  ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_VALMERC","SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_VALICM" ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_VALIPI" ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_FRETE"  ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_SEGURO" ,"SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_DESPESA","SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF1,"F1_VALBRUT","SF1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSectSD1:= TRSection():New(oSectSF1,STR0019,{"SD1","SB1"},/*aOrdem*/) //"Relação de Notas Transferência entre Filiais"

TRCell():New(oSectSD1,"D1_COD"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"D1_VALICM" ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"D1_VALIPI" ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"D1_QUANT"  ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"D1_VUNIT"  ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"D1_VALDESC","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD1,"D1_TOTAL"  ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

TRFunction():New(oSectSF1:Cell("F1_VALMERC"),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece)
TRFunction():New(oSectSF1:Cell("F1_VALICM" ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece) 
TRFunction():New(oSectSF1:Cell("F1_VALIPI" ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece) 
TRFunction():New(oSectSF1:Cell("F1_FRETE"  ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece) 
TRFunction():New(oSectSF1:Cell("F1_SEGURO" ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece) 
TRFunction():New(oSectSF1:Cell("F1_DESPESA"),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece) 
TRFunction():New(oSectSF1:Cell("F1_VALBRUT"),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oFornece) 


oCliente:= TRSection():New(oReport,STR0020,{"SF2","SA1"},/*aOrdem*/) //"Relação de Notas Transferência entre Filiais"

TRCell():New(oCliente,"F2_CLIENTE","SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oCliente,"F2_LOJA"   ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oCliente,"A1_NOME"   ,"SA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oCliente:SetTotalInLine(.F.)
oCliente:SetTotalText(STR0011) // "TOTAL SAIDAS   ->>> "

oSectSF2:= TRSection():New(oCliente,STR0021,{"SF2"},/*aOrdem*/) //"Relação de Notas Transferência entre Filiais"
oSectSF2:SetHeaderPage()

TRCell():New(oSectSF2,"F2_EMISSAO","SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_DOC"    ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_SERIE"  ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_VALMERC","SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_VALICM" ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_VALIPI" ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_FRETE"  ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_SEGURO" ,"SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_DESPESA","SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSF2,"F2_VALBRUT","SF2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSectSD2:= TRSection():New(oSectSF2,STR0022,{"SD2","SB1"},/*aOrdem*/) //"Relação de Notas Transferência entre Filiais"

TRCell():New(oSectSD2,"D2_COD"    ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_VALICM" ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_VALIPI" ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_QUANT"  ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_PRCVEN" ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_DESC"   ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_TOTAL"  ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSectSD2,"D2_FILIAL" ,"SD2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

TRFunction():New(oSectSF2:Cell("F2_VALMERC"),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente)
TRFunction():New(oSectSF2:Cell("F2_VALICM" ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente) 
TRFunction():New(oSectSF2:Cell("F2_VALIPI" ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente) 
TRFunction():New(oSectSF2:Cell("F2_FRETE"  ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente) 
TRFunction():New(oSectSF2:Cell("F2_SEGURO" ),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente) 
TRFunction():New(oSectSF2:Cell("F2_DESPESA"),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente) 
TRFunction():New(oSectSF2:Cell("F2_VALBRUT"),NIL,"SUM",/*oBreak*/,,/*cPicture*/,/*uFormula*/,.T.,.F.,,oCliente) 

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Alexandre Inacio Lemes ³Data  ³19/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Relatorio de Notas de Transferencia entre Filiais.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

Local aAreaSM0   := SM0->(GetArea())
Local aFornece   := {}
Local aCliente   := {}
Local oFornece   := oReport:Section(1) 
Local oSectSF1   := oReport:Section(1):Section(1) 
Local oSectSD1   := oReport:Section(1):Section(1):Section(1) 
Local oCliente   := oReport:Section(2) 
Local oSectSF2   := oReport:Section(2):Section(1) 
Local oSectSD2   := oReport:Section(2):Section(1):Section(1) 
Local nX         := 0
Local lUsaFilTrf := IIF(FindFunction('UsaFilTrf'), UsaFilTrf(), .F.)
Local cArqIdx    := ""
Local nIndex     := 0

#IFDEF TOP
	Local cAliasSF1 := GetNextAlias()
	Local cAliasSF2 := GetNextAlias()
	Local cSelect   := "%%"
	Local cFrom     := "%%"
	Local cWhere    := "%%"
	Local cWhereSF1 := "%%"
	Local cWhereSF2 := "%%"
#ELSE
	Local cAliasSF1 := "SF1"
	Local cAliasSF2 := "SF2"
	Local cCondicao := ""
	Local cCondAdd  := ""
#ENDIF

If mv_par04 == 1
	oReport:SetTitle(oReport:Title() + STR0005) //" - ENTRADAS "
ElseIf mv_par04 == 2
	oReport:SetTitle(oReport:Title() + STR0006) //" - SAIDAS   "
ElseIf mv_par04 == 3
	oReport:SetTitle(oReport:Title() + STR0016) //" - ENTRADAS E SAIDAS"
Endif

dbSelectArea("SM0")
dbSetOrder(1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Procura o CNPJ da Filial para localizar o Fornecedor e/ou Cliente cadastrado ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(mv_par02)
	MsSeek(Subs(cNumEmp,1,2)+mv_par01,.T.)
	While SM0->M0_CODIGO+SM0->M0_CODFIL >= (Subs(cNumEmp,1,2)+mv_par01) .And. SM0->M0_CODIGO+SM0->M0_CODFIL <= (Subs(cNumEmp,1,2)+mv_par02)
		If mv_par04 <> 1
			dbSelectArea("SA1")
			If !lUsaFilTrf
				dbSetOrder(3)
				If dbSeek(xFilial("SA1")+SM0->M0_CGC) 
					If cPaisLoc == "BRA"
				   		aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME})
					Else   
					While Trim(SA1->A1_CGC) == Trim(SM0->M0_CGC)   
							If Ascan(aCliente,{|x| x[4]== SA1->A1_COD+SA1->A1_LOJA})==0 
		  					   	aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME,SA1->A1_COD+SA1->A1_LOJA})
		  					EndIf
						   	DBSkip()
						 EndDo				     
					EndIf
				EndIf
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta filtro e indice temporario na SA1 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cArqIdx := CriaTrab(,.F.)
				IndRegua("SA1", cArqIdx, "A1_FILIAL+A1_FILTRF",,,STR0023) //"Selecionando Registros ..."
				nIndex := RetIndex("SA1")
				#IFNDEF TOP
					dbSetIndex(cArqIdx+OrdBagExt())
				#ENDIF
				dbSetOrder(nIndex+1) // A1_FILIAL+A1_FILTRF
				
				If dbSeek(xFilial("SA1")+SM0->M0_CODFIL) 
					If cPaisLoc == "BRA"
				   		aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME})
					Else   
						While SA1->A1_CGC == SM0->M0_CGC   
							If Ascan(aCliente,{|x| x[4]== SA1->A1_COD+SA1->A1_LOJA})==0 
		  					   	aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME,SA1->A1_COD+SA1->A1_LOJA})
		  					EndIf
						   	DBSkip()
						 EndDo				     
					EndIf
				EndIf
				
				// Apagar o indice temporario
				dbSelectArea("SA1")
				RetIndex("SA1")
				Ferase( cArqIdx + OrdBagExt() )
			EndIf
		EndIf
		If mv_par04 <> 2
			dbSelectArea("SA2")
			If !lUsaFilTrf
				dbSetOrder(3)  
				If dbSeek(xFilial("SA2")+SM0->M0_CGC) 
					If cPaisLoc == "BRA"
						aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME})
					Else   
					While Trim(SA2->A2_CGC) == Trim(SM0->M0_CGC)   
							If Ascan(aFornece,{|x| x[4]== SA2->A2_COD+SA2->A2_LOJA})==0 
			  					aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME,SA2->A2_COD+SA2->A2_LOJA})
			  				EndIf
						   	DBSkip()
						EndDo
					EndIf
				EndIf
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta filtro e indice temporario na SA2 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cArqIdx := CriaTrab(,.F.)
				IndRegua("SA2", cArqIdx, "A2_FILIAL+A2_FILTRF",,,STR0023) //"Selecionando Registros ..."
				nIndex := RetIndex("SA2")
				#IFNDEF TOP
					dbSetIndex(cArqIdx+OrdBagExt())
				#ENDIF
				dbSetOrder(nIndex+1) // A2_FILIAL+A2_FILTRF
				
				If dbSeek(xFilial("SA2")+SM0->M0_CODFIL) 
					If cPaisLoc == "BRA"
						aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME})
					Else   
						While SA2->A2_CGC == SM0->M0_CGC   
							If Ascan(aFornece,{|x| x[4]== SA2->A2_COD+SA2->A2_LOJA})==0 
			  					aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME,SA2->A2_COD+SA2->A2_LOJA})
			  				EndIf
						   	DBSkip()
						EndDo
					EndIf
				EndIf

				// Apagar o indice temporario
				dbSelectArea("SA2")
				RetIndex("SA2")
				Ferase( cArqIdx + OrdBagExt() )
			EndIf
		EndIf
		dbSelectArea("SM0")
		dbSkip()
	EndDo
EndIf

If (Len(aFornece)+Len(aCliente)) > 0

	If mv_par04 <> 2 .And. Len(aFornece) > 0

		TRPosition():New(oFornece,"SA2",1,{|| xFilial("SA2")+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA})	

		dbSelectArea("SF1")
		dbSetOrder(2)
		dbSeek(xFilial("SF1")+aFornece[1][1]+aFornece[1][2])

		#IFDEF TOP
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Transforma parametros Range em expressao SQL                        ³	
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MakeSqlExpr(oReport:uParam)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Query do relatório da secao 1                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oReport:Section(1):BeginQuery()	

			cWhereSF1 := "%"
			cWhereSF1 += If(Len(aFornece) > 0 , "(" , "" )
			For nX := 1 To Len(aFornece)
				If nX > 1
					cWhereSF1 += " OR "
				Endif
				cWhereSF1 += "(F1_FORNECE='"+aFornece[nx][1] + "' AND F1_LOJA='"+aFornece[nx][2]+"')"
			Next 
			cWhereSF1 += If(Len(aFornece) > 0 , ")" , "" )
			If Len(cWhereSF1) > 2
				cWhereSF1 += " AND "
			EndIf
			cWhereSF1 += "%"
 
			If mv_par03 == 1 //lista itens
                cSelect := "%" 
				cSelect += ",D1_FILIAL ,D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA, D1_COD, D1_VALICM, D1_VALIPI, D1_QUANT, D1_VUNIT, D1_VALDESC, D1_TOTAL, "
				cSelect += "B1_COD, B1_DESC "
                cSelect += "%" 

                cFrom := "%"
                cFrom += "," + RetSqlName("SD1") + " SD1, " + RetSqlName("SB1") + " SB1 "
                cFrom += "%" 

                cWhere := "%"
                cWhere += "AND D1_DOC = F1_DOC AND D1_SERIE = F1_SERIE AND D1_FORNECE = F1_FORNECE "
				cWhere += "AND D1_LOJA = F1_LOJA AND D1_TIPO = F1_TIPO AND B1_COD = D1_COD "
				
				If !Empty(xFilial("SB1")) //.And. cPaisloc <> "BRA"
					cWhere += "AND F1_FILIAL  = B1_FILIAL  "
				EndIf
				
				cWhere += "AND SD1.D_E_L_E_T_ = ' ' AND SB1.D_E_L_E_T_ = ' ' "
				cWhere += "%"  
            EndIf

			BeginSql Alias cAliasSF1
			
			SELECT F1_FILIAL,  F1_DOC, F1_SERIE, F1_FORNECE, F1_LOJA, F1_EMISSAO, F1_VALBRUT,
			       F1_VALMERC, F1_VALIPI, F1_VALICM, F1_FRETE, F1_DESPESA, F1_SEGURO, SF1.R_E_C_N_O_ SF1RECNO
			       %Exp:cSelect%

			FROM %table:SF1% SF1
			     %Exp:cFrom%

			WHERE F1_STATUS <> ' ' AND F1_STATUS <> 'B' AND 
		          F1_EMISSAO >= %Exp:Dtos(mv_par05)% AND 
				  F1_EMISSAO <= %Exp:Dtos(mv_par06)% AND 
				  %Exp:cWhereSF1%
				  SF1.%NotDel% 
				  %Exp:cWhere%
				  
			ORDER BY %Order:SF1% 
					
			EndSql 

			oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)

	        oSectSF2:Disable()
	
			oSectSF1:SetParentQuery()
		 	oSectSF1:SetParentFilter( { |cParam| (cAliasSF1)->F1_FORNECE + (cAliasSF1)->F1_LOJA == cParam },{ || (cAliasSF1)->F1_FORNECE + (cAliasSF1)->F1_LOJA })
	
	        If mv_par03 == 1 
				oSectSD1:SetParentQuery()        
				oSectSD1:SetParentFilter( { |cParam| (cAliasSF1)->D1_DOC+(cAliasSF1)->D1_SERIE+(cAliasSF1)->D1_FORNECE+(cAliasSF1)->D1_LOJA == cParam },;
			                                { || (cAliasSF1)->D1_DOC+(cAliasSF1)->D1_SERIE+(cAliasSF1)->D1_FORNECE+(cAliasSF1)->D1_LOJA })	
			Else
	            oSectSD1:Disable() 		
			EndIf
			
			oFornece:Print()	
	
		#ELSE

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Transforma parametros Range em expressao Advpl                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MakeAdvplExpr(oReport:uParam)

			cCondAdd := If(Len(aFornece) > 0 , "(" , "" )
			For nX:=1 to Len(aFornece)
				If nX > 1
					cCondAdd += " .Or. "
				Endif
				cCondAdd += "(F1_FORNECE=='" +aFornece[nX][1] + "' .And. F1_LOJA=='"+aFornece[nX][2]+"')"
			Next
			cCondAdd  += If(Len(aFornece) > 0 , ")" , "" )
			cCondicao := "F1_STATUS <> ' ' .And. F1_STATUS <> 'B' .And. "
			cCondicao += "DTOS(F1_EMISSAO) >= '" + Dtos(mv_par05)	+ "' .And. "
			cCondicao += "DTOS(F1_EMISSAO) <= '" + DtoS(mv_par06) +"'"
			If Len(cCondAdd) > 2
				cCondicao += " .And. " + cCondAdd
			EndIf

			oReport:Section(1):SetFilter(cCondicao,IndexKey())

			SD1->(dbSetOrder(1))

	        oSectSF2:Disable()
	
			oSectSF1:SetParentFilter( { |cParam| (cAliasSF1)->F1_FORNECE + (cAliasSF1)->F1_LOJA == cParam },{ || (cAliasSF1)->F1_FORNECE + (cAliasSF1)->F1_LOJA })
	
	        If mv_par03 == 1 
				TRPosition():New(oSectSD1,"SB1",1,{|| xFilial("SB1")+SD1->D1_COD })
	
				oSectSD1:SetRelation({|| xFilial("SD1")+(cAliasSF1)->(F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA) },"SD1", 1 , .T. )	
				oSectSD1:SetParentFilter( { |cParam| SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA == cParam },;
									         { ||(cAliasSF1)->F1_DOC+(cAliasSF1)->F1_SERIE+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA })
	        Else
	            oSectSD1:Disable() 
	        EndIf
	        
			oFornece:Print()	
	
		#ENDIF
	EndIf

	If mv_par04 <> 1 .And. Len(aCliente) > 0

		TRPosition():New(oCliente,"SA1",1,{|| xFilial("SA1")+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA})

		dbSelectArea("SF2")
		dbSetOrder(2)
		dbSeek(xFilial("SF2")+aCliente[1][1]+aCliente[1][2])

		#IFDEF TOP
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Transforma parametros Range em expressao SQL                        ³	
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MakeSqlExpr(oReport:uParam)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Query do relatório da secao 2                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oReport:Section(2):BeginQuery()	

			cWhereSF2 := "%"
			cWhereSF2 += If(Len(aCliente) > 0 , "(" , "" )
			For nX := 1 To Len(aCliente)
				If nX > 1
					cWhereSF2 += " OR "
				Endif
				cWhereSF2 += "(F2_CLIENTE='"+aCliente[nx][1] + "' AND F2_LOJA='"+aCliente[nx][2]+"')"
			Next 
			cWhereSF2 += If(Len(aCliente) > 0 , ")" , "" )
			If Len(cWhereSF2) > 2
				cWhereSF2 += " AND "
			EndIf
			cWhereSF2 += "%"
 
			If mv_par03 == 1 //lista itens
                cSelect := "%" 
				cSelect += ",D2_FILIAL ,D2_DOC, D2_SERIE, D2_CLIENTE, D2_LOJA, D2_COD, D2_VALICM, D2_VALIPI, D2_QUANT, D2_PRCVEN, D2_DESC, D2_TOTAL, "
				cSelect += "B1_COD, B1_DESC "
                cSelect += "%" 

                cFrom := "%"
                cFrom += "," + RetSqlName("SD2") + " SD2, " + RetSqlName("SB1") + " SB1 "
                cFrom += "%" 

                cWhere := "%"
                cWhere += "AND D2_DOC     	  = F2_DOC     "
                cWhere += "AND D2_SERIE   	  = F2_SERIE   "
                cWhere += "AND D2_CLIENTE 	  = F2_CLIENTE "
				cWhere += "AND D2_LOJA    	  = F2_LOJA    "
				cWhere += "AND D2_TIPO    	  = F2_TIPO    "     
				
				If !Empty(xFilial("SB1")) //.And. cPaisloc <> "BRA"
					cWhere += "AND F2_FILIAL  = B1_FILIAL  AND F2_FILIAL = D2_FILIAL "
				EndIf
				   
				cWhere += "AND B1_COD     	  = D2_COD	   "
				cWhere += "AND SD2.D_E_L_E_T_ = ' ' "
				cWhere += "AND SB1.D_E_L_E_T_ = ' ' "
				cWhere += "%"  
            EndIf

			BeginSql Alias cAliasSF2
			
			SELECT F2_FILIAL,  F2_DOC, F2_SERIE, F2_CLIENTE, F2_LOJA, F2_EMISSAO, F2_VALBRUT,
			       F2_VALMERC, F2_VALIPI, F2_VALICM, F2_FRETE, F2_DESPESA, F2_SEGURO, SF2.R_E_C_N_O_ SF2RECNO
			       %Exp:cSelect%

			FROM %table:SF2% SF2
			     %Exp:cFrom%
 
		    WHERE F2_EMISSAO >= %Exp:Dtos(mv_par05)% AND 
				  F2_EMISSAO <= %Exp:Dtos(mv_par06)% AND 
				  %Exp:cWhereSF2%
				  SF2.%NotDel% 
				  %Exp:cWhere%
				  
			ORDER BY %Order:SF2% 
					
			EndSql 

			oReport:Section(2):EndQuery(/*Array com os parametros do tipo Range*/)

	        oSectSF1:Disable()
	        oSectSF2:Enable()
	
			oSectSF2:SetParentQuery()
			oSectSF2:SetParentFilter( { |cParam| (cAliasSF2)->F2_CLIENTE + (cAliasSF2)->F2_LOJA == cParam },{ || (cAliasSF2)->F2_CLIENTE + (cAliasSF2)->F2_LOJA })
	
	        If mv_par03 == 1 
				oSectSD2:SetParentQuery()
				oSectSD2:SetParentFilter( { |cParam| (cAliasSF2)->D2_DOC+(cAliasSF2)->D2_SERIE+(cAliasSF2)->D2_CLIENTE+(cAliasSF2)->D2_LOJA == cParam },;
			                                { || (cAliasSF2)->D2_DOC+(cAliasSF2)->D2_SERIE+(cAliasSF2)->D2_CLIENTE+(cAliasSF2)->D2_LOJA })
			Else
	            oSectSD2:Disable() 		
			EndIf
	
			oCliente:Print()

		#ELSE

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Transforma parametros Range em expressao Advpl                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MakeAdvplExpr(oReport:uParam)

			cCondAdd := If(Len(aCliente) > 0 , "(" , "" )
			For nX:=1 to Len(aCliente)
				If nX > 1
					cCondAdd += " .Or. "
				Endif
				cCondAdd += "(F2_CLIENTE=='" +aCliente[nX][1] + "' .And. F2_LOJA=='"+aCliente[nX][2]+"')"
			Next
			cCondAdd  += If(Len(aCliente) > 0 , ")" , "" )
			cCondicao := "DTOS(F2_EMISSAO) >= '" + Dtos(mv_par05)	+ "' .And. "
			cCondicao += "DTOS(F2_EMISSAO) <= '" + DtoS(mv_par06) +"'"
			If Len(cCondAdd) > 2
				cCondicao += " .And. " + cCondAdd
			EndIf

			oReport:Section(2):SetFilter(cCondicao,IndexKey())

			SD2->(dbSetOrder(3))
							
			oSectSF1:Disable()
			oSectSF2:Enable()
			
			oSectSF2:SetParentFilter( { |cParam| (cAliasSF2)->F2_CLIENTE + (cAliasSF2)->F2_LOJA == cParam },{ || (cAliasSF2)->F2_CLIENTE + (cAliasSF2)->F2_LOJA })
			
			If mv_par03 == 1
				TRPosition():New(oSectSD2,"SB1",1,{|| xFilial("SB1")+SD2->D2_COD })
				
				oSectSD2:SetRelation({|| xFilial("SD2")+(cAliasSF2)->(F2_DOC+F2_SERIE+F2_CLIENTE+F2_LOJA) },"SD2", 3 , .T. )
				oSectSD2:SetParentFilter( { |cParam| SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA == cParam },;
				{ || (cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA })
			Else
				oSectSD2:Disable()
			EndIf
			
			oCliente:Print()
						
		#ENDIF
	EndIf
Else
	Help(" ",1,"RECNO")
EndIf

SM0->(RestArea(aAreaSM0))

Return Nil 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATR103R3º Autor ³ Aline Correa do Valeº Data ³  17/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Relatorio de Notas de Transferencia entre Filiais          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Generico                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MATR103R3

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cDesc1   := STR0002 //"Este programa tem como objetivo imprimir relatorio "
Local cDesc2   := STR0003 //"de acordo com os parametros informados pelo usuario."
Local cDesc3   := STR0001 //"Relação de Notas Transferência entre Filiais"
Local titulo   := STR0001 //"Relação de Notas Transferência entre Filiais"
Local nLin     := 80

Local Cabec1   := STR0004 //"Data       Documento           Valor Mercadoria    Valor ICMS     Valor IPI   Valor Frete  Valor Seguro Vlr. Despesas     Total da Nota"
Local aOrd     := {}

Private lEnd        := .F.
Private lAbortPrint := .F.
Private limite      := 132
Private tamanho     := "M"
Private nomeprog    := "MATR103"
Private nTipo       := 15
Private aReturn     := { STR0007, 1, STR0008, 1, 2, 1, "", 1} //"Zebrado" ##"Administracao"
Private nLastKey    := 0
Private cPerg       := "MTR103"
Private cbtxt       := Space(10)
Private cbcont      := 00
Private CONTFL      := 01
Private m_pag       := 01
Private wnrel       := "MATR103"

Private cString := "SF1"

dbSelectArea("SF1")
dbSetOrder(2)

pergunte(cPerg,.F.)

/********************************************************
* mv_par01 Da Filial
* mv_par02 Ate a Filial
* mv_par03 Imprime Itens?   Sim(1) / Nao(2)
* mv_par04 Imprimir:   Entradas(1) / Saidas(2) / Ambos(3)
* mv_par05 Emissao de
* mv_par06 Emissao ate
*********************************************************/

wnrel := SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.T.,aOrd,.F.,Tamanho,,.F.)

If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
   Return
Endif

nTipo := If(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento. RPTSTATUS monta janela com a regua de processamento. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RptStatus({|| RunReport(Cabec1,Titulo,nLin) },Titulo)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³RUNREPORT º Autor ³ Aline Correa do Vale º Data ³17/12/2003 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Funcao auxiliar chamada pela RPTSTATUS. A funcao RPTSTATUS º±±
±±º          ³ monta a janela com a regua de processamento.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Programa principal                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function RunReport(Cabec1,Titulo,nLin)

Local aAreaSM0   := SM0->(GetArea())
Local aFornece   := {}
Local aCliente   := {}
Local cQuery     := ""
Local cQAdd      := ""
Local cIndex 	 := CriaTrab("",.F.)
Local cIndexF2   := CriaTrab("",.F.)
Local lContinua  := .T.
Local nX         := 0
Local cAliasSF1  := "SF1"
Local cAliasSD1  := "SD1"
Local cAliasSF2  := "SF2"
Local cAliasSD2  := "SD2"
Local lUsaFilTrf := IIF(FindFunction('UsaFilTrf'), UsaFilTrf(), .F.)
#IFDEF TOP
	Local aStrucSF1:= {}
	Local aStrucSD1:= {}
	Local aStrucSF2:= {}
	Local aStrucSD2:= {}
#ENDIF
dbSelectArea(cString)
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ SETREGUA -> Indica quantos registros serao processados para a regua ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SetRegua(RecCount())

dbSelectArea("SD2")
dbSetOrder(3)
dbSelectArea("SD1")
dbSetOrder(1)
dbSelectArea("SB1")
dbSetOrder(1)
dbSelectArea("SM0")
dbSetOrder(1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Procura o CNPJ da Filial para localizar o Fornecedor e/ou Cliente cadastrado ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(mv_par02)
	MsSeek(Subs(cNumEmp,1,2)+mv_par01,.T.)
	While SM0->M0_CODIGO+SM0->M0_CODFIL >= (Subs(cNumEmp,1,2)+mv_par01) .And.;
			SM0->M0_CODIGO+SM0->M0_CODFIL <= (Subs(cNumEmp,1,2)+mv_par02)
		If mv_par04 != 1
			dbSelectArea("SA1")
			If !lUsaFilTrf
				dbSetOrder(3)
				If dbSeek(xFilial("SA1")+SM0->M0_CGC) 
					If cPaisLoc == "BRA"
				   		aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME})
					Else   
						While Trim(SA1->A1_CGC) == Trim(SM0->M0_CGC)   
							If Ascan(aCliente,{|x| x[4]== SA1->A1_COD+SA1->A1_LOJA})==0 
		  					   	aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME,SA1->A1_COD+SA1->A1_LOJA})
		  					EndIf
						   	DBSkip()
						 EndDo				     
					EndIf
		   		EndIf
			Else
				dbSetOrder(1)
				If dbSeek(xFilial("SA1")+SM0->M0_CODFIL)
					aAdd(aCliente, {SA1->A1_COD,SA1->A1_LOJA,SA1->A1_NOME})
				EndIf
			EndIf
		EndIf
		If mv_par04 != 2
			dbSelectArea("SA2")
			If !lUsaFilTrf
				dbSetOrder(3)
				If dbSeek(xFilial("SA2")+SM0->M0_CGC) 
					If cPaisLoc == "BRA"
						aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME})
					Else   
						While Trim(SA2->A2_CGC) == Trim(SM0->M0_CGC)   
							If Ascan(aFornece,{|x| x[4]== SA2->A2_COD+SA2->A2_LOJA})==0 
			  					aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME,SA2->A2_COD+SA2->A2_LOJA})
			  				EndIf
						   	DBSkip()
						EndDo
					EndIf
				EndIf
			Else
				dbSetOrder(1)
				If dbSeek(xFilial("SA2")+SM0->M0_CODFIL)
					aAdd(aFornece, {SA2->A2_COD,SA2->A2_LOJA,SA2->A2_NOME})
				EndIf
			EndIf
		EndIf
		dbSelectArea("SM0")
		dbSkip()
	EndDo
EndIf

If !(Len(aFornece)+Len(aCliente)) > 0
	Help(" ",1,"RECNO")
   lContinua := .F.
EndIf

If mv_par04!=1 .And. Len(aCliente) > 0
	dbSelectArea("SF2")
	dbSetOrder(2)
	dbSeek(xFilial("SF2")+aCliente[1][1]+aCliente[1][2])

	#IFDEF TOP
      aStrucSF2 := SF2->(dbStruct())
      aStrucSD2 := SD2->(dbStruct())
		cQAdd := if(len(aCliente)>0,"(","")
		for nx:=1 to len(aCliente)
			if nx>1
				cQAdd += " OR "
			endif
			cQAdd += "(F2_CLIENTE='" +aCliente[nx][1] + "' AND F2_LOJA='"+aCliente[nx][2]+"')"
		next
		cQAdd += if(len(aCliente)>0,")","")

		cQuery := "SELECT F2_FILIAL, F2_DOC, F2_SERIE, F2_CLIENTE, F2_LOJA, F2_EMISSAO, F2_VALBRUT,"
		cQuery += "F2_VALMERC, F2_VALIPI, F2_VALICM, F2_FRETE, F2_DESPESA, F2_SEGURO, SF2.R_E_C_N_O_ SF2RECNO "
		If mv_par03 == 1 //lista itens
			cQuery += ",D2_FILIAL, D2_DOC, D2_SERIE, D2_CLIENTE, D2_LOJA, D2_COD, D2_VALICM, D2_VALIPI, D2_QUANT, D2_PRCVEN, D2_DESC, D2_TOTAL, "
			cQuery += "B1_COD, B1_DESC "
			cQuery += "FROM " + RetSqlName("SD2") + " SD2, " + RetSqlName("SF2") + " SF2, "
			cQuery += RetSqlName("SB1") + " SB1 "
			cQuery += "WHERE D2_DOC = F2_DOC AND D2_SERIE = F2_SERIE AND D2_CLIENTE = F2_CLIENTE "
			cQuery += "AND D2_LOJA = F2_LOJA AND D2_TIPO = F2_TIPO AND B1_COD = D2_COD "
			
			If !Empty(xFilial("SB1")) //.And. cPaisloc <> "BRA"
				cQuery += "AND F2_FILIAL  = B1_FILIAL  "
			EndIf   
			
			cQuery += "AND SD2.D_E_L_E_T_ <> '*' AND SB1.D_E_L_E_T_ <> '*' AND "
		Else
			cQuery += "FROM " + RetSqlName("SF2") + " SF2 "
			cQuery += "WHERE "
		EndIf
		cQuery += "F2_EMISSAO >= '" + dToS(mv_par05)	+ "' AND "
		cQuery += "F2_EMISSAO <= '" + dToS(mv_par06)	+ "' AND "
		If Len(cQAdd) > 2
			cQuery += cQAdd + " AND "
		EndIf
		cQuery += "SF2.D_E_L_E_T_ <> '*' "
		cQuery += "ORDER BY " + SqlOrder(SF2->(IndexKey()))
		cQuery := ChangeQuery(cQuery)
		cAliasSD2 := 'QRYSD2'
		cAliasSF2 := 'QRYSD2'
		cAliasSB1 := 'QRYSD2'
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery),'QRYSD2', .T., .T.)
      For nX := 1 to Len(aStrucSD2)
         If aStrucSD2[nX,2] != 'C' .And. FieldPos(aStrucSD2[nx,1]) > 0
            TCSetField('QRYSD2', aStrucSD2[nX,1], aStrucSD2[nX,2],aStrucSD2[nX,3],aStrucSD2[nX,4])
         EndIf	
      Next nX

      For nX := 1 to Len(aStrucSF1)
         If aStrucSF2[nX,2] != 'C'.And. FieldPos(aStrucSF2[nx,1]) > 0
            TCSetField('QRYSD2', aStrucSF2[nX,1], aStrucSF2[nX,2],aStrucSF2[nX,3],aStrucSF2[nX,4])
         EndIf	
      Next nX
	#ELSE
		cQAdd := if(len(aCliente)>0,"(","")
		for nx:=1 to len(aCliente)
			if nx>1
				cQAdd += " .OR. "
			endif
			cQAdd += "(F2_CLIENTE=='" +aCliente[nx][1] + "' .AND. F2_LOJA=='"+aCliente[nx][2]+"')"
		next
		cQAdd += if(len(aCliente)>0,")","")

		cQuery := "DTOS(F2_EMISSAO) >= '" + dToS(mv_par05)	+ "' .AND. "
		cQuery += "DTOS(F2_EMISSAO) <= '" + dToS(mv_par06) +"'"
		If Len(cQAdd) > 2
			cQuery += " .AND. " + cQAdd
		EndIf
	   IndRegua("SF2",cIndexF2,SF2->(indexkey()),,cQuery)
	#ENDIF
EndIf

If mv_par04!=2 .And. Len(aFornece) > 0
	dbSelectArea("SF1")
	dbSetOrder(2)
	dbSeek(xFilial("SF1")+aFornece[1][1]+aFornece[1][2])

	#IFDEF TOP
      aStrucSF1 := SF1->(dbStruct())
      aStrucSD1 := SD1->(dbStruct())
      aStrucSB1 := SB1->(dbStruct())
		cQAdd := if(len(aFornece)>0,"(","")
		for nx:=1 to len(aFornece)
			if nx>1
				cQAdd += " OR "
			endif
			cQAdd += "(F1_FORNECE='"+aFornece[nx][1] + "' AND F1_LOJA='"+aFornece[nx][2]+"')"
		next
		cQAdd += if(len(aFornece)>0,")","")
		cQuery := "SELECT F1_FILIAL, F1_DOC, F1_SERIE, F1_FORNECE, F1_LOJA, F1_EMISSAO, F1_VALBRUT,"
		cQuery += "F1_VALMERC, F1_VALIPI, F1_VALICM, F1_FRETE, F1_DESPESA, F1_SEGURO, SF1.R_E_C_N_O_ SF1RECNO "
		If mv_par03 == 1 //lista itens
			cQuery += ",D1_FILIAL ,D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA, D1_COD, D1_VALICM, D1_VALIPI, D1_QUANT, D1_VUNIT, D1_VALDESC, D1_TOTAL, "
			cQuery += "B1_COD, B1_DESC "
			cQuery += "FROM " + RetSqlName("SD1") + " SD1, " + RetSqlName("SF1") + " SF1, "
			cQuery += RetSqlName("SB1") + " SB1 "
			cQuery += "WHERE D1_DOC = F1_DOC AND D1_SERIE = F1_SERIE AND D1_FORNECE = F1_FORNECE "
			cQuery += "AND D1_LOJA = F1_LOJA AND D1_TIPO = F1_TIPO AND B1_COD = D1_COD "
			
			If !Empty(xFilial("SB1")) //.And. cPaisloc <> "BRA"
				cQuery += "AND F1_FILIAL  = B1_FILIAL  "
			EndIf

			cQuery += "AND SD1.D_E_L_E_T_ <> '*' AND SB1.D_E_L_E_T_ <> '*' AND "
		Else
			cQuery += "FROM " + RetSqlName("SF1") + " SF1 "
			cQuery += "WHERE "
		EndIf
		cQuery += "F1_STATUS <> ' ' AND F1_STATUS <> 'B' AND "
		cQuery += "F1_EMISSAO >= '" + dToS(mv_par05)	+ "' AND "
		cQuery += "F1_EMISSAO <= '" + dToS(mv_par06)	+ "' AND "
		If Len(cQAdd) > 2
			cQuery += cQAdd + " AND "
		EndIf
		cQuery += "SF1.D_E_L_E_T_ <> '*' "
		cQuery += "ORDER BY " + SqlOrder(SF1->(IndexKey()))
		cAliasSD1 := 'QRYSD1'
		cAliasSF1 := 'QRYSD1'
		cAliasSB1 := 'QRYSD1'
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery),'QRYSD1', .T., .T.)
      For nX := 1 to Len(aStrucSD1)
         If aStrucSD1[nX,2] != 'C' .And. FieldPos(aStrucSD1[nx,1]) > 0
            TCSetField('QRYSD1', aStrucSD1[nX,1], aStrucSD1[nX,2],aStrucSD1[nX,3],aStrucSD1[nX,4])
         EndIf	
      Next nX

      For nX := 1 to Len(aStrucSF1)
         If aStrucSF1[nX,2] != 'C'.And. FieldPos(aStrucSF1[nx,1]) > 0
            TCSetField('QRYSD1', aStrucSF1[nX,1], aStrucSF1[nX,2],aStrucSF1[nX,3],aStrucSF1[nX,4])
         EndIf	
      Next nX
	
	#ELSE
		cQAdd := if(len(aFornece)>0,"(","")
		for nx:=1 to len(aFornece)
			if nx>1
				cQAdd += " .OR. "
			endif
			cQAdd += "(F1_FORNECE=='" +aFornece[nx][1] + "' .AND. F1_LOJA=='"+aFornece[nx][2]+"')"
		next
		cQAdd += if(len(aFornece)>0,")","")
		cQuery := "F1_STATUS <> ' ' .AND. F1_STATUS <> 'B' .AND. "
		cQuery += "DTOS(F1_EMISSAO) >= '" + dToS(mv_par05)	+ "' .AND. "
		cQuery += "DTOS(F1_EMISSAO) <= '" + dToS(mv_par06) +"'"
		If Len(cQAdd) > 2
			cQuery += " .AND. " + cQAdd
		EndIf
	   IndRegua("SF1",cIndex,SF1->(indexkey()),,cQuery)
	#ENDIF
EndIf

nX := 1
If lContinua

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³ Impressao do cabecalho do relatorio. . .                            ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   If mv_par04 == 1 .Or. mv_par04 == 3
	   For nX:=1 to Len(aFornece)
		   ImprSF1(aFornece, @nLin, nx, cAliasSF1, cAliasSD1, Cabec1, Titulo)
		Next
   	If nLin < 80
   		roda(cbcont,cbtxt,Tamanho)
		EndIf
	EndIf

   If mv_par04 == 2 .Or. mv_par04 == 3
   	nLin := 80
	   For nX:=1 to Len(aCliente)
		   ImprSF2(aCliente, @nLin, nx, cAliasSF2, cAliasSD2, Cabec1, Titulo)
		Next
   	If nLin < 80
   		roda(cbcont,cbtxt,Tamanho)
		EndIf
	EndIf
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza a execucao do relatorio...                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SET DEVICE TO SCREEN

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se impressao em disco, chama o gerenciador de impressao...          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If aReturn[5]==1
   dbCommitAll()
   SET PRINTER TO
   OurSpool(wnrel)
Endif

MS_FLUSH()

dbSelectArea("SD2")
dbSetOrder(3)
dbSelectArea("SD1")
dbSetOrder(1)
dbSelectArea("SA1")
dbSetOrder(1)
dbSelectArea("SA2")
dbSetOrder(1)
#IFDEF TOP
	If Select(cAliasSF1) > 0
		dbSelectArea(cAliasSF1)
		dbCloseArea()
	EndIf
	If Select(cAliasSF2) > 0
		dbSelectArea(cAliasSF2)
		dbCloseArea()
	EndIf
#ENDIF
dbSelectArea("SF1")
set filter to
RetIndex("SF1")
If File(cIndex+OrdBagExt())
	Ferase(cIndex+OrdBagExt())
Endif
dbSelectArea("SF2")
RetIndex("SF2")
set filter to
If File(cIndexF2+OrdBagExt())
	Ferase(cIndexF2+OrdBagExt())
Endif

SM0->(RestArea(aAreaSM0))

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³ ImprSF1  º Autor ³ Aline Correa do Vale º Data ³17/12/2003 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Imprime notas de entrada - SF1                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ImprSF1(aFornece, nLin, nX, cAliasSF1, cAliasSD1, Cabec1, Titulo)

Local cChaveSD1:= ""
Local nTValMerc:= 0
Local nTValBrut:= 0
Local nTValICM := 0
Local nTValIPI := 0
Local nTFrete  := 0
Local nTSeguro := 0
Local nTDespesa:= 0
Local cAliasSB1:= "SB1"

dbSelectArea(cAliasSF1)
#IFDEF TOP
	cAliasSB1 := cAliasSD1
#ELSE
	dbseek(xFilial("SF1")+aFornece[nx][1]+aFornece[nx][2])
#ENDIF

While aFornece[nx][1]+aFornece[nx][2] == F1_FORNECE+F1_LOJA .AND. !Eof()
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³ Verifica o cancelamento pelo usuario...                             ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

   If lAbortPrint
      @nLin,00 PSAY STR0012 //"*** CANCELADO PELO OPERADOR ***"
      Exit
   Endif

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³ Impressao do cabecalho do relatorio. . .                            ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   If nLin > 55 // Salto de Página. Neste caso o formulario tem 55 linhas...
      Cabec(Titulo+STR0005,Cabec1,"",NomeProg,Tamanho,nTipo) //- ENTRADAS
      nLin := 8
      @ nLin, 00 pSay STR0009 + aFornece[nx][1] + "/" + aFornece[nx][2] + " - " + aFornece[nx][3] //Codigo / Loja:
      nLin := nLin + 2
   EndIf

//a       Documento           Valor Mercadoria    Valor ICMS     Valor IPI   Valor Frete  Valor Seguro Vlr. Despesas    Total da Nota
//10/2003 123456123456/123456 11123,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 11123,123,123,99
	@ nLin, 00 pSay (cAliasSF1)->F1_EMISSAO Picture PesqPict("SF1","F1_EMISSAO")
	@ nLin, 11 pSay (cAliasSF1)->F1_DOC+"/"+(cAliasSF1)->F1_SERIE
	@ nLin, 31 pSay (cAliasSF1)->F1_VALMERC Picture PesqPict("SF1","F1_VALMERC",16)
	@ nLin, 48 pSay (cAliasSF1)->F1_VALICM  Picture PesqPict("SF1","F1_VALICM",13)
	@ nLin, 62 pSay (cAliasSF1)->F1_VALIPI  Picture PesqPict("SF1","F1_VALIPI",13)
	@ nLin, 76 pSay (cAliasSF1)->F1_FRETE   Picture PesqPict("SF1","F1_FRETE",13)
	@ nLin, 90 pSay (cAliasSF1)->F1_SEGURO  Picture PesqPict("SF1","F1_SEGURO",13)
	@ nLin,104 pSay (cAliasSF1)->F1_DESPESA Picture PesqPict("SF1","F1_DESPESA",13)
	@ nLin,118 pSay (cAliasSF1)->F1_VALBRUT Picture PesqPict("SF1","F1_VALBRUT",14)
	nTValMerc += (cAliasSF1)->F1_VALMERC
	nTValIcm  += (cAliasSF1)->F1_VALICM
	nTValIPI  += (cAliasSF1)->F1_VALIPI
	nTFrete   += (cAliasSF1)->F1_FRETE
	nTSeguro  += (cAliasSF1)->F1_SEGURO
	nTDespesa += (cAliasSF1)->F1_DESPESA
	nTValBrut += (cAliasSF1)->F1_VALBRUT
	
   nLin ++

   If mv_par03 == 1 //Lista itens
   	dbSelectArea(cAliasSD1)
   	cChaveSD1 := (cAliasSD1)->D1_FILIAL+(cAliasSF1)->(F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA)
   	#IFNDEF TOP
   		dbSeek(cChaveSD1)
   	#ENDIF
   	If !Eof()
			@ nLin, 00 pSay STR0013 //"DESCRICAO DA MERCADORIA                            VALOR ICMS     VALOR IPI    QUANTIDADE  VLR UNITARIO VLR. DESCONTO    TOTAL DO ITEM"
			nLin ++
		EndIf
   	While cChaveSD1 == (cAliasSD1)->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA) .And. !Eof()

//CRICAO DA MERCADORIA                            VALOR ICMS     VALOR IPI    QUANTIDADE  VLR UNITARIO VLR. DESCONTO    TOTAL DO ITEM
//456789012345 123456789012345678901234567890  23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 11123,123,123,99
			#IFNDEF TOP
				(cAliasSB1)->(dbSeek(xFilial("SB1")+(cAliasSD1)->D1_COD))
			#ENDIF
			@ nLin, 00 pSay (cAliasSD1)->D1_COD Picture PesqPict("SD1","D1_COD")
			@ nLin, 16 pSay (cAliasSB1)->B1_DESC
			@ nLin, 48 pSay (cAliasSD1)->D1_VALICM  Picture PesqPict("SD1","D1_VALICM",13)
			@ nLin, 62 pSay (cAliasSD1)->D1_VALIPI  Picture PesqPict("SD1","D1_VALIPI",13)
			@ nLin, 76 pSay (cAliasSD1)->D1_QUANT   Picture PesqPict("SD1","D1_QUANT",13)
			@ nLin, 90 pSay (cAliasSD1)->D1_VUNIT   Picture PesqPict("SD1","D1_VUNIT",13)
			@ nLin,104 pSay (cAliasSD1)->D1_VALDESC Picture PesqPict("SD1","D1_VALDESC",13)
			@ nLin,118 pSay (cAliasSD1)->D1_TOTAL   Picture PesqPict("SD1","D1_TOTAL",14)
			nLin ++
   		dbSkip()
   	EndDo
  		nLin++
   EndIf
   dbSelectArea(cAliasSF1)
   dbSkip() // Avanca o ponteiro do registro no arquivo
	cbcont ++
EndDo
If nTValBrut > 0
	nLin ++
	@ nLin, 00 PSAY STR0010 //"TOTAL ENTRADAS >>>>"
	@ nLin, 31 pSay nTValMerc Picture PesqPict("SF1","F1_VALMERC",16)
	@ nLin, 48 pSay nTValICM  Picture PesqPict("SF1","F1_VALICM",13)
	@ nLin, 62 pSay nTValIPI  Picture PesqPict("SF1","F1_VALIPI",13)
	@ nLin, 76 pSay nTFrete   Picture PesqPict("SF1","F1_FRETE",13)
	@ nLin, 90 pSay nTSeguro  Picture PesqPict("SF1","F1_SEGURO",13)
	@ nLin,104 pSay nTDespesa Picture PesqPict("SF1","F1_DESPESA",13)
	@ nLin,118 pSay nTValBrut Picture PesqPict("SF1","F1_VALBRUT",14)
	nLin ++
	nLin ++
EndIf

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³ ImprSF2  º Autor ³ Aline Correa do Vale º Data ³17/12/2003 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Imprime notas de saidas - SF2                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ImprSF2(aCliente, nLin, nX, cAliasSF2, cAliasSD2, Cabec1, Titulo)

Local cChaveSD2:= ""
Local nTValMerc:= 0
Local nTValBrut:= 0
Local nTValICM := 0
Local nTValIPI := 0
Local nTFrete  := 0
Local nTSeguro := 0
Local nTDespesa:= 0
Local cAliasSB1:= "SB1"

dbSelectArea(cAliasSF2)
#IFDEF TOP
	cAliasSB1 := cAliasSD2
#ELSE
	dbseek(xFilial("SF2")+aCliente[nx][1]+aCliente[nx][2])
#ENDIF

While aCliente[nx][1]+aCliente[nx][2] == F2_CLIENTE+F2_LOJA .AND. !Eof()
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³ Verifica o cancelamento pelo usuario...                             ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

   If lAbortPrint
      @nLin,00 PSAY STR0012 //"*** CANCELADO PELO OPERADOR ***"
      Exit
   Endif

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³ Impressao do cabecalho do relatorio. . .                            ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   If nLin > 55 // Salto de Página. Neste caso o formulario tem 55 linhas...
      Cabec(Titulo+STR0006,Cabec1,"",NomeProg,Tamanho,nTipo) //" - Saidas"
      nLin := 8
      @ nLin, 00 pSay STR0009 + aCliente[nx][1] + "/" + aCliente[nx][2] + " - " + aCliente[nx][3] //Codigo / Loja:
      nLin := nLin + 2
   EndIf

//a       Documento           Valor Mercadoria    Valor ICMS     Valor IPI   Valor Frete  Valor Seguro Vlr. Despesas    Total da Nota
//10/2003 123456123456/123456 11123,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 11123,123,123,99
	@ nLin, 00 pSay (cAliasSF2)->F2_EMISSAO Picture PesqPict("SF2","F2_EMISSAO")
	@ nLin, 11 pSay (cAliasSF2)->F2_DOC+"/"+(cAliasSF2)->F2_SERIE
	@ nLin, 31 pSay (cAliasSF2)->F2_VALMERC Picture PesqPict("SF2","F2_VALMERC",16)
	@ nLin, 48 pSay (cAliasSF2)->F2_VALICM  Picture PesqPict("SF2","F2_VALICM",13)
	@ nLin, 62 pSay (cAliasSF2)->F2_VALIPI  Picture PesqPict("SF2","F2_VALIPI",13)
	@ nLin, 76 pSay (cAliasSF2)->F2_FRETE   Picture PesqPict("SF2","F2_FRETE",13)
	@ nLin, 90 pSay (cAliasSF2)->F2_SEGURO  Picture PesqPict("SF2","F2_SEGURO",13)
	@ nLin,104 pSay (cAliasSF2)->F2_DESPESA Picture PesqPict("SF2","F2_DESPESA",13)
	@ nLin,118 pSay (cAliasSF2)->F2_VALBRUT Picture PesqPict("SF2","F2_VALBRUT",14)
	nTValMerc += (cAliasSF2)->F2_VALMERC
	nTValIcm  += (cAliasSF2)->F2_VALICM
	nTValIPI  += (cAliasSF2)->F2_VALIPI
	nTFrete   += (cAliasSF2)->F2_FRETE
	nTSeguro  += (cAliasSF2)->F2_SEGURO
	nTDespesa += (cAliasSF2)->F2_DESPESA
	nTValBrut += (cAliasSF2)->F2_VALBRUT
	
   nLin ++

   If mv_par03 == 1 //Lista itens
   	dbSelectArea(cAliasSD2)
   	cChaveSD2 := (cAliasSD2)->D2_FILIAL+(cAliasSF2)->(F2_DOC+F2_SERIE+F2_CLIENTE+F2_LOJA)
   	#IFNDEF TOP
   		dbSeek(cChaveSD2)
   	#ENDIF
   	If !Eof()
			@ nLin, 00 pSay STR0013 //"DESCRICAO DA MERCADORIA                            VALOR ICMS     VALOR IPI    QUANTIDADE  VLR UNITARIO VLR. DESCONTO    TOTAL DO ITEM"
			nLin ++
		EndIf
   	While cChaveSD2 == (cAliasSD2)->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA) .And. !Eof()

//CRICAO DA MERCADORIA                            VALOR ICMS     VALOR IPI    QUANTIDADE  VLR UNITARIO VLR. DESCONTO    TOTAL DO ITEM
//456789012345 123456789012345678901234567890  23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 23,123,123,99 11123,123,123,99
			#IFNDEF TOP
				(cAliasSB1)->(dbSeek(xFilial("SB1")+(cAliasSD2)->D2_COD))
			#ENDIF
			@ nLin, 00 pSay (cAliasSD2)->D2_COD Picture PesqPict("SD2","D2_COD")
			@ nLin, 16 pSay (cAliasSB1)->B1_DESC
			@ nLin, 48 pSay (cAliasSD2)->D2_VALICM  Picture PesqPict("SD2","D2_VALICM",13)
			@ nLin, 62 pSay (cAliasSD2)->D2_VALIPI  Picture PesqPict("SD2","D2_VALIPI",13)
			@ nLin, 76 pSay (cAliasSD2)->D2_QUANT   Picture PesqPict("SD2","D2_QUANT",13)
			@ nLin, 90 pSay (cAliasSD2)->D2_PRCVEN  Picture PesqPict("SD2","D2_PRCVEN",13)
			@ nLin,104 pSay (cAliasSD2)->D2_DESC    Picture PesqPict("SD2","D2_DESC",13)
			@ nLin,118 pSay (cAliasSD2)->D2_TOTAL   Picture PesqPict("SD2","D2_TOTAL",14)
			nLin ++
   		dbSkip()
   	EndDo
  		nLin++
   EndIf
   dbSelectArea(cAliasSF2)
   dbSkip() // Avanca o ponteiro do registro no arquivo
	cbcont ++
EndDo
If nTValBrut > 0
	nLin ++
	@ nLin, 00 PSAY STR0011 //"TOTAL SAIDAS >>>>>>"
	@ nLin, 31 pSay nTValMerc Picture PesqPict("SF2","F2_VALMERC",16)
	@ nLin, 48 pSay nTValICM  Picture PesqPict("SF2","F2_VALICM",13)
	@ nLin, 62 pSay nTValIPI  Picture PesqPict("SF2","F2_VALIPI",13)
	@ nLin, 76 pSay nTFrete   Picture PesqPict("SF2","F2_FRETE",13)
	@ nLin, 90 pSay nTSeguro  Picture PesqPict("SF2","F2_SEGURO",13)
	@ nLin,104 pSay nTDespesa Picture PesqPict("SF2","F2_DESPESA",13)
	@ nLin,118 pSay nTValBrut Picture PesqPict("SF2","F2_VALBRUT",14)
	nLin ++
	nLin ++
EndIf

Return()
