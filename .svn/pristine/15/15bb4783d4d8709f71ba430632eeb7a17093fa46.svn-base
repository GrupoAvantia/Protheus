#INCLUDE "PROTHEUS.CH" 
#INCLUDE "RWMAKE.CH"   
#INCLUDE "TOPCONN.CH" 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AVPMSR07  ³ Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Relatorio Relação de Insumos                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Gestao de Projetos                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function AVPMSR07()

	Local cMens := ""
	Local nOpca := 0
	
	Private cTitRel := OemToAnsi("Cotação de Materiais")
	Private lEnd	:= .F.
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ajustar perguntas do SX1                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AjustaSX1("APMSR07")
	
	Pergunte("APMSR07",.F.)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tela de configuracao do Relatorio                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg FROM  96,9 TO 310,592 TITLE OemToAnsi("Cotação de Materiais") PIXEL    //"Contratos"
	@ 18, 6 TO 66, 287 LABEL "" OF oDlg  PIXEL
	@ 29, 15 SAY OemToAnsi("Este programa ira emitir a relacao de materiais") SIZE 268, 8 OF oDlg PIXEL
	@ 38, 15 SAY OemToAnsi("pre-selecionados pelo usuário com a finalidade") SIZE 268, 8 OF oDlg PIXEL
	@ 48, 15 SAY OemToAnsi("de gerar uma cotação para o fornecedor.") SIZE 268, 8 OF oDlg PIXEL
	DEFINE SBUTTON FROM 80, 190 TYPE 5 ACTION Pergunte("APMSR07",.t.) ENABLE OF oDlg
	DEFINE SBUTTON FROM 80, 223 TYPE 1 ACTION (oDlg:End(),nOpca:=1) ENABLE OF oDlg
	DEFINE SBUTTON FROM 80, 255 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTER
	
	If nOpca == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processamento do Relatorio                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RptStatus({|lEnd| APMS01Imp(@lEnd)})
	EndIf	

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³APMS01Imp ³ Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ AVPMSR07 		                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function APMS01Imp(lEnd)

	Local cQuery		:= ""
	Local cSeek		:= ""
	Local nX			:= 0
	Local cDirSpool 	:= GetMv("MV_RELT")
	Local cDescri 	:= ""
	Local aMaterial	:= {}
	Local cMark := GetMark()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa Fontes                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private oFont06	:= TFont():New("Arial",08,08,,.T.,,,,.T.,.F.)
	Private oFont07	:= TFont():New("Courier New",08,08,,.F.,,,,.T.,.F.)
	Private oFont08	:= TFont():New("Arial",08,08,,.F.,,,,.T.,.F.)
	Private oFont08n	:= TFont():New("Arial",08,08,,.T.,,,,.T.,.F.)
	Private oFont10n	:= TFont():New("Arial",10,10,,.T.,,,,.T.,.F.)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas pelo Objeto TMSPrinter					  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private nMaxLin := 3000
	Private nlin	:= nMaxLin
	Private nPagina := 0
	Private oPrint
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis Gerais                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa Objeto TMSPrinter                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPrint:=TMSPrinter():New(OemToAnsi("Relação de Insumos"))
	oPrint:SetPortrait()	//-- Retrato //SetLandscape()//paisagem
	oPrint:Setup() 		//-- Configurar Impressao
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Query do Contrato                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAliasTop := "XAF1"
	
	cQuery := "SELECT * "
	cQuery += "FROM " + RetSqlName("AF1") + " AF1 "  
	cQuery += "WHERE AF1.D_E_L_E_T_ = '' AND "
	cQuery += "AF1_FILIAL =  '"		+	xFilial("AF1")	+ "' AND "
	cQuery += "AF1_ORCAME >= '"		+	mv_par01			+ "' AND "
	cQuery += "AF1_ORCAME <= '"		+	mv_par02			+ "' AND "
	cQuery += "AF1_DATA >= '"		+	dtos(mv_par03)	+ "' AND "
	cQuery += "AF1_DATA <= '"		+	dtos(mv_par04)	+ "' "
	cQuery += "ORDER BY AF1_ORCAME "
	
	cQuery := ChangeQuery(cQuery)
	
	MsAguarde({|| dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliasTop,.F.,.T.)},OemToAnsi("Processando"))
	
	dbSelectArea(cAliasTop)
	SetRegua(LastRec())
	
	nLin := nMaxLin //-- Forca a Quebra da pagina por orçamento
	nAjus := 10
	
	(cAliasTop)->(dbGoTop())
	
	Do While (cAliasTop)->(!Eof())
		
		IncProc()
		
		If lEnd
			CNRBox(0050,2350,OemToAnsi("CANCELADO PELO OPERADOR"),,.T.,1)
			Exit
		EndIf
	
		If nLin >= nMaxLin
			CNRCabec(Nil,Nil,OemToAnsi(cTitRel),.t.) //-- Impressao do Cabecalho da pagina
		EndIf
		
		//Materiais
		cQryMA:="SELECT AF3_PRODUT, B1_CEME, B1_UM, SUM(AF3_QUANT*AF2_QUANT) AF3_QUANT "
		cQryMA+="FROM "+RetSqlName("AF3")+" AF3 "
		cQryMA+="INNER JOIN "+RetSqlName("AF2")+" AF2 "
		cQryMA+="ON AF3.AF3_FILIAL = AF2.AF2_FILIAL "
		cQryMA+="AND AF3.AF3_ORCAME = AF2.AF2_ORCAME "
		cQryMA+="AND AF3_TAREFA = AF2_TAREFA "
		cQryMA+="INNER JOIN "+RetSqlName("SB1")+" SB1 "
		cQryMA+="ON AF3.AF3_PRODUT = SB1.B1_COD "
		cQryMA+="WHERE AF3.D_E_L_E_T_ <> '*' "
		cQryMA+="AND AF2.D_E_L_E_T_ <> '*' "
		cQryMA+="AND SB1.D_E_L_E_T_ <> '*' "
		cQryMA+="AND AF3_PRODUT = B1_COD "
		cQryMA+="AND AF3_RECURS = '' "
		cQryMA+="AND AF3_ORCAME = '"	+(cAliasTop)->AF1_ORCAME + "' "
		cQryMA+="AND B1_FILIAL =  '"	+	xFilial("SB1")	+ "' "
		cQryMA+="AND AF3_FILIAL = '"	+	xFilial("AF3")	+ "' "
		cQryMA+="GROUP BY AF3_PRODUT,B1_CEME,B1_UM "
		cQryMA+="ORDER BY B1_CEME,AF3_PRODUT,B1_UM "
		
		dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQryMA),"XMA",.F.,.T.)
		
		aMaterial	:= {}
		While XMA->(!EOF())
			AADD(aMaterial,{.T., XMA->AF3_PRODUT, XMA->B1_CEME, XMA->B1_UM, XMA->AF3_QUANT})
			XMA->(dbSkip())
		EndDo
		
		dbSelectArea("XMA")
		dbCloseArea()
		
		MSDFiltro(aMaterial, cMark)
		nItem := 1
		
		/*
		aMaterial[c][1] = True/False - Imprime/Não Imprime
		aMaterial[c][2] = Código do Produto
		aMaterial[c][3] = Descrição estendida
		aMaterial[c][4] = Unidade de Medida
		aMaterial[c][5] = Quantidade
		*/
		
		For c := 1 to Len(aMaterial)
			If(aMaterial[c][1] == .T.)
				If(Len(AllTrim(aMaterial[c][3])) < 96)
					CNRBox(0050, 0200, PADL(nItem,3,"0")										, "", .F., 2, oFont07, .F.)
					CNRBox(0200, 1850, aMaterial[c][3]											, "", .F., 2, oFont07, .F.)
					CNRBox(1850, 2000, aMaterial[c][4]											, "", .F., 2, oFont07, .F.)
					CNRBox(2000, 2351, Transform(aMaterial[c][5],"@E 999,999,999.9999")	, "", .T., 2, oFont07, .T.)
				Else
					CNRBox(0050, 0200, PADL(nItem,3,"0")				,""														, .F., 3, oFont07, .F.)
					CNRBox(0200, 1850, Substr(aMaterial[c][3],1,95)	,Substr(aMaterial[c][3],96,95)						, .F., 3, oFont07, .F.)
					CNRBox(1850, 2000, ""								,aMaterial[c][4]										, .F., 3, oFont07, .F.)
					CNRBox(2000, 2351, ""								,Transform(aMaterial[c][5],"@E 999,999,999.9999"), .T., 3, oFont07, .T.)
				EndIf
				nItem++
			EndIf
		Next
		
		nLin := nLin+50
		//-- Escreve o rodapé do relatório
		CNRRodap(nLin,.F.)
		
		nLin := 3000
		(cAliasTop)->(dbSkip())
	EndDo
	
	//-- Encerra arquivo de Trabalho
	dbSelectArea(cAliasTop)
	dbCloseArea()
	
	If !Empty(cDirSpool)
		oPrint:SaveAllAsJPEG(cDirSpool+"JBRR01",875,1170,140)
	EndIF	
	
	//-- Visualiza antes de Imprimir
	oPrint:Preview()  

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³ MSDFiltroº Autor ³ Carlos Beltrão     º Data ³  18/12/2014 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Funcão que exibe uma janela para ser selecionados os       º±±
±±º          ³ materiais desejados.                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Programa principal                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MSDFiltro(aMaterial, cMark)

	Local aCpoBro 	:= {}
	Local nCont 		:= 0
	Local _stru		:= {}
	Local lConfirma	:= .F.
	Local lInverte 	:= .F.
	Local oMark
	
	//Cria um arquivo de Apoio
	AADD(_stru,{"OK" 		,"C"	,2 		,0	})
	AADD(_stru,{"COD"   	,"C"	,15		,0	})
	AADD(_stru,{"DESCR"  ,"C"	,120 	,0	})
	cArq := Criatrab(_stru,.T.)
	dbUseArea(.t.,,cArq,"TTRB")
	
	//Dando carga nos dados para exibição
	For c := 1 to Len(aMaterial)
		DbSelectArea("TTRB")	
		RecLock("TTRB",.T.)	
		TTRB->COD 		:=  aMaterial[c][2]	
		TTRB->DESCR 	:=  aMaterial[c][3]
		MsunLock()		
	Next
	
	//Define quais colunas (campos da TTRB) serao exibidas na MsSelect
	aCpoBro := {	{ "OK"			,, "Mark"			,"@!"		},;			
					{ "COD"		,, "Código"		,"@!"		},;			
					{ "DESCR"		,, "Descrição"	,"@!"		}}		
	
	//Cria uma Dialog
	DEFINE MSDIALOG oDlg TITLE "Seleção dos Materiais" From 9,0 To 315,600 PIXEL
		DbSelectArea("TTRB")
		DbGotop()
		//Cria a MsSelect
		oMark := MsSelect():New("TTRB","OK",,aCpoBro,@lInverte,@cMark,{17,1,150,500},,,,,)
		oMark:bMark := {| | Mark(oMark, cMark)}
	//Exibe a Dialog
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{|| lConfirma := .T., oDlg:End()},{|| oDlg:End()})
	
	If(lConfirma)
		DbSelectArea("TTRB")
		DbGotop()
		c := 1
		While TTRB->(!EOF())
			IIF(TTRB->OK == cMark, aMaterial[c][1] := .T., aMaterial[c][1] := .F.) 
			c++
			TTRB->(dbSkip())
		EndDo
	EndIf

	//Fecha a Area e elimina os arquivos de apoio criados em disco.
	TTRB->(DbCloseArea())
	IIf(File(cArq + GetDBExtension()),FErase(cArq  + GetDBExtension()) ,Nil)
	
Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³ Mark     º Autor ³ Carlos Beltrão     º Data ³ 18/12/2014  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Funcão executada ao marcar/desmarcar um registro.          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FiltroCC                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Mark(oMark, cMark)

	RecLock("TTRB",.F.)
	If Marked("OK")	
		TTRB->OK := cMark
	Else	
		TTRB->OK := ""
	Endif             
	MSUNLOCK()
	oMark:oBrowse:Refresh()

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CNRCabec ³ Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cabecalho padrao para relatorios graficos                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Posicao da coluna para impressao do cTexto         ³±±
±±³          ³ ExpC2 = Texto a ser impresso	                             ³±±
±±³          ³ ExpC3 = Titulo do Relatorio                                ³±±
±±³          ³ ExpO4 = Fonte para impressao do cTexto                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR010 / CNR020	                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function CNRCabec(nCol,cTexto,cTitulo,oFontT,lPaisagem)

	Local cFileLogo		:= ""
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa Fontes                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local oFont10			:= TFont():New("Arial",10,10,,.F.,,,,.T.,.F.)
	Local oFont08n		:= TFont():New("Arial",08,08,,.T.,,,,.T.,.F.)
	
	Default nCol 			:= 0
	Default cTexto		:= ""
	Default cTitulo 		:= ""
	Default oFontT  		:= oFont10
	Default lPaisagem 	:= .F.
	
	If (If(lPaisagem,(nLin >= 2350),(nLin >= 3000)))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cabecalho da Pagina                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nPagina++ //-- Variavel Private
		nLin := 200 //-- Variavel private
		oPrint:EndPage() 	//-- Encerra a pagina anterior
		oPrint:StartPage()	//-- Inicia uma nova pagina
		oPrint:Line(50,50,50,If(lPaisagem,3181,2350))
		oPrint:Line(51,51,51,If(lPaisagem,3181,2351))
		oPrint:Line(200,50,200,If(lPaisagem,3181,2351))
		oPrint:Line(201,51,201,If(lPaisagem,3181,2351))
		oPrint:Say(065,0200,Upper(OemToAnsi(cTitulo)),oFont08n) 	// Texto do Cabecalho
		oPrint:Say(110,0200,Upper((cAliasTop)->AF1_ORCAME+"   "+Posicione("AF1",1,xFilial("AF1")+(cAliasTop)->AF1_ORCAME,"AF1_DESCRI")),oFont08n)	// ORÇAMENTO
		oPrint:Say(155,0200,Upper("Solicitante: " + UsrFullName(RetCodUsr())),oFont08n)	// Nome do Usuário
		oPrint:Say(065,If(lPaisagem,2830,2000),OemToAnsi("Folha....: ") + StrZero(nPagina,3),oFont10)
		oPrint:Say(110,If(lPaisagem,2830,2000),OemToAnsi("DT.Ref..: ") + Dtoc(dDataBase),oFont10)
		oPrint:Say(155,If(lPaisagem,2830,2000),OemToAnsi("Emissao.: ") + Dtoc(Date()),oFont10)
		cFileLogo += "LGRL"+SM0->M0_CODIGO+".BMP"
		oPrint:SayBitmap(70,60,cFileLogo,120,120) //-- Logotipo
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Salta para a proxima linha            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nLin += 50
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressao do texto                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cTexto)
		oPrint:Say(nLin,nCol,cTexto,oFontT)
	EndIf
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cabecalho dos Itens                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CNRBox(0050,0200,"Item"			,,.F.,2,oFont06)
	CNRBox(0200,1850,"Descição"		,,.F.,2,oFont06)
	CNRBox(1850,2000,"Unid."			,,.F.,2,oFont06)
	CNRBox(2000,2351,"Quantidade"	,,.T.,2,oFont06)

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CNRRodap ³ Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rodape padrao para relatorios graficos                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Número da Linha                                    ³±±
±±³          ³ ExpL2 = Formato Paisagem	                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR010 / CNR020                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function CNRRodap(nlin, lPaisagem)

	Local cCodiUsu	:= RetCodUsr()
	Local cNomeUsu	:= UsrFullName(cCodiUsu)
	Local cFuncUsu	:= ""
	Local cEmaiUsu	:= UsrRetMail(cCodiUsu)
	Local cTeleUsu	:= "Tel. 81 3797 9304 | Fax. 81 3797 9300"
	Local cNomeEmp	:= FWFilRazSocial() //"Avantia Tecnologia e Engenharia S/A"
	Local cEndeEmp	:= "Av. Caxangá, 104, Madalena – Recife/PE"
	Local cSiteEmp	:= "www.grupoavantia.com.br"
	Local aInfUser	:= {}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa Fontes                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local oFont08s := TFont():New("Arial",,08,,.T.,,,,,.T.,.F.)
	Local oFont08 := TFont():New("Trebuchet MS",,08,,.F.,,,,,.F.,.F.)
	Local oFont08n := TFont():New("Trebuchet MS",,08,,.T.,,,,,.F.,.F.)
	
	PswOrder(1)
	PswSeek(RetCodUsr (), .T.)
	aInfUser := aClone(PswRet())
	cFuncUsu := aInfUser[1][13]
	
	oPrint:Line(nlin,50,nlin,If(lPaisagem,3080,2350))
	oPrint:Line(nlin+1,50,nlin+1,If(lPaisagem,3181,2351))
	oPrint:Say(nlin+15	,1175,cNomeUsu,oFont08n	,,CLR_GREEN	,,2)
	oPrint:Say(nlin+65	,1175,cFuncUsu,oFont08	,,CLR_GRAY		,,2)
	oPrint:Say(nlin+115	,1175,cEmaiUsu,oFont08s	,,CLR_BLUE		,,2)
	oPrint:Say(nlin+165	,1175,cTeleUsu,oFont08	,,CLR_GRAY		,,2)
	oPrint:Say(nlin+215	,1175,cNomeEmp,oFont08n	,,CLR_GREEN	,,2)
	oPrint:Say(nlin+265	,1175,cEndeEmp,oFont08	,,CLR_GRAY		,,2)
	oPrint:Say(nlin+315	,1175,cSiteEmp,oFont08s	,,CLR_BLUE		,,2)
	oPrint:Line(nlin+365,50,nlin+365,If(lPaisagem,3080,2350))
	oPrint:Line(nlin+366,50,nlin+366,If(lPaisagem,3181,2351))

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CNRBox() ³ Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria Box para Textos                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Posicao inicial da coluna para criacao do Box      ³±±
±±³          ³ ExpN2 = Posicao final da coluna para criacao do Box        ³±±
±±³          ³ ExpC3 = Texto da 1a. linha do Box                          ³±±
±±³          ³ ExpC4 = Texto da 2a. linha do Box                          ³±±
±±³          ³ ExpL5 = Quebra para proxima linha apos a impressao do Box  ³±±
±±³          ³         (.T. Com  Quebra / .F. Sem Quebra)                 ³±±
±±³          ³ ExpN6 = Tipo do Box :                                      ³±±
±±³          ³                      1 - Linha de Cabecalho                ³±±
±±³          ³                      2 - Linha Unica Simples               ³±±
±±³          ³                      3 - Linha de Detalhe                  ³±±
±±³          ³                      4 - Somente Texto                     ³±±
±±³          ³ ExpO7 = Fonte para impressao do Texto                      ³±±
±±³          ³ ExpL8 = Se .T. realiza o alinhamento do cTexto1 a Direita. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR010  / CNTR020                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function CNRBox(nCol1,nCol2,cTexto1,cTexto2,lQuebra,nTipo,oFontB,lAlinDir)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa Fontes                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local oFont08 := TFont():New("Arial",08,08,,.F.,,,,.T.,.F.)
	Local oFont08n := TFont():New("Arial",08,08,,.T.,,,,.T.,.F.)
	
	Default nCol1	:= 0
	Default nCol2	:= 0
	Default cTexto1 := ""
	Default cTexto2 := ""
	Default lQuebra := .T.
	Default nTipo   := 3
	Default oFontB  := oFont08
	Default lAlinDir:= .F.
	
	If nLin >= 3000
		CNRCabec(Nil,Nil,OemToAnsi(cTitRel)) //-- Impressao do Cabecalho da pagina
	EndIf
	
	If nTipo == 1  //-- Linha de Cabecalho
		oPrint:Box(nLin,nCol1,nLin+55,nCol2)
		oPrint:Box(nLin+5,nCol1+5,nLin+50,nCol2-5)
		oPrint:Say(nLin+10,((nCol2-nCol1)/2)-((Len(alltrim(cTexto1))/2)*20),cTexto1,oFont08n)
	ElseIf nTipo == 2 //-- Linha Unica Simples
		oPrint:Box(nLin,nCol1,nLin+45,nCol2)
		If lAlinDir //-- Alinhamento a Direita
			oPrint:Say(nLin+7,nCol2-5-17.241*Len(Alltrim(cTexto1)),AllTrim(cTexto1),oFontB)
		Else 
			oPrint:Say(nLin+7,nCol1+10,cTexto1,oFontB)
		EndIf
	ElseIf nTipo == 3 //-- Linha de Detalhe
		oPrint:Box(nLin,nCol1,nLin+90,nCol2)
		oPrint:Say(nLin+10,nCol1+10,cTexto1,oFontB)
		If lAlinDir //-- Alinhamento a Direita
			oPrint:Say(nLin+50,nCol2-5-17.241*Len(Alltrim(cTexto2)),AllTrim(cTexto2),oFontB)
		Else
			oPrint:Say(nLin+50,nCol1+10,cTexto2,oFontB)
		EndIf
	ElseIf nTipo == 4 //-- Somente Texto
		//oPrint:Say(nLin+7,nCol1,cTexto1,oFontB)
		If lAlinDir //-- Alinhamento a Direita
			oPrint:Say(nLin+7,nCol2-5-TamTexto(Alltrim(cTexto1)),AllTrim(cTexto1),oFontB)
		Else 
			oPrint:Say(nLin+7,nCol1+10,cTexto1,oFontB)
		EndIf
	ElseIf nTipo == 5 //-- Descrição Detalhada
		oPrint:Box(nLin,nCol1,nLin+135,nCol2)
		oPrint:Say(nLin+10,nCol1+10,cTexto1,oFontB)
		If lAlinDir //-- Alinhamento a Direita
			oPrint:Say(nLin+50,nCol2-5-17.241*Len(Alltrim(Substr(cTexto2,1,67))),AllTrim(Substr(cTexto2,1,67)),oFontB)
			oPrint:Say(nLin+90,nCol2-5-17.241*Len(Alltrim(Substr(cTexto2,68,133))),AllTrim(Substr(cTexto2,68,133)),oFontB)
		Else
			oPrint:Say(nLin+50,nCol1+10,Substr(cTexto2,1,67),oFontB)
			oPrint:Say(nLin+90,nCol1+10,Substr(cTexto2,68,133),oFontB)
		EndIf
	EndIf
	If lQuebra //-- Quebra Linha
		If nTipo == 1
			nLin += 50	//-- Cabecalho
		ElseIf nTipo == 2
			nLin += 50	//-- Unica Simples
		ElseIf nTipo == 3
			nLin += 95	//-- Detalhe
		ElseIf nTipo == 4 
			nLin += 50 //-- Somente Texto
		ElseIf nTipo == 5 
			nLin += 140 //-- Descrição estendida
		EndIf
	EndIf	

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ TamTexto | Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recupera o tamanho de uma string em pixels                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR010 / CNTR020                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function TamTexto(cTexto)
	
	Local nSize := 0
	Local nx
	Local aSize := {{17.241,2.692,15.241,13.692}} 
	For nx := 1 to Len(cTexto)
		If Substr(cTexto,nx,1)=="." .Or. Substr(cTexto,nx,1)=="," .or.  Substr(UPPER(cTexto),nx,1)=="I" 
			nSize += aSize[1,2]
		ElseIf Substr(cTexto,nx,1)$"abcdefghijklmnopqrstuvxyzw"
			nSize += aSize[1,4]
		ElseIf Substr(UPPER(cTexto),nx,1)$"ABCDEFGHIJKLMNOPQRSTUVXWYZ""
			nSize += aSize[1,3]
		Else                     
			nSize += aSize[1,1]
		EndIf
	Next
	
Return nSize


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ AjustaSX1³ Autor ³ Carlos Beltrão        ³ Data ³18/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta as perguntas do SX1                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CNTR010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1(cPerg)

	Local aAreaAnt := GetArea()
	Local aHelpPor := {}
	Local aHelpEng := {}
	Local aHelpSpa := {}
	
	dbSelectArea("SX1")
	dbSetOrder(1)
	If !SX1->(dbSeek(cPerg)) 
		//---------------------------------------MV_PAR01--------------------------------------------------
		aHelpPor := {"Numero inicial do Orçamento"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"01","Orçamento de:","","","mv_ch1","C",12,0,0,"G","","AF1","","S","MV_PAR01","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR02--------------------------------------------------
		aHelpPor := {"Numero final do Orçamento"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"02","Orçamento ate:","","","mv_ch2","C",12,0,0,"G","","AF1","","S","mv_par02","","","","ZZZZZZZZZZZZZZZ","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR05--------------------------------------------------
		aHelpPor := {"Da data do orçamento"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"03","Da data    :","","","mv_ch3","D",08,0,0,"G","","","","S","mv_par03","","","","01/01/06","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
		//---------------------------------------MV_PAR06--------------------------------------------------
		aHelpPor := {"Ate a data do orçamento"}
		aHelpEng := {""}
		aHelpSpa := {""}
		
		PutSX1(cPerg,"04","Ate a data  :","","","mv_ch4","D",08,0,0,"G","","","","S","mv_par04","","","","31/12/49","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
		
	EndIf	
	
	RestArea(aAreaAnt)
	
Return