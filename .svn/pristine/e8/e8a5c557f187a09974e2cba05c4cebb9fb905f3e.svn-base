#INCLUDE "MATR126.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR126  ³ Autor ³ Alexandre Inacio Lemes³ Data ³24/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mapa de rastreamento do contrato de Parceria.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR126(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Matr126( cAlias, nReg )

Local oReport

PRIVATE lAuto     := (nReg!=Nil) 
PRIVATE lExisteCC := SC3->(FieldPos("C3_CC")) > 0

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef(nReg)
	oReport:PrintDialog()
Else
	MATR126R3( cAlias, nReg )
EndIf
                                               
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ ReportDef³Autor  ³Alexandre Inacio Lemes ³Data  ³25/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mapa de rastreamento do contrato de Parceria.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nExp01: nReg = Registro posicionado do SC3 apartir Browse  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oExpO1: Objeto do relatorio                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef(nReg)

Local oReport 
Local oSection1 
Local oCell         
Local oBreak
Local cTitle := STR0001 // "Mapa de Contratos de Parceria Demonstrado "

#IFDEF TOP
	Local cAliasSC3 := GetNextAlias()
#ELSE
	Local cAliasSC3 := "SC3"
#ENDIF

AjustaSx1()
AjustaSx2()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                   					    ³
//³ mv_par01      // Emissao de				                                    ³
//³ mv_par02      // Emissao Ate              				                    ³
//³ mv_par03      // Contrato de                             				    ³
//³ mv_par04      // Contrato Ate                                				³
//³ mv_par05      // Fornecedor de                               				³
//³ mv_par06      // Fornecedor Ate                              				³
//³ mv_par07      // Loja de                                     				³
//³ mv_par08      // Loja Ate				                                    ³
//³ mv_par09      // Imprime Contratos todos/em Aberto/Atendidos/Parcialm.Atend.|
//³ mv_par10      // Centro de Custos de				                        |
//³ mv_par11      // Centro de Custos ate               				        |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR126",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MTR126",cTitle,If(lAuto,Nil,"MTR126"), {|oReport| ReportPrint(oReport,cAliasSC3,nReg)},STR0002+" "+STR0003) //"Emite um mapa para rastreamento dos contratos de parceria ate os titulos emitidos no SE2." 
oReport:SetLandscape() 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1:= TRSection():New(oReport,STR0017,{"SC3","SA2","SC7","SF1","SD1","SE2"},/*aOrdem*/) //"Emite um mapa para rastreamento dos contratos de parceria ate os titulos emitidos no SE2." 
oSection1:SetHeaderPage()

oSection1:SetNoFilter("SA2")
oSection1:SetNoFilter("SC7")
oSection1:SetNoFilter("SF1")
oSection1:SetNoFilter("SD1")
oSection1:SetNoFilter("SE2")

TRCell():New(oSection1,"C3_NUM"    ,"SC3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C3_EMISSAO","SC3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"A2_COD"    ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"A2_LOJA"   ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"A2_NREDUZ" ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C3_ITEM"   ,"SC3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C3_QUANT"  ,"SC3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C3_TOTAL"  ,"SC3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C7_NUM"    ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"NREDUZSC7" ,"SA2",STR0014,"@!",20,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C7_QUANT"  ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"C7_TOTAL"  ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D1_SERIE"  ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D1_DOC"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"E2_PREFIXO","SE2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"E2_NUM"    ,"SE2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"E2_PARCELA","SE2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"E2_TIPO"   ,"SE2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"E2_VENCTO" ,"SE2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"SaldoSE2"  ,"SE2",STR0015,"@E 999,999,999.99",14,/*lPixel*/,/*{|| code-block de impressao }*/)

oBreak := TRBreak():New(oSection1,oSection1:Cell("C3_NUM"),STR0013,.F.) // TOTAIS
TRFunction():New(oSection1:Cell("C7_QUANT"),NIL,"SUM",oBreak,,/*cPicture*/,/*uFormula*/,.F.,.F.) //"Total da Quantida da AE "
TRFunction():New(oSection1:Cell("C7_TOTAL"),NIL,"SUM",oBreak,,/*cPicture*/,/*uFormula*/,.F.,.F.) //"Total do Valor Total da AE"
TRFunction():New(oSection1:Cell("SaldoSE2"),NIL,"SUM",oBreak,,/*cPicture*/,/*uFormula*/,.F.,.F.) //"Total do Valor Total da AE"

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Alexandre Inacio Lemes ³Data  ³25/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Mapa de rastreamento do contrato de Parceria.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,cAliasSC3,nReg)

Local oSection1 := oReport:Section(1) 
Local nOrdem    := oReport:Section(1):GetOrder() 
Local nVlrAbat  := 0
Local nSaldoSE2 := 0
Local cPrefixo  := ""

#IFDEF TOP
	Local cQuery := ""
#ELSE
	Local cCondicao := ""
#ENDIF

dbSelectArea("SC3")
dbSetOrder(1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega os parametro caso o relatorio for usado a partir do browse.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAuto
	dbSelectArea("SC3")
	dbGoto(nReg)	
	mv_par01 := SC3->C3_EMISSAO
	mv_par02 := SC3->C3_EMISSAO
	mv_par03 := SC3->C3_NUM      
	mv_par04 := SC3->C3_NUM      
	mv_par05 := SC3->C3_FORNECE  
	mv_par06 := SC3->C3_FORNECE  
	mv_par07 := SC3->C3_LOJA     
	mv_par08 := SC3->C3_LOJA     
	mv_par09 := 1
	mv_par10 := SC3->C3_CC
	mv_par11 := SC3->C3_CC		
EndIf

If mv_par09 == 1
	oReport:SetTitle(oReport:Title() + OemToAnsi(STR0008)) //" - Todos"
ElseIf mv_par09 == 2
	oReport:SetTitle(oReport:Title() + OemToAnsi(STR0009)) //" - Pendentes"
ElseIf mv_par09 == 3
	oReport:SetTitle(oReport:Title() + OemToAnsi(STR0010)) //" - Atendidos"
ElseIf mv_par09 == 4
	oReport:SetTitle(oReport:Title() + OemToAnsi(STR0016)) //" - Parcialmente Atendidos"
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatório                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao SQL                        ³	
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lAuto
		MakeSqlExpr(oReport:uParam)
	Endif	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query do relatório da secao 1                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):BeginQuery()	

	cQuery :="%"
	If mv_par09 == 2 
		cQuery += "AND C3_QUJE = 0 "	
	ElseIf mv_par09 == 3
		cQuery += "AND C3_QUJE >= C3_QUANT "	
	ElseIf mv_par09 == 4
		cQuery += "AND C3_QUJE <> 0 AND C3_QUJE < C3_QUANT "	
	Endif
	cQuery +="%"	

	BeginSql Alias cAliasSC3

	SELECT SC3.*
	
	FROM %table:SC3% SC3
	
	WHERE C3_FILIAL = %xFilial:SC3% AND 
  		  C3_EMISSAO >= %Exp:Dtos(mv_par01)% AND 
		  C3_EMISSAO <= %Exp:Dtos(mv_par02)% AND 
		  C3_NUM >= %Exp:mv_par03% AND 
		  C3_NUM <= %Exp:mv_par04% AND 	 	  
	 	  C3_FORNECE >= %Exp:mv_par05% AND 
		  C3_FORNECE <= %Exp:mv_par06% AND 
		  C3_LOJA >= %Exp:mv_par07% AND 
		  C3_LOJA <= %Exp:mv_par08% AND 	 	  	
		  C3_CC >= %Exp:mv_par10% AND 
		  C3_CC <= %Exp:mv_par11% AND 	 	  	
		  SC3.%NotDel% 
		  %Exp:cQuery%
		  
	ORDER BY %Order:SC3% 
			
	EndSql 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Metodo EndQuery ( Classe TRSection )                                    ³
	//³                                                                        ³
	//³Prepara o relatório para executar o Embedded SQL.                       ³
	//³                                                                        ³
	//³ExpA1 : Array com os parametros do tipo Range                           ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
#ELSE
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao Advpl                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lAuto
		MakeAdvplExpr(oReport:uParam)
	Endif	

	cCondicao := 'C3_FILIAL == "'+xFilial("SC3")+'".And.' 
	cCondicao += 'Dtos(C3_EMISSAO) >= "'+Dtos(mv_par01)+'".And. Dtos(C3_EMISSAO) <="'+Dtos(mv_par02)+'".And.'
	cCondicao += 'C3_NUM >= "'+mv_par03+'".And.C3_NUM <="'+mv_par04+'".And.'
	cCondicao += 'C3_FORNECE >= "'+mv_par05+'".And.C3_FORNECE <="'+mv_par06+'".And.'
	cCondicao += 'C3_LOJA >= "'+mv_par05+'".And.C3_LOJA <="'+mv_par06+'".And.'
	cCondicao += 'C3_CC >= "'+mv_par10+'".And.C3_CC <="'+mv_par11+'" '

	If mv_par09 == 2
		cCondicao += '.And. C3_QUJE = 0 '
	ElseIf mv_par09 == 3
		cCondicao += '.And. C3_QUJE >= C3_QUANT '
	ElseIf mv_par09 == 4
		cCondicao += '.And. C3_QUJE<>0.And.C3_QUJE<C3_QUANT '
	Endif
	
	oReport:Section(1):SetFilter(cCondicao,IndexKey())
#ENDIF		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Novo Indice para pesquisa do SC7.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndSC7 := CriaTrab(,.F.)
IndRegua( "SC7", cIndSC7, "C7_FILIAL+C7_NUMSC+C7_ITEMSC" )
nOrderSC7 := RetIndex("SC7") + 1
#IFNDEF TOP
	SC7->( dbSetIndex( cIndSC7 + OrdBagExt() ) )
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Novo Indice para pesquisa do SD1.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndSD1 := CriaTrab(,.F.)
IndRegua( "SD1", cIndSD1, "D1_FILIAL+D1_PEDIDO+D1_ITEMPC" )
nOrderSD1 := RetIndex("SD1") + 1
#IFNDEF TOP
	SD1->( dbSetIndex( cIndSD1 + OrdBagExt() ) )
#ENDIF

oReport:SetMeter(SC3->(LastRec()))
oSection1:Init()

dbSelectArea(cAliasSC3)
While !oReport:Cancel() .And. !(cAliasSC3)->(Eof())
	
	If oReport:Cancel()
		Exit
	EndIf
	
	oReport:IncMeter()
	
	oSection1:Cell("C3_NUM"):Show()
	oSection1:Cell("C3_EMISSAO"):Show()
	oSection1:Cell("A2_COD"):Show()
	oSection1:Cell("A2_LOJA"):Show()
	oSection1:Cell("A2_NREDUZ"):Show()
	oSection1:Cell("C3_ITEM"):Show()
	oSection1:Cell("C3_QUANT"):Show()
	oSection1:Cell("C3_TOTAL"):Show()
	oSection1:Cell("C7_NUM"):Show()
	oSection1:Cell("NREDUZSC7"):Show()
	oSection1:Cell("C7_QUANT"):Show()
	oSection1:Cell("C7_TOTAL"):Show()
	oSection1:Cell("D1_SERIE"):Show()
	oSection1:Cell("D1_DOC"):Show()
	oSection1:Cell("E2_PREFIXO"):Show()
	oSection1:Cell("E2_NUM"):Show()
	oSection1:Cell("E2_PARCELA"):Show()
	oSection1:Cell("E2_TIPO"):Show()
	oSection1:Cell("E2_VENCTO"):Show()
	oSection1:Cell("SaldoSE2"):Show()
	
	oSection1:Cell("C7_QUANT"):SetValue(0)
	oSection1:Cell("C7_TOTAL"):SetValue(0)
	oSection1:Cell("SaldoSE2"):SetValue(0)
	
	SA2->(dbSetOrder(1))
	SA2->(dbSeek(xFilial("SA2")+(cAliasSC3)->C3_FORNECE+(cAliasSC3)->C3_LOJA))
	oSection1:Cell("NREDUZSC7"):SetValue(SA2->A2_NREDUZ)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Busca todas as autorizacoes de entrega SC7 a qual o item do SC3 esteja vinculado.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC7")
	SC7->(dbSetOrder(nOrderSC7))
	If SC7->(dbSeek(xFilial("SC7")+(cAliasSC3)->C3_NUM+(cAliasSC3)->C3_ITEM))
		
		While SC7->(!Eof()) .And. SC7->C7_NUMSC+SC7->C7_ITEMSC == (cAliasSC3)->C3_NUM+(cAliasSC3)->C3_ITEM
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Processar somente Autorizacoes de Entrega                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SC7->C7_TIPO == 2
				
				oSection1:Cell("C7_NUM"):Show()
				oSection1:Cell("NREDUZSC7"):Show()
				oSection1:Cell("C7_QUANT"):Show()
				oSection1:Cell("C7_TOTAL"):Show()
				
				SA2->(dbSetOrder(1))
				SA2->(dbSeek(xFilial("SA2")+SC7->C7_FORNECE+SC7->C7_LOJA))
				
				oSection1:Cell("NREDUZSC7"):SetValue(SA2->A2_NREDUZ)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Totaliza valor e quantidade das AES                                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSection1:Cell("C7_QUANT"):SetValue( SC7->C7_QUANT )
				oSection1:Cell("C7_TOTAL"):SetValue( SC7->C7_TOTAL )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca todas as notas de entrada SD1 a qual o item do SC7 AE esteja vinculado.    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SD1")
				SD1->(dbSetOrder(nOrderSD1))
				If SD1->(dbSeek(xFilial("SD1")+SC7->C7_NUM+SC7->C7_ITEM))
					While SD1->(!Eof()) .And. SD1->D1_PEDIDO+SD1->D1_ITEMPC == SC7->C7_NUM+SC7->C7_ITEM
						
						oSection1:Cell("D1_SERIE"):Show()
						oSection1:Cell("D1_DOC"):Show()
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Posiciona o SF1 para buscar todos os titulos SE2 vinculados. ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dbSelectArea("SF1")
						SF1->(dbSetOrder(1))
						SF1->(dbSeek(xFilial("SF1")+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA))
						
						cPrefixo := If(Empty(SF1->F1_PREFIXO),&(GetMV("MV_2DUPREF")),SF1->F1_PREFIXO)
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Busca todos os Titulos SE2 vinculados a NFE.  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dbSelectArea("SE2")
						SE2->(dbSetOrder(6))
						If SE2->(dbSeek(xFilial("SE2")+SF1->F1_FORNECE+SF1->F1_LOJA+cPrefixo+SF1->F1_DOC))
							
							While SE2->(!Eof()) .And. xFilial("SE2")+SF1->F1_FORNECE+SF1->F1_LOJA+cPrefixo+SF1->F1_DOC == ;
								SE2->E2_FILIAL+SE2->E2_FORNECE+SE2->E2_LOJA+SE2->E2_PREFIXO+SE2->E2_NUM
								
								oSection1:Cell("E2_PREFIXO"):Show()
								oSection1:Cell("E2_NUM"):Show()
								oSection1:Cell("E2_PARCELA"):Show()
								oSection1:Cell("E2_TIPO"):Show()
								oSection1:Cell("E2_VENCTO"):Show()
								
								If SE2->E2_TIPO == PadR(MVNOTAFIS,Len(SE2->E2_TIPO))
									
									nVlrAbat := FaAbatCP(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_FORNECE,SE2->E2_LOJA,SE2->E2_MOEDA,"S",dDataBase)
									nSaldoSE2:= Max(SE2->E2_SALDO - nVlrAbat,0)
									
									oSection1:Cell("SaldoSE2"):SetValue(nSaldoSE2)
									
									oSection1:PrintLine()
									
									oSection1:Cell("C3_NUM"):Hide()
									oSection1:Cell("C3_EMISSAO"):Hide()
									oSection1:Cell("A2_COD"):Hide()
									oSection1:Cell("A2_LOJA"):Hide()
									oSection1:Cell("A2_NREDUZ"):Hide()
									oSection1:Cell("C3_ITEM"):Hide()
									oSection1:Cell("C3_QUANT"):Hide()
									oSection1:Cell("C3_TOTAL"):Hide()
									oSection1:Cell("C7_NUM"):Hide()
									oSection1:Cell("NREDUZSC7"):Hide()
									oSection1:Cell("C7_QUANT"):Hide()
									oSection1:Cell("C7_TOTAL"):Hide()
									oSection1:Cell("D1_SERIE"):Hide()
									oSection1:Cell("D1_DOC"):Hide()
									
									oSection1:Cell("C7_QUANT"):SetValue( 0 )
									oSection1:Cell("C7_TOTAL"):SetValue( 0 )
									
								EndIf
								
								dbSelectArea('SE2')
								dbSkip()
								
							EndDo
							
						Else
							oSection1:PrintLine()
							
							oSection1:Cell("C3_NUM"):Hide()
							oSection1:Cell("C3_EMISSAO"):Hide()
							oSection1:Cell("A2_COD"):Hide()
							oSection1:Cell("A2_LOJA"):Hide()
							oSection1:Cell("A2_NREDUZ"):Hide()
							oSection1:Cell("C3_ITEM"):Hide()
							oSection1:Cell("C3_QUANT"):Hide()
							oSection1:Cell("C3_TOTAL"):Hide()
							oSection1:Cell("C7_NUM"):Hide()
							oSection1:Cell("NREDUZSC7"):Hide()
							oSection1:Cell("C7_QUANT"):Hide()
							oSection1:Cell("C7_TOTAL"):Hide()
							
							oSection1:Cell("C7_QUANT"):SetValue( 0 )
							oSection1:Cell("C7_TOTAL"):SetValue( 0 )
							
						EndIf
						
						dbSelectArea("SD1")
						dbSkip()
						
					EndDo
					
				Else

					oSection1:Cell("E2_PREFIXO"):Hide()
					oSection1:Cell("E2_NUM"):Hide()
					oSection1:Cell("E2_PARCELA"):Hide()
					oSection1:Cell("E2_TIPO"):Hide()
					oSection1:Cell("E2_VENCTO"):Hide()
					oSection1:Cell("SaldoSE2"):Hide()
			
					oSection1:PrintLine()
					
					oSection1:Cell("C3_NUM"):Hide()
					oSection1:Cell("C3_EMISSAO"):Hide()
					oSection1:Cell("A2_COD"):Hide()
					oSection1:Cell("A2_LOJA"):Hide()
					oSection1:Cell("A2_NREDUZ"):Hide()
					oSection1:Cell("C3_ITEM"):Hide()
					oSection1:Cell("C3_QUANT"):Hide()
					oSection1:Cell("C3_TOTAL"):Hide()
									
				EndIf
				
			EndIf
			
			dbSelectArea("SC7")
			dbSkip()
			
		EndDo
		
	Else
		
		SA2->(dbSetOrder(1))
		SA2->(dbSeek(xFilial("SA2")+(cAliasSC3)->C3_FORNECE+(cAliasSC3)->C3_LOJA))
		
		oSection1:Cell("C7_NUM"):Hide()
		oSection1:Cell("NREDUZSC7"):Hide()
		oSection1:Cell("C7_QUANT"):Hide()
		oSection1:Cell("C7_TOTAL"):Hide()
		oSection1:Cell("D1_SERIE"):Hide()
		oSection1:Cell("D1_DOC"):Hide()
		oSection1:Cell("E2_PREFIXO"):Hide()
		oSection1:Cell("E2_NUM"):Hide()
		oSection1:Cell("E2_PARCELA"):Hide()
		oSection1:Cell("E2_TIPO"):Hide()
		oSection1:Cell("E2_VENCTO"):Hide()
		oSection1:Cell("SaldoSE2"):Hide()
		
		oSection1:PrintLine()
		
	EndIf
	
	dbSelectArea(cAliasSC3)
	dbSkip()
	
EndDo

oSection1:Finish()

RetIndex("SC7")
dbSelectArea("SC7")
dbSetOrder(1)   

RetIndex("SD1")
dbSelectArea("SD1")
dbSetOrder(1)   

dbSelectArea("SE2")
dbSetOrder(1)   

If File(cIndSC7+OrdBagExt())
	Ferase(cIndSC7+OrdBagExt())
Endif

If File(cIndSD1+OrdBagExt())
	Ferase(cIndSD1+OrdBagExt())
Endif

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MATR126R3³ Autor ³Alexandre Inacio Lemes.³ Data ³06/12/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Mapa de rastreamento do contrato de Parceria                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATR126R3( cAlias, nReg )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Define Variaveis                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local Titulo  := STR0001 // "Mapa de Contratos de Parceria Demonstrado "
Local cDesc1  := STR0002 // "Emite um mapa para rastreamento dos contratos  "
Local cDesc2  := STR0003 // "de parceria ate os titulos emitidos no SE2." 
Local cDesc3  := ""
Local cString := "SC3"  
Local wnrel   := "MATR126"  
Local nomeprog:= "MATR126"  
Local lDic    := .F.                     
Local lComp   := .F. 
Local lFiltro := .T. 

Private Tamanho := "G" 
Private Limite  := 220 
Private cPerg   := "MTR126"  
Private aReturn := { STR0004, 1,STR0005, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"

Private lEnd    := .F.
Private m_pag   := 1  
Private nLastKey:= 0 
Private lAuto	:= (nReg!=Nil) 
Private lExisteCC:=SC3->(FieldPos("C3_CC")) > 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica as Perguntas Seleciondas.                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSx1()
Pergunte(cPerg,.F.)                   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                   					    ³
//³ mv_par01      // Emissao de				                                    ³
//³ mv_par02      // Emissao Ate              				                    ³
//³ mv_par03      // Contrato de                             				    ³
//³ mv_par04      // Contrato Ate                                				³
//³ mv_par05      // Fornecedor de                               				³
//³ mv_par06      // Fornecedor Ate                              				³
//³ mv_par07      // Loja de                                     				³
//³ mv_par08      // Loja Ate				                                    ³
//³ mv_par09      // Imprime Contratos todos/em Aberto/Atendidos/Parcialm.Atend.|
//³ mv_par10      // Centro de Custos de				                        |
//³ mv_par11      // Centro de Custos ate               				        |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Envia para a SetPrinter.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,wnrel,If(lAuto,Nil,cPerg),@titulo,cDesc1,cDesc2,cDesc3,lDic,,lComp,Tamanho,,!lAuto)

If ( nLastKey==27 )
	dbSelectArea(cString)
	dbSetOrder(1)
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If ( nLastKey==27 )
	dbSelectArea(cString)
	dbSetOrder(1)
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| R126Imp(@lEnd,wnRel,cString,nomeprog,Titulo,nReg)},Titulo)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ R126Imp  ³ Autor ³Alexandre Inacio Lemes ³ Data ³06/12/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao dos Contratos de Parceria.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R126Imp(lEnd,wnrel,cString,nomeprog,Titulo,nReg)

Local cAliasSC3 := "SC3"
Local cbtxt     := ""
Local cCabec1   := ""
Local cCabec2   := ""
Local cCondicao := ""
Local cQuebra   := ""
Local cPrefixo  := ""
Local cIndSC3   := ""
Local cIndSC7   := ""
Local cIndSD1   := ""
Local nOrderSC3 := 0
Local nOrderSC7 := 0
Local nOrderSD1 := 0
Local cbCont    := 0   
Local nVlrAbat  := 0
Local nSaldoSE2 := 0
Local nTSaldoSE2:= 0
Local nTotalAE  := 0
Local nTotalQAE := 0
Local li        := 100 
Local lQuery    := .F.
Local lRodape   := .F.
Local nOrdem    := aReturn[8]
#IFDEF TOP
	Local cQuery:= ""
	Local nX    := 1
#ENDIF	

Titulo := AllTrim(Titulo)+" - "+STR0007 // "Por Numero"
cCabec1:= STR0011  //"CONTRATO DE PARCERIA                                                                                 AUTORIZACAO DE ENTREGA                                   NOTAS       TITULOS                                           "
cCabec2:= STR0012  //"Numero Emissao    Fornecedor/Loja              Nome Reduzido        Item   Quantidade      Vlr.Total Numero Fornecedor             Quantidade    Valor Total  Ser.Numero  Prx Numero Parc Tipo  Vencimento             Saldo"
                   // 999999 99/99/9999 XXXXXXXXXXXXXXXXXXXX - XXXX  XXXXXXXXXXXXXXXXXXXX 9999 999999999.99 999,999,999.99 999999 XXXXXXXXXXXXXXXXXXXX 999999999.99 999,999,999.99  XXX XXXXXX  XXX XXXXXX  XXX  XXX  99/99/9999 999,999,999,999.99"                                                       
                   // 0        10        20        30        40        50        60        70        80        90       100       110       120       130       140       150       160       170       180       190       200       210       220
                   // 01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890	
					
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega os parametro caso o relatorio for usado a partir do browse.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAuto
	dbSelectArea("SC3")
	dbGoto(nReg)	
	mv_par01 := SC3->C3_EMISSAO
	mv_par02 := SC3->C3_EMISSAO
	mv_par03 := SC3->C3_NUM      
	mv_par04 := SC3->C3_NUM      
	mv_par05 := SC3->C3_FORNECE  
	mv_par06 := SC3->C3_FORNECE  
	mv_par07 := SC3->C3_LOJA     
	mv_par08 := SC3->C3_LOJA     
	mv_par09 := 1
	If lExisteCC
		mv_par10:=SC3->C3_CC
		mv_par11:=SC3->C3_CC		
	EndIf		
Endif

If mv_par09 == 1
	Titulo += STR0008 //" - Todos"
ElseIf mv_par09 == 2
	Titulo += STR0009 //" - Pendentes"
ElseIf mv_par09 == 3
	Titulo += STR0010 //" - Atendidos"
ElseIf mv_par09 == 4
	Titulo += STR0016 //" - Parcialmente Atendidos"
Endif

dbSelectArea("SC3")
dbSetOrder(1)
SetRegua(SC3->(LastRec()))

#IFDEF TOP
    If TcSrvType()<>"AS/400"
	    lQuery := .T.
		cAliasSC3 := "QRYSC3"
		aStruSC3  := SC3->(dbStruct())
		cQuery := "SELECT * "
		cQuery += "FROM "+RetSqlName("SC3")+" "
		cQuery += "WHERE C3_FILIAL='"+xFilial("SC3")+"' AND "
		cQuery += "C3_EMISSAO>='"+Dtos(mv_par01)+"' AND "
		cQuery += "C3_EMISSAO<='"+Dtos(mv_par02)+"' AND "	
		cQuery += "C3_NUM>='"+mv_par03+"' AND "
		cQuery += "C3_NUM<='"+mv_par04+"' AND "	
		cQuery += "C3_FORNECE>='"+mv_par05+"' AND "
		cQuery += "C3_FORNECE<='"+mv_par06+"' AND "	
		cQuery += "C3_LOJA>='"+mv_par07+"' AND "
		cQuery += "C3_LOJA<='"+mv_par08+"' AND "	
		If mv_par09 == 2 
			cQuery += "C3_QUJE = 0 AND "	
		ElseIf mv_par09 == 3
			cQuery += "C3_QUJE >= C3_QUANT AND "	
		ElseIf mv_par09 == 4
			cQuery += "C3_QUJE <> 0 AND C3_QUJE < C3_QUANT AND "	
		Endif

		If lExisteCC
			cQuery += "C3_CC>='"+mv_par10+"' AND "
			cQuery += "C3_CC<='"+mv_par11+"' AND "	
		EndIf		
		
		cQuery += "D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY "+SqlOrder(SC3->(IndexKey()))
	
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC3)
	
		For nX := 1 To len(aStruSC3)
			If aStruSC3[nX][2] <> "C" .And. FieldPos(aStruSC3[nX][1])<>0
				TcSetField(cAliasSC3,aStruSC3[nX][1],aStruSC3[nX][2],aStruSC3[nX][3],aStruSC3[nX][4])
			EndIf
		Next nX
		dbSelectArea(cAliasSC3)	
	Else
#ENDIF
	    cIndSC3   := CriaTrab(NIL,.F.)
	    cCondicao := 'C3_FILIAL=="'+xFilial("SC3")+'".And.'
	   	cCondicao += 'DTOS(C3_EMISSAO)>="'+DTOS(mv_par01)+'".And.DTOS(C3_EMISSAO)<="'+DTOS(mv_par02)+'" '
	   	cCondicao += '.And.C3_NUM>="'+mv_par03+'".And.C3_NUM<="'+mv_par04+'" '
	   	cCondicao += '.And.C3_FORNECE>="'+mv_par05+'".And.C3_FORNECE<="'+mv_par06+'" '
	   	cCondicao += '.And.C3_LOJA>="'+mv_par07+'".And.C3_LOJA<="'+mv_par08+'" '
		If mv_par09 == 2 
			cCondicao += '.And.C3_QUJE=0 '	
		ElseIf mv_par09 == 3
			cCondicao += '.And.C3_QUJE>=C3_QUANT '	
		ElseIf mv_par09 == 4
			cCondicao += '.And.C3_QUJE<>0.And.C3_QUJE<C3_QUANT '
		Endif
	   	
		If lExisteCC
			cCondicao += '.And. C3_CC >="'+mv_par10+'".And.'
			cCondicao += 'C3_CC<="'+mv_par11+'"'	
		EndIf		

	    IndRegua(cAliasSC3,cIndSC3,SC3->(IndexKey()),,cCondicao)
	    nOrderSC3 := RetIndex("SC3")
		#IFNDEF TOP
			dbSetIndex(cIndSC3+OrdBagExt())
		#ENDIF
		dbSelectArea("SC3")
	    dbSetOrder(nOrderSC3+1)
	
	    dbSelectArea(cAliasSC3)
	    ProcRegua(LastRec())
	    dbGoTop()
#IFDEF TOP
	Endif    
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Novo Indice para pesquisa do SC7.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndSC7 := CriaTrab(,.F.)
IndRegua( "SC7", cIndSC7, "C7_FILIAL+C7_NUMSC+C7_ITEMSC" )
nOrderSC7 := RetIndex("SC7") + 1
#IFNDEF TOP
	SC7->( dbSetIndex( cIndSC7 + OrdBagExt() ) )
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Novo Indice para pesquisa do SD1.                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndSD1 := CriaTrab(,.F.)
IndRegua( "SD1", cIndSD1, "D1_FILIAL+D1_PEDIDO+D1_ITEMPC" )
nOrderSD1 := RetIndex("SD1") + 1
#IFNDEF TOP
	SD1->( dbSetIndex( cIndSD1 + OrdBagExt() ) )
#ENDIF

dbSelectArea(cAliasSC3)
cQuebra := (cAliasSC3)->C3_NUM 
While !(cAliasSC3)->(Eof())
	
	IncRegua()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Validacao do filtro do usuario no SC3.                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(aReturn[7]) .And. !&(aReturn[7]) //Filtro do Usuario
		(cAliasSC3)->(dbSkip())
		cQuebra := (cAliasSC3)->C3_NUM
		Loop
	EndIf
	
	If lEnd
		@ Prow()+1,001 PSAY STR0006 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Limite para o salto de pagina e apresentacao da cabec.                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( li > 58 )
		li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,15)
		li++
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³`Posiciona o SA2 para a impressao do nome reduzido do fornecedor.       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SA2->(dbSetOrder(1))
	SA2->(dbSeek(xFilial("SA2")+(cAliasSC3)->C3_FORNECE+(cAliasSC3)->C3_LOJA))
	
	@Li, 000 PSAY (cAliasSC3)->C3_NUM
	@Li, 007 PSAY Dtoc((cAliasSC3)->C3_EMISSAO)
	@Li, 018 PSAY SA2->A2_COD+" - "+SA2->A2_LOJA
	@Li, 047 PSAY SA2->A2_NREDUZ
	@Li, 068 PSAY (cAliasSC3)->C3_ITEM
	@Li, 073 PSAY (cAliasSC3)->C3_QUANT Picture "@E 999999999.99"
	@Li, 086 PSAY (cAliasSC3)->C3_TOTAL	 Picture "@E 999,999,999.99"
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Busca todas as autorizacoes de entrega SC7 a qual o item do SC3 esteja vinculado.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC7")
	SC7->(dbSetOrder(nOrderSC7))
	SC7->(dbSeek(xFilial("SC7")+(cAliasSC3)->C3_NUM+(cAliasSC3)->C3_ITEM))
	While SC7->(!Eof()) .And. SC7->C7_NUMSC+SC7->C7_ITEMSC == (cAliasSC3)->C3_NUM+(cAliasSC3)->C3_ITEM
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processar somente Autorizacoes de Entrega                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SC7->C7_TIPO == 2
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Limite para o salto de pagina e apresentacao da cabec. ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( li > 58 )
				li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,15)
				li++
			Endif
			
			SA2->(dbSetOrder(1))
			SA2->(dbSeek(xFilial("SA2")+SC7->C7_FORNECE+SC7->C7_LOJA))
			
			@Li, 101 PSAY SC7->C7_NUM
			@Li, 108 PSAY SA2->A2_NREDUZ
			@Li, 129 PSAY SC7->C7_QUANT Picture "@E 999999999.99"
			@Li, 142 PSAY SC7->C7_TOTAL	 Picture "@E 999,999,999.99"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Totaliza valor e quantidade das AES                                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nTotalAE += SC7->C7_TOTAL
			nTotalQAE += SC7->C7_QUANT
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Busca todas as notas de entrada SD1 a qual o item do SC7 AE esteja vinculado.    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SD1")
			SD1->(dbSetOrder(nOrderSD1))
			SD1->(dbSeek(xFilial("SD1")+SC7->C7_NUM+SC7->C7_ITEM))
			While SD1->(!Eof()) .And. SD1->D1_PEDIDO+SD1->D1_ITEMPC == SC7->C7_NUM+SC7->C7_ITEM
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Limite para o salto de pagina e apresentacao da cabec. ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( li > 58 )
					li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,15)
					li++
				Endif
				
				@Li, 158 PSAY SD1->D1_SERIE
				@Li, 162 PSAY SD1->D1_DOC
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Posiciona o SF1 para buscar todos os titulos SE2 vinculados. ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SF1")
				SF1->(dbSetOrder(1))
				SF1->(dbSeek(xFilial("SF1")+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA))
				
				cPrefixo := If(Empty(SF1->F1_PREFIXO),&(GetMV("MV_2DUPREF")),SF1->F1_PREFIXO)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca todos os Titulos SE2 vinculados a NFE.  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SE2")
				SE2->(dbSetOrder(6))
				SE2->(dbSeek(xFilial("SE2")+SF1->F1_FORNECE+SF1->F1_LOJA+cPrefixo+SF1->F1_DOC))
				
				While SE2->(!Eof()) .And. xFilial("SE2")+SF1->F1_FORNECE+SF1->F1_LOJA+cPrefixo+SF1->F1_DOC == ;
					SE2->E2_FILIAL+SE2->E2_FORNECE+SE2->E2_LOJA+SE2->E2_PREFIXO+SE2->E2_NUM
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Limite para o salto de pagina e apresentacao da cabec. ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( li > 58 )
						li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,15)
			 					li++
					Endif
					
					If SE2->E2_TIPO == PadR(MVNOTAFIS,Len(SE2->E2_TIPO))
						
						nVlrAbat := FaAbatCP(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_FORNECE,SE2->E2_LOJA,SE2->E2_MOEDA,"S",dDataBase)
						nSaldoSE2:= Max(SE2->E2_SALDO - nVlrAbat,0)
						nTSaldoSE2+= Max(SE2->E2_SALDO - nVlrAbat,0)
						
						@Li, 172 PSAY SE2->E2_PREFIXO
						@Li, 176 PSAY SE2->E2_NUM
						@Li, 186 PSAY SE2->E2_PARCELA
						@Li, 188 PSAY SE2->E2_TIPO
						@Li, 193 PSAY SE2->E2_VENCTO        
						@Li, 203 PSAY nSaldoSE2 Picture "@E 99,999,999,999.99"
					EndIf
					
					dbSelectArea('SE2')
					dbSkip()
					
					If xFilial("SF1")+SF1->F1_FORNECE+SF1->F1_LOJA+cPrefixo+SF1->F1_DOC == SE2->E2_FILIAL+SE2->E2_FORNECE+SE2->E2_LOJA+SE2->E2_PREFIXO+SE2->E2_NUM .And. SE2->E2_TIPO == PadR(MVNOTAFIS,Len(SE2->E2_TIPO))
						li++
					EndIf
					
				EndDo
				
				dbSelectArea("SD1")
				dbSkip()
				
				If SD1->D1_PEDIDO+SD1->D1_ITEMPC == SC7->C7_NUM+SC7->C7_ITEM
					li++
				EndIf
				
			EndDo
			
		EndIf
		
		dbSelectArea("SC7")
		dbSkip()
		
		If ( SC7->C7_NUMSC+SC7->C7_ITEMSC == (cAliasSC3)->C3_NUM+(cAliasSC3)->C3_ITEM ) .And. SC7->C7_TIPO == 2
			li++
		EndIf
		
	EndDo
	
	li++
	
	dbSelectArea(cAliasSC3)
	dbSkip()
	
	If cQuebra <> (cAliasSC3)->C3_NUM
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Limite para o salto de pagina e apresentacao da cabec. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( li > 58 )
			li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,15)
			li++
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Totaliza valor e quantidade das AES                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		@Li, 000 PSAY OemToAnsi(STR0013)
		@Li, 129 PSAY nTotalQAE Picture "@E 999999999.99"
		@Li, 142 PSAY nTotalAE  Picture "@E 999,999,999.99"
		@Li, 203 PSAY nTSaldoSE2 Picture "@E 99,999,999,999.99"
		li++
		@ li,000 PSAY __PrtThinLine()
		li++
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Zera totalizadores de valor e quantidade das AES                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotalQAE :=0
		nTotalAE  :=0
		nTSaldoSE2:=0
		cQuebra := (cAliasSC3)->C3_NUM
	Endif
	
	cbCont++
	lRodape := .T.
	
EndDo

If lRodape
	Roda(cbcont,cbtxt,tamanho)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga os arquivos de trabalho, cancela os filtros e restabelece as ordens originais.|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	dbSelectArea(cAliasSC3)
	dbCloseArea()
Else
  	dbSelectArea("SC3")
	RetIndex("SC3")
	dbClearFilter()
	Ferase(cIndSC3+OrdBagExt())
EndIf

RetIndex("SC7")
dbSelectArea("SC7")
dbSetOrder(1)   

RetIndex("SD1")
dbSelectArea("SD1")
dbSetOrder(1)   

dbSelectArea("SE2")
dbSetOrder(1)   

If File(cIndSC7+OrdBagExt())
	Ferase(cIndSC7+OrdBagExt())
Endif

If File(cIndSD1+OrdBagExt())
	Ferase(cIndSD1+OrdBagExt())
Endif

Set Device To Screen
Set Printer To
If ( aReturn[5] = 1 )
	dbCommitAll()
	OurSpool(wnrel)
Endif
MS_FLUSH()

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AjustaSX1 ³ Autor ³Alexandre Inacio Lemes ³ Data ³06/12/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria as perguntas necesarias para o programa                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()
Local aArea	    := GetArea()
Local aAreaSX1	:= SX1->(GetArea())
Local aAreaSX3	:= SX3->(GetArea())
Local aSXB		:= {}
Local nTamSX1   := Len(SX1->X1_GRUPO)
Local nTamSXB   := Len(SXB->XB_ALIAS)
Local aEstrut	:= {"XB_ALIAS","XB_TIPO","XB_SEQ","XB_COLUNA","XB_DESCRI","XB_DESCSPA","XB_DESCENG","XB_CONTEM"}
Local i			:= 1
Local j			:= 1
Local aHelpPor  :={} 
Local aHelpEng  :={} 
Local aHelpSpa  :={} 

/*-----------------------MV_PAR01--------------------------*/
Aadd( aHelpPor, "Informe a Data Emissao Inicial para a   " )
Aadd( aHelpPor, "geracao do relatorio                    " )

Aadd( aHelpEng, "Enter initial issue date to generate    " )
Aadd( aHelpEng, "report.                                 " )

Aadd( aHelpSpa, "Informe la fecha de emision inicial para" ) 
Aadd( aHelpSpa, " generacion del informe.                " ) 

PutSx1( "MTR126","01","Emissao De ?","Emissao De","Emissao De","mv_ch1",;
"D",8,0,0,"G","","","","","mv_par01","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR02--------------------------*/
Aadd( aHelpPor, "Informe a Data Emissao Final para a     " )
Aadd( aHelpPor, "geracao do relatorio                    " )

Aadd( aHelpEng, "Enter final issue date to generate      " )
Aadd( aHelpEng, "report.                                 " )

Aadd( aHelpSpa, "Informe la fecha de emision final para  " ) 
Aadd( aHelpSpa, "la generacion del informe.              " ) 

PutSx1( "MTR126","02","Emissao Ate ?","Emissao Ate ?","Emissao Ate ?","mv_ch2",;
"D",8,0,0,"G","","","","","mv_par02","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR03--------------------------*/
Aadd( aHelpPor, "Informe o numero do Contrato Inicial.   " )
Aadd( aHelpEng, "Enter initial contract number.          " )
Aadd( aHelpSpa, "Informe el numero de contrato inicial.  " ) 

PutSx1( "MTR126","03","Contrato De  ?","Contrato De  ?","Contrato De  ?","mv_ch3",;
"C",6,0,0,"G","","SC3","","","mv_par03","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR04--------------------------*/
Aadd( aHelpPor, "Informe o numero do Contrato Final.     " )
Aadd( aHelpEng, "Enter final contract number.            " )
Aadd( aHelpSpa, "Informe el numero de contrato final.    " ) 

PutSx1( "MTR126","04","Contrato Ate ?","Contrato Ate ?","Contrato Ate ?","mv_ch4",;
"C",6,0,0,"G","","SC3","","","mv_par04","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR05--------------------------*/
Aadd( aHelpPor, "Informe o numero do Fornecedor Inicial. " )
Aadd( aHelpEng, "Enter initial supplier number.          " )
Aadd( aHelpSpa, "Informe el numero del proveedor inicial." ) 

PutSx1( "MTR126","05","Fornecedor De  ?","Fornecedor De  ?","Fornecedor De  ?","mv_ch5",;
"C",6,0,0,"G","","SA2","","","mv_par05","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR06--------------------------*/
Aadd( aHelpPor, "Informe o numero do Fornecedor Final.   " )
Aadd( aHelpEng, "Enter final supplier number.            " )
Aadd( aHelpSpa, "Informe el numero de proveedor final.   " ) 

PutSx1( "MTR126","06","Fornecedor Ate ?","Fornecedor Ate ?","Fornecedor Ate ?","mv_ch6",;
"C",6,0,0,"G","","SA2","","","mv_par06","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR07--------------------------*/
Aadd( aHelpPor, "Informe a Loja Inicial do Fornecedor.   " )
Aadd( aHelpEng, "Enter supplier initial store.           " )
Aadd( aHelpSpa, "Informe la tienda inicial del proveedor." ) 

PutSx1( "MTR126","07","Loja De  ?","Loja De  ?","Loja De  ?","mv_ch7",;
"C",2,0,0,"G","","","","","mv_par07","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR08--------------------------*/
Aadd( aHelpPor, "Informe a Loja Final do Fornecedor.     " )
Aadd( aHelpEng, "Enter supplier final store.             " )
Aadd( aHelpSpa, "Informe la tienda final del proveedor.  " ) 

PutSx1( "MTR126","08","Loja Ate ?","Loja Ate ?","Loja Ate ?","mv_ch8",;
"C",2,0,0,"G","","","","","mv_par08","","","","","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)
aHelpPor :={} 
aHelpEng :={} 
aHelpSpa :={} 
/*-----------------------MV_PAR09--------------------------*/
Aadd( aHelpPor, "Informe o tipo de Contrato p/ impressao " )
Aadd( aHelpEng, "Enter contract type to print.           " )
Aadd( aHelpSpa, "Informe el tipo de contrato p/impresion." ) 

PutSx1( "MTR126","09","Imprime Contratos ?","Imprime Contratos ?","Imprime Contratos ?","mv_ch9",;
"N",1,0,1,"C","","","","","mv_par09","Todos","Todos","Todos","",;
"Em Aberto","Em Aberto","Em Aberto","Atendidos","Atendidos","Atendidos","Parcialm.Atend.","Parcialm.Atend.","Part. Attend.","","","",;
aHelpPor,aHelpEng,aHelpSpa)

dbSelectArea("SX1")
dbSetOrder(1)
If dbSeek(PADR("MTR126",nTamSX1)+"09") .And. Empty(X1_DEF04)
	RecLock("SX1",.F.)
	Replace X1_DEF04 	with "Parcialm.Atend."
	Replace X1_DEFSPA4  with "Parcialm.Atend."
	Replace X1_DEFENG4 	with "Part. Attend.  "
	MsUnLock()
	PutSX1Help("P.MTR12609.",aHelpPor,aHelpEng,aHelpSpa)
EndIf

If lExisteCC
	/*-----------------------MV_PAR10--------------------------*/
	Aadd( aHelpPor, "Informe o centro de custos inicial.     " )
	Aadd( aHelpEng, "Enter initial cost center.              " )
	Aadd( aHelpSpa, "Informe el centro de costos inicial.    " ) 
	PutSx1("MTR126","10","Centro de Custos de","¿De Centro de Costo?","From Cost Center?","mv_cha",;
	"C", 9,0,0,"G","","","","","mv_par10","","","","","","","","","","","","","",;
	aHelpPor,aHelpEng,aHelpSpa)
	aHelpPor :={} 
	aHelpEng :={} 
	aHelpSpa :={} 
	/*-----------------------MV_PAR11--------------------------*/
	Aadd( aHelpPor, "Informe o centro de custos final.       " )
	Aadd( aHelpEng, "Enter final cost center.                " )
	Aadd( aHelpSpa, "Informe el centro de costos final.      " ) 
	PutSx1("MTR126","11","Centro de Custos ate","¿A Centro de Costo?","To Cost Center?","mv_chb",;
	"C",9,0,0,"G","","","","","mv_par11","","","","","","","","","","","","","",;
	aHelpPor,aHelpEng,aHelpSpa)
EndIf

/*-----------------------Consulta SXB--------------------------*/
Aadd(aSXB,{"SC7CP","1","01","DB"	,"Contrato de Parceria"	,"Contrato de sociedad"	,"Partnership Contract"	,"SC7"       })
Aadd(aSXB,{"SC7CP","2","01","01"	,"Numero"				,"Numero"				,"Number"				,""          })
Aadd(aSXB,{"SC7CP","4","01","01"	,"Numero"				,"Numero"				,"Number"				,"C7_NUM"    })
Aadd(aSXB,{"SC7CP","4","01","02"	,"Item"					,"Item"					,"Item"					,"C7_ITEM"   })
Aadd(aSXB,{"SC7CP","4","01","03"	,"Fornecedor"			,"Proveedor"			,"Supplier"				,"C7_FORNECE"})
Aadd(aSXB,{"SC7CP","4","01","04"	,"Produto"				,"Producto"				,"Product"				,"C7_PRODUTO"})
Aadd(aSXB,{"SC7CP","4","01","05"	,"Saldo"				,"Saldo"				,"Balance"				,"IF(Empty(C7_RESIDUO),C7_QUANT-C7_QUJE,0)"})
Aadd(aSXB,{"SC7CP","4","01","06"	,"Entrega"				,"Entrega"				,"Delivery"				,"C7_DATPRF" })
Aadd(aSXB,{"SC7CP","5","01",""		,""						,""						,""						,"SC7->C7_NUM"})
Aadd(aSXB,{"SC7CP","6","01",""		,""						,""						,""						,"SC7->C7_TIPO==2"})

dbSelectArea("SXB")
dbSetOrder(1)
For i := 1 To Len(aSXB)
	If !Empty(aSXB[i][1])
		If !dbSeek(PADR(aSXB[i,1],nTamSXB)+aSXB[i,2]+aSXB[i,3]+aSXB[i,4])
			lSXB := .T.
			RecLock("SXB",.T.)
			For j:=1 To Len(aSXB[i])
				If !Empty(FieldName(FieldPos(aEstrut[j])))
					FieldPut(FieldPos(aEstrut[j]),aSXB[i,j])
				EndIf
			Next j
			dbCommit()
			MsUnLock()
		EndIf
	EndIf
Next i

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajustando o dicionario SX1        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX1")
dbSetOrder(1)
If dbSeek(PADR('MTR441',nTamSX1)+"10") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR441',nTamSX1)+"11") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf                                     
If dbSeek(PADR('MTR125',nTamSX1)+"01") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR125',nTamSX1)+"02") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR126',nTamSX1)+"03") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR126',nTamSX1)+"04") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR952',nTamSX1)+"03") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR952',nTamSX1)+"04") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SC7CP'
	MsUnLock()
EndIf
RestArea(aAreaSX1)
RestArea(aAreaSX3)
RestArea(aArea)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AjustaSX2 ³ Autor ³Andrews Fernandes Egas ³ Data ³06/12/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ajustar nome da rotina 					                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX2()
Local aArea := GetArea() 

dbSelectArea("SX2")
dbSetOrder(1)
If dbSeek("SC7") .And. X2_NOME <> "Pedido de Com / Contrato Parc"  
	RecLock("SX2",.F.)
	Replace X2_NOME	with "Pedido de Com / Contrato Parc"
	MsUnLock()
	RestArea(aArea)
EndIf

Return