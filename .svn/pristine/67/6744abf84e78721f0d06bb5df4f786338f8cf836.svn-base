#INCLUDE "MATR131.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR131  ³ Autor ³Alexandre Inacio Lemes ³ Data ³14/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Informe de Pedidos de Compra/Remitos/Notas Fiscais         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR131(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATR131()

Local oReport

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta perguntas no SX1 									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef()
	oReport:PrintDialog()
Else 
	MATR131R3()
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ ReportDef³Autor  ³Alexandre Inacio Lemes ³Data  ³14/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Informe de Pedidos de Compra/Remitos/Notas Fiscais         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oExpO1: Objeto do relatorio                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local aOrdem    := {STR0003,STR0004,STR0005} //"Fecha emision" , "Proveedor" , "Producto"
Local cTitle    := ""
Local oReport
Local oSection1
Local cRetTitle := Rtrim(RetTitle("D2_REMITO"))	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros ³
//³ mv_par01       // ¨Basado por ?      ³
//³ mv_par02       // ¨De proveedor?     ³
//³ mv_par03       // ¨Hasta proveedor?  ³
//³ mv_par04       // ¨De producto ?     ³
//³ mv_par05       // ¨Hasta producto?   ³
//³ mv_par06       // ¨De grupo ?        ³
//³ mv_par07       // ¨Hasta grupo ?     ³
//³ mv_par08       // ¨De Fecha Emision? ³
//³ mv_par09       // ¨Hasta Fech.Emis.? ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR131",.F.)

If mv_par01 = 1                                                        //OC/RM/FT
	cTitle := STR0072 + cRetTitle + STR0073 + "/" + STR0074            //"Relacion de Orden de Compras/Remitos/Facturas"
ElseIf mv_par01 = 2                                                    //RM/OC/FT
	cTitle := STR0075 + cRetTitle + STR0073 + STR0076 + "/" + STR0074  //"Relacion de Remitos/Orden de Compras/Facturas"
ElseIf mv_par01 = 3                                                    //FT/RM/OC
	cTitle := STR0075 + STR0074 + "/" + cRetTitle + STR0073 + STR0076  //"Relacion de Facturas/Remitos/Orden de Compras"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:= TReport():New("MTR131",cTitle,"MTR131", {|oReport| ReportPrint(oReport,aOrdem)},STR0001 + " " + cRetTitle + " " + STR0073 + STR0071 )//"Imprime relaci¢n de las ordenes de compra"+cRetTitle+"Remito(s) y Facturas"
oReport:SetLandscape()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSect1Ped:= TRSection():New(oReport,STR0085,{"SC7","SB1","SA2"},aOrdem)
TRCell():New(oSect1Ped,"C7_EMISSAO","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Ped,"C7_FORNECE","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Ped,"C7_LOJA"   ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Ped,"A2_NOME"   ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Ped,"C7_PRODUTO","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Ped,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSect2Ped:= TRSection():New(oSect1Ped,STR0086,{"SC7","SD1","SF1","SB1","SA2"})
oSect2Ped:SetTotalInLine(.F.)
oSect2Ped:SetHeaderPage()
TRCell():New(oSect2Ped,"C7_EMISSAO","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_FORNECE","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_LOJA"   ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_PRODUTO","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_NUM"    ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_ITEM"   ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_PRECO"  ,"SC7",/*Titulo*/,PesqPict("SC7","C7_PRECO",16),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_QUANT"  ,"SC7",/*Titulo*/,PesqPictQt( "C7_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"C7_DATPRF" ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"REMDOC"    ,"   ",STR0079,/*Picture*/,TamSX3('D1_DOC')[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"REMITEM"   ,"   ",STR0080,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"REMQUANT"  ,"   ",STR0081,PesqPictQt( "C7_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"REMEMISSAO","   ",STR0082,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"D1_DOC"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"D1_SERIE"  ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"D1_ITEM"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"D1_VUNIT"  ,"SD1",/*Titulo*/,PesqPict("SD1","D1_VUNIT",16),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"D1_QUANT"  ,"SD1",/*Titulo*/,PesqPictQt( "D1_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Ped,"DIVPRECO"  ,"   ",STR0083,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIf((cAliasQry)->C7_PRECO  <> SD1->D1_VUNIT  , STR0069 , STR0070 )})
TRCell():New(oSect2Ped,"DIVDATE"   ,"   ",STR0084,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIf((cAliasQry)->C7_DATPRF <> SD1->D1_EMISSAO, STR0069 , STR0070 )})

oSect1Rem:= TRSection():New(oReport,STR0087,{"SD1","SB1","SA2"},aOrdem)
TRCell():New(oSect1Rem,"D1_EMISSAO","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Rem,"D1_FORNECE","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Rem,"D1_LOJA"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Rem,"A2_NOME"   ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Rem,"D1_COD"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Rem,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSect2Rem:= TRSection():New(oSect1Rem,STR0088,{"SD1","SF1","SC7","SB1","SA2"})
oSect2Rem:SetTotalInLine(.F.)
oSect2Rem:SetHeaderPage()
TRCell():New(oSect2Rem,"D1_EMISSAO","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"D1_FORNECE","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"D1_LOJA"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"D1_COD"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"D1_DOC"    ,"SD1",STR0079,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"D1_ITEM"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"D1_QUANT"  ,"SD1",/*Titulo*/,PesqPictQt("D1_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"DT_EMISSAO","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"C7_NUM"    ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"C7_ITEM"   ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"C7_PRECO"  ,"SC7",/*Titulo*/,PesqPict("SC7","C7_PRECO",16),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"C7_QUANT"  ,"SC7",/*Titulo*/,PesqPictQt("C7_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"C7_DATPRF" ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"NFDOC"     ,"   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"NFITEM"    ,"   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"NFVUNIT"   ,"   ",/*Titulo*/,PesqPict("SD1","D1_VUNIT",16),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"NFQUANT"   ,"   ",/*Titulo*/,PesqPictQt("D1_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Rem,"DIVPRECO"  ,"   ",STR0083,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIf( lSC7 , IIf( SC7->C7_PRECO  <> SD1->D1_VUNIT   , STR0069 , STR0070 ) , "" )})
TRCell():New(oSect2Rem,"DIVDATE"   ,"   ",STR0084,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIf( lSC7 , IIf( SC7->C7_DATPRF <> SD1->D1_EMISSAO , STR0069 , STR0070 ) , "" )})

oSect2Rem:Cell("NFDOC"):GetFieldInfo("D1_DOC")
oSect2Rem:Cell("NFITEM"):GetFieldInfo("D1_ITEM")
oSect2Rem:Cell("NFVUNIT"):GetFieldInfo("D1_VUNIT")
oSect2Rem:Cell("NFQUANT"):GetFieldInfo("D1_QUANT")
oSect2Rem:Cell("DT_EMISSAO"):GetFieldInfo("D1_EMISSAO")

oSect1Nfe:= TRSection():New(oReport,STR0089,{"SD1","SB1","SA2"},aOrdem)
TRCell():New(oSect1Nfe,"D1_EMISSAO","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Nfe,"D1_FORNECE","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Nfe,"D1_LOJA"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Nfe,"A2_NOME"   ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Nfe,"D1_COD"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect1Nfe,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSect2Nfe:= TRSection():New(oSect1Nfe,STR0090,{"SD1","SF1","SC7","SB1","SA2"})
oSect2Nfe:SetTotalInLine(.F.)
oSect2Nfe:SetHeaderPage()
TRCell():New(oSect2Nfe,"D1_EMISSAO","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_FORNECE","SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_LOJA"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_COD"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_DOC"    ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_SERIE"  ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_ITEM"   ,"SD1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_VUNIT"  ,"SD1",/*Titulo*/,PesqPict("SD1","D1_VUNIT",16),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"D1_QUANT"  ,"SD1",/*Titulo*/,PesqPictQt("D1_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"REMDOC"    ,"   ",STR0079,/*Picture*/,TamSX3('D1_DOC')[1],/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"REMITEM"   ,"   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"REMQUANT"  ,"   ",/*Titulo*/,PesqPictQt("D1_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"REMEMISSAO","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"C7_NUM"    ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"C7_ITEM"   ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"C7_PRECO"  ,"SC7",/*Titulo*/,PesqPict("SC7","C7_PRECO",16),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"C7_QUANT"  ,"SC7",/*Titulo*/,PesqPictQt("C7_QUANT",13),/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"C7_DATPRF" ,"SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSect2Nfe,"DIVPRECO"  ,"   ",STR0083,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| Iif( lSC7 , Iif( SC7->C7_PRECO  <> SD1->D1_VUNIT   , STR0069 , STR0070 ) , "" ) })
TRCell():New(oSect2Nfe,"DIVDATE"   ,"   ",STR0084,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| Iif( lSC7 , Iif( SC7->C7_DATPRF <> SD1->D1_EMISSAO , STR0069 , STR0070 ) , "" ) })

oSect2Nfe:Cell("REMITEM"):GetFieldInfo("D1_ITEM")
oSect2Nfe:Cell("REMQUANT"):GetFieldInfo("D1_QUANT")
oSect2Nfe:Cell("REMEMISSAO"):GetFieldInfo("D1_EMISSAO")


oSect1Ped:SetNoFilter("SB1")
oSect1Ped:SetNoFilter("SA2")
oSect2Ped:SetNoFilter("SF1")

oSect1Rem:SetNoFilter("SB1")
oSect1Rem:SetNoFilter("SA2")
oSect2Rem:SetNoFilter("SF1")

oSect1Nfe:SetNoFilter("SB1")
oSect1Nfe:SetNoFilter("SA2")
oSect2Nfe:SetNoFilter("SF1")

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Alexandre Inacio Lemes ³Data  ³14/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Informe de Pedidos de Compra/Remitos/Notas Fiscais         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,aOrdem)

Local oSection1
Local oSection2
Local aArea      := {}
Local cCondicao  := ""
Local cIndexKey  := ""
Local cOrder     := ""
Local cOrdem     := ""
Local cInitOrdem := ""
Local cRemito    := ""
Local cFatuRem   := ""
Local cSeriRem   := ""
Local cFornRem   := ""
Local cLojaRem   := ""
Local cCodRem    := ""
Local cItemRem   := ""
Local DtEmisRem  := ""
Local cWhere     := ""
Local nQuantRem  := 0
Local nRecnoSD1  := 0
Local nPrinted   := 0
Local nTPrinted  := 0
Local nOrdem     := oReport:Section(1):GetOrder()
Local lQuery     := .F.
Local lPrintLine := .F.
Local lPassPrint := .F. //Variavel para verificar se ja passou pelo print 1, se sim, não passar pelo print 2

Private cAliasQry:= ""
Private lSC7     := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declara as Sections 1 e 2 conforme o parametro selecionado no ³
//³mv_par01 e desabilata as sessoes que nao serao utilizadas     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par01 == 1
	oSection1  := oReport:Section(1)
	oSection2  := oReport:Section(1):Section(1)
	oReport:Section(2):Disable()
	oReport:Section(2):Section(1):Disable()
	oReport:Section(3):Disable()
	oReport:Section(3):Section(1):Disable()
ElseIf mv_par01 == 2
	oSection1  := oReport:Section(2)
	oSection2  := oReport:Section(2):Section(1)
	oReport:Section(1):Disable()
	oReport:Section(1):Section(1):Disable()
	oReport:Section(3):Disable()
	oReport:Section(3):Section(1):Disable()
ElseIf mv_par01 == 3
	oSection1  := oReport:Section(3)
	oSection2  := oReport:Section(3):Section(1)
	oReport:Section(1):Disable()
	oReport:Section(1):Section(1):Disable()
	oReport:Section(2):Disable()
	oReport:Section(2):Section(1):Disable()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desabilita as celulas conforme a Ordem seleciona e o mv_par01 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par01 == 1
	If nOrdem == 1
		oSection1:Cell("C7_FORNECE"):Disable()
		oSection1:Cell("C7_LOJA"):Disable()
		oSection1:Cell("A2_NOME"):Disable()
		oSection1:Cell("C7_PRODUTO"):Disable()
		oSection1:Cell("B1_DESC"):Disable()
		oSection2:Cell("C7_EMISSAO"):Disable()
	ElseIf nOrdem == 2
		oSection1:Cell("C7_EMISSAO"):Disable()
		oSection1:Cell("C7_PRODUTO"):Disable()
		oSection1:Cell("B1_DESC"):Disable()
		oSection2:Cell("C7_FORNECE"):Disable()
		oSection2:Cell("C7_LOJA"):Disable()
	ElseIf nOrdem == 3
		oSection1:Cell("C7_EMISSAO"):Disable()
		oSection1:Cell("C7_FORNECE"):Disable()
		oSection1:Cell("C7_LOJA"):Disable()
		oSection1:Cell("A2_NOME"):Disable()
		oSection2:Cell("C7_PRODUTO"):Disable()
	EndIf
Else
	If nOrdem == 1
		oSection1:Cell("D1_FORNECE"):Disable()
		oSection1:Cell("D1_LOJA"):Disable()
		oSection1:Cell("A2_NOME"):Disable()
		oSection1:Cell("D1_COD"):Disable()
		oSection1:Cell("B1_DESC"):Disable()		
		oSection2:Cell("D1_EMISSAO"):Disable()
	ElseIf nOrdem == 2
		oSection1:Cell("D1_EMISSAO"):Disable()
		oSection1:Cell("D1_COD"):Disable()
		oSection1:Cell("B1_DESC"):Disable()		
		oSection2:Cell("D1_FORNECE"):Disable()
		oSection2:Cell("D1_LOJA"):Disable()
	ElseIf nOrdem == 3
		oSection1:Cell("D1_EMISSAO"):Disable()
		oSection1:Cell("D1_FORNECE"):Disable()
		oSection1:Cell("D1_LOJA"):Disable()
		oSection1:Cell("A2_NOME"):Disable()
		oSection2:Cell("D1_COD"):Disable()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona a ordem escolhida ao titulo do relatorio          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetTitle(oReport:Title() + " ("+AllTrim(aOrdem[nOrdem])+") ")

#IFDEF TOP

	lQuery    := .T.	

	cAliasQry := GetNextAlias()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query da Section1 em Pedido de compras conforme o mv_par01 =1 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If mv_par01 == 1

		dbSelectArea("SC7")

		MakeSqlExpr(oReport:uParam)

		oSection1:BeginQuery()	

		If nOrdem == 1
			cOrder := "% C7_FILIAL,C7_EMISSAO,C7_FORNECE,C7_PRODUTO,C7_NUM %"
			cOrdem := "C7_FILIAL+DTOS(C7_EMISSAO)"

		ElseIf nOrdem == 2
			cOrder := "% C7_FILIAL,C7_FORNECE,C7_EMISSAO,C7_PRODUTO,C7_NUM %"
			cOrdem := "C7_FILIAL+C7_FORNECE+C7_LOJA"

		ElseIf nOrdem == 3
			cOrder := "% C7_FILIAL,C7_PRODUTO,C7_EMISSAO,C7_FORNECE,C7_NUM %"
			cOrdem := "C7_FILIAL+C7_PRODUTO"
		EndIf

		BeginSql Alias cAliasQry

		SELECT SC7.*

		FROM %table:SC7% SC7

		WHERE C7_FILIAL  = %xFilial:SC7% AND
		C7_FORNECE  >= %Exp:mv_par02% AND
		C7_FORNECE  <= %Exp:mv_par03% AND
		C7_PRODUTO  >= %Exp:mv_par04% AND
		C7_PRODUTO  <= %Exp:mv_par05% AND
		C7_EMISSAO  >= %Exp:Dtos(mv_par08)% AND
		C7_EMISSAO  <= %Exp:Dtos(mv_par09)% AND
		SC7.%NotDel%

		ORDER BY %Exp:cOrder%

		EndSql

		oSection1:EndQuery(/*Array com os parametros do tipo Range*/)

        oSection2:SetParentQuery()
         
	Else

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Query da Section1 no SD1 conforme o mv_par01 == 2-Remito 3-NFE³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		dbSelectArea("SD1")

		MakeSqlExpr(oReport:uParam)

		oSection1:BeginQuery()	

		cWhere :="%"
		If mv_par01 == 2
			cWhere += "AND ("+IsRemito(3,'D1_TIPODOC')+ ")"
		Else
			cWhere += "AND NOT ("+IsRemito(3,'D1_TIPODOC')+ ")"
		EndIf
		cWhere +="%"	

		If nOrdem == 1
			cOrder := "% D1_FILIAL,D1_EMISSAO,D1_FORNECE,D1_COD,D1_DOC %"
			cOrdem := "D1_FILIAL+DTOS(D1_EMISSAO)"
		ElseIf nOrdem == 2
			cOrder := "% D1_FILIAL,D1_FORNECE,D1_EMISSAO,D1_COD,D1_DOC %"
			cOrdem := "D1_FILIAL+D1_FORNECE+D1_LOJA"
		ElseIf nOrdem == 3
			cOrder := "% D1_FILIAL,D1_COD,D1_EMISSAO,D1_FORNECE,D1_DOC %"
			cOrdem := "D1_FILIAL+D1_COD"
		EndIf

		BeginSql Alias cAliasQry

		SELECT D1_FILIAL,D1_DOC,D1_SERIE,D1_FORNECE,D1_LOJA,D1_COD,D1_ITEM,D1_EMISSAO,D1_DTDIGIT,D1_QUANT,D1_VUNIT,
		D1_TOTAL,D1_TIPODOC,D1_SERIREM,D1_REMITO,D1_ITEMREM,D1_PEDIDO,D1_ITEMPC,D1_TIPO,D1_GRUPO

		FROM %table:SD1% SD1

		WHERE D1_FILIAL  = %xFilial:SD1% AND
		D1_FORNECE  >= %Exp:mv_par02% AND
		D1_FORNECE  <= %Exp:mv_par03% AND
		D1_COD      >= %Exp:mv_par04% AND
		D1_COD      <= %Exp:mv_par05% AND
		D1_EMISSAO  >= %Exp:Dtos(mv_par08)% AND
		D1_EMISSAO  <= %Exp:Dtos(mv_par09)% AND
		SD1.%NotDel%
		%Exp:cWhere%

		ORDER BY %Exp:cOrder%

		EndSql

		oSection1:EndQuery(/*Array com os parametros do tipo Range*/)

	EndIf

#ELSE

	MakeAdvplExpr(oReport:uParam)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query da Section1 em Pedido de compras conforme o mv_par01 =1 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If mv_par01  == 1

		dbSelectArea("SC7")
		cAliasQry := "SC7"

		If nOrdem == 1
			cIndexKey := "C7_FILIAL+DTOS(C7_EMISSAO)+C7_FORNECE+C7_PRODUTO+C7_NUM"
			cOrdem    := "C7_FILIAL+Dtos(C7_EMISSAO)"
		ElseIf nOrdem == 2
			cIndexKey := "C7_FILIAL+C7_FORNECE+DTOS(C7_EMISSAO)+C7_PRODUTO+C7_NUM"
			cOrdem    := "C7_FILIAL+C7_FORNECE+C7_LOJA"
		Else
			cIndexKey := "C7_FILIAL+C7_PRODUTO+DTOS(C7_EMISSAO)+C7_FORNECE+C7_NUM"
			cOrdem    := "C7_FILIAL+C7_PRODUTO"
		EndIf

		cCondicao := "C7_FORNECE >='"  + mv_par02       + "'.And.C7_FORNECE <='"      + mv_par03 + "'" + ;
			".And.C7_PRODUTO >='"      + mv_par04       + "'.And.C7_PRODUTO <='"      + mv_par05 + "'" + ;
			".And.Dtos(C7_EMISSAO)>='" + Dtos(mv_par08) + "'.And.Dtos(C7_EMISSAO)<='" + Dtos(mv_par09) + "'"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query da Section1 no SD1 conforme o mv_par01 == 2-Remito 3-NFE³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ElseIf mv_par01  == 2

		dbSelectArea("SD1")
		cAliasQry := "SD1"

		If nOrdem == 1
			cIndexKey := "D1_FILIAL+DTOS(D1_EMISSAO)+D1_FORNECE+D1_COD+D1_DOC"
			cOrdem    := "D1_FILIAL+Dtos(D1_EMISSAO)"
		ElseIf nOrdem == 2
			cIndexKey := "D1_FILIAL+D1_FORNECE+DTOS(D1_EMISSAO)+D1_COD+D1_DOC"
			cOrdem    := "D1_FILIAL+D1_FORNECE+SD1->D1_LOJA"
		Else
			cIndexKey := "D1_FILIAL+D1_COD+DTOS(D1_EMISSAO)+D1_FORNECE+D1_DOC"
			cOrdem    := "D1_FILIAL+D1_COD"
		EndIf

		cCondicao := "D1_FORNECE >='" + mv_par02 + "'.And.D1_FORNECE <='" + mv_par03 + "'" ;
			+ ".And.D1_COD >='" + mv_par04 + "'.And.D1_COD <='" + mv_par05 + "'";
			+ ".And.Dtos(D1_EMISSAO)>='" + Dtos(mv_par08) + "'.And.Dtos(D1_EMISSAO)<='" + Dtos(mv_par09) + "'"
		cCondicao += ".And. ("+IsRemito(2,'SD1->D1_TIPODOC')+")"

	ElseIf mv_par01  == 3

		dbSelectArea("SD1")
		cAliasQry := "SD1"

		If nOrdem == 1
			cIndexKey := "D1_FILIAL+DTOS(D1_EMISSAO)+D1_FORNECE+D1_COD+D1_DOC+D1_SERIE"
			cOrdem    := "D1_FILIAL+Dtos(D1_EMISSAO)"
		ElseIf nOrdem == 2
			cIndexKey := "D1_FILIAL+D1_FORNECE+DTOS(D1_EMISSAO)+D1_COD+D1_DOC+D1_SERIE"
			cOrdem    := "D1_FILIAL+D1_FORNECE+SD1->D1_LOJA"
		Else
			cIndexKey := "D1_FILIAL+D1_COD+DTOS(D1_EMISSAO)+D1_FORNECE+D1_DOC+D1_SERIE"
			cOrdem    := "D1_FILIAL+D1_COD"
		EndIf

		cCondicao := "D1_FORNECE >='" + mv_par02 + "'.And.D1_FORNECE <='" + mv_par03 + "'";
			+ ".And.D1_COD >='" + mv_par04 + "'.And.D1_COD <='" + mv_par05 + "'";
			+ ".And.Dtos(D1_EMISSAO)>='" + Dtos(mv_par08) + "'.And.Dtos(D1_EMISSAO)<='" + Dtos(mv_par09) + "'"
		cCondicao += ".And. !("+IsRemito(2,'SD1->D1_TIPODOC')+")"

	EndIf

	oSection1:SetFilter(cCondicao,cIndexKey)

#ENDIF

oReport:SetMeter( Iif( mv_par01 == 1 , SC7->(LastRec()) , SD1->(LastRec()) ) )
dbSelectArea(cAliasQry)

While !oReport:Cancel() .And. !(cAliasQry)->(Eof())

	oReport:IncMeter()
	If oReport:Cancel()
		Exit
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posicionamento do SB1 - Produtos e SA2 - Fornecedores        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	SB1->(dbSeek( xFilial("SB1")+ Iif( mv_par01 == 1 , (cAliasQry)->C7_PRODUTO , (cAliasQry)->D1_COD )))
	SA2->(dbSeek( xFilial("SA2")+ Iif( mv_par01 == 1 , (cAliasQry)->C7_FORNECE + (cAliasQry)->C7_LOJA , (cAliasQry)->D1_FORNECE + (cAliasQry)->D1_LOJA )))

	If SB1->B1_GRUPO < mv_par06 .Or. SB1->B1_GRUPO > mv_par07
		dbskip()
		Loop
	Endif

	cInitOrdem := (cAliasQry)->&(cOrdem)

	oReport:SkipLine()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Section1 imprime o Dado da Quebra conforme a Ordem Selecionada³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	oSection1:Init()
	oSection1:PrintLine()		
	oReport:SkipLine()

	oSection2:Init()	
	While !oReport:Cancel() .And. !(cAliasQry)->(Eof()) .And. cInitOrdem	==	(cAliasQry)->&(cOrdem)

		oReport:IncMeter()
		If oReport:Cancel()
			Exit
		EndIf  



	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posicionamento do SB1 - Produtos e SA2 - Fornecedores        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	SB1->(dbSeek( xFilial("SB1")+ Iif( mv_par01 == 1 , (cAliasQry)->C7_PRODUTO , (cAliasQry)->D1_COD )))
	SA2->(dbSeek( xFilial("SA2")+ Iif( mv_par01 == 1 , (cAliasQry)->C7_FORNECE + (cAliasQry)->C7_LOJA , (cAliasQry)->D1_FORNECE + (cAliasQry)->D1_LOJA )))

	If SB1->B1_GRUPO < mv_par06 .Or. SB1->B1_GRUPO > mv_par07
		dbskip()
		Loop
	Endif



		If mv_par01  == 3
			SF1->(dbseek( xFilial("SF1")+(cAliasQry)->D1_DOC+(cAliasQry)->D1_SERIE+(cAliasQry)->D1_FORNECE+(cAliasQry)->D1_LOJA))
		EndIf
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Relatorio com Base no Pedido De Compras                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		If mv_par01 == 1

			lPrintline := .F.

 			dbSelectArea("SD1")

			SD1->(dbSetOrder(5))
			SD1->(dbSeek( xFilial("SD1")+(cAliasQry)->C7_PRODUTO))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³While de Busca dos Remito SD1                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			While !SD1->(Eof()) .And. SD1->D1_COD == (cAliasQry)->C7_PRODUTO

				oSection2:Cell("REMDOC"):Show()
				oSection2:Cell("REMITEM"):Show()
				oSection2:Cell("REMQUANT"):Show()
				oSection2:Cell("REMEMISSAO"):Show()

				If SD1->D1_PEDIDO == (cAliasQry)->C7_NUM .And. SD1->D1_ITEMPC == (cAliasQry)->C7_ITEM .And. IsRemito(1,"SD1->D1_TIPODOC")

					// Informacoes da nota baseadas no item do remito localizado
					cFatuRem	:= SD1->D1_DOC
					cSeriRem	:= SD1->D1_SERIE
					cFornRem	:= SD1->D1_FORNECE
					cLojaRem	:= SD1->D1_LOJA
					cCodRem	    := SD1->D1_COD
					cItemRem	:= SD1->D1_ITEM

					SF1->(dbseek( xFilial("SF1")+SD1->D1_DOC+SD1->D1_SERIE))
		
					oSection2:Cell("REMDOC"):SetValue(SD1->D1_DOC)
					oSection2:Cell("REMITEM"):SetValue(SD1->D1_ITEM)
					oSection2:Cell("REMQUANT"):SetValue(SD1->D1_QUANT)
					oSection2:Cell("REMEMISSAO"):SetValue(SD1->D1_EMISSAO)

					nRecnoSD1 := SD1->(Recno())

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ While de Busca das NFES                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
					SD1->(dbSetOrder(10))
					If SD1->(dbSeek( xFilial("SD1")+cFornRem+cLojaRem+cSeriRem+cFatuRem+cItemRem))

						While !SD1->(Eof()) .And. cFatuRem == SD1->D1_REMITO .And. cSeriRem == SD1->D1_SERIREM .And.;
								cFornRem == SD1->D1_FORNECE .And. cLojaRem == SD1->D1_LOJA .And. cCodRem == SD1->D1_COD

							If cFatuRem == SD1->D1_REMITO .And. Alltrim(cItemRem) == SD1->D1_ITEMREM  
							
								oSection2:Cell("D1_DOC"):Show()
								oSection2:Cell("D1_SERIE"):Show()
								oSection2:Cell("D1_ITEM"):Show()
								oSection2:Cell("D1_VUNIT"):Show()
								oSection2:Cell("D1_QUANT"):Show()
								oSection2:Cell("DIVPRECO"):Show()
								oSection2:Cell("DIVDATE"):Show()
								
								lPrintline := .T.
								oSection2:PrintLine()
								lPassPrint := .T.

								oSection2:Cell("C7_EMISSAO"):Hide()
								oSection2:Cell("C7_FORNECE"):Hide()
								oSection2:Cell("C7_LOJA"):Hide()
								oSection2:Cell("C7_PRODUTO"):Hide()
								oSection2:Cell("C7_NUM"):Hide()
								oSection2:Cell("C7_ITEM"):Hide()
								oSection2:Cell("C7_PRECO"):Hide()
								oSection2:Cell("C7_QUANT"):Hide()
								oSection2:Cell("C7_DATPRF"):Hide()
								oSection2:Cell("REMDOC"):Hide()
								oSection2:Cell("REMITEM"):Hide()
								oSection2:Cell("REMQUANT"):Hide()
								oSection2:Cell("REMEMISSAO"):Hide()

							EndIf
							SD1->(dbSkip())
						Enddo
					Else
						lPrintline := .T.
						oSection2:Cell("D1_DOC"):Hide()
						oSection2:Cell("D1_SERIE"):Hide()
						oSection2:Cell("D1_ITEM"):Hide()
						oSection2:Cell("D1_VUNIT"):Hide()
						oSection2:Cell("D1_QUANT"):Hide()
						oSection2:Cell("DIVPRECO"):Hide()
						oSection2:Cell("DIVDATE"):Hide()

						oSection2:PrintLine()

						oSection2:Cell("C7_EMISSAO"):Hide()
						oSection2:Cell("C7_FORNECE"):Hide()
						oSection2:Cell("C7_LOJA"):Hide()
						oSection2:Cell("C7_PRODUTO"):Hide()
						oSection2:Cell("C7_NUM"):Hide()
						oSection2:Cell("C7_ITEM"):Hide()
						oSection2:Cell("C7_PRECO"):Hide()
						oSection2:Cell("C7_QUANT"):Hide()
						oSection2:Cell("C7_DATPRF"):Hide()
					EndIf

					SD1->(dbSetOrder(5))
					SD1->(dbGoto(nRecnoSD1))
				EndIf

				SD1->(dbSkip())
			Enddo

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Busca por NFs caso nao exista Remitos                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			SD1->(dbSetOrder(5))
			If SD1->(dbSeek( xFilial("SD1") + (cAliasQry)->C7_PRODUTO))
			  
				While !SD1->(Eof()) .And. (cAliasQry)->C7_PRODUTO == SD1->D1_COD

					SF1->(dbseek( xFilial("SF1")+SD1->D1_DOC+SD1->D1_SERIE))
					
					If SD1->D1_PEDIDO == (cAliasQry)->C7_NUM .And. SD1->D1_ITEMPC == (cAliasQry)->C7_ITEM .And. (!IsRemito(1,"SD1->D1_TIPODOC"))
						oSection2:Cell("D1_DOC"):Show()
						oSection2:Cell("D1_SERIE"):Show()
						oSection2:Cell("D1_ITEM"):Show()
						oSection2:Cell("D1_VUNIT"):Show()
						oSection2:Cell("D1_QUANT"):Show()
						oSection2:Cell("DIVPRECO"):Show()
						oSection2:Cell("DIVDATE"):Show()
							
						lPrintline := .T.

						oSection2:Cell("REMDOC"):Hide()
						oSection2:Cell("REMITEM"):Hide()
						oSection2:Cell("REMQUANT"):Hide()
						oSection2:Cell("REMEMISSAO"):Hide()

                        If !(lPassPrint .And. nOrdem==1)            //tratamento para não imprimir 2 vezes iguais em linhas diferentes
							oSection2:PrintLine() 
                        Endif                     
                        	lPassPrint :=.F.

						oSection2:Cell("C7_EMISSAO"):Hide()
						oSection2:Cell("C7_FORNECE"):Hide()
						oSection2:Cell("C7_LOJA"):Hide()
						oSection2:Cell("C7_PRODUTO"):Hide()
						oSection2:Cell("C7_NUM"):Hide()
						oSection2:Cell("C7_ITEM"):Hide()
						oSection2:Cell("C7_PRECO"):Hide()
						oSection2:Cell("C7_QUANT"):Hide()
						oSection2:Cell("C7_DATPRF"):Hide()
					EndIf

					SD1->(dbSkip())
				Enddo
			EndIf

			oSection2:Cell("C7_EMISSAO"):Show()
			oSection2:Cell("C7_FORNECE"):Show()
			oSection2:Cell("C7_LOJA"):Show()
			oSection2:Cell("C7_PRODUTO"):Show()
			oSection2:Cell("C7_NUM"):Show()
			oSection2:Cell("C7_ITEM"):Show()
			oSection2:Cell("C7_PRECO"):Show()
			oSection2:Cell("C7_QUANT"):Show()
			oSection2:Cell("C7_DATPRF"):Show()
  
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime somente os Pedidos caso na haja Remitos nem NFEs     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
 			If !lPrintline
				SF1->(dbseek( xFilial("SF1")+space(TamSX3('D1_DOC')[1])+space(TamSX3('D1_SERIE')[1])))
				
				oSection2:Cell("REMDOC"):Hide()
				oSection2:Cell("REMITEM"):Hide()
				oSection2:Cell("REMQUANT"):Hide()
				oSection2:Cell("REMEMISSAO"):Hide()
				oSection2:Cell("D1_DOC"):Hide()
				oSection2:Cell("D1_SERIE"):Hide()
				oSection2:Cell("D1_ITEM"):Hide()
				oSection2:Cell("D1_VUNIT"):Hide()
				oSection2:Cell("D1_QUANT"):Hide()
				oSection2:Cell("DIVPRECO"):Hide()
				oSection2:Cell("DIVDATE"):Hide()
 
				oSection2:PrintLine()

			EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Relatorio baseado nos Remitos de compras                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		ElseIf mv_par01 == 2  

			oSection2:Cell("D1_EMISSAO"):Show()
			oSection2:Cell("D1_FORNECE"):Show()
			oSection2:Cell("D1_LOJA"):Show()
			oSection2:Cell("D1_COD"):Show()
			oSection2:Cell("D1_DOC"):Show()
			oSection2:Cell("D1_ITEM"):Show()
			oSection2:Cell("D1_QUANT"):Show()
			oSection2:Cell("DT_EMISSAO"):Show()
			oSection2:Cell("C7_NUM"):Show()
			oSection2:Cell("C7_ITEM"):Show()
			oSection2:Cell("C7_PRECO"):Show()
			oSection2:Cell("C7_QUANT"):Show()
			oSection2:Cell("C7_DATPRF"):Show()
			

			cFatuRem	:= (cAliasQry)->D1_DOC
			cSeriRem	:= (cAliasQry)->D1_SERIE
			cFornRem	:= (cAliasQry)->D1_FORNECE
			cLojaRem	:= (cAliasQry)->D1_LOJA
			cCodRem	    := (cAliasQry)->D1_COD
			cItemRem	:= (cAliasQry)->D1_ITEM
			nQuantRem	:= (cAliasQry)->D1_QUANT
			dtEmisRem   := (cAliasQry)->D1_EMISSAO

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe Pedido de compras vinculado e posiciona   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If !Empty((cAliasQry)->D1_PEDIDO)
				SC7->(dbSetOrder(1))
				lSC7 := SC7->(dbSeek( xFilial("SC7")+(cAliasQry)->D1_PEDIDO+(cAliasQry)->D1_ITEMPC))
			Else
				lSC7 := .F.
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Guarda a area para nao perder o filtro do SD1 ao reposicionar ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			aArea := GetArea()

			dbSelectArea("SD1")
			SD1->(dbSetOrder(10))
			If SD1->(dbSeek( xFilial("SD1")+cFornRem+cLojaRem+cSeriRem+cFatuRem+cItemRem))

				While !SD1->(Eof()) .And. cFatuRem == SD1->D1_REMITO .And. cSeriRem == SD1->D1_SERIREM .And.;
						cFornRem == SD1->D1_FORNECE .And. cLojaRem == SD1->D1_LOJA .And. cCodRem == SD1->D1_COD

					If cFatuRem == SD1->D1_REMITO .And. Alltrim(cItemRem) == SD1->D1_ITEMREM

						oSection2:Cell("NFDOC"):SetValue(SD1->D1_DOC)
						oSection2:Cell("NFITEM"):SetValue(SD1->D1_ITEM)
						oSection2:Cell("NFVUNIT"):SetValue(SD1->D1_VUNIT)
						oSection2:Cell("NFQUANT"):SetValue(SD1->D1_QUANT)

						oSection2:Cell("D1_EMISSAO"):SetValue(dtEmisRem)
						oSection2:Cell("D1_FORNECE"):SetValue(cFornRem)
						oSection2:Cell("D1_LOJA"):SetValue(cLojaRem)
						oSection2:Cell("D1_COD"):SetValue(cCodRem)
						oSection2:Cell("D1_DOC"):SetValue(cFatuRem)
						oSection2:Cell("D1_ITEM"):SetValue(cItemRem)
						oSection2:Cell("D1_QUANT"):SetValue(nQuantRem)

						If !lSC7 		
							oSection2:Cell("C7_NUM"):Hide()
							oSection2:Cell("C7_ITEM"):Hide()
							oSection2:Cell("C7_PRECO"):Hide()
							oSection2:Cell("C7_QUANT"):Hide()
							oSection2:Cell("C7_DATPRF"):Hide()			
						EndIf   
						
						oSection2:Cell("NFDOC"):SHOW()
						oSection2:Cell("NFITEM"):SHOW()
						oSection2:Cell("NFVUNIT"):SHOW()
						oSection2:Cell("NFQUANT"):SHOW() 
						oSection2:Cell("DIVPRECO"):SHOW()
						oSection2:Cell("DIVDATE"):SHOW()

						oSection2:PrintLine()

						oSection2:Cell("D1_EMISSAO"):Hide()
						oSection2:Cell("D1_FORNECE"):Hide()
						oSection2:Cell("D1_LOJA"):Hide()
						oSection2:Cell("D1_COD"):Hide()				
						oSection2:Cell("D1_DOC"):Hide()
						oSection2:Cell("D1_ITEM"):Hide()
						oSection2:Cell("D1_QUANT"):Hide()
						oSection2:Cell("D1_EMISSAO"):Hide()
						oSection2:Cell("C7_NUM"):Hide()
						oSection2:Cell("C7_ITEM"):Hide()
						oSection2:Cell("C7_PRECO"):Hide()
						oSection2:Cell("C7_QUANT"):Hide()
						oSection2:Cell("C7_DATPRF"):Hide()

					EndIf
					SD1->(dbSkip())
				Enddo
			Else
				RestArea(aArea)

				oSection2:Cell("D1_EMISSAO"):SetValue(dtEmisRem)
				oSection2:Cell("D1_FORNECE"):SetValue(cFornRem)
				oSection2:Cell("D1_LOJA"):SetValue(cLojaRem)
				oSection2:Cell("D1_COD"):SetValue(cCodRem)
				oSection2:Cell("D1_DOC"):SetValue(cFatuRem)
				oSection2:Cell("D1_ITEM"):SetValue(cItemRem)
				oSection2:Cell("D1_QUANT"):SetValue(nQuantRem)
				oSection2:Cell("DT_EMISSAO"):SetValue(dtEmisRem)				

				oSection2:Cell("NFDOC"):Hide()
				oSection2:Cell("NFITEM"):Hide()
				oSection2:Cell("NFVUNIT"):Hide()
				oSection2:Cell("NFQUANT"):Hide()
				oSection2:Cell("DIVPRECO"):Hide()
				oSection2:Cell("DIVDATE"):Hide()

				If !lSC7 		
					oSection2:Cell("C7_NUM"):Hide()
					oSection2:Cell("C7_ITEM"):Hide()
					oSection2:Cell("C7_PRECO"):Hide()
					oSection2:Cell("C7_QUANT"):Hide()
					oSection2:Cell("C7_DATPRF"):Hide()			
				EndIf

				oSection2:PrintLine()
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Restaura a ordem original apos a pesquisa no SD1              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			RestArea(aArea)

			dbSelectArea(cAliasQry)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Relatorio com base nas NFEs                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		ElseIf mv_par01 == 3  

			oSection2:Cell("REMDOC"):Show()
			oSection2:Cell("REMITEM"):Show()
			oSection2:Cell("REMQUANT"):Show()
			oSection2:Cell("REMEMISSAO"):Show()
			oSection2:Cell("C7_NUM"):Show()
			oSection2:Cell("C7_ITEM"):Show()
			oSection2:Cell("C7_PRECO"):Show()
			oSection2:Cell("C7_QUANT"):Show()
			oSection2:Cell("C7_DATPRF"):Show()
			oSection2:Cell("DIVPRECO"):Show()
			oSection2:Cell("DIVDATE"):Show()		

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe Pedido de compras vinculado e posiciona   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If !Empty((cAliasQry)->D1_PEDIDO)
				SC7->(dbSetOrder(1))
				lSC7 := SC7->(dbSeek( xFilial("SC7")+(cAliasQry)->D1_PEDIDO+(cAliasQry)->D1_ITEMPC))
			Else
				lSC7 := .F.
			EndIf

			cRemito 	:= (cAliasQry)->D1_REMITO
			cItemRem	:= (cAliasQry)->D1_ITEMREM
			cSeriRem    := (cAliasQry)->D1_SERIREM
			cFornRem	:= (cAliasQry)->D1_FORNECE
			cLojaRem	:= (cAliasQry)->D1_LOJA
			cCodRem	    := (cAliasQry)->D1_COD

			oSection2:Cell("D1_EMISSAO"):SetValue((cAliasQry)->D1_EMISSAO)
			oSection2:Cell("D1_FORNECE"):SetValue((cAliasQry)->D1_FORNECE)
			oSection2:Cell("D1_LOJA"):SetValue((cAliasQry)->D1_LOJA)
			oSection2:Cell("D1_COD"):SetValue((cAliasQry)->D1_COD)
			oSection2:Cell("D1_DOC"):SetValue((cAliasQry)->D1_DOC)
			oSection2:Cell("D1_SERIE"):SetValue((cAliasQry)->D1_SERIE)
			oSection2:Cell("D1_ITEM"):SetValue((cAliasQry)->D1_ITEM)
			oSection2:Cell("D1_VUNIT"):SetValue((cAliasQry)->D1_VUNIT)
			oSection2:Cell("D1_QUANT"):SetValue((cAliasQry)->D1_QUANT)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Guarda a area para nao perder o filtro do SD1 ao reposicionar ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			aArea := GetArea()

			dbSelectArea("SD1")
			SD1->(dbSetOrder(1))
			If SD1->(dbSeek( xFilial("SD1")+cRemito+cSeriRem+cFornRem+cLojaRem+cCodRem+Alltrim(cItemRem)))
				oSection2:Cell("REMDOC"):SetValue(SD1->D1_DOC)
				oSection2:Cell("REMITEM"):SetValue(SD1->D1_ITEM)
				oSection2:Cell("REMQUANT"):SetValue(SD1->D1_QUANT)
				oSection2:Cell("REMEMISSAO"):SetValue(SD1->D1_EMISSAO)

				If !Empty(SD1->D1_PEDIDO) .And. !lSC7
					SC7->(dbSetOrder(1))
					lSC7 := SC7->(dbSeek( xFilial("SC7")+SD1->D1_PEDIDO+SD1->D1_ITEMPC))
				EndIf

			Else
				oSection2:Cell("REMDOC"):Hide()
				oSection2:Cell("REMITEM"):Hide()
				oSection2:Cell("REMQUANT"):Hide()
				oSection2:Cell("REMEMISSAO"):Hide()
			EndIf

			If !lSC7
				oSection2:Cell("C7_NUM"):Hide()
				oSection2:Cell("C7_ITEM"):Hide()
				oSection2:Cell("C7_PRECO"):Hide()
				oSection2:Cell("C7_QUANT"):Hide()
				oSection2:Cell("C7_DATPRF"):Hide()
				oSection2:Cell("DIVPRECO"):Hide()
				oSection2:Cell("DIVDATE"):Hide()		
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Restaura a ordem original apos a pesquisa no SD1              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			RestArea(aArea)
			oSection2:PrintLine()
			dbSelectArea(cAliasQry)
		EndIf

		nPrinted ++
		dbSelectArea(cAliasQry)
		dbskip()

	Enddo

	oReport:SkipLine()

	If nOrdem == 1      // Emissao
		oReport:PrintText(STR0023+" "+TransForm(nPrinted,"@R 999,999"),,oSection1:Cell(Iif( mv_par01 == 1 , "C7_EMISSAO" , "D1_EMISSAO" )):ColPos()) //"Total Fecha -> "
	ElseIf nOrdem == 2  // Fornecedor
		oReport:PrintText(STR0024+" "+TransForm(nPrinted,"@R 999,999"),,oSection1:Cell(Iif( mv_par01 == 1 , "C7_EMISSAO" , "D1_EMISSAO" )):ColPos()) //"Total Proveedor -> "
	ElseIf nOrdem == 3  // Producto
		oReport:PrintText(STR0025+" "+TransForm(nPrinted,"@R 999,999"),,oSection1:Cell(Iif( mv_par01 == 1 , "C7_EMISSAO" , "D1_EMISSAO" )):ColPos()) //"Total Producto -> "
	EndIf

	nTPrinted := nTPrinted + nPrinted
	nPrinted  := 0

	oSection1:Finish()
	oSection2:Finish()

Enddo

oReport:SkipLine() // "Total General -> "
oReport:PrintText(STR0044+" "+TransForm(nTPrinted,"@R 999,999"),,oSection1:Cell(Iif( mv_par01 == 1 , "C7_EMISSAO" , "D1_EMISSAO" )):ColPos()) //"Total Fecha -> "

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATR131R3³ Autor ³ Leonardo Ruben        ³ Data ³ 24.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Informe de Pedidos de Compra/Remitos/Notas Fiscais         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ MATR131R3(void)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGACOM - Localizacoes                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Matr131R3()

lEnd := .F.
cDesc1 := OemToAnsi(STR0001)  //"Imprime relaci¢n de las ordenes de compra"
Private cRetTitle := RTrim(RetTitle("D2_REMITO"))	
cDesc2:= cRetTitle + OemToAnsi(STR0073)+ OemToAnsi(STR0071)  // "Remito(s) y Facturas"

cDesc3 :=""
cString:="SC7"

cabec1  := ""
cabec2  := ""
aLinha  := {}
aOrd    := { OemToAnsi(STR0003), OemToAnsi(STR0004), OemToAnsi(STR0005)}  // "Fecha emision","Proveedor","Producto"
aReturn := { OemToAnsi(STR0006), 1, OemToAnsi(STR0007), 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
cPerg   := "MTR131"
nJuros  := 0
nLastKey:= 0
nomeprog:= "MATR131"
limite  := 186
tamanho := "G"
Titulo  := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("MTR131",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros ³
//³ mv_par01       // ¨Basado por ?      ³
//³ mv_par02       // ¨De proveedor?     ³
//³ mv_par03       // ¨Hasta proveedor?  ³
//³ mv_par04       // ¨De producto ?     ³
//³ mv_par05       // ¨Hasta producto?   ³
//³ mv_par06       // ¨De grupo ?        ³
//³ mv_par07       // ¨Hasta grupo ?     ³
//³ mv_par08       // ¨De Fecha Emision? ³
//³ mv_par09       // ¨Hasta Fech.Emis.? ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

MR131Tit()  //montagem do Titulo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a fun‡„o SETPRINT ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel :="MATR131"            //Nome Default do relatorio em Disco
wnrel :=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.F.,tamanho,,.F.)
MR131Tit()  //montagem do Titulo de acordo com a escolha nos parametros
Titulo:= Padc(Titulo,60)
If nLastKey == 27
	Return
End

SetDefault(aReturn,cString)

If nLastKey == 27
	Return
End

nTipo := If(aReturn[4]==1,15,18)

RptStatus({|lEnd| R131Imp(@lEnd)})

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R131IMP  ³ Autor ³ Leonardo Ruben        ³ Data ³ 28.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R131Imp(lEnd)

Local nTamVazio := 0                              
Local nIncCol	:= If(cPaisLoc == "MEX",9,0)
Private cAlias
Private nindex

lContinua := .T.

nOrdem:=aReturn[8]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Vari veis utilizadas para Impress„o do Cabe‡alho e Rodap‚ ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem = 1       // data emissao
	cbIni := OemToAnsi(STR0011)  // "Proveedor Producto        "   //26 POSICOES
	nIni  := 26
ElseIf nOrdem = 2   // fornecedor
	cbIni := OemToAnsi(STR0012)  // "Fecha      Producto        "  //27 POSICOES
	nIni  := 27
ElseIf nOrdem = 3   // produto
	cbIni := OemToAnsi(STR0013)  // "Fecha      Proveedor "        //21 POSICOES
	nIni  := 21
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Defini‡„o dos cabe‡alhos ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbPed1 := OemToAnsi(STR0014)  // "DATOS DE LA ORDEM DE COMPRA                            "
cbPed2 := OemToAnsi(STR0015)  // "Num.OC/Item     Precio Unit.       Cantidad Fecha Ent. "
// XXXXXX XX  99,999,999,999.99 999,999,999.99 XX/XX/XXXX

cbRem1 := OemToAnsi(STR0077)+cRetTitle
nTamVazio := 42 - Len(cbRem1)
cbRem1 += Replicate(" ",nTamVazio)   // "DATOS DEL REMITO                          "
cbRem2 := Substr(cRetTitle,1,10)+OemToAnsi(STR0078)   // "Num.Remito/Item       Cantidad Fecha Ent. "
cbFac1 := OemToAnsi(STR0018)  // "DATOS DE LA FACTURA                                  "
cbFac2 := OemToAnsi(STR0019)  // "Num.Factura/Item         Precio Unit.       Cantidad "
// XXXXXXXXXXXX XXX XX 99,999,999,999.99 999,999,999.99
cbDiv  := OemToAnsi(STR0020)  // "Div.precio/Fch.Entr?"
//     XX     XX
If mv_par01	==	1
		cabec1 := Repl(" ",nIni)+cbPed1+cbRem1+Space(nIncCol)+cbFac1
		cabec2 := cbIni         +cbPed2+cbRem2+cbFac2+cbDiv
		cAlias := "SC7"
Elseif mv_par01 == 2
	cabec1 := Repl(" ",nIni)+cbRem1+Space(nIncCol)+cbPed1+cbFac1
	cabec2 := cbIni         +cbRem2+cbPed2+cbFac2+cbDiv
	cAlias := "SD1"
Else
	cabec1 := Repl(" ",nIni)+cbFac1+cbRem1+cbPed1
	cabec2 := cbIni         +cbFac2+cbRem2+cbPed2+cbDiv
	cAlias := "SD1"
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento das ordens e montagem do indice temporario        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt 	:= ""
cbcont	:= 1
li      := 80
m_pag 	:= 1

#IFDEF TOP
	If mv_par01 == 1
		cAlias:="SC7TMP"
		If Select(cAlias)<>0
			DbSelectArea(cAlias)
			DbCloseArea()
		Endif
		cQuery:="SELECT C7_FILIAL,C7_NUM,C7_EMISSAO,C7_FORNECE,C7_LOJA,C7_PRODUTO,C7_PRECO,"
		cQuery+="C7_DATPRF,C7_QUANT,C7_ITEM"
		cQuery+=",R_E_C_N_O_ RECNO FROM "+RetSqlName("SC7")+" "+cAlias+" "
		cQuery+="WHERE C7_FILIAL='"+xFilial("SC7")+"'"
		cQuery+=" AND C7_FORNECE >='"+mv_par02+"' And C7_FORNECE <='" +mv_par03+"'"
		cQuery+=" AND C7_PRODUTO >='"+mv_par04+"' And C7_PRODUTO <='"+mv_par05+"'"
		cQuery+=" AND C7_EMISSAO >='"+DTOS(mv_par08)+"' and C7_EMISSAO<='"+DTOS(mv_par09)+"'"
		cQuery+=" AND D_E_L_E_T_<>'*' ORDER BY "
		If nOrdem = 1      // emissao
			cQuery   += "C7_FILIAL,C7_EMISSAO,C7_FORNECE,C7_PRODUTO,C7_NUM"
			cOrdem   := +(cAlias)+"->C7_EMISSAO"
		ElseIf nOrdem = 2  // fornecedor
			cQuery   += "C7_FILIAL,C7_FORNECE,C7_EMISSAO,C7_PRODUTO,C7_NUM"
			cOrdem   := (cAlias)+"->C7_FORNECE+"+(cAlias)+"->C7_LOJA"
		Else
			cQuery   += "C7_FILIAL,C7_PRODUTO,C7_EMISSAO,C7_FORNECE,C7_NUM"
			cOrdem   := (cAlias)+"->C7_PRODUTO"
		EndIf
		cQuery:=ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery),cAlias, .F., .T.)
		TCSetField(cAlias,"C7_EMISSAO","D",8,0)
		TCSetField(cAlias,"C7_DATPRF","D",8,0)
	Else
		cAlias:="SD1TMP"
		If Select(cAlias)<>0
			DbSelectArea(cAlias)
			DbCloseArea()
		Endif
		cQuery:="SELECT D1_FILIAL,D1_SERIE,D1_DOC,D1_ITEM,D1_EMISSAO,D1_QUANT,D1_TOTAL"
		cQuery+=",D1_COD,D1_VUNIT,D1_GRUPO,D1_REMITO,D1_ITEMREM,D1_SERIREM"
		cQuery+=",D1_PEDIDO,D1_ITEMPC,D1_FORNECE,D1_LOJA"
		cQuery+=",R_E_C_N_O_ RECNO FROM "+RetSqlName("SD1")+" "+cAlias+" "
		cQuery+="WHERE D1_FILIAL='"+xFilial("SD1")+"'"
		cQuery+=" AND D1_FORNECE >='"+mv_par02+"' And D1_FORNECE <='"+mv_par03+"'"
		cQuery+=" AND D1_COD >='"+mv_par04+"' And D1_COD <='"+mv_par05+"'"
		cQuery+=" AND D1_EMISSAO>='"+DTOS(mv_par08)+"' and D1_EMISSAO<='"+DTOS(mv_par09)+"'"
		If mv_par01 == 2
			cQuery += " AND  ("+IsRemito(3,'D1_TIPODOC')+")"			
		Else
			cQuery += " AND NOT ("+IsRemito(3,'D1_TIPODOC')+")"			
		Endif
		cQuery+=" AND D_E_L_E_T_<>'*' ORDER BY "
		If nOrdem = 1      // emissao
			cQuery   += "D1_FILIAL,D1_EMISSAO,D1_FORNECE,D1_COD,D1_DOC"
			cOrdem   := +cAlias+"->D1_EMISSAO
		ElseIf nOrdem = 2  // fornecedor
			cQuery   += "D1_FILIAL,D1_FORNECE,D1_EMISSAO,D1_COD,D1_DOC"
			cOrdem   := cAlias+"->D1_FORNECE+"+cAlias+"->D1_LOJA"
		Else
			cQuery   += "D1_FILIAL,D1_COD,D1_EMISSAO,D1_FORNECE,D1_DOC"
			cOrdem   := cAlias+"->D1_COD"
		EndIf
		cQuery:=ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery),cAlias, .F., .T.)
		TCSetField(cAlias,"D1_EMISSAO","D",8,0)
	Endif
#ELSE
	Do Case
	Case mv_par01  == 1   // baseado no pedido de compra
		If nOrdem = 1      // emissao
			cKey     := "C7_FILIAL+DTOS(C7_EMISSAO)+C7_FORNECE+C7_PRODUTO+C7_NUM"
			cOrdem   := "SC7->C7_EMISSAO"
		ElseIf nOrdem = 2  // fornecedor
			cKey     := "C7_FILIAL+C7_FORNECE+DTOS(C7_EMISSAO)+C7_PRODUTO+C7_NUM"
			cOrdem   := "SC7->C7_FORNECE+SC7->C7_LOJA"
		Else
			cKey     := "C7_FILIAL+C7_PRODUTO+DTOS(C7_EMISSAO)+C7_FORNECE+C7_NUM"
			cOrdem   := "SC7->C7_PRODUTO"
		EndIf
		cCondicao:= "C7_FORNECE >='"+mv_par02+"'.And.C7_FORNECE <='"+mv_par03+"'"+;
			".and.C7_PRODUTO >='"+mv_par04+"'.And.C7_PRODUTO <='"+mv_par05+"'"+;
			".AND.DTOS(C7_EMISSAO)>='"+DTOS(mv_par08)+"'.and.DTOS(C7_EMISSAO)<='"+DTOS(mv_par09)+"'"
	Case mv_par01  == 2  // baseado no remito
		If nOrdem = 1      // emissao
			cKey     := "D1_FILIAL+DTOS(D1_EMISSAO)+D1_FORNECE+D1_COD+D1_DOC"
			cOrdem   := "SD1->D1_EMISSAO"
		ElseIf nOrdem = 2  // fornecedor
			cKey     := "D1_FILIAL+D1_FORNECE+DTOS(D1_EMISSAO)+D1_COD+D1_DOC"
			cOrdem   := "SD1->D1_FORNECE+SD1->D1_LOJA"
		Else
			cKey     := "D1_FILIAL+D1_COD+DTOS(D1_EMISSAO)+D1_FORNECE+D1_DOC"
			cOrdem   := "SD1->D1_COD"
		EndIf
		cCondicao:= "D1_FORNECE >='"+mv_par02+"'.And.D1_FORNECE <='"+mv_par03+"'";
			+".and.D1_COD >='"+mv_par04+"'.And.D1_COD <='"+mv_par05+"'";
			+".AND.DTOS(D1_EMISSAO)>='"+DTOS(mv_par08)+"'.and.DTOS(D1_EMISSAO)<='"+DTOS(mv_par09)+"'"
		cCondicao   += ".AND. ("+IsRemito(2,'SD1->D1_TIPODOC')+")"			
	Case mv_par01  == 3  // baseado na nota fiscal
		If nOrdem = 1      // emissao
			cKey     := "D1_FILIAL+DTOS(D1_EMISSAO)+D1_FORNECE+D1_COD+D1_DOC+D1_SERIE"
			cOrdem   := "SD1->D1_EMISSAO"
		ElseIf nOrdem = 2  // fornecedor
			cKey     := "D1_FILIAL+D1_FORNECE+DTOS(D1_EMISSAO)+D1_COD+D1_DOC+D1_SERIE"
			cOrdem   := "SD1->D1_FORNECE+SD1->D1_LOJA"
		Else
			cKey     := "D1_FILIAL+D1_COD+DTOS(D1_EMISSAO)+D1_FORNECE+D1_DOC+D1_SERIE"
			cOrdem   := "SD1->D1_COD"
		EndIf
		cCondicao:= "D1_FORNECE >='"+mv_par02+"'.And.D1_FORNECE <='"+mv_par03+"'";
			+".and.D1_COD >='"+mv_par04+"'.And.D1_COD <='"+mv_par05+"'";
			+".AND.DTOS(D1_EMISSAO)>='"+DTOS(mv_par08)+"'.and.DTOS(D1_EMISSAO)<='"+DTOS(mv_par09)+"'"
		cCondicao   += ".AND. !("+IsRemito(2,'SD1->D1_TIPODOC')+")"			
	EndCase

	cArqTrab := CriaTrab(Nil,.F.)

	IndRegua(cAlias,cArqTrab,cKey,,cCondicao,OemToAnsi(STR0021)) //"Filtrando registros..."

	If mv_par01 == 1
		nIndex := RetIndex("SC7")
		SC7->( dbSetIndex( cArqTrab + OrdBagExt() ) )
		dbSetOrder(nIndex+1)
    Else
		nIndex := RetIndex("SD1")
		SD1->( dbSetIndex( cArqTrab + OrdBagExt() ) )
		dbSetOrder(nIndex+1)
	Endif

#ENDIF

SetRegua(RecCount())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializar variaveis         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cOrdAnt := If( nOrdem = 1,CtoD("01/01/1980","mmddyy"),"")
nTotIt  := 0
nTotGe  := 0

DbGoTop()
While !Eof().and.!lEnd

	IF lEnd
		@PROW()+1,001 PSAY OemToAnsi(STR0022)  //"CANCELADO PELO OPERADOR"
		Exit
	Endif
	SB1->(dbSeek( xFilial("SB1")+ If( cAlias="SD1", SD1->D1_COD, &(cAlias+"->"+Subs(cAlias,2,2)+"_PRODUTO"))))
	If SB1->B1_GRUPO < mv_par06 .Or. SB1->B1_GRUPO > mv_par07
		dbskip()
		Loop
	Endif

	IF li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,GetMv("MV_COMP"))
	EndIF

	If cOrdAnt # &cOrdem  //primeira vez e sempre que mudar a ordem escolhida
		ImprOrd()   // IMPRESSAO CABECALHO DE MUDANCA DA ORDEM ESCOLHIDA
	EndIf

	cOrdAnt	:=	&cOrdem

	DbSelectArea(cAlias)

	While !Eof() .and. lContinua .And.;
			cOrdAnt==&cOrdem .and. If(mv_par01==1,xFilial("SC7"),xFilial("SD1"))==&(Subs(cAlias,2,2)+"_FILIAL")

		IncRegua()

		IF lEnd
			@PROW()+1,001 PSAY OemToAnsi(STR0022)  //"CANCELADO PELO OPERADOR"
			lContinua := .F.
			Exit
		EndIF

		ImprDet()  // IMPRESSAO DA LINHA DETALHE
		dbSelectArea(cAlias)
		dbskip()

	Enddo

	dbSelectArea(cAlias)
	IF li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,GetMv("MV_COMP"))
	EndIF

	If Eof() .Or. cOrdAnt#&cOrdem
		li:=li+1
		If nOrdem = 1      // emissao
			@li,000 PSAY OemToAnsi(STR0023) + TransForm(nTotIt,"@R 999,999")  //"Total Fecha -> "
		ElseIf nOrdem = 2  // fornecedor
			@li,000 PSAY OemToAnsi(STR0024) + TransForm(nTotIt,"@R 999,999")  //"Total Proveedor -> "
		Else               // producto
			@li,000 PSAY OemToAnsi(STR0025) + TransForm(nTotIt,"@R 999,999")  //"Total Producto -> "
		EndIf
		nTotIt := 0
		Li:=Li+2
	Endif
	dbSelectArea(cAlias)
Enddo

If nTotGe >0
	li:=li+1
	@li,000 PSAY OemToAnsi(STR0044) + TransForm(nTotGe,"@R 999,999")  //"Total General -> "
	nTotGe := 0
	li:=li+2
Endif

IF li != 80
	Roda(cbcont,cbtxt,tamanho)
EndIF

Set Device To Screen
dbSelectArea(cAlias)
dbClearFilter()

If mv_par01 == 1
	RetIndex("SC7")
Else
	RetIndex("SD1")
EndIf

dbSelectArea("SD1")
dbSetOrder(1)

If aReturn[5] == 1
	Set Printer TO
	dbCommitAll()
	Ourspool(wnrel)
Endif
MS_FLUSH()
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ IMPRORD  ³ Autor ³ Leonardo Ruben        ³ Data ³ 28.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao da Linha de mudanca de Ordem                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ImprOrd()

If li > 58
	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
Endif

If nOrdem = 1  // emissao
	@ li,000 PSAY OemToAnsi(STR0026)+DtoC( &cOrdem)  //"Fecha: "
ElseIf nOrdem = 2   // fornecedor
	SA2->(dbSeek(xFilial("SA2")+&cOrdem))
	@ li,000 PSAY OemToAnsi(STR0027)+AllTrim(SA2->A2_COD)+" "+SA2->A2_NOME  //"Proveedor: "
ElseIf nOrdem = 3   // produto
	SB1->(dbSeek(xFilial("SB1")+&cOrdem))
	@ li,000 PSAY OemToAnsi(STR0028)+SB1->B1_COD+" "+SB1->B1_DESC   //"Producto: "
EndIf

Li:=Li+2

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ IMPRDET  ³ Autor ³ Leonardo Ruben        ³ Data ³ 28.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao da Linha detalhe                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ImprDet()

Local cPQuant := PesqPictQt( "C7_QUANT",13)
Local cPValor := PesqPict("SC7","C7_PRECO",16)
Local lSC7    := .F.
Local lRem    := .F. 
Local lSD1    := .F.
Local nIncCol := If(cPaisLoc == "MEX",10,0)

Do Case
Case mv_par01  == 1   // baseado no pedido de compra
	If nOrdem = 1      // emissao
		@ li,000 PSAY (cAlias)->C7_FORNECE+" "+(cAlias)->C7_LOJA
		@ li,010 PSAY (cAlias)->C7_PRODUTO
	ElseIf nOrdem = 2  // fornecedor
		@ li,000 PSAY (cAlias)->C7_EMISSAO
		@ li,011 PSAY (cAlias)->C7_PRODUTO
	Else
		@ li,000 PSAY (cAlias)->C7_EMISSAO
		@ li,011 PSAY (cAlias)->C7_FORNECE+" "+(cAlias)->C7_LOJA
	EndIf
	@ li, nIni    PSAY (cAlias)->C7_NUM+" "+(cAlias)->C7_ITEM
	@ li, nIni+11 PSAY (cAlias)->C7_PRECO Picture cPValor
	@ li, nIni+29 PSAY (cAlias)->C7_QUANT Picture cPQuant
	If Valtype((cAlias)->C7_DATPRF) == "D"
		@ li, nIni+44 PSAY (cAlias)->C7_DATPRF
	Else
		@ li, nIni+44 PSAY STOD((cAlias)->C7_DATPRF)
	Endif
	SD1->(dbSetOrder(5))
	SD1->(dbSeek( xFilial("SD1")+(cAlias)->C7_PRODUTO))
	lPrim := .t.
	While !SD1->(Eof()) .And. (cAlias)->C7_PRODUTO==SD1->D1_COD	
		If SD1->D1_PEDIDO == (cAlias)->C7_NUM  .And.;
				SD1->D1_ITEMPC == (cAlias)->C7_ITEM .And. IsRemito(1,"SD1->D1_TIPODOC")
			li := If( lPrim, li, li+1)
			@ li, nIni+54 			PSAY SD1->D1_DOC+" "+SD1->D1_ITEM
			@ li, nIni+70+nIncCol	PSAY SD1->D1_QUANT Picture cPQuant
			@ li, nIni+85+nIncCol	PSAY SD1->D1_EMISSAO
			lPrim := .f.
			// Informacoes da nota baseadas no item do remito localizado
			cFatuRem	:= SD1->D1_DOC
			cSeriRem	:= SD1->D1_SERIE
			cFornRem	:= SD1->D1_FORNECE
			cLojaRem	:= SD1->D1_LOJA
			cCodRem		:= SD1->D1_COD
			cItemRem	:= SD1->D1_ITEM
			If Select("SD1") == 0
				ChkFile("__SD1",.F.,"SD1")
			Else
				dbSelectArea("SD1")
			Endif
			SD1->(dbSetOrder(10))
			nReg := Recno()
			DbGoTop()
			If SD1->(dbSeek( xFilial("SD1")+cFornRem+cLojaRem+cSeriRem+cFatuRem+cItemRem))
				lPrim2 := .t.
				While !SD1->(Eof()) .And. cFatuRem==SD1->D1_REMITO .And. cSeriRem==SD1->D1_SERIREM .And.;
						cFornRem == SD1->D1_FORNECE .And. cLojaRem == SD1->D1_LOJA .And.;
						cCodRem == SD1->D1_COD
					If cFatuRem= SD1->D1_REMITO .AND. Alltrim(cItemRem)==SD1->D1_ITEMREM
						li := If( lPrim2, li, li+1)
						@ li, nIni+96+nIncCol  PSAY SD1->D1_DOC+" "+SD1->D1_SERIE+" "+SD1->D1_ITEM
						@ li, nIni+117+nIncCol PSAY SD1->D1_VUNIT Picture cPValor
						@ li, nIni+135+nIncCol PSAY SD1->D1_QUANT Picture cPQuant
						@ li, nIni+154+nIncCol PSAY If( (cAlias)->C7_PRECO!=SD1->D1_VUNIT, OemToAnsi(STR0069), OemToAnsi(STR0070))  // "Si", "No"
						@ li, nIni+161+nIncCol PSAY If( (cAlias)->C7_DATPRF!=SD1->D1_EMISSAO, OemToAnsi(STR0069), OemToAnsi(STR0070))  // "Si", "No"
						lPrim2 := .f.
					EndIf
					SD1->(dbSkip())
				Enddo
			EndIf

			dbGoto(nReg)
		EndIf
		SD1->(dbSkip())
	Enddo

	// Informacoes da nota baseadas no pedido, pois nao existe remito
	SD1->(dbSetOrder(5))
	lSD1 := SD1->(dbSeek( xFilial("SD1")+(cAlias)->C7_PRODUTO))
	If IsRemito(1,"SD1->D1_TIPODOC")
		lSD1 := .F.
	Endif

	If lSD1
		While !SD1->(Eof()) .And. (cAlias)->C7_PRODUTO==SD1->D1_COD
			lPrim := .t.
			If SD1->D1_PEDIDO == (cAlias)->C7_NUM .And.;
					SD1->D1_ITEMPC == (cAlias)->C7_ITEM .And. (!IsRemito(1,"SD1->D1_TIPODOC"))
				li := If( lPrim, li, li+1)
				@ li, nIni+96+nIncCol  PSAY SD1->D1_DOC+" "+SD1->D1_SERIE+" "+SD1->D1_ITEM
				@ li, nIni+117+nIncCol PSAY SD1->D1_VUNIT Picture cPValor
				@ li, nIni+135+nIncCol PSAY SD1->D1_QUANT Picture cPQuant
				@ li, nIni+154+nIncCol PSAY If( (cAlias)->C7_PRECO!=SD1->D1_VUNIT, OemToAnsi(STR0069), OemToAnsi(STR0070)) //"Si", "No"
				lPrim := .f.
			EndIf
			SD1->(dbSkip())
		Enddo
	EndIf
Case mv_par01  == 2  // baseado no remito
	If !Empty( (cAlias)->D1_PEDIDO)
		SC7->(dbSetOrder(1))
		lSC7 := SC7->(dbSeek( xFilial("SC7")+(cAlias)->D1_PEDIDO+(cAlias)->D1_ITEMPC))
	EndIf
	If nOrdem = 1      // emissao
		@ li,000 PSAY (cAlias)->D1_FORNECE+" "+(cAlias)->D1_LOJA
		@ li,010 PSAY (cAlias)->D1_COD
	ElseIf nOrdem = 2  // fornecedor
		@ li,000 PSAY (cAlias)->D1_EMISSAO
		@ li,011 PSAY (cAlias)->D1_COD
	Else
		@ li,000 PSAY (cAlias)->D1_EMISSAO
		@ li,011 PSAY (cAlias)->D1_FORNECE+" "+(cAlias)->D1_LOJA
	EndIf
	@ li, nIni-1  PSAY (cAlias)->D1_DOC+" "+(cAlias)->D1_ITEM
	@ li, nIni+18+nIncCol PSAY (cAlias)->D1_QUANT Picture cPQuant
	@ li, nIni+31+nIncCol PSAY (cAlias)->D1_EMISSAO
	If lSC7
		@ li, nIni+41+nIncCol PSAY SC7->C7_NUM+" "+SC7->C7_ITEM
		@ li, nIni+52+nIncCol PSAY SC7->C7_PRECO Picture cPValor
		@ li, nIni+70+nIncCol PSAY SC7->C7_QUANT Picture cPQuant
		@ li, nIni+85+nIncCol PSAY SC7->C7_DATPRF
	EndIf

	cFatuRem	:= (cAlias)->D1_DOC
	cSeriRem	:= (cAlias)->D1_SERIE
	cFornRem	:= (cAlias)->D1_FORNECE
	cLojaRem	:= (cAlias)->D1_LOJA
	cCodRem	:= (cAlias)->D1_COD
	cItemRem	:= (cAlias)->D1_ITEM
	If Select("SD1") == 0
		ChkFile("__SD1",.F.,"SD1")
	Else
		dbSelectArea("SD1")
	Endif
	SD1->(dbSetOrder(10))
	nReg := Recno()
	DbGoTop()
	If SD1->(dbSeek( xFilial("SD1")+cFornRem+cLojaRem+cSeriRem+cFatuRem+cItemRem))
		lPrim := .t.
		While !SD1->(Eof()) .And. cFatuRem==SD1->D1_REMITO .And. cSeriRem==SD1->D1_SERIREM .And.;
				cFornRem==SD1->D1_FORNECE .And. cLojaRem==SD1->D1_LOJA .And.;
				cCodRem==SD1->D1_COD
			If cFatuRem==SD1->D1_REMITO .AND. Alltrim(cItemRem)==SD1->D1_ITEMREM
				li := If( lPrim, li, li+1)
				@ li, nIni+97  PSAY SD1->D1_DOC+" "+SD1->D1_ITEM
				@ li, nIni+118+nIncCol PSAY SD1->D1_VUNIT Picture cPValor
				@ li, nIni+136+nIncCol PSAY SD1->D1_QUANT Picture cPQuant
				@ li, nIni+154+nIncCol PSAY If( lSC7 , If( SC7->C7_PRECO!=SD1->D1_VUNIT, OemToAnsi(STR0069), OemToAnsi(STR0070)), "")  //"Si", "No"
				@ li, nIni+161+nIncCol PSAY If( lSC7 , If( SC7->C7_DATPRF!=SD1->D1_EMISSAO, OemToAnsi(STR0069), OemToAnsi(STR0070)), "")  //"Si", "No"
				lPrim := .f.
			EndIf
			SD1->(dbSkip())
		Enddo
	Else
		@ li, nIni+161 PSAY If( lSC7 , If( SC7->C7_DATPRF!=SD1->D1_EMISSAO, OemToAnsi(STR0069), OemToAnsi(STR0070)), "")  //"Si", "No"
	EndIf
	#IFNDEF TOP
		If mv_par01 <> 1
			dbSetOrder(nIndex+1)
		Endif
		dbGoto(nReg)
	#ENDIF

Case mv_par01  == 3  // baseado na nota fiscal
	If !Empty( (cAlias)->D1_PEDIDO)
		SC7->(dbSetOrder(1))
		lSC7 := SC7->(dbSeek( xFilial("SC7")+(cAlias)->D1_PEDIDO+(cAlias)->D1_ITEMPC))
	EndIf

	If nOrdem = 1      // emissao
		@ li,000 PSAY (cAlias)->D1_FORNECE+" "+(cAlias)->D1_LOJA
		@ li,010 PSAY (cAlias)->D1_COD
	ElseIf nOrdem = 2  // fornecedor
		@ li,000 PSAY (cAlias)->D1_EMISSAO
		@ li,011 PSAY (cAlias)->D1_COD
	Else
		@ li,000 PSAY (cAlias)->D1_EMISSAO
		@ li,011 PSAY (cAlias)->D1_FORNECE+" "+(cAlias)->D1_LOJA
	EndIf
	@ li, nIni    PSAY (cAlias)->D1_DOC+" "+(cAlias)->D1_SERIE+" "+(cAlias)->D1_ITEM
	@ li, nIni+21 PSAY (cAlias)->D1_VUNIT Picture cPValor
	@ li, nIni+39 PSAY (cAlias)->D1_QUANT Picture cPQuant

	cRemito 	:= (cAlias)->D1_REMITO
	cItemRem	:= (cAlias)->D1_ITEMREM
	cSerieRem	:= (cAlias)->D1_SERIREM
	cFornRem	:= (cAlias)->D1_FORNECE
	cLojaRem	:= (cAlias)->D1_LOJA
	cCodRem		:= (cAlias)->D1_COD

	nReg := Recno()
	dbSelectArea("SD1")
	SD1->(dbSetOrder(1))
	dbGoTop()
//	lRem := SD1->(dbSeek( xFilial("SD1")+cRemito+cSerieRem+cFornRem)) //+cLojaRem+cCodRem+Alltrim(cItemRem)))
	lRem := SD1->(dbSeek( xFilial("SD1")+cRemito+cSerieRem+cFornRem+cLojaRem+cCodRem+Alltrim(cItemRem)))
	If lRem .And. !lSC7 .And. !Empty( SD1->D1_PEDIDO)
		SC7->(dbSetOrder(1))
		lSC7 := SC7->(dbSeek( xFilial("SC7")+SD1->D1_PEDIDO+SD1->D1_ITEMPC))
	EndIf

	If lRem
		@ li, nIni+54 PSAY SD1->D1_DOC+" "+SD1->D1_ITEM
		@ li, nIni+70 PSAY SD1->D1_QUANT Picture cPQuant
		@ li, nIni+85 PSAY SD1->D1_EMISSAO
	EndIf
	If lSC7
		@ li, nIni+96  PSAY SC7->C7_NUM+" "+SC7->C7_ITEM
		@ li, nIni+107 PSAY SC7->C7_PRECO Picture cPValor
		@ li, nIni+125 PSAY SC7->C7_QUANT Picture cPQuant
		@ li, nIni+140 PSAY SC7->C7_DATPRF
		@ li, nIni+154 PSAY If( SC7->C7_PRECO!=SD1->D1_VUNIT, OemToAnsi(STR0069), OemToAnsi(STR0070))    //"Si", "No"
		@ li, nIni+161 PSAY If( lSD1, If( SC7->C7_DATPRF!=SD1->D1_EMISSAO, OemToAnsi(STR0069), OemToAnsi(STR0070)), "")  //"Si", "No"
	EndIf
	#IFNDEF TOP
		If mv_par01 <> 1
			dbSetOrder(nIndex+1)
		Endif
		dbGoto(nReg)
	#ENDIF
EndCase

nTotIt := nTotIt+1
nTotGe := nTotGe+1

Li:=Li+1

If li > 55
	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MR131TIT ³ Autor ³ Leonardo Ruben        ³ Data ³ 04.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Montagem do titulo do relatorio (antes e depois das pergs) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MR131Tit()
If mv_par01 = 1        // OC/RM/FT
	Titulo := OemToAnsi(STR0072)+cRetTitle+OemToAnsi(STR0073)+"/"+OemToAnsi(STR0074)  //"Relacion de Orden de Compras/Remitos/Facturas"
ElseIf mv_par01 = 2    // RM/OC/FT
	Titulo := OemToAnsi(STR0075)+cRetTitle+OemToAnsi(STR0073)+OemToAnsi(STR0076)+"/"+OemToAnsi(STR0074)  //"Relacion de Remitos/Orden de Compras/Facturas"

ElseIf mv_par01 = 3      // FT/RM/OC
	Titulo := OemToAnsi(STR0075)+OemToAnsi(STR0074)+"/"+cRetTitle+OemToAnsi(STR0073)+OemToAnsi(STR0076)  //"Relacion de Facturas/Remitos/Orden de Compras"
EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ AjustaSX1³ Autor ³ Totvs S/A             ³ Data ³ 25/03/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta Dicionario de Dados                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR441			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()

Local aAreaAnt	:= GetArea()
Local aAreaSX1	:= SX1->(GetArea())
Local nTamSX1   := Len(SX1->X1_GRUPO)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajustando o dicionario SX1        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX1")
dbSetOrder(1)
If dbSeek(PADR('MTR131',nTamSX1)+"06") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SBM'
	MsUnLock()
EndIf
If dbSeek(PADR('MTR131',nTamSX1)+"07") .And. Empty(SX1->X1_F3)
	RecLock("SX1",.F.)
	Replace X1_F3 With 'SBM'
	MsUnLock()
EndIf
RestArea(aAreaSX1)
RestArea(aAreaAnt)
Return