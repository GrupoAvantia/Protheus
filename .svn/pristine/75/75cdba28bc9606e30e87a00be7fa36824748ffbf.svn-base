#INCLUDE "Protheus.Ch"
#INCLUDE "MatrBol3.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATRBOL3  ºAutor  ³Marcello Gabriel    ºFecha ³ 11/12/2008  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Impressao dos livros de compras e vendas para demonstracao  º±±
±±º          ³dos dados gerados para os arquivos magneticos (DaVinci)     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Bolivia                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function MatrBol3()
Local cPerg		:= "MTRBO3"
Local cTitulo	:= STR0020 + " " + STR0021 + "/" + STR0022
Local cDescr1	:= STR0030
Local cNomeProg	:= "MATRBOL3" 
Local aArea		:= {}			

Private cAliasLCV	:= "LCV"
Private cTam		:= "G"
Private wnRel		:= ""
Private aReturn		:= {"Zebrado", 1,"Administracao", 2, 2, 1, "", 1}
Private lEnd		:= .F.
Private nLastKey	:= 0
Private m_pag		:= 1

aArea := GetArea()
AjustaSX1(cPerg)
Pergunte(cPerg,.F.)
wnRel := SetPrint("",cNomeProg,cPerg,cTitulo,cDescr1,,,.F.,,.T.,cTam,,.T.)
If !(nLastKey == 27)
	SetDefault(aReturn,cAliasLCV)
	If !(nLastKey == 27)
		RptStatus({|lEnd| BOL3Impr(@lEnd)},cTitulo)
	Endif
Endif
RestArea(aArea)
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³BOL3Impr  ºAutor  ³Marcello Gabrriel   ºFecha ³ 22/12/2008  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime o livro escolhido                                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MatrBol3 - Boliivia                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function BOL3Impr(lEnd)
Local nLin		:= 0
Local nPag		:= 0
Local nAltPag	:= 0
Local nLarPag	:= 0
Local nCpo		:= 0
Local nPos		:= 0
Local nEspTot	:= 0
Local cTitulo	:= ""
Local cTit		:= ""
Local aTitulo	:= {}
Local aDados	:= {}

Private aCposImp	:= {}
Private aTitDet		:= {}
Private aCab		:= {}
Private cLinDet		:= ""
Private nLinTit		:= 0
Private nTotFat		:= 0
Private nTotIC		:= 0
Private nTotIse		:= 0
Private nTotBas		:= 0
Private nTotIVA		:= 0

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Executa a rotina DaVinci() para geracao das informacoes ³
³fiscais                                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If MV_PAR03 == 1
	MsgRun(STR0002,STR0001,{|| DaVinci("C")}) //"Preparando as informacoes para o livro de compras"###"Livros fiscais"
	/*  
	Campos do detalhe: {nome do campos, tamanho para impressao, mascara, tipo, cabecalho} */
	Aadd(aCposImp,{"TIPONF",07,"","C",STR0003})						//"TIPO DE FATURA"
	Aadd(aCposImp,{"NIT",10,"","C",STR0004})							//"NIT DO FORNECEDOR"
	Aadd(aCposImp,{"RAZSOC",25,"","C",STR0005})						//"RAZAO SOCIAL"
	Aadd(aCposImp,{"NFISCAL",12,"","C",STR0006})						//"FATURA"
	Aadd(aCposImp,{"POLIZA",20,"","C",STR0007})						//"POLIZA DE IMPORTACAO"
	Aadd(aCposImp,{"NUMAUT",15,"","C",STR0008})						//"NUMERO DE AUTORIZACAO"
	Aadd(aCposImp,{"EMISSAO",10,"","D",STR0009})						//"DATA DA FATURA"
	Aadd(aCposImp,{"VALCONT",17,"@E 99,999,999,999.99","N",STR0010})	//"VALOR TOTAL DA FATURA"
	Aadd(aCposImp,{"ICE",17,"@E 99,999,999,999.99","N",STR0011})		//"VALOR ICE"
	Aadd(aCposImp,{"EXENTAS",17,"@E 99,999,999,999.99","N",STR0012})	//"VALORES ISENTOS OU A TAXA ZERO"
	Aadd(aCposImp,{"BASEIMP",17,"@E 99,999,999,999.99","N",STR0013})	//"VALOR SUJEITO A CREDITO FISCAL"
	Aadd(aCposImp,{"VALIMP",17,"@E 99,999,999,999.99","N",STR0014})	//"CREDITO FISCAL IVA"
	Aadd(aCposImp,{"CODCTR",14,"","C",STR0015})						//"CODIGO DE CONTROLE"
Else
	MsgRun(STR0016,STR0001,{|| DaVinci("V")}) //"Preparando as informacoes para o livro de vendas"###"Livros fiscais"
	/*  
	Campos do detalhe: {nome do campos, tamanho para impressao, mascara, tipo, cabecalho} */
	Aadd(aCposImp,{"NIT",10,"","C","NIT"})
	Aadd(aCposImp,{"RAZSOC",25,"","C",STR0005})						//"RAZAO SOCIAL"
	Aadd(aCposImp,{"NFISCAL",12,"","C",STR0006})						//"FATURA"
	Aadd(aCposImp,{"NUMAUT",15,"","C",STR0008})						//"NUMERO DE AUTORIZACAO"
	Aadd(aCposImp,{"EMISSAO",10,"","D",STR0009})						//"DATA DA FATURA"
	Aadd(aCposImp,{"VALCONT",17,"@E 99,999,999,999.99","N",STR0010})	//"VALOR TOTAL DA FATURA"
	Aadd(aCposImp,{"ICE",17,"@E 99,999,999,999.99","N",STR0011})	 	//"VALOR ICE"
	Aadd(aCposImp,{"EXENTAS",17,"@E 99,999,999,999.99","N",STR0012})	//"VALORES ISENTOS OU A TAXA ZERO"
	Aadd(aCposImp,{"BASEIMP",17,"@E 99,999,999,999.99","N",STR0017}) 	//"VALOR SUJEITO A DEBITO FISCAL"
	Aadd(aCposImp,{"VALIMP",17,"@E 99,999,999,999.99","N",STR0018})	//"DEBITO FISCAL IVA"
	Aadd(aCposImp,{"STATUSNF",9,"","C",STR0019})						//"ESTADO DA FATURA"
	Aadd(aCposImp,{"CODCTR",14,"","C",STR0015})						//"CODIGO DE CONTROLE"
Endif
/*
cria a linha de detalhe com a distribuicao dos campos para uso da funcao FmtLin() */
For nCpo := 1 To Len(aCposImp)
	If nCpo > 1
		cLinDet += " "
	Endif
	cLinDet += Replicate("#",aCposImp[nCpo,2])
Next
/*
Cria array com os cabecalhos dos campos, distribuindo as palavras ("quebrando o titulo") em linhas para que o titulo do campo caiba no espaco
que foi reservado para esse campo*/
(cAliasLCV)->(DbGoTop())
nLarPag := Len(cLinDet)
nAltPag := 62
nLinTit := 0
aTitDet := {}
For nCpo := 1 To Len(aCposImp)
	cTitulo := Upper(AllTrim(aCposImp[nCpo,5]))
	aTitulo := {}
	While !Empty(cTitulo)
		cTit := AllTrim(Substr(cTitulo,1,aCposImp[nCpo,2]))
		nPos := aCposImp[nCpo,2]
		If !Empty(Substr(cTitulo,aCposImp[nCpo,2] + 1,1))
			While nPos > 0 .And. !Empty(Substr(cTitulo,nPos,1))
				nPos--
			Enddo
			If nPos > 0
				cTit := Substr(cTitulo,1,nPos - 1)
			Else
				nPos := aCposImp[nCpo,2]
			Endif
		Endif
		//Os campos numericos sao "jogados" para a direita
		If aCposImp[nCpo,4] == "N"
			cTit := PadL(cTit,aCposImp[nCpo,2])
		Endif
		Aadd(aTitulo,cTit)
		cTitulo := LTrim(Substr(cTitulo,nPos + 1))
	Enddo
	nLinTit := Max(nLinTit,Len(aTitulo))
	Aadd(aTitDet,aTitulo)
Next
//
nTotFat	:= 0
nTotIC	:= 0
nTotIse	:= 0
nTotBas	:= 0
nTotIVA := 0
SetRegua((cAliasLCV)->(RecCount()))
While !((cAliasLCV)->(Eof())) .And. !lEnd
	/*
	Impressao do cabecalho do livro */
	nPag++
	nLin := BOL3Cabec(nPag)
	/*
	Impressao do detalhe do livro */
	nPos := 0
	While !((cAliasLCV)->(Eof())) .And. nLin < nAltPag .And. !lEnd
		aDados := {}
		For nCpo := 1 To Len(aCposImp)
			If Empty(aCposImp[nCpo,3])
				Aadd(aDados,PadR((cAliasLCV)->(FieldGet(FieldPos(aCposImp[nCpo,1]))),aCposImp[nCpo,2]))
			Else
				Aadd(aDados,Transform((cAliasLCV)->(FieldGet(FieldPos(aCposImp[nCpo,1]))),aCposImp[nCpo,3]))
			Endif
		Next
		FmtLin(aDados,cLinDet,,,@nLin)
		If AllTrim((cAliasLCV)->STATUSNF) == "V"
			nTotFat += (cAliasLCV)->VALCONT
			nTotIC  += (cAliasLCV)->ICE
			nTotIse += (cAliasLCV)->EXENTAS
			nTotBas += (cAliasLCV)->BASEIMP
			nTotIVA += (cAliasLCV)->VALIMP
		Endif
		If lEnd
			(cAliasLCV)->(DbGoBottom())
		Endif
		(cAliasLCV)->(DbSkip())
		IncRegua()
	Enddo
	If (cAliasLCV)->(Eof()) .And. !lEnd
		If nTotFat <> 0
	  		BOL3Totais()
		Endif
	Endif
Enddo
If Select(cAliasLCV) > 0
	DbSelectArea(cAliasLCV)
	DbCloseArea()
Endif
If lEnd
	cTit := Upper(AllTrim(STR0032))
	nPos := (nLarPag - Len(cTit)) / 2
	@PRow() + 2,nPos PSay cTit
Endif
//	
If aReturn[5]==1
   dbCommitAll()
   Set Printer To
   OurSpool(wnRel)
Endif
MS_FLUSH() 
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³BOL3CABEC ºAutor  ³Marcello Gabriel    ºFecha ³23/12/2008   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime o cabecalho do livro fiscal                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATRBOL3 - Bolivia                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function BOL3Cabec(nPag)
Local cTitLivro	:= ""
Local nLin		:= 0
Local nX		:= 0
Local nY		:= 0
Local nLarPag	:= 0
Local nColTit	:= 0
Local aDados	:= {}

nLarPag := Len(cLinDet)
/*
Titulo do livro */
cTitLivro := "IVA - " + STR0020 + " " + If(MV_PAR03 == 1,STR0021,STR0022)	//"LIVRO DE "###"COMPRAS"###"VENDAS"
nColTit := 0
nLin := 0
nColTit := (nLarPag - Len(cTitLivro)) / 2
@nLin,nColTit PSay cTitLivro
nLin++
@nLin,0 PSay Replicate("_",nLarPag)
nLin++
/*
1a linha do cabecalho: nome, filial e NIT */
@nLin,0 PSay STR0023 + ": " + AllTrim(SM0->M0_NOMECOM) //"Nome ou Razao social"
@nLin,nLarPag/2 PSay STR0024 + ": " + cFilAnt //"Filial"
@nLin,(nLarPag*3)/4 PSay "NIT: " + AllTrim(SM0->M0_CGC)
nLin++
/*
2a linha do cabecalho: endereco e periodo */
@nLin,0 PSay STR0025 + ": " + AllTrim(SM0->M0_ENDCOB)	//"Endereco"
@nLin,(nLarPag*3)/4 PSay STR0026 + ": " + Dtoc(MV_PAR01) + " - " + Dtoc(MV_PAR02)		//"Periodo"
nLin++
/*
3a linha do cabecalho: data e pagina */
@nLin,0 PSay STR0027 + ": " + Dtoc(dDataBase)	//"Apresentacao"
@nLin,(nLarPag*3)/4 PSay STR0028 + ": " + StrZero(nPag,4) //"Folio"
nLin++
@nLin,0 PSay Replicate("_",nLarPag)
nLin++
/*
Impressao do cabecalho das colunas */
For nY := 1 To nLinTit
	aDados := {}
	For nX := 1 To Len(aTitDet)
		Aadd(aDados,Space(aCposImp[nX,2]))
		If nY <= Len(aTitDet[nX])
			Adados[Len(aDados)] := aTitDet[nX,nY]
		Endif
	Next
	FmtLin(aDados,cLinDet,,,@nLin)
Next
@nLin,0 PSay Replicate("_",nLArPag)
/**/
nLin++
Return(nLin)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATRBOL3  ºAutor  ³Microsiga           ºFecha ³ 29/12/2008  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprimi a somatoria do livro fiscal e o nome do responsavel º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATRBOL3                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function BOL3Totais()
Local nCpo		:= 0
Local nLin		:= 0
Local nLarPag	:= 0
Local aDados	:= {}

nLin := PRow() + 1
nLarPag := Len(cLinDet)
/*
Impressao dos totais */
@nLin,0 PSay Replicate("_",nLArPag)
nLin++
For nCpo := 1 To Len(aCposImp)
	Aadd(aDados,Space(aCposImp[nCpo,2]))
Next
//titulo
nCpo := Ascan(aCposImp,{|cpo| cpo[1] == "NUMAUT"})
aDados[nCpo] := STR0031		//"Total"
//total da fatura
nCpo := Ascan(aCposImp,{|cpo| cpo[1] == "VALCONT"})
aDados[nCpo] := Transform(nTotFat,aCposImp[nCpo,3])
//I.C.E
nCpo := Ascan(aCposImp,{|cpo| cpo[1] == "ICE"})
aDados[nCpo] := Transform(nTotIC,aCposImp[nCpo,3])
//Isentos
nCpo := Ascan(aCposImp,{|cpo| cpo[1] == "EXENTAS"})
aDados[nCpo] := Transform(nTotIse,aCposImp[nCpo,3])
//Base
nCpo := Ascan(aCposImp,{|cpo| cpo[1] == "BASEIMP"})
aDados[nCpo] := Transform(nTotBas,aCposImp[nCpo,3])
//Imposto
nCpo := Ascan(aCposImp,{|cpo| cpo[1] == "VALIMP"})
aDados[nCpo] := Transform(nTotIVA,aCposImp[nCpo,3])
//
FmtLin(aDados,cLinDet,,,@nLin)
/*
Impressao do nome e CI do responsavel */
nLin++
MV_PAR04 := Upper(AllTrim(MV_PAR04))
MV_PAR05 := Upper(AllTrim(MV_PAR05))
@nLin,00 Psay PadC(MV_PAR04,20)
@nLin,40 Psay PadC(MV_PAR05,50)
nLin++
@nLin,00 Psay Replicate("_",20)
@nLin,40 Psay Replicate("_",50)
nLin++
@nLin,00 PSay PadC(Upper(Alltrim("CI")),20)
@nLin,40 PSay PadC(Upper(AllTrim(STR0029)),50)  //"NOME COMPLETO DO RESPONSAVEL"
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AJUSTASX1 ºAutor  ³Marcello Gabriel    ºFecha ³ 22/12/2008  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Criacao de parametros para a impressa dos livros fiscais    º±±
±±º          ³de compras e vendas.                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATRBOL3                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX1(cPerg)
Local aHelpP  := {}
Local aHelpE  := {}
Local aHelpS  := {}  

/*
 mv_par01 - Data Inicial*/
aHelpP  := {}
aHelpE  := {}
aHelpS  := {}
aHelpP	:= {"Informe a data inicial para geracao do","livro."}
aHelpE	:= {"Enter the initial date to generate the","record."}
aHelpS	:= {"Informe fecha inicial para generar el ","libro."}
PutSx1(cPerg,"01","Data inicial","Fecha inicial","Initial date","mv_ch1","D",8,0,0,"G",,,,,"mv_par01",,,,,,,,,,,,,,,,,aHelpP,aHelpE,aHelpS)
/*
mv_par02 - Data Final*/
aHelpP  := {}
aHelpE  := {}
aHelpS  := {}
aHelpP	:= {"Informe a data final para geracao do","livro."}
aHelpE	:= {"Enter the final date to generate the","record."}
aHelpS	:= {"Informe fecha final para generar el ","libro."}
PutSx1(cPerg,"02","Data final","Fecha Final","Final date","mv_ch2","D",8,0,0,"G",,,,,"mv_par02",,,,,,,,,,,,,,,,,aHelpP,aHelpE,aHelpS)
/*
mv_par01 - Livro: Compras ou Vendas*/
aHelpP  := {}
aHelpE  := {}
aHelpS  := {}
aHelpP	:= {"Informe o Livro que deseja imprimir:","compras ou vendas"}
aHelpE	:= {"Enter the Record you want to print:","Purchases or Sales"}
aHelpS	:= {"Informe el Libro que desea imprimir:","Compras o Ventas"}
PutSx1(cPerg,"03","Livro","Libro","Record","mv_ch3","N",1,0,0,"C",,,,,"mv_par03","Compras","Compras","Purchases",,"Vendas","Ventas","Sales",,,,,,,,,,aHelpP,aHelpE,aHelpS)
/*
mv_par04 - RG*/
aHelpP  := {}
aHelpE  := {}
aHelpS  := {}  
aHelpP	:= {"Informe o numero do C.I. do responsavel"}
aHelpE	:= {"Enter the responsible C.I. number"}
aHelpS	:= {"Informe el numero del C.I. del responsable"}
PutSx1(cPerg,"04","C.I.","C.I.","C.I.","mv_ch4","C",20,0,0,"G",,,,,"mv_par04",,,,,,,,,,,,,,,,,aHelpP,aHelpE,aHelpS)
/*
mv_par05 - Responsavel*/
aHelpP  := {}
aHelpE  := {}
aHelpS  := {}  
aHelpP	:= {"Informe o nome completo do responsavel."}
aHelpE	:= {"Enter the responsible full name"}
aHelpS	:= {"Informe el nombre completo del responsable."}
PutSx1(cPerg,"05","Responsavel","Responsable","Resonsible","mv_ch5","C",55,0,0,"G",,,,,"mv_par05",,,,,,,,,,,,,,,,,aHelpP,aHelpE,aHelpS)
Return()