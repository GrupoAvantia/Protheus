#INCLUDE "MATR445.CH"
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR445  ³ Autor ³ Marcos V. Ferreira    ³ Data ³ 30/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Analise da movimentacao de Estoques                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MATR445()

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef()
	oReport:PrintDialog()
Else
	MATR445R3()
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³Marcos V. Ferreira     ³ Data ³30/05/05  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR445			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local aOrdem 	 := {STR0006,STR0007,STR0008,STR0009}	//" Por Codigo "###" Por Tipo "###" Por Descricao "###" Por Grupo "
Local cPictQuant := PesqPict('SD3','D3_QUANT')	
Local cPictValor := PesqPict('SD3','D3_CUSTO1')
Local nQtdTam    := TamSX3('D3_QUANT')[1]
Local nVltTam    := TamSX3('D3_CUSTO1')[1]
Local oSession 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:= TReport():New("MATR445",STR0002,"MTR445", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003+" "+STR0004+" "+STR0005)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Perido de                                    ³
//³ mv_par02     // Periodo ate                                  ³
//³ mv_par03     // codigo de                                    ³
//³ mv_par04     // codigo ate                                   ³
//³ mv_par05     // local de                                     ³
//³ mv_par06     // local ate                                    ³
//³ mv_par07     // Moeda                                        ³
//³ mv_par08     // De  Tipo                                     ³
//³ mv_par09     // Ate Tipo                                     ³
//³ mv_par10     // Lista Prod. Sem Mov. Sim/N„o                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Sessao 1                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSession := TRSection():New(oReport,STR0034,{"SB1","SD1","SD2","SD3"},aOrdem) // "Analise da Movimentacao" ##"Movimentação dos Produtos"
oSession:SetTotalInLine(.F.)

oSession:SetNoFilter("SD1")
oSession:SetNoFilter("SD2")
oSession:SetNoFilter("SD3")

TRCell():New(oSession,'B1_COD'		,'SB1',STR0022				,/*Picture*/	,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSession,'B1_TIPO'		,'SB1',STR0023				,/*Picture*/	,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSession,'B1_GRUPO'	,'SB1',STR0024				,/*Picture*/	,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSession,'B1_DESC'		,'SB1',STR0025				,/*Picture*/	,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSession,'B1_UM'		,'SB1',STR0026				,/*Picture*/	,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSession,'QTDE_PER'	,'   ',STR0029+CRLF+STR0027	,cPictQuant		,nQtdTam	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSession,'VLR_PER'		,'   ',STR0029+CRLF+STR0028	,cPictValor		,nVltTam	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSession,'QTDE_MED'	,'   ',STR0030+CRLF+STR0027	,cPictQuant		,nQtdTam	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSession,'VLR_MED'		,'   ',STR0030+CRLF+STR0028	,cPictValor		,nVltTam	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSession,'VLR_COM'		,'   ',STR0032+CRLF+STR0031	,cPictValor		,nVltTam	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")

oSession:SetHeaderPage()
oSession:SetTotalText(STR0033) // "T o t a l   G e r a l :"
Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrint ³ Autor ³Marcos V. Ferreira   ³ Data ³30/05/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportPrint devera ser criada para todos  ³±±
±±³          ³os relatorios que poderao ser agendados pelo usuario.       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatorio                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR445			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

Local oSession := oReport:Section(1)
Local nOrdem   := oSession:GetOrder()
Local cMoeda   := StrZero(mv_par07,1,0)
Local aOrdem   := {STR0006,STR0007,STR0008,STR0009} 	//" Por Codigo "###" Por Tipo "###" Por Descricao "###" Por Grupo "
Local aVars    := {}
Local aProd    := {}
Local nSiVl    := 0
Local nSiQt    := 0
Local nQtCo    := 0
Local nVlCo    := 0
Local nQtMed   := 0
Local nVlMed   := 0 
Local lMov     := .F.
Local cAliasTop:= ''
Local cQueryD3 := ''
Local cProduto := ''
Local nTotRegs := 0
Local nX       := 0 
Local cFiltro  := ''
Local cQryOrdem:= ''
Local aSaldo   := {}
Local oBreak

Local aStrucSB1 := SB1->(dbStruct())      
Local cFilUsrSB1:= ""
Local cSelectSB1:= ""
Local cSelect   := ""
Local cName		:= ""

Private lVeic  := Upper(GetMV("MV_VEICULO"))=="S"

oReport:SetTitle(oReport:Title()+" - ("+AllTrim(aOrdem[oSession:GetOrder()])+STR0014+Dtoc(mv_par01)+STR0015+Dtoc(mv_par02))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da linha de SubTotal                               |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem == 2
	oBreak := TRBreak():New(oSession,oSession:Cell("B1_TIPO"),STR0017+STR0018,.F.)
ElseIf nOrdem == 4
	oBreak := TRBreak():New(oSession,oSession:Cell("B1_GRUPO"),STR0017+STR0019,.F.)
EndIf	

If nOrdem == 2 .Or. nOrdem == 4
	TRFunction():New(oSession:Cell('QTDE_PER'	),NIL,"SUM",oBreak,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.F.)
	TRFunction():New(oSession:Cell('VLR_PER'	),NIL,"SUM",oBreak,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.F.)
	TRFunction():New(oSession:Cell('QTDE_MED'	),NIL,"SUM",oBreak,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.F.)
	TRFunction():New(oSession:Cell('VLR_MED'	),NIL,"SUM",oBreak,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.F.)
	TRFunction():New(oSession:Cell('VLR_COM'	),NIL,"SUM",oBreak,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao da linha de Total Geral                            |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRFunction():New(oSession:Cell('QTDE_PER'	),NIL,"SUM",/*oBreak*/,STR0020,/*cPicture*/,/*uFormula*/,IIf(nOrdem == 2 .Or. nOrdem == 4 ,.F.,.T.),.F.)
TRFunction():New(oSession:Cell('VLR_PER'	),NIL,"SUM",/*oBreak*/,STR0020,/*cPicture*/,/*uFormula*/,IIf(nOrdem == 2 .Or. nOrdem == 4 ,.F.,.T.),.F.)
TRFunction():New(oSession:Cell('QTDE_MED'	),NIL,"SUM",/*oBreak*/,STR0020,/*cPicture*/,/*uFormula*/,IIf(nOrdem == 2 .Or. nOrdem == 4 ,.F.,.T.),.F.)
TRFunction():New(oSession:Cell('VLR_MED'	),NIL,"SUM",/*oBreak*/,STR0020,/*cPicture*/,/*uFormula*/,IIf(nOrdem == 2 .Or. nOrdem == 4 ,.F.,.T.),.F.)
TRFunction():New(oSession:Cell('VLR_COM'	),NIL,"SUM",/*oBreak*/,STR0020,/*cPicture*/,/*uFormula*/,IIf(nOrdem == 2 .Or. nOrdem == 4 ,.F.,.T.),.F.)

nTotRegs += SD1->(LastRec())
nTotRegs += SD2->(LastRec())
nTotRegs += SD3->(LastRec())

cFilUsrSB1:= oSession:GetAdvplExp()

#IFDEF TOP
	/*/ **** ATENCAO - O ORDER BY UTILIZA AS POSICOES DO SELECT, SE ALGUM CAMPO
	    **** FOR INCLUIDO NA QUERY OU ALTERADO DE LUGAR DEVE SER REVISTA A SINTAXE
	    **** DO ORDER BY
	    01.ARQ				02.PRODUTO		    03.TIPO				04.UM
	    05.GRUPO			06.DESCR		    07.CODITE			08.DATA
	    09.TES				10.CF		    	11.SEQUENCIA		12.DOCUMENTO
	    13.SERIE			14.QUANTIDADE		15.QUANT2UM			16.ARMAZEM
	    17.OP				18.FORNECEDOR		19.LOJA				20.TIPO NF
	    21.CODITE			22.REMITO		    23.TPDCENV			24.CUSTO1
	    25.CUSTO2			26.CUSTO3			27.CUSTO4			28.CUSTO5
	/*/                     
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtro adicional no SD3 para integracao com o modulo do WMS            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cQueryD3 := "%"
	If SuperGetMV('MV_D3SERVI',.F.,'N')=='N' .And. IntDL()
		cQueryD3 += " AND ( (SD3.D3_SERVIC = '   ') OR
		cQueryD3 += " (SD3.D3_SERVIC <> '   ' AND SD3.D3_TM <= '500') OR "
		cQueryD3 += " (SD3.D3_SERVIC <> '   ' AND SD3.D3_TM > '500' AND "
		cQueryD3 += " SD3.D3_LOCAL ='"+SuperGetMV('MV_CQ', .F., '98')+"') )"
	EndIf
	cQueryD3 += "%"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ordem selecionada para exibicao do relatorio                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cQryOrdem := "%"
	If nOrdem == 1
		cQryOrdem += IIf(!lVeic,"2,3,1,8,11","7,3,1,8,11")	//(PRODUTO/CODITE)+TIPO+ARQ+DATA+SEQUENCIA
	ElseIf nOrdem == 2
		cQryOrdem += IIf(!lVeic,"3,2,1,8,11","3,7,1,8,11")	//TIPO+(PRODUTO/CODITE)+ARQ+DATA+SEQUENCIA
	ElseIf nOrdem == 3
		cQryOrdem += "6,2,1,8,11" 							//DESCR+PRODUTO+ARQ+DATA+SEQUENCIA
	Else
		cQryOrdem += IIf(!lVeic,"5,2,1,8,11","5,7,1,8,11")	//GRUPO+(PRODUTO/CODITE)+ARQ+DATA+SEQUENCIA
	EndIf

	cQryOrdem += "%"

    If !Empty(cFilUsrSB1)
		For nX := 1 To SB1->(FCount())
			cName := SB1->(FieldName(nX))
		 	If AllTrim( cName ) $ cFilUsrSB1
	      		If aStrucSB1[nX,2] <> "M"  
	      			If !cName $ cSelect .And. !cName $ cSelectSB1
		        		cSelectSB1 += "SB1."+cName+"," 
		          	Endif 	
		       	EndIf
			EndIf 			       	
		Next
    Endif

  	If !Empty(cSelectSB1)  
	    cSelectSB1 := ","+Substr(cSelectSB1, 1, Len(cSelectSB1)-1) 
    EndIf

   	cSelectSB1 := "%"+cSelectSB1+"%" 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao SQL                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeSqlExpr(oReport:uParam)

	cAliasTop := GetNextAlias()    
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query do relatorio da Sessao 1                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):BeginQuery()			
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inicio do Embedded SQL                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	BeginSql Alias cAliasTop

		Column DATA as Date

		SELECT 'SD1' ARQ,				//01 - ARQUIVO
		       SB1.B1_COD PRODUTO,		//02 - PRODUTO
		       SB1.B1_TIPO TIPO,		//03 - TIPO
		       SB1.B1_UM UM,			//04 - UM
		       SB1.B1_GRUPO GRUPO,		//05 - GRUPO
		       SB1.B1_DESC DESCR,		//06 - DESCRICAO
		       SB1.B1_CODITE CODITE,	//07 - CODITE
		       D1_DTDIGIT DATA,			//08 - DATA
		       D1_TES TES,				//09 - TES
		       D1_CF CF,				//10 - CF
		       D1_NUMSEQ SEQUENCIA,		//11 - SEQUENCIA
		       D1_DOC DOCUMENTO,		//12 - DOCUMENTO	
		       D1_SERIE SERIE,			//13 - SERIE
		       D1_QUANT QUANTIDADE,		//14 - QUANTIDADE
		       D1_QTSEGUM QUANT2UM,		//14 - QUANT2UM
		       D1_LOCAL ARMAZEM,		//15 - ARMAZEM
		       ' ' OP,					//16 - OP
		       D1_FORNECE FORNECEDOR,	//17 - FORNECEDOR	
		       D1_LOJA LOJA,			//18 - LOJA
		       D1_TIPO TIPONF,			//19 - TIPONF
		       D1_REMITO REMITO,		//20 - REMITO
		       ' ' TPDCENV,				//21 - TPDCENV
		       D1_CUSTO CUSTO1,			//22 - CUSTO1
		       D1_CUSTO2 CUSTO2,		//23 - CUSTO2
		       D1_CUSTO3 CUSTO3,		//24 - CUSTO3
		       D1_CUSTO4 CUSTO4,		//25 - CUSTO4
		       D1_CUSTO5 CUSTO5,		//26 - CUSTO5
		       SD1.R_E_C_N_O_ NRECNO 	//27 RECNO
		       %Exp:cSelectSB1%

		FROM %table:SB1% SB1,%table:SD1% SD1,%table:SF4% SF4

		WHERE	SD1.D1_FILIAL  =  %xFilial:SD1%		AND SD1.D1_TES     =  SF4.F4_CODIGO		AND
				SD1.D1_DTDIGIT >= %Exp:mv_par01%	AND	SD1.D1_DTDIGIT <= %Exp:mv_par02%	AND
				SD1.D1_LOCAL   >= %Exp:mv_par05%	AND	SD1.D1_LOCAL   <= %Exp:mv_par06%	AND
				SD1.D1_ORIGLAN <> 'LF'		       	AND SD1.%NotDel% 						AND
				SB1.B1_FILIAL  =  %xFilial:SB1%		AND	SB1.B1_COD     =  SD1.D1_COD	   	AND
				SB1.B1_COD	   >= %Exp:mv_par03%	AND	SB1.B1_COD	   <= %Exp:mv_par04%	AND
				SB1.B1_TIPO	   >= %Exp:mv_par08%	AND	SB1.B1_TIPO	   <= %Exp:mv_par09%	AND
 			    SB1.%NotDel%						AND
 			    SF4.F4_FILIAL  =  %xFilial:SF4%		AND	SF4.F4_ESTOQUE =  'S'				AND
 			    SF4.%NotDel%						

		UNION

		SELECT 	'SD2' ARQ,
		        SB1.B1_COD PRODUTO,
		        SB1.B1_TIPO TIPO,
		        SB1.B1_UM UM,
		        SB1.B1_GRUPO GRUPO,
		        SB1.B1_DESC DESCR,
		        ' ' CODITE,
		        D2_EMISSAO DATA,
		        D2_TES TES,
				D2_CF CF,
				D2_NUMSEQ SEQUENCIA,
				D2_DOC DOCUMENTO,
				D2_SERIE SERIE,
				D2_QUANT QUANTIDADE,
				D2_QTSEGUM QUANT2UM,
				D2_LOCAL ARMAZEM,
				' ' OP,
				D2_CLIENTE FORNECEDOR,
				D2_LOJA LOJA,
				D2_TIPO TIPONF,
				D2_REMITO REMITO,
				D2_TPDCENV TPDCENV,
				D2_CUSTO1 CUSTO1,
				D2_CUSTO2 CUSTO2,
				D2_CUSTO3 CUSTO3,
				D2_CUSTO4 CUSTO4,
				D2_CUSTO5 CUSTO5,
		        SD2.R_E_C_N_O_ SD2RECNO  //27 RECNO
		        %Exp:cSelectSB1%
				
		FROM %table:SB1% SB1,%table:SD2% SD2, %table:SF4% SF4
		
		WHERE   SD2.D2_FILIAL 	=  %xFilial:SD2%	AND SD2.D2_TES 		=  SF4.F4_CODIGO	AND
				SD2.D2_EMISSAO 	>= %Exp:mv_par01%	AND	SD2.D2_EMISSAO 	<= %Exp:mv_par02%	AND
				SD2.D2_ORIGLAN 	<> 'LF'				AND	SD2.D2_LOCAL 	>= %Exp:mv_par05%	AND
				SD2.D2_LOCAL 	<= %Exp:mv_par06%	AND	SD2.%NotDel% 						AND
				SB1.B1_FILIAL   =  %xFilial:SB1%	AND	SB1.B1_COD 		=  SD2.D2_COD		AND
				SB1.B1_COD	    >= %Exp:mv_par03%	AND	SB1.B1_COD	    <= %Exp:mv_par04%	AND
			    SB1.B1_TIPO     >= %Exp:mv_par08%	AND SB1.B1_TIPO     <= %Exp:mv_par09%	AND
			    SB1.%NotDel% 						AND
			    SF4.F4_FILIAL	=  %xFilial:SF4%	AND	SF4.F4_ESTOQUE 	=  'S'				AND
			    SF4.%NotDel%
	
		UNION
			
		SELECT 	'SD3' ARQ, 
		        SB1.B1_COD PRODUTO,
		        SB1.B1_TIPO TIPO,
		        SB1.B1_UM UM, 
		        SB1.B1_GRUPO GRUPO,
		        SB1.B1_DESC DESCR,
		        ' ' CODITE,
		        D3_EMISSAO DATA,
		        D3_TM TES,
				D3_CF CF,
				D3_NUMSEQ SEQUENCIA,
				D3_DOC DOCUMENTO,
				' ' SERIE,
				D3_QUANT QUANTIDADE,
				D3_QTSEGUM QUANT2UM,
				D3_LOCAL ARMAZEM,
				D3_OP OP,
				' ' FORNECEDOR,
				' ' LOJA,
				' ' TIPONF,
				' ' REMITO,
				' ' TPDCENV,
				D3_CUSTO1 CUSTO1,
				D3_CUSTO2 CUSTO2,
				D3_CUSTO3 CUSTO3,
				D3_CUSTO4 CUSTO4,
				D3_CUSTO5 CUSTO5,
		        SD3.R_E_C_N_O_ SD3RECNO  //27 RECNO
		       %Exp:cSelectSB1%
					
		FROM %table:SB1% SB1,%table:SD3% SD3
			
		WHERE	SD3.D3_FILIAL	=  %xFilial:SD3%	AND	SD3.D3_COD		=  SB1.B1_COD  		AND
				SD3.D3_EMISSAO	>= %Exp:mv_par01%	AND	SD3.D3_EMISSAO	<= %Exp:mv_par02%	AND	
				SD3.D3_LOCAL	>= %Exp:mv_par05%	AND	SD3.D3_LOCAL	<= %Exp:mv_par06%	AND
				SD3.D3_ESTORNO	<> 'S'				AND	SD3.%NotDel%						AND
				SB1.B1_FILIAL   =  %xFilial:SB1%	AND	SB1.B1_TIPO     >= %Exp:mv_par08%	AND	
				SB1.B1_TIPO     <= %Exp:mv_par09%	AND	SB1.B1_COD	    >= %Exp:mv_par03%	AND
				SB1.B1_COD	    <= %Exp:mv_par04%	AND	SB1.%NotDel%
				%Exp:cQueryD3%
	
		UNION
			
		SELECT 	'SB1' ARQ,
		        SB1.B1_COD PRODUTO,
		        SB1.B1_TIPO TIPO,
		        SB1.B1_UM UM, 
		        SB1.B1_GRUPO GRUPO,
		        SB1.B1_DESC DESCR,
		        ' ' CODITE,
		        ' ' DATA,
		        ' ' TES,
		        ' ' CF,
		        ' ' SEQUENCIA,
		        ' ' DOCUMENTO,
		        ' ' SERIE,
				0 QUANTIDADE,
				0 QUANT2UM,
				' ' ARMAZEM,
				' ' OP,
				' ' FORNECEDOR,
				' ' LOJA,
				' ' TIPONF,
				' ' REMITO,
				' ' TPDCENV,
				0 CUSTO1,
				0 CUSTO2,
				0 CUSTO3,
				0 CUSTO4,
				0 CUSTO5,
				0 SB1RECNO // 27
		       %Exp:cSelectSB1%
					
		FROM %table:SB1% SB1
	
		WHERE 	SB1.B1_FILIAL   = %xFilial:SB1%		AND SB1.B1_COD 		>= %Exp:mv_par03%	AND
			  	SB1.B1_COD 		<= %Exp:mv_par04%	AND SB1.B1_TIPO 	>= %Exp:mv_par08%	AND
				SB1.B1_TIPO 	    <= %Exp:mv_par09%	AND	SB1.B1_LOCPAD >= %Exp:mv_par05%	AND SB1.B1_LOCPAD <= %Exp:mv_par06% AND
				SB1.%NotDel%
				
	
		ORDER BY %Exp:cQryOrdem%
	
   	EndSql

	oReport:Section(1):EndQuery()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Abertura do arquivo de trabalho                              |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAliasTop)
	oReport:SetMeter(nTotRegs)
			
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicio da impressao da Sessao 1                              |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSession:Init()

	While !oReport:Cancel() .And. !Eof() .And. (mv_par01 <= mv_par02)
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera filtro escolhido                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea(cAliasTop)
		If !Empty(cFilUsrSB1)
		    If !(&(cFilUsrSB1))
		       dbSkip()
	    	   Loop
	    	EndIf   
		EndIf

		cProduto := (cAliasTop)->PRODUTO
		lMov	 := .F. // Validacao para produtos sem Movimentacao
		aProd    := {}
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Limpa array de saldos e o inicializa com as datas            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aVars:= {}
		For nX:=mv_par01 to mv_par02    // Montando Variaveis de Calculo
			Aadd(Avars,{nX,0,0})
		Next
		nSiVl := 0
		nSiQt := 0
		nQtCo := 0
		nVlCo := 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ SB1 - Verifica Produtos Sem Movimento						 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea(cAliasTop)
		While !Eof() .And. (cAliasTop)->PRODUTO == cProduto .And. Alltrim((cAliasTop)->ARQ) == "SB1"
			aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
			dbSkip()
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD1 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea(cAliasTop)
		Do While !oReport:Cancel() .And. !Eof() .And. (cProduto == (cAliasTop)->PRODUTO) .And. Alltrim((cAliasTop)->ARQ) == "SD1"
			oReport:IncMeter()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza os movimentos feitos por remito.                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc # "BRA" .And. !Empty(AllTrim((cAliasTop)->REMITO))
				dbSkip()
				Loop
			EndIf
			dbSelectArea("SF4")
			If MsSeek(xFilial("SF4")+(cAliasTop)->TES)
				dbSelectArea(cAliasTop)
				If SF4->F4_ESTOQUE == "S"
					lMov:= .T.
					nX:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
					aVars[nX,2] += (cAliasTop)->QUANTIDADE
					aVars[nX,3] += &(cAliasTop+'->CUSTO'+cMoeda)
					nQtco += (cAliasTop)->QUANTIDADE
					nVlco += &(cAliasTop+'->CUSTO'+cMoeda)
				EndIf
			EndIf	
			dbSelectArea(cAliasTop)
			aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD2 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea(cAliasTop)
		Do While !oReport:Cancel() .And. !Eof() .And. (cProduto == (cAliasTop)->PRODUTO) .And. Alltrim((cAliasTop)->ARQ) == "SD2"
			oReport:IncMeter()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza os movimentos feitos por remito.                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc # "BRA"  .And. !Empty(AllTrim((cAliasTop)->REMITO)) .And. !((cAliasTop)->TPDCENV $ "1A")
				dbSkip()
				Loop
			EndIf
			dbSelectArea("SF4")
			If MsSeek(xFilial("SF4")+(cAliasTop)->TES)
				dbSelectArea(cAliasTop)
				If SF4->F4_ESTOQUE == "S"
					lMov:= .T.
					nX:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
					aVars[nX,2] -= (cAliasTop)->QUANTIDADE
					aVars[nX,3] -= &(cAliasTop+'->CUSTO'+cMoeda)
				EndIf
			EndIf
			dbSelectArea(cAliasTop)
			aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
			dbSkip()
		EndDo
				
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD3 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea(cAliasTop)
		Do While !oReport:Cancel() .And. !Eof() .And. (cProduto == (cAliasTop)->PRODUTO) .And. Alltrim((cAliasTop)->ARQ) == "SD3"
			oReport:IncMeter()
			lMov:= .T.
			If (cAliasTop)->TES > "500"
				nX:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
				aVars[nX,2] -= (cAliasTop)->QUANTIDADE
				aVars[nX,3] -= &(cAliasTop+'->CUSTO'+cMoeda)
			Else
				nX:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
				aVars[nX,2] += (cAliasTop)->QUANTIDADE
				aVars[nX,3] += &(cAliasTop+'->CUSTO'+cMoeda)
			EndIf
			aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
			dbSkip()
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula Saldo inicial pelo SB9 e monta variaveis             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB2")
		dbSetOrder(1)
		MsSeek(xFilial("SB2")+cProduto+Alltrim(mv_par05),.T.)
		While !Eof() .And. (B2_COD == cProduto .And. B2_LOCAL <= mv_par06)
			aSaldo := CalcEst(SB2->B2_COD,SB2->B2_LOCAL,mv_par01)
			nSiVl  += aSaldo[mv_par07+1]
			nSiQt  += aSaldo[1]
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma linha do array c/ linha anterior para ter o saldo do prd.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nQtMed:= 0
		nVlMed:= 0
		For nX:= 1 to Len(aVars)
			If nX == 1
				aVars[1,2] += nSiQt
				aVars[1,3] += nSiVl
			Else
				aVars[nX,2] += aVars[nX-1,2]
				aVars[nX,3] += aVars[nX-1,3]
			EndIf
			nQtMed += aVars[nX,2]
			nVlMed += aVars[nX,3]
		Next
		nQtMed:= nQtMed / Len (aVars)
		nVlMed:= nVlMed / Len (aVars)

		If mv_par10 == 1 .Or. (mv_par10 == 2 .And. lMov)

			If !lVeic
				oSession:Cell('B1_COD'):SetValue(aProd[1]) // PRODUTO
			Else
				oSession:Cell('B1_COD'):SetValue(aProd[2] + " " + aProd[1]) // CODITE + PRODUTO
			EndIf

			oSession:Cell('B1_TIPO'	):SetValue(aProd[3]) 	// TIPO
			oSession:Cell('B1_GRUPO'):SetValue(aProd[4]) 	// GRUPO
			oSession:Cell('B1_DESC'	):SetValue(aProd[5])	// DESCR
			oSession:Cell('B1_UM'	):SetValue(aProd[6])	// UM
			oSession:Cell('QTDE_PER'):SetValue(aVars[Len(aVars),2])
			oSession:Cell('VLR_PER' ):SetValue(aVars[Len(aVars),3])
			oSession:Cell('QTDE_MED'):SetValue(nQtMed)
			oSession:Cell('VLR_MED' ):SetValue(nVlMed)
			oSession:Cell('VLR_COM' ):SetValue(nVlco/nQtco)
	
			oSession:PrintLine()

		EndIf

		dbSelectArea(cAliasTop)

	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Termino da impressao da Sessao 1                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSession:Finish()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Encerra a Query de Trabalho                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	(cAliasTop)->(dbCloseArea())
	
#ELSE

	dbSelectArea("SB1")
	dbSetOrder(nOrdem)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao SQL                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeAdvplExpr(oReport:uParam)

	cFiltro := "B1_FILIAL == '" + xFilial('SB1') + "' .And. "
	If !lVeic
		cFiltro += "B1_COD      >= '" + mv_par03 + "' .And. B1_COD      <= '" + mv_par04 + "' .And. "
	Else
		cFiltro += "B1_CODITE   >= '" + mv_par03 + "' .And. B1_CODITE   <= '" + mv_par04 + "' .And. "
	EndIf	
	cFiltro += "B1_TIPO  >= '" + mv_par08 + "' .And. B1_TIPO  <= '" + mv_par09 + "' "
		
	oReport:Section(1):SetFilter(cFiltro,IndexKey())
		
	oReport:SetMeter(nTotRegs)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicio da impressao da Sessao 1                              |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSession:Init()
		
	While !oReport:Cancel() .And. !Eof() .And. B1_FILIAL == xFilial("SB1") .And. (mv_par01 <= mv_par02)
			
		oReport:IncMeter()

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera filtro escolhido                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cFilUsrSB1)
		    If !(&(cFilUsrSB1))
		       dbSkip()
	    	   Loop
	    	EndIf   
		EndIf

		lMov   := .F. // Validacao para produtos sem Movimentacao
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Limpa array de saldos e o inicializa com as datas            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aVars:= {}
		For nX:=mv_par01 To mv_par02    // Montando Variaveis de Calculo
			Aadd(aVars,{nX,0,0})
		Next
		nSiVl := 0
		nSiQt := 0
		nQtCo := 0
		nVlCo := 0
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD1 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD1")
		dbSetOrder(7)
		MsSeek(xFilial("SD1")+SB1->B1_COD,.T.)
		Do While !oReport:Cancel() .And. !Eof() .And. (D1_FILIAL+D1_COD == xFilial("SD1")+SB1->B1_COD)
			oReport:IncMeter()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If D1_ORIGLAN $ "LF"
				dbSkip()
				Loop
			EndIf
			If D1_LOCAL < mv_par05 .Or. D1_LOCAL > mv_par06
				dbSkip()
				Loop
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Os Movimentos feitos por remito.                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc # "BRA"  .And. !Empty(SD1->D1_REMITO)
				dbSkip()
				Loop
			EndIf
			dbSelectArea("SF4")
			If MsSeek(xFilial("SF4")+SD1->D1_TES)
				dbSelectArea("SD1")
				If SF4->F4_ESTOQUE == "S"
					If D1_DTDIGIT < mv_par01 .Or. D1_DTDIGIT > mv_par02
						dbSkip()
						Loop
					Else
						lMov:= .T.
						nX:=Ascan(aVars, {|x| x[1] == D1_DTDIGIT })
						aVars[nX,2] += D1_QUANT
						aVars[nX,3] += &("D1_CUSTO"+IIf(cMoeda == "1"," ",cMoeda))
						nQtco += D1_QUANT
						nVlco += &("D1_CUSTO"+IIf(cMoeda == "1"," ",cMoeda))
					EndIf
				EndIf
			EndIf	
			dbSelectArea("SD1")
			dbSkip()
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD2 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD2")
		dbSetOrder(6)
		MsSeek(xFilial("SD2")+SB1->B1_COD,.T.)
		Do While !oReport:Cancel() .And. !Eof() .And. (D2_FILIAL+D2_COD == xFilial("SD2")+SB1->B1_COD)
			oReport:IncMeter()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If D2_ORIGLAN == "LF"
				dbSkip()
				Loop
			EndIf
			If D2_LOCAL < mv_par05 .Or. D2_LOCAL > mv_par06
				dbSkip()
				Loop
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza os movimentos feitos por remito.                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc # "BRA"  .And. !Empty(SD2->D2_REMITO) .And. !(SD2->D2_TPDCENV $ "1A")
				SD2->(DbSkip())
				Loop
			EndIf
			dbSelectArea("SF4")
			If MsSeek(xFilial("SF4")+SD2->D2_TES)
				dbSelectArea("SD2")
				If SF4->F4_ESTOQUE == "S"
					If D2_EMISSAO < mv_par01 .Or.  D2_EMISSAO > mv_par02
						dbSkip()
						Loop
					Else
						lMov:= .T.
						nX:=Ascan(aVars, {|x| x[1] == D2_EMISSAO })
						aVars[nX,2] -= D2_QUANT
						aVars[nX,3] -= &("D2_CUSTO"+cMoeda)
					EndIf
				EndIf
			EndIf	
			dbSelectArea("SD2")
			dbSkip()
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD3 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD3")
		dbSetOrder(7)
		MsSeek(xFilial("SD3")+SB1->B1_COD,.T.)
		Do While !oReport:Cancel() .And. !Eof() .And. (D3_FILIAL+D3_COD == xFilial("SD3")+SB1->B1_COD)
			oReport:IncMeter()
			If D3_LOCAL < mv_par05 .Or. D3_LOCAL > mv_par06
				dbSkip()
				Loop
			EndIf
			If D3_ESTORNO # "S"
				If D3_TM > "500"
					If D3_EMISSAO < mv_par01 .Or.  D3_EMISSAO > mv_par02
						dbSkip()
						Loop
					Else
						lMov:= .T.
						nX:=Ascan(aVars, {|x| x[1] == D3_EMISSAO })
						aVars[nX,2] -= D3_QUANT
						aVars[nX,3] -= &("D3_CUSTO"+cMoeda)
					EndIf
				Else
					If D3_EMISSAO < mv_par01 .Or.  D3_EMISSAO > mv_par02
						dbSkip()
						Loop
					Else
						lMov:= .T.
						nX:=Ascan(aVars, {|x| x[1] == D3_EMISSAO })
						aVars[nX,2] += D3_QUANT
						aVars[nX,3] += &("D3_CUSTO"+cMoeda)
					EndIf
				EndIf
			EndIf
			dbSkip()
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula Saldo inicial pelo SB9 e monta variaveis             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB2")
		dbSetOrder(1)
		MsSeek(xFilial("SB2")+SB1->B1_COD+Alltrim(mv_par05),.T.)
		While !Eof() .And. (B2_COD == SB1->B1_COD .And. B2_LOCAL <= mv_par06)
			aSaldo := CalcEst(SB2->B2_COD,SB2->B2_LOCAL,mv_par01)
			nSiVl  += aSaldo[mv_par07+1]
			nSiQt  += aSaldo[1]
			dbSkip()
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma linha do array c/ linha anterior para ter o saldo do prd.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nQtMed:= 0
		nVlMed:= 0
		For nX:= 1 to Len(aVars)
			If nX == 1
				aVars[1,2] += nSiQt
				aVars[1,3] += nSiVl
			Else
				aVars[nX,2] += aVars[nX-1,2]
				aVars[nX,3] += aVars[nX-1,3]
			EndIf
			nQtMed += aVars[nX,2]
			nVlMed += aVars[nX,3]
		Next
		
		nQtMed:= nQtMed / Len (aVars)
		nVlMed:= nVlMed / Len (aVars)
		
		If mv_par10 == 1 .Or. (mv_par10 == 2 .And. lMov)
		
			If !lVeic
				oSession:Cell('B1_COD'):SetValue(SB1->B1_COD)
			Else
				oSession:Cell('B1_COD'):SetValue(SB1->B1_CODITE + " " + SB1->B1_COD)
			EndIf
		
			oSession:Cell('QTDE_PER'):SetValue(aVars[Len(aVars),2])
			oSession:Cell('VLR_PER' ):SetValue(aVars[Len(aVars),3])
			oSession:Cell('QTDE_MED'):SetValue(nQtMed)
			oSession:Cell('VLR_MED' ):SetValue(nVlMed )
			oSession:Cell('VLR_COM' ):SetValue(nVlco/nQtco)
		
			oSession:PrintLine()
		
		EndIf
		
		dbSelectArea("SB1")
		dbSkip()
		
	EndDo
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Termino da impressao da Sessao 1                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSession:Finish()
	
#ENDIF	
		
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR445R3³ Autor ³ Wilson Junior         ³ Data ³ 05/08/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Analise da movimentcao de Estoques                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Rodrigo Sart³23/03/98³15001A³ Acerto na validacao sobre movimentacao   ³±±
±±³            ³        ³      ³ de produtos.                             ³±±
±±³CesarValadao³30/03/99³XXXXXX³ Manutencao na SetPrint()                 ³±±
±±³CesarValadao³14/07/99³21855A³ Acerto no Parametro Tipo De/Ate          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcos Hirak³25/06/04³XXXXXX³Imprimir B1_CODITE quando for gestao de   ³±±
±±³            ³        ³      ³Concessionarias ( MV_VEICULO = "S")       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATR445R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL Tamanho  :="M"
LOCAL cRodaTxt := STR0001		//"REGISTRO(S)"
LOCAL nCntImpr := 0
LOCAL titulo   := STR0002		//"Analise da Movimentacao"
LOCAL cDesc1   := STR0003		//"Este relat¢rio emite a an lise da movimenta‡„o dos estoques dos produtos"
LOCAL cDesc2   := STR0004		//"num determinado periodo, sendo que este deve estar registrados nos ar-"
LOCAL cDesc3   := STR0005		//"quivos de movimenta‡„o."
LOCAL cString  := "SB1"
LOCAL wnrel    := "MATR445"
LOCAL nTamSX1  := Len(SX1->X1_GRUPO)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Local para SIGAVEI, SIGAPEC e SIGAOFI         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL aArea1	:= Getarea()
LOCAL lEnd		:= .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private para SIGAVEI, SIGAPEC e SIGAOFI       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE lVeic		:= UPPER(GETMV("MV_VEICULO"))=="S"
PRIVATE aSB1Cod		:= {}
PRIVATE aSB1Ite		:= {}
PRIVATE nCOL1		:= 0
PRIVATE xSB1, xSB2, xSD1, xSD2, xSD3, xSF4

dbSelectArea("SB1")
xSB1 := xFilial("SB1")
dbSelectArea("SB2")
xSB2 := xFilial("SB2")
dbSelectArea("SD1")
xSD1 := xFilial("SD1")
dbSelectArea("SD2")
xSD2 := xFilial("SD2")
dbSelectArea("SD3")
xSD3 := xFilial("SD3")
dbSelectArea("SF4")
xSF4 := xFilial("SF4")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aOrd     := {OemToAnsi(STR0006),OemToAnsi(STR0007),OemToAnsi(STR0008),OemToAnsi(STR0009)}		//" Por Codigo         "###" Por Tipo           "###" Por Descricao     "###" Por Grupo        "
PRIVATE aReturn  := {OemToAnsi(STR0010), 1,OemToAnsi(STR0011), 2, 2, 1, "",1 }		//"Zebrado"###"Administracao"
PRIVATE nLastKey := 0 ,cPerg := "MTR445"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Perido de                                    ³
//³ mv_par02     // Periodo ate                                  ³
//³ mv_par03     // codigo de                                    ³
//³ mv_par04     // codigo ate                                   ³
//³ mv_par05     // local de                                     ³
//³ mv_par06     // local ate                                    ³
//³ mv_par07     // Moeda                                        ³
//³ mv_par08     // De  Tipo                                     ³
//³ mv_par09     // Ate Tipo                                     ³
//³ mv_par10     // Lista Prod. Sem Mov. Sim/N„o                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajustar o SX1 para SIGAVEI, SIGAPEC e SIGAOFI                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aSB1Cod	:= TamSX3("B1_COD")
aSB1Ite	:= TamSX3("B1_CODITE")

If lVeic
	Tamanho := "G"
	nCOL1	:= ABS(aSB1Cod[1] - aSB1Ite[1]) + 1 +  aSB1Cod[1]
	dbSelectArea("SX1")
	dbSetOrder(1)
	dbSeek(PADR(cPerg,nTamSX1))
	Do While SX1->X1_GRUPO == PADR(cPerg,nTamSX1) .And. !SX1->(Eof())
		If "PRODU" $ Upper(SX1->X1_PERGUNT) .And. Upper(SX1->X1_TIPO) == "C" .And. ;
			(SX1->X1_TAMANHO <> aSB1Ite[1] .Or. Upper(SX1->X1_F3) <> "VR4")
			
			RecLock("SX1",.F.)
			SX1->X1_TAMANHO := aSB1Ite[1]
			SX1->X1_F3 := "VR4"
			dbCommit()
			MsUnlock()
		EndIf
		dbSkip()
	EndDo
	dbCommitAll()
	RestArea(aArea1)
Else
	dbSelectArea("SX1")
	dbSetOrder(1)
	dbSeek(PADR(cPerg,nTamSX1))
	Do While SX1->X1_GRUPO == PADR(cPerg,nTamSX1) .And. !SX1->(Eof())
		If "PRODU" $ Upper(SX1->X1_PERGUNT) .And. Upper(SX1->X1_TIPO) == "C" .And. ;
			(SX1->X1_TAMANHO <> aSB1Cod[1] .OR. UPPER(SX1->X1_F3) <> "SB1")
			
			RecLock("SX1",.F.)
			SX1->X1_TAMANHO := aSB1Cod[1]
			SX1->X1_F3 := "SB1"
			dbCommit()
			MsUnlock()
			
		EndIf
		dbSkip()
	EndDo
	dbCommitAll()
	RestArea(aArea1)
EndIf

Pergunte(cPerg,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,Tamanho)

If nLastKey = 27
	Set Filter To
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter To
	Return
EndIf

If mv_par02 - mv_par01 > 4090
	Set Printer To
	Set Device to Screen
	Help(" ",1,"R445PARAM")
	Set Filter To
	Return Nil
EndIf

RptStatus({|lEnd| R445Imp(@lEnd,wnRel,titulo,Tamanho)},alltrim(titulo) + " - " + aOrd[areturn[8]])

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R445IMP  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 14.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR445			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R445Imp(lEnd,WnRel,titulo,Tamanho)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis locais e private exclusivas deste programa         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cCond1 ,lContinua:=.t., aVars:={}, nSiVl:=0 ,nSiQt:=0
LOCAL ni ,nQtCo:=0 ,nVlCo:=0 ,nQtMed:=0 ,nVlMed:=0 ,nSsVl:=0 ,nSmVl:=0
LOCAL nTsVl:=0, nTmVl:=0 ,cMoeda, aSaldo

LOCAL nTipo    := 0
LOCAL lMov	   :=.F.		// Flag que indica se ocorreu movimenta‡„o
LOCAL nDecs    := GetMv("MV_CENT"+(IIf(mv_par07 > 1 , Str(mv_par07,1),"")))
LOCAL cPicture := IIf(cPaisLoc == "BRA","@E 999,999,999.99",PesqPict("SD3","D3_CUSTO"+STR(mv_par07,1),16,mv_par07) )
LOCAL cPictTot := IIf(cPaisLoc == "BRA",cPicture,PesqPict("SD3","D3_CUSTO"+STR(mv_par07,1),19,mv_par07) )
LOCAL nDesp	   := IIf(cPaisLoc == "BRA",0,-3)
LOCAL aTam     := TamSX3("B9_QINI")
LOCAL nDecs1   := aTam[2]
LOCAL lQuery   := .F.
LOCAL cQuery   := ""
LOCAL cQueryD1 := ""
LOCAL cQueryD2 := ""
LOCAL cQueryD3 := ""
LOCAL cQueryB1A:= ""
LOCAL cQueryB1C:= ""
LOCAL cQueryB1 := ""
LOCAL cProduto := ""
LOCAL aProd    := {}
LOCAL cAliasTop:= CriaTrab("",.F.)
LOCAL nTotRegs := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Local para SIGAVEI, SIGAPEC e SIGAOFI         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cArqB1 := "", cIndB1 := "", nIndB1 := 0
LOCAL lT

PRIVATE cGrpAnt,cTipAnt

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE li    := 80
PRIVATE m_pag := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cabec1 := STR0012	//"CODIGO          TP GRUP DESCRICAO                   UM        SALDO   DO   PERIODO         ESTOQUE MEDIO DO PERIODO      VALOR MEDIO"
PRIVATE cabec2 := STR0013	//"                                                           QUANTIDADE          VALOR      QUANTIDADE          VALOR      DAS COMPRAS"
*************      123456789012345 12 1234 123456789012345678901234567 12  99,999,999.99 999,999,999.99   99,999,999.99 999,999,999.99   999,999,999.99
*************      0         1         2         3         4         5         6         7         8         9        10        11        12        13
*************      0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012

If lVeic
	cabec1  := substr(cabec1 ,1,aSB1Cod[1]) + SPACE(nCOL1) + substr(cabec1 ,aSB1Cod[1]+1)
	cabec2  := substr(cabec2 ,1,aSB1Cod[1]) + SPACE(nCOL1) + substr(cabec2 ,aSB1Cod[1]+1)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os codigos de caracter Comprimido/Normal da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Soma a ordem escolhida ao titulo do relatorio                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("NewHead")#"U"
	NewHead += " ("+AllTrim(aOrd[aReturn[8]])+STR0014+Dtoc(mv_par01)+STR0015+Dtoc(mv_par02)		//") de "###" ate "
Else
	Titulo  += " ("+AllTrim(aOrd[aReturn[8]])+STR0014+Dtoc(mv_par01)+STR0015+Dtoc(mv_par02)		//") de "###" ate "
EndIf

nTotRegs += SD1->(LastRec())
nTotRegs += SD2->(LastRec())
nTotRegs += SD3->(LastRec())

#IFDEF TOP
	If !(TcSrvType()=="AS/400") .And. !("POSTGRES" $ TCGetDB())
		// **** ATENCAO - O ORDER BY UTILIZA AS POSICOES DO SELECT, SE ALGUM CAMPO
		// **** FOR INCLUIDO NA QUERY OU ALTERADO DE LUGAR DEVE SER REVISTA A SINTAXE
		// **** DO ORDER BY
		// 1 ARQ
		// 2 PRODUTO
		// 3 TIPO
		// 4 UM
		// 5 GRUPO
		// 6 DESCR
		// 7 CODITE
		// 8 DATA
		// 9 TES
		// 10 CF
		// 12 SEQUENCIA
		// 13 DOCUMENTO
		// 14 SERIE
		// 15 QUANTIDADE
		// 16 QUANT2UM
		// 17 ARMAZEM
		// 18 OP
		// 19 FORNECEDOR
		// 20 LOJA
		// 21 TIPO NF
		// 22 CODITE
		// 23 REMITO
		// 24 TPDCENV
		// 25 CUSTO
		// 26 RECNO
		
		lQuery 	  := .T.
		cQueryD1:= "SELECT 'SD1' ARQ,SB1.B1_COD PRODUTO,SB1.B1_TIPO TIPO,SB1.B1_UM UM,SB1.B1_GRUPO GRUPO,SB1.B1_DESC DESCR,B1_CODITE CODITE,D1_DTDIGIT DATA,D1_TES TES,D1_CF CF,D1_NUMSEQ SEQUENCIA,D1_DOC DOCUMENTO,D1_SERIE SERIE,D1_QUANT QUANTIDADE,D1_QTSEGUM QUANT2UM,D1_LOCAL ARMAZEM,'' OP,D1_FORNECE FORNECEDOR,D1_LOJA LOJA,D1_TIPO TIPONF,D1_REMITO REMITO,'' TPDCENV,D1_CUSTO"
		// COLOCA A MOEDA DO CUSTO
		If mv_par07 > 1
			cQueryD1+= Str(mv_par07,1,0)
		EndIf
		cQueryD1 += " CUSTO,SD1.R_E_C_N_O_ NRECNO FROM "
		cQueryD1 += RetSqlName("SB1") + " SB1 , "+ RetSqlName("SD1")+ " SD1 , "+ RetSqlName("SF4")+" SF4 "
		cQueryD1 += " WHERE SB1.B1_COD = D1_COD AND D1_FILIAL = '"+xFilial("SD1")+"'"
		cQueryD1 += " AND F4_FILIAL = '"+xFilial("SF4")+"' AND SD1.D1_TES = F4_CODIGO AND F4_ESTOQUE = 'S'"
		cQueryD1 += " AND D1_DTDIGIT >= '"+DTOS(mv_par01)+"' AND D1_DTDIGIT <= '"+DTOS(mv_par02)+"'"
		cQueryD1 += " AND D1_ORIGLAN <> 'LF'"
		cQueryD1 += " AND D1_LOCAL >= '"+mv_par05+"'"+" AND D1_LOCAL <= '"+mv_par06+"'"
		cQueryD1 += " AND SD1.D_E_L_E_T_<>'*' AND SF4.D_E_L_E_T_<>'*'"
		
		cQueryD2 := " SELECT 'SD2',SB1.B1_COD,SB1.B1_TIPO,SB1.B1_UM,SB1.B1_GRUPO,SB1.B1_DESC,'',D2_EMISSAO,D2_TES,D2_CF,D2_NUMSEQ,D2_DOC,D2_SERIE,D2_QUANT,D2_QTSEGUM,D2_LOCAL,'',D2_CLIENTE,D2_LOJA,D2_TIPO,D2_REMITO,D2_TPDCENV,D2_CUSTO"
		cQueryD2 += Str(mv_par07,1,0)
		cQueryD2 += ",SD2.R_E_C_N_O_ SD2RECNO FROM "
		cQueryD2 += RetSqlName("SB1") + " SB1 , "+ RetSqlName("SD2")+ " SD2 , "+ RetSqlName("SF4")+" SF4 "
		cQueryD2 += " WHERE SB1.B1_COD = D2_COD AND D2_FILIAL = '"+xFilial("SD2")+"'"
		cQueryD2 += " AND F4_FILIAL = '"+xFilial("SF4")+"' AND SD2.D2_TES = F4_CODIGO AND F4_ESTOQUE = 'S'"
		cQueryD2 += " AND D2_EMISSAO >= '"+DTOS(mv_par01)+"' AND D2_EMISSAO <= '"+DTOS(mv_par02)+"'"
		cQueryD2 += " AND D2_ORIGLAN <> 'LF'"
		cQueryD2 += " AND D2_LOCAL >= '"+mv_par05+"'"+" AND D2_LOCAL <= '"+mv_par06+"'"
		cQueryD2 += " AND SD2.D_E_L_E_T_<>'*' AND SF4.D_E_L_E_T_<>'*'"
		
		cQueryD3 := " SELECT 'SD3',SB1.B1_COD,SB1.B1_TIPO,SB1.B1_UM,SB1.B1_GRUPO,SB1.B1_DESC,'',D3_EMISSAO,D3_TM,D3_CF,D3_NUMSEQ,D3_DOC,'',D3_QUANT,D3_QTSEGUM,D3_LOCAL,D3_OP,'','','','','',D3_CUSTO"
		cQueryD3 += Str(mv_par07,1,0)
		cQueryD3 += ",SD3.R_E_C_N_O_ SD3RECNO FROM "
		cQueryD3 += RetSqlName("SB1") + " SB1 , "+ RetSqlName("SD3")+ " SD3 "
		cQueryD3 += " WHERE SB1.B1_COD = D3_COD AND D3_FILIAL = '"+xFilial("SD3")+"' AND "
		cQueryD3 += " D3_EMISSAO >= '"+DTOS(mv_par01)+"' AND D3_EMISSAO <= '"+DTOS(mv_par02)+"'"
		cQueryD3 += " AND D3_ESTORNO <> 'S'"
		
		If SuperGetMV('MV_D3SERVI', .F., 'N') == 'N' .And. IntDL()
			cQueryD3 += " AND ( (D3_SERVIC = '   ') OR (D3_SERVIC <> '   ' AND D3_TM <= '500')  "
			cQueryD3 += " OR  (D3_SERVIC <> '   ' AND D3_TM > '500' AND D3_LOCAL ='"+SuperGetMV('MV_CQ', .F., '98')+"') )"
		EndIf
		
		cQueryD3 += " AND D3_LOCAL >= '"+mv_par05+"'"+" AND D3_LOCAL <= '"+mv_par06+"'"
		cQueryD3 += " AND SD3.D_E_L_E_T_<>'*'"
		
		cQueryB1:= "SELECT 'SB1',SB1.B1_COD,SB1.B1_TIPO,SB1.B1_UM,SB1.B1_GRUPO,SB1.B1_DESC,'','','','','','','',0,0,'','','','','','','',0,0"
		cQueryB1 += " FROM "
		cQueryB1 += RetSqlName("SB1") + " SB1 "
		cQueryB1 += " WHERE SB1.B1_COD >= '"+mv_par03+"' AND SB1.B1_COD <= '"+mv_par04+"'"
		cQueryB1 += " AND SB1.B1_LOCPAD >= '"+mv_par05+"'"+" AND SB1.B1_LOCPAD <= '"+mv_par06+"'"

		If !lVeic
			cQueryB1A:= " AND SB1.B1_COD >= '"+mv_par03+"' AND SB1.B1_COD <= '"+mv_par04+"'"
		Else
			cQueryB1A:= " AND SB1.B1_CODITE >= '"+mv_par03+"' AND SB1.B1_CODITE <= '"+mv_par04+"'"
		EndIf
		cQueryB1C:= " SB1.B1_FILIAL = '"+xFilial("SB1")+"' AND SB1.B1_TIPO >= '"+mv_par08+"' AND SB1.B1_TIPO <= '"+mv_par09+"'"
		cQueryB1C+= " AND SB1.D_E_L_E_T_<>'*'"
		
		cQuery := cQueryD1+cQueryB1A+" AND "+cQueryB1C+" UNION "+cQueryD2+cQueryB1A+" AND "+cQueryB1C+" UNION "+cQueryD3+cQueryB1A+" AND "+cQueryB1C+" UNION "+cQueryB1+" AND "+cQueryB1C
		
		If aReturn[8] == 1
			cQuery += " ORDER BY " + IIF(!lVeic,"2,3,1","7,3,1") //(PRODUTO/CODITE)+TIPO+ARQ
		ElseIf aReturn[8] == 2
			cQuery += " ORDER BY " + IIF(!lVeic,"3,2,1","3,7,1") //TIPO+(PRODUTO/CODITE)+ARQ
		ElseIf aReturn[8] == 3
			cQuery += " ORDER BY 6,2,1" //DESCR+PRODUTO+ARQ
		Else
			cQuery += " ORDER BY " + IIF(!lVeic,"5,2,1","5,7,1") //GRUPO+(PRODUTO/CODITE)+ARQ
		EndIf
		cQuery += ",8,12" //DATA+SEQUENCIA
		
		cQuery:=ChangeQuery(cQuery)
		MsAguarde({|| dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliasTOP,.F.,.T.)},STR0021)
		TcSetField(cAliasTop,"DATA","D",TamSx3("D2_EMISSAO")[1],TamSx3("D2_EMISSAO")[2])
		dbSelectArea(cAliasTop)
		SetRegua(nTotRegs)
		
		cMoeda:=StrZero(mv_par07,1,0)
		
		If !lVeic
			If aReturn[8] == 1
				cCondicao := "PRODUTO <= mv_par04"
			Else
				cCondicao := ".T."
			EndIf
			cCond1 := "TIPO == cTipant"
			If aReturn[8] == 4
				cCond1 += " .And. GRUPO == cGrpant"
			EndIf
		Else
			cCond1 := "TIPO == cTipant"
			cCondicao := ".T."
			If aReturn[8] == 4 //GRUPO
				cCond1 := "GRUPO == cGrpant"
			ElseIf aReturn[8] == 1 //CODIGO
				cCondicao := "CODITE <= mv_par04"
			EndIf
		EndIf
		
		While !Eof() .And. &(cCondicao) .And. lContinua .And. (mv_par01 <= mv_par02)
			
			If lEnd
				@ PROW()+1,001 PSay STR0016		//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Loop
			EndIf

			cGrpAnt  := GRUPO
			cTipAnt  := TIPO
			
			While !Eof() .And. &cCondicao .And. &cCond1
				
				cProduto := (cAliasTop)->PRODUTO
				aProd    := {}

		        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		        //³ Filtra Registros de Acordo com a Pasta  Filtro da Janela de Impressao  ³
			    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	        	If !Empty(aReturn[7])
		           dbSelectArea("SB1")
	    	       dbSetOrder(1) 
	        	   If dbSeek(xFilial()+alltrim((cAliasTop)->PRODUTO))
	    	 	      If !&(aReturn[7])
    	  		         dbSelectArea(cAliasTop)
        			     dbSkip()
		    	         Loop
		        	  EndIf
			       Else
   		    	      dbSelectArea(cAliasTop)
        	    	  dbSkip()
		    	      Loop
			       EndIf
  			       dbSelectArea(cAliasTop)
			    EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Limpa array de saldos e o inicializa com as datas            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aVars:= {}
				lMov:=.F.
				For ni:=mv_par01 to mv_par02    // Montando Variaveis de Calculo
					Aadd(Avars,{ni,0,0})
				Next
				nSiVl := 0
				nSiQt := 0
				nQtCo := 0
				nVlCo := 0
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ SB1 - Verifica Produtos Sem Movimento						 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea(cAliasTop)
				While !Eof() .And. (cAliasTop)->PRODUTO == cProduto .And. Alltrim((cAliasTop)->ARQ) == "SB1"
					aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
					dbSkip()
				EndDo

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta movimentacao pelo SD1 dentro do array de Saldos        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea(cAliasTop)
				Do While !Eof() .And. (cProduto == (cAliasTop)->PRODUTO) .And. Alltrim((cAliasTop)->ARQ) == "SD1"
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Despreza Os Movimentos feitos por remito.                    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					If cPaisLoc # "BRA"  .And. !Empty(AllTrim((cAliasTop)->REMITO))
						dbSkip()
						Loop
					EndIf
					
					dbSelectArea("SF4")
					Seek xSF4+(cAliasTop)->TES
					dbSelectArea(cAliasTop)
					
					If SF4->F4_ESTOQUE == "S"
						lMov:=.T.
						ni:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
						Avars[ni,2] += (cAliasTop)->QUANTIDADE
						AVars[ni,3] += (cAliasTop)->CUSTO
						nQtco += (cAliasTop)->QUANTIDADE
						nVlco += (cAliasTop)->CUSTO
					EndIf
					aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
					dbSkip()
				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta movimentacao pelo SD2 dentro do array de Saldos        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea(cAliasTop)
				Do While !Eof() .And. (cProduto == (cAliasTop)->PRODUTO) .And. Alltrim((cAliasTop)->ARQ) == "SD2"
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Despreza Os Movimentos feitos por remito.                    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					If cPaisLoc # "BRA"  .And. !Empty(AllTrim((cAliasTop)->REMITO)) .And. !((cAliasTop)->TPDCENV $ "1A")
						dbSkip()
						Loop
					EndIf
					
					dbSelectArea("SF4")
					Seek xSF4+(cAliasTop)->TES
					dbSelectArea(cAliasTop)
					If SF4->F4_ESTOQUE == "S"
						lMov:=.T.
						ni:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
						Avars[ni,2] -= (cAliasTop)->QUANTIDADE
						AVars[ni,3] -= (cAliasTop)->CUSTO
					EndIf
					aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
					dbSkip()
				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta movimentacao pelo SD3 dentro do array de Saldos        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea(cAliasTop)
				Do While !Eof() .And. (cProduto == (cAliasTop)->PRODUTO) .And. Alltrim((cAliasTop)->ARQ) == "SD3"
					If (cAliasTop)->TES > "500"
						lMov:=.T.
						ni:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
						Avars[ni,2] -= (cAliasTop)->QUANTIDADE
						AVars[ni,3] -= (cAliasTop)->CUSTO
					Else
						lMov:=.T.
						ni:=Ascan(aVars, {|x| x[1] == (cAliasTop)->DATA })
						Avars[ni,2] += (cAliasTop)->QUANTIDADE
						AVars[ni,3] += (cAliasTop)->CUSTO
					EndIf
					aProd := {(cAliasTop)->PRODUTO,(cAliasTop)->CODITE,(cAliasTop)->TIPO,(cAliasTop)->GRUPO,(cAliasTop)->DESCR,(cAliasTop)->UM}
					dbSkip()
				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula Saldo inicial pelo SB9 e monta variaveis             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SB2")
				dbSeek(xSB2 + cProduto+Alltrim(mv_par05),.T.)
				While !Eof() .And. (B2_COD == cProduto .And. B2_LOCAL <= mv_par06)
					aSaldo := CalcEst(SB2->B2_COD,SB2->B2_LOCAL,mv_par01)
					nSiVl  += aSaldo[mv_par07+1]
					nSiQt  += aSaldo[1]
					dbSkip()
				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Soma linha do array c/ linha anterior para ter o saldo do prd.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nQtMed:= 0
				nVlMed:= 0
				For ni:= 1 to Len(aVars)
					If ni == 1
						aVars[1,2] += nSiQt
						aVars[1,3] += nSiVl
					Else
						aVars[ni,2] += aVars[ni-1,2]
						aVars[ni,3] += aVars[ni-1,3]
					EndIf
					nQtMed += aVars[ni,2]
					nVlMed += aVars[ni,3]
				Next
				nQtMed:= nQtMed / Len (aVars)
				nVlMed:= nVlMed / Len (aVars)
				If mv_par10 == 1 .Or. (mv_par10 == 2 .And. lMov)
					If li > 58
						Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
					EndIf
					If !lVeic
						@ li,000  PSay aProd[1] //PRODUTO
					Else
						@ li,000  PSay aProd[2] + " " + aProd[1] //CODITE + PRODUTO
					EndIf
					
					@ li,016 + nCOL1 PSay aProd[3] 				//TIPO
					@ li,019 + nCOL1  PSay aProd[4] 			//GRUPO
					@ li,024 + nCOL1  PSay Subs(aProd[5],1,22)	//DESCR
					@ li,047 + nCOL1  PSay aProd[6] 			//UM
					@ li,053 + nCOL1  PSay aVars[Len(aVars),2] Picture TM((aVars[Len(aVars),2]),11,nDecs1)
					@ li,065 + nCOL1  PSay aVars[Len(aVars),3] Picture cPicture
					@ li,084 + nCOL1  PSay nQtMed  Picture TM(nQtMed,11,nDecs1)
					@ li,096 + nCOL1  PSay nVlMed  Picture cPicture
					If nQtco > 0
						@ li,113 + nCOL1  PSay Round((nVlco/nQtco),nDecs) Picture cPicture
					EndIf
					li++
					nSsVl += aVars[Len(aVars),3]
					nTsVl += aVars[Len(aVars),3]
					nSmVl += nVlMed
					nTmVl += nVlMed
				EndIf
				dbSelectArea(cAliasTop)
				Incregua()
			EndDo
			If aReturn[8] == 2 .or. aReturn[8] == 4 .And. (nSmvl+nSsvl) > 0
				@ li,000 PSay STR0017+Iif(Areturn[8] == 2,STR0018+cTipAnt,STR0019+cGrpAnt)		//"Sub-Total do "###"Tipo "###"Grupo "
				@ li,065 + nDesp + nCOL1 PSay nSsVl Picture cPictTot
				@ li,096 + nDesp + nCOL1 PSay nSmVl Picture cPictTot
				li+=2
			EndIf
			nSsVl:= 0
			nSmvl:= 0
		EndDo
	Else
#ENDIF

dbSelectArea("SB1")
SetRegua(nTotRegs)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Prepara SB1 para comecar o relatorio e cond. fim do While    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Set SoftSeek On
dbSetOrder(aReturn[8])
cMoeda:=StrZero(mv_par07,1,0)
If !lVeic
	
	If aReturn[8] == 1
		Seek xSB1 + mv_par03
		cCondicao := "B1_COD <= mv_par04"
	Else
		Seek xSB1
		cCondicao := ".T."
	EndIf
	
	cCond1 := "B1_TIPO == cTipant"
	If aReturn[8] == 4
		cCond1 += " .And. B1_GRUPO == cGrpant"
	EndIf
Else
	cCond1 := "B1_TIPO == cTipant"
	cCondicao := ".T."
	If aReturn[8] == 4 //GRUPO
		dbSetOrder(7)   // B1_FILIAL+B1_GRUPO+B1_CODITE
		cCond1 := "B1_GRUPO == cGrpant"
	ElseIf aReturn[8] == 3 //DESCRICAO
		dbSetOrder(3)   // B1_FILIAL+B1_DESC+B1_COD
		
	Else // INDREGUA
		cArqB1 := CriaTrab( nil,.F. )
		cFilB1 := ""
		If aReturn[8] == 2 //TIPO
			cIndB1 := "B1_FILIAL + B1_TIPO + B1_CODITE"
		Else // IF aReturn[8] == 1 //CODIGO
			cIndB1 := "B1_FILIAL + B1_CODITE"
			cCondicao := "B1_CODITE <= mv_par04"
		EndIf
		IndRegua('SB1',cArqB1,cIndB1)
		nIndB1 := RetIndex('SB1')
		#IFNDEF TOP
			dbSetIndex(cArqB1 + OrdBagExt())
		#ENDIF
		dbSetOrder(nIndB1 + 1)
	EndIf
	If aReturn[8] <> 1
		dbSeek(xSB1)
	Else
		dbSeek(xSB1 + mv_par03)
	EndIf
EndIf
Set Softseek Off

While .Not. Eof() .And. &(cCondicao) .And. lContinua .And. B1_FILIAL == xSB1 .And. (mv_par01 <= mv_par02)
	
	If lEnd
		@ PROW()+1,001 PSay STR0016		//"CANCELADO PELO OPERADOR"
		lContinua := .F.
		Loop
	EndIf
	lT := .F.
	If !lVeic
		If B1_COD < mv_par03 .Or. B1_COD > mv_par04
			lT := .T.
		EndIf
	Else
		If B1_CODITE < mv_par03 .Or. B1_CODITE > mv_par04
			lT := .T.
		EndIf
	EndIf
	If lT
		Incregua()
		dbSkip()
		Loop
	EndIf
	If B1_TIPO < mv_par08 .Or. B1_TIPO > mv_par09
		Incregua()
		dbSkip()
		Loop
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtro do Usuario ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(aReturn[7])
		If !&(aReturn[7])
			dbSkip()
			Loop
		EndIf
	EndIf			
	cGrpAnt:= B1_GRUPO
	cTipAnt:= B1_TIPO
	While .Not. Eof() .And. &cCondicao .And. &cCond1 .And. B1_FILIAL == xSB1
		lT := .F.
		If !lVeic
			If B1_COD < mv_par03 .Or. B1_COD > mv_par04
				lT := .T.
			EndIf
		Else
			If B1_CODITE < mv_par03 .Or. B1_CODITE > mv_par04
				lT := .T.
			EndIf
		EndIf
		If lT
			Incregua()
			dbSkip()
			Loop
		EndIf
		
		If B1_TIPO < mv_par08 .Or. B1_TIPO > mv_par09
			Incregua()
			dbSkip()
			Loop
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtro do Usuario ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aReturn[7])
			If !&(aReturn[7])
				dbSkip()
				Loop
			EndIf
		EndIf			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Limpa array de saldos e o inicializa com as datas            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aVars:= {}
		lMov:=.F.
		For ni:=mv_par01 to mv_par02    // Montndo Variaveis de Calculo
			Aadd(Avars,{ni,0,0})
		Next
		nSiVl := 0
		nSiQt := 0
		nQtCo := 0
		nVlCo := 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD1 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD1")
		dbSetOrder(7)
		Set SoftSeek On
		Seek xSD1 + SB1->B1_COD
		Set Softseek Off
		Do While .Not. Eof() .And. (D1_FILIAL + D1_COD == xSD1 + SB1->B1_COD)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If D1_ORIGLAN $ "LF"
				dbSkip()
				Loop
			EndIf
			If D1_LOCAL < mv_par05 .Or. D1_LOCAL > mv_par06
				dbSkip()
				Loop
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Os Movimentos feitos por remito.                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			If cPaisLoc # "BRA"  .And. !Empty(SD1->D1_REMITO)
				dbSkip()
				Loop
			EndIf
			
			dbSelectArea("SF4")
			Seek xSF4 + SD1->D1_TES
			dbSelectArea("SD1")
			If SF4->F4_ESTOQUE == "S"
				If D1_DTDIGIT < mv_par01 .Or. D1_DTDIGIT > mv_par02
					dbSkip()
					Loop
				Else
					lMov:=.T.
					ni:=Ascan(aVars, {|x| x[1] == D1_DTDIGIT })
					Avars[ni,2] += D1_QUANT
					AVars[ni,3] += &("D1_CUSTO"+Iif(cMoeda == "1"," ",cMoeda))
					nQtco += D1_QUANT
					nVlco += &("D1_CUSTO"+Iif(cMoeda == "1"," ",cMoeda))
				EndIf
			EndIf
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD2 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD2")
		dbSetOrder(6)
		Set SoftSeek On
		Seek xSD2 + SB1->B1_COD
		Set Softseek Off
		Do While .Not. Eof() .And. (D2_FILIAL+D2_COD == xSD2 + SB1->B1_COD)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If D2_ORIGLAN == "LF"
				dbSkip()
				Loop
			EndIf
			If D2_LOCAL < mv_par05 .Or. D2_LOCAL > mv_par06
				dbSkip()
				Loop
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza Os Movimentos feitos por remito.                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			If cPaisLoc # "BRA"  .And. !Empty(SD2->D2_REMITO) .And. !(SD2->D2_TPDCENV $ "1A")
				SD2->(DbSkip())
				Loop
			EndIf
			dbSelectArea("SF4")
			Seek xSF4 + SD2->D2_TES
			dbSelectArea("SD2")
			If SF4->F4_ESTOQUE == "S"
				If D2_EMISSAO < mv_par01 .Or.  D2_EMISSAO > mv_par02
					dbSkip()
					Loop
				Else
					lMov:=.T.
					ni:=Ascan(aVars, {|x| x[1] == D2_EMISSAO })
					Avars[ni,2] -= D2_QUANT
					AVars[ni,3] -= &("D2_CUSTO"+cMoeda)
				EndIf
			EndIf
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta movimentacao pelo SD3 dentro do array de Saldos        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD3")
		dbSetOrder(7)
		Set SoftSeek On
		Seek xSD3 + SB1->B1_COD
		Set Softseek Off
		Do While .Not. Eof() .And. (D3_FILIAL+D3_COD == xSD3 + SB1->B1_COD)
			If D3_LOCAL < mv_par05 .Or. D3_LOCAL > mv_par06
				dbSkip()
				Loop
			EndIf
			If D3_ESTORNO # "S"
				If D3_TM > "500"
					If D3_EMISSAO < mv_par01 .Or.  D3_EMISSAO > mv_par02
						dbSkip()
						Loop
					Else
						lMov:=.T.
						ni:=Ascan(aVars, {|x| x[1] == D3_EMISSAO })
						Avars[ni,2] -= D3_QUANT
						AVars[ni,3] -= &("D3_CUSTO"+cMoeda)
					EndIf
				Else
					If D3_EMISSAO < mv_par01 .Or.  D3_EMISSAO > mv_par02
						dbSkip()
						Loop
					Else
						lMov:=.T.
						ni:=Ascan(aVars, {|x| x[1] == D3_EMISSAO })
						Avars[ni,2] += D3_QUANT
						AVars[ni,3] += &("D3_CUSTO"+cMoeda)
					EndIf
				EndIf
			EndIf
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula Saldo inicial pelo SB9 e monta variaveis             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB2")
		dbSeek(xSB2 + SB1->B1_COD+Alltrim(mv_par05),.T.)
		While !Eof() .And. (B2_COD == SB1->B1_COD .And. B2_LOCAL <= mv_par06)
			aSaldo := CalcEst(SB2->B2_COD,SB2->B2_LOCAL,mv_par01)
			nSiVl  += aSaldo[mv_par07+1]
			nSiQt  += aSaldo[1]
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma linha do array c/ linha anterior para ter o saldo do prd.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nQtMed:= 0
		nVlMed:= 0
		For ni:= 1 to Len(aVars)
			If ni == 1
				aVars[1,2] += nSiQt
				aVars[1,3] += nSiVl
			Else
				aVars[ni,2] += aVars[ni-1,2]
				aVars[ni,3] += aVars[ni-1,3]
			EndIf
			nQtMed += aVars[ni,2]
			nVlMed += aVars[ni,3]
		Next
		nQtMed:= nQtMed / Len (aVars)
		nVlMed:= nVlMed / Len (aVars)
		If mv_par10 == 1 .Or. (mv_par10 == 2 .And. lMov)
			If li > 58
				Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
			EndIf
			If !lVeic
				@ li,000  PSay SB1->B1_COD
			Else
				@ li,000  PSay SB1->B1_CODITE + " " + SB1->B1_COD
			EndIf
			
			@ li,016 + nCOL1 PSay SB1->B1_TIPO
			@ li,019 + nCOL1  PSay SB1->B1_GRUPO
			@ li,024 + nCOL1  PSay Subs(SB1->B1_DESC,1,22)
			@ li,047 + nCOL1  PSay SB1->B1_UM
			@ li,053 + nCOL1  PSay aVars[Len(aVars),2] Picture TM((aVars[Len(aVars),2]),11,nDecs1)
			@ li,065 + nCOL1  PSay aVars[Len(aVars),3] Picture cPicture
			@ li,084 + nCOL1  PSay nQtMed  Picture TM(nQtMed,11,nDecs1)
			@ li,096 + nCOL1  PSay nVlMed  Picture cPicture
			If nQtco > 0
				@ li,113 + nCOL1  PSay Round((nVlco/nQtco),nDecs) Picture cPicture
			EndIf
			li++
			nSsVl += aVars[Len(aVars),3]
			nTsVl += aVars[Len(aVars),3]
			nSmVl += nVlMed
			nTmVl += nVlMed
		EndIf
		dbSelectArea("SB1")
		Incregua()
		dbSkip()
	EndDo
	If aReturn[8] == 2 .or. aReturn[8] == 4 .And. (nSmvl+nSsvl) > 0
		@ li,000 PSay STR0017+Iif(Areturn[8] == 2,STR0018+cTipAnt,STR0019+cGrpAnt)		//"Sub-Total do "###"Tipo "###"Grupo "
		@ li,065 + nDesp + nCOL1 PSay nSsVl Picture cPictTot
		@ li,096 + nDesp + nCOL1 PSay nSmVl Picture cPictTot
		li+=2
	EndIf
	nSsVl:= 0
	nSmvl:= 0
EndDo

#IFDEF TOP
	EndIf
#ENDIF

If nTsVl*nTmVl <> 0
	li++
	@ li,000 PSay STR0020		//"Total Geral ..."
	@ li,065 + nDesp + nCOL1 PSay nTsVl Picture cPictTot
	@ li,096 + nDesp + nCOL1 PSay nTmVl Picture cPictTot
	li+=2
	If cPaisLoc # "BRA"
		@ li,000 PSAY CHR(13)+CHR(10)
	EndIf
EndIf

dbSelectArea("SB1")
RetIndex('SB1')
Set Filter to
dbSetOrder(1)
dbSelectArea("SB2")
dbSetOrder(1)
dbSelectArea("SB9")
dbSetOrder(1)
dbSelectArea("SD1")
dbSetOrder(1)
dbSelectArea("SD2")
dbSetOrder(1)
dbSelectArea("SD3")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga indices de trabalho                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cArqB1 += OrdBagExt())
	fErase(cArqB1)
EndIf

#IFDEF TOP
	If lQuery
		(cAliasTop)->(dbCloseArea())
	EndIf
#ENDIF

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
EndIf

MS_FLUSH()
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MR445DT  ³ Autor ³ Wilson Junior         ³ Data ³ 05/08/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Testa se periodo 'de/ate' - periodo de maior que 4096      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MR445DT()
If mv_par02-mv_par01 > 4095
	Help(" ",1,"R4096DIAS")
	Return .F.
EndIf
Return .T.
