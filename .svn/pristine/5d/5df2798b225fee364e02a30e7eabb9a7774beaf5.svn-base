#INCLUDE "MATR760.CH" 
#Include "FIVEWIN.Ch"

/*

Ŀ
Programa   MATR760   Autor  Marco Bianchi          Data  15/08/06 
Ĵ
Descrio  Demonstrativo de Reajuste                                  
Ĵ
Uso        GENERICO                                                   
ٱ

*/
Function MATR760()

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	MATR760R3()
EndIf

Return

/*

Ŀ
Programa  ReportDef  Autor  Marco Bianchi          Data  15/08/06 
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatrio                                  
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportDef()

Local oReport
#IFDEF TOP
	Local cAliasSD2 := cAliasSF2 := cAliasSB1 := cAliasSE4 := cAliasSA1 :=  GetNextAlias()
#ELSE
	Local cAliasSD2 := "SD2"
	Local cAliasSF2 := "SF2"
	Local cAliasSB1 := "SC5"
	Local cAliasSE4 := "SE4"
	Local cAliasSA1 := "SA1"
#ENDIF	

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MATR760",STR0001,"MTR760", {|oReport| ReportPrint(oReport,cAliasSD2,cAliasSF2,cAliasSB1,cAliasSE4,cAliasSA1)},STR0002)	// "Demonstrativo de Reajuste"###"Emissao do demonstrativo de reajuste."
oReport:SetPortrait() 
oReport:HideParamPage()
oReport:SetTotalInLine(.F.)

AjustaSX1()                              	
Pergunte(oReport:uParam,.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//
//Ŀ
//Secao de Cabecalho - Section(1)                                         
//
oCabec := TRSection():New(oReport,STR0001,{"SF2","SA1","SE4"},/*{Array com as ordens do relatrio}*/,/*Campos do SX3*/,/*Campos do SIX*/)	// "Demonstrativo de Reajuste"
oCabec:SetTotalInLine(.F.)
oReport:Section(1):SetLineStyle(.T.)		// Imprime secao em forma de Enchoice
TRCell():New(oCabec,"F2_DOC"		,"SF2"		,/*Titulo*/	,PesqPict("SF2","F2_DOC"	)	,TamSx3("F2_DOC"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"F2_SERIE"		,"SF2"		,/*Titulo*/	,PesqPict("SF2","F2_SERIE"	)	,TamSx3("F2_SERIE"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"F2_EMISSAO"	,"SF2"		,/*Titulo*/	,PesqPict("SF2","F2_EMISSAO")	,TamSx3("F2_EMISSAO")[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"F2_COND"		,"SF2"		,/*Titulo*/	,PesqPict("SF2","F2_COND"	)	,TamSx3("F2_COND"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"E4_DESCRI"		,"SE4"		,/*Titulo*/	,PesqPict("SE4","E4_DESCRI"	)	,TamSx3("E4_DESCRI"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"F2_CLIENTE"	,"SF2"		,/*Titulo*/	,PesqPict("SF2","F2_CLIENTE")	,TamSx3("F2_CLIENTE")[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"F2_LOJA"		,"SF2"		,/*Titulo*/	,PesqPict("SF2","F2_LOJA"	)	,TamSx3("F2_LOJA"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"A1_NREDUZ"		,"SA1"		,/*Titulo*/	,PesqPict("SA1","A1_NREDUZ"	)	,TamSx3("A1_NREDUZ"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"A1_EST"		,"SA1"		,/*Titulo*/	,PesqPict("SA1","A1_EST"	)	,TamSx3("A1_EST"	)[1]	,/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oCabec,"CTIPO"			,"SF2"		,STR0007	,/*Picture*/					,15							,/*lPixel*/,{|| IIf(!Empty((cAliasSF2)->F2_REAJUST),STR0008,STR0009)		})	// "REAJ.POR: "###"FORMULA"###"TABELA DE PRECO"

//Ŀ
//Secao de Itens - Section(1):Section(1)                                  
//
oItens := TRSection():New(oCabec,STR0001,{"SD2","SF2","SB1"},/*{Array com as ordens do relatrio}*/,/*Campos do SX3*/,/*Campos do SIX*/)			// "Demonstrativo de Reajuste"
oItens:SetTotalInLine(.F.)
TRCell():New(oItens,"D2_COD"	,"SD2"		,/*Titulo*/	,PesqPict("SD2","D2_COD"	),TamSx3("D2_COD"		)[1],/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oItens,"B1_DESC"	,"SB1"		,/*Titulo*/	,PesqPict("SB1","B1_DESC"	),TamSx3("B1_DESC"		)[1],/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oItens,"D2_QUANT"	,"SD2"		,/*Titulo*/	,PesqPict("SD2","D2_QUANT"	),TamSx3("D2_QUANT"		)[1],/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oItens,"D2_PRCVEN"	,"SD2"		,STR0013	,PesqPict("SD2","D2_PRCVEN"	),TamSx3("D2_PRCVEN"	)[1],/*lPixel*/,/*{|| code-block de impressao}*/)	
TRCell():New(oItens,"NVALREAJ"	,/*Tabela*/	,STR0010	,PesqPict("SD2","D2_PRCVEN"	),TamSx3("D2_PRCVEN"	)[1],/*lPixel*/,{|| a760Reaj(1,cAliasSF2,cAliasSD2)})		// "Reaj.Unitario"
TRCell():New(oItens,"NREAJITEM"	,/*Tabela*/	,STR0011	,PesqPict("SD2","D2_PRCVEN"	),TamSx3("D2_PRCVEN"	)[1],/*lPixel*/,{|| a760Reaj(2,cAliasSF2,cAliasSD2)})		// "Reaj.Total"

TRFunction():New(oItens:Cell("NREAJITEM"),/* cID */,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor  Marco Bianchi          Data  14/07/06 
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport,cAliasSD2,cAliasSF2,cAliasSB1,cAliasSE4,cAliasSA1)

Local lQuery    := .F.

#IFNDEF TOP
	Local cCondicao := ""
#ELSE	
	Local cWhere := ""
#ENDIF

//Ŀ
//Transforma parametros Range em expressao SQL                            
//
MakeSqlExpr(oReport:uParam)

//Ŀ
//Filtragem do relatrio                                                  
//
dbSelectArea("SF2")		// Cabecalho da Nota Fiscal
dbSetOrder(1)			// Filial,Doc,Serie,Cliente,Loja,Produto,Item
#IFDEF TOP
	//Ŀ
	//Query do relatrio da secao 1                                           
	//
	cWhere := "% NOT ("+IsRemito(2,'F2_TIPODOC')+")%"
	lQuery := .T.
	oReport:Section(1):BeginQuery()	
	BeginSql Alias cAliasSF2
	SELECT F2_FILIAL,F2_TIPODOC,F2_CLIENTE,F2_LOJA,F2_COND,F2_EMISSAO,F2_DOC,F2_SERIE,F2_REAJUST,
		   D2_FILIAL,D2_PEDIDO,D2_QUANT,D2_PRCVEN,D2_DOC,D2_SERIE,D2_ITEMPV,D2_IDENTB6,D2_COD,D2_CLIENTE,D2_LOJA,
		   D2_DESC,D2_PRUNIT,D2_TES,D2_TIPO,E4_COND,E4_DESCRI,A1_NOME,A1_EST,B1_DESC
	FROM %Table:SD2% SD2, %Table:SF2% SF2, %Table:SE4% SE4, %Table:SA1% SA1,  %Table:SB1% SB1
		
	WHERE F2_FILIAL = %xFilial:SF2% AND 
		F2_DOC >= %Exp:mv_par01% AND F2_DOC <= %Exp:mv_par02% AND
		F2_SERIE = %Exp:mv_par03% AND F2_TIPO = "N" AND
		SF2.%NotDel% AND
		D2_FILIAL = %xFilial:SD2% AND 
		D2_DOC = F2_DOC AND D2_SERIE = F2_SERIE AND D2_CLIENTE = F2_CLIENTE AND D2_LOJA = F2_LOJA AND
		SD2.%NotDel% AND
		A1_FILIAL = %xFilial:SA1% AND 
		A1_COD = D2_CLIENTE AND A1_LOJA = D2_LOJA AND
		SA1.%NotDel% AND
		B1_FILIAL = %xFilial:SB1% AND 
		B1_COD = D2_COD AND
		SB1.%NotDel% AND
		E4_FILIAL = %xFilial:SE4% AND 
		E4_CODIGO = F2_COND AND
		SE4.%NotDel% AND		
		%Exp:cWhere%
	ORDER BY F2_FILIAL,F2_DOC,F2_SERIE,D2_COD,D2_ITEM
	EndSql 
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)

	//Ŀ
	//Secao filha utiliza query da secao pai                                  
	//
	oReport:Section(1):Section(1):SetParentQuery()
		
#ELSE

	dbSelectArea(cAliasSF2)
	cCondicao := 'F2_FILIAL=="'+xFilial("SF2")+ '"'
	cCondicao += '.And. F2_DOC >= "'+MV_PAR01 + '" .And. F2_DOC <= "'+MV_PAR02+'"'
	cCondicao += '.And. F2_SERIE == "'+MV_PAR03 + '"'
	cCondicao += '.And. F2_TIPO == "N"'
	cCondicao += '.And. !('+IsRemito(2,'F2_TIPODOC')+')'
	oReport:Section(1):SetFilter(cCondicao,IndexKey())
	
	//Ŀ
	// Relacionamento da Secao Pai com a Secao Filha                          
	//
	oReport:Section(1):Section(1):SetRelation({|| xFilial("SD2")+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE },"SD2",3,.T.)	

	//Ŀ
	//Metodo TrPosition()                                                     
	//                                                                        
	//Posiciona em um registro de uma outra tabela. O posicionamento ser     
	//realizado antes da impressao de cada linha do relatrio.                
	//                                                                        
	//                                                                        
	//ExpO1 : Objeto Report da Secao                                          
	//ExpC2 : Alias da Tabela                                                 
	//ExpX3 : Ordem ou NickName de pesquisa                                   
	//ExpX4 : String ou Bloco de cdigo para pesquisa. A string ser macroexe-
	//        cutada.                                                         
	//                                                                        
	//
	TRPosition():New(oReport:Section(1),"SA1",1,{|| xFilial("SA1")+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA })
	TRPosition():New(oReport:Section(1),"SE4",1,{|| xFilial("SE4")+(cAliasSF2)->F2_COND })
	TRPosition():New(oReport:Section(1):Section(1),"SB1",1,{|| xFilial("SB1")+(cAliasSD2)->D2_COD })
	
#ENDIF		

//Ŀ
// Altera texto do total da secao                                         
//
oReport:Section(1):Section(1):SetTotalText(STR0012)

//Ŀ
// Define regra para saida do loop da secao filha                         
//
oReport:Section(1):Section(1):SetParentFilter( { |x| (cAliasSD2)->D2_FILIAL+(cAliasSD2)->D2_DOC+(cALiasSD2)->D2_SERIE == x },{ || (cAliasSF2)->F2_FILIAL+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE })	

//Ŀ
//                         I M P R E S S A O 							    
//
oReport:SetMeter((cAliasSF2)->(LastRec()))
dbSelectArea(cAliasSF2)

oReport:Section(1):Print()


Return

/*

Ŀ
Programa   a760Reaj  Autor  Marco Bianchi          Data  15/08/06 
Ĵ
Descrio  Calcula valor do reajuste da nota                          
Ĵ
Uso        GENERICO                                                   
ٱ

*/
Static Function a760Reaj(nCol,cAliasSF2,cAliasSD2)
		
//Ŀ
// Calcula reajuste                                            
//
nValReaj  := 0
nReajItem := 0
nRet      := 0
If !Empty((cAliasSD2)->D2_IDENTB6)
	aSaldoSB6 := CalcTerc((cAliasSD2)->D2_COD,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,(cAliasSD2)->D2_IDENTB6,(cAliasSD2)->D2_TES,(cAliasSD2)->D2_TIPO)
	nQuant    := aSaldoSB6[1]-aSaldoSB6[2]
Else
	nQuant    := (cAliasSD2)->D2_QUANT
Endif

If !Empty((cAliasSF2)->F2_REAJUST)		// Por Formula
	nValReaj  := Formula((cAliasSF2)->F2_REAJUST)
	nReajItem := nValReaj * nQuant
Else				 					// Por Tabela de Preco
	dbSelectarea("SC5")
	dbSetOrder(1)
	dbSeek(xFilial("SC5")+(cAliasSD2)->D2_PEDIDO)
	
	nValReaj := (MaTabPrVen(SC5->C5_TABELA,(cAliasSD2)->D2_COD,nQuant,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,SC5->C5_MOEDA)-(cAliasSD2)->D2_PRUNIT)
	
   	If SC5->C5_DESC1 <> 0 .Or. SC5->C5_DESC2 <> 0 .Or. SC5->C5_DESC3 <> 0 .Or. SC5->C5_DESC4 <> 0
		nValReaj :=FtDescCab(nValReaj,{SC5->C5_DESC1,SC5->C5_DESC2,SC5->C5_DESC3,SC5->C5_DESC4})
 	Endif
	
	If (cAliasSD2)->D2_DESC > 0
		nReajItem := (nValReaj - (nValReaj * ((cAliasSD2)->D2_DESC / 100))) * nQuant
	Else
		nReajItem := nValReaj * nQuant
	Endif
	
EndIf

If nCol == 1
	nRet := nValReaj
Else
	nRet := nReajItem
EndIf
		
Return(nRet)


/*/


Ŀ
Funo     MATR760R3 Autor  Marco Bianchi          Data  15/08/06 
Ĵ
Descrio  Emissao de Planilha de Reajuste                            
Ĵ
Sintaxe e  MATR760(void)                                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MATR760R3()

//Ŀ
// Define Variaveis                                             
//
Local 	wnrel
Local 	cDesc1:=OemToAnsi(STR0002)		//"Emissao do demonstrativo de reajuste."
Local 	cDesc2:=" "
Local	cDesc3:=" "
Local 	cPerg  :="MTR760"
Private	m_pag := 01
Private aReturn := {STR0003, 1,STR0004, 2, 2, 1, "",0 }		//"Zebrado"###"Administracao"
Private nTipo := IIF(aReturn[04]==1,GetMv("MV_COMP"),GetMv("MV_NORM"))
Private tamanho:="M"
Private titulo:=OemToAnsi(STR0001)		//"Demonstrativo de Reajuste"
Private nomeprog:="MATR760",nLastKey := 0,nBegin:=0,aLinha:={ }
Private li:=80,limite:=132,lRodape:=.F.
Private nPag := 0, nTotReaj:=0

cabec1:= STR0005 //"NOTA   SERIE EMISSAO   COND.PAG.            CLIENTE                       EST|      R E A J U S T E         |"
//        999999 999   99/99/99  XXX-XXXXXXXXXXXXXXX  999999/99 XXXXXXXXXXXXXXXXXXXX XX 
//        0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21         
//        0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789

cabec2:= STR0006 //"PRODUTO         DESCRICAO                           QUANT     P.UNIT.BASE    |       UNIT.            TOTAL |"
//        999999999999999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 99999999.99  9,999,999.9999   9,999,999.9999   9,999,999.9999
//        0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21         
//        0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789


wnrel    := "MATR760"
cString  := "SD2"
//Ŀ
// Verifica as perguntas selecionadas                          
//
AjustaSX1()
pergunte("MTR760",.F.)   

//Ŀ
// Variaveis utilizadas para parametos                         
// mv_par01	     	  Da Nota Fiscal                        
// mv_par02	     	  At a Nota Fiscal                     
// mv_par03	     	  Da Srie                              
//
Titulo:= STR0001 
wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho)

If nLastKey==27
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| C760Imp(@lEnd,wnRel,cString)},Titulo)
Return

/*/


Ŀ
Funo     C760IMP   Autor  Marco Bianchi          Data  15/08/06 
Ĵ
Descrio  Chamada do Relatorio                                       
Ĵ
 Uso       MATR760			                                          
ٱ


/*/
Static Function C760Imp(lEnd,WnRel,cString)

//Ŀ
// Define Variaveis                                             
//
Local cNota     := ""
Local cSerie    := ""
Local nTotReaj  := 0 
Local nValReaj  := 0
Local nReajItem := 0

//Ŀ
// Faz manualmente porque nao chama a funcao Cabec()                 
//
@ 0,0 psay AvalImp(Limite)

dbSelectArea("SD2")
dbSetOrder(3)
MSSeek(cFilial+mv_par01+mv_par03,.T.)

SetRegua(RecCount())		// Total de Elementos da regua
While !Eof() .And. D2_FILIAL+D2_DOC+D2_SERIE <= cFilial+mv_par02+mv_par03
	
	If IsRemito(1,"SD2->D2_TIPODOC") .Or. SD2->D2_TIPO <> "N"
		dbSkip()
		Loop
	Endif
	
	//Ŀ
	// Recupera variaveis de quebra                                
	//
	cNota   := D2_DOC
	cSerie  := D2_SERIE
	nTotReaj:= 0
	
	//Ŀ
	// Posiciona ponteiro no arquivo de Notas Fiscais              
	//
	dbSelectArea("SF2")
	dbSetOrder(1)
	dbSeek(cFilial+SD2->D2_DOC+SD2->D2_SERIE)
	
	//Ŀ
	// Posiciona ponteiro no arquivo de Clientes                   
	//
	dbSelectArea("SA1")
	dbSetOrder(1)
	dbSeek(cFilial+SF2->F2_CLIENTE+SF2->F2_LOJA)
	
	//Ŀ
	// Posiciona ponteiro no arquivo de Condio de Pagto.         
	//
	dbSelectArea("SE4")
	dbSetOrder(1)
	dbSeek(cFilial+SF2->F2_COND)
	
	If Li > 55
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		Li ++
	Endif
	
	@ Li,00 PSay SF2->F2_DOC
	@ Li,07 PSay SF2->F2_SERIE
	@ Li,13 PSay SF2->F2_EMISSAO
	@ Li,23 PSay SF2->F2_COND + "-" + SE4->E4_DESCRI
	@ Li,44 PSay SF2->F2_CLIENTE + "-" + SF2->F2_LOJA + " " + SA1->A1_NREDUZ
	@ Li,75 PSay SA1->A1_EST
	@ Li,80 PSay STR0007 + IIf(!Empty(SF2->F2_REAJUST),STR0008,STR0009)
	Li+=2
	
	dbSelectArea("SD2")
	While !Eof() .And. D2_FILIAL+D2_DOC+D2_SERIE == cFilial+cNota+cSerie
		
		If Li > 55
			Li := cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		Endif
		
		dbSelectArea("SB1")
		dbSetOrder(1)
		dbSeek(xFilial("SB1")+SD2->D2_COD)
		
		//Ŀ
		// Calcula reajuste                                            
		//
		nValReaj := 0
		If !Empty(SD2->D2_IDENTB6)
			aSaldoSB6 := CalcTerc(SD2->D2_COD,SD2->D2_CLIENTE,SD2->D2_LOJA,SD2->D2_IDENTB6,SD2->D2_TES,SD2->D2_TIPO)
			nQuant    := aSaldoSB6[1]-aSaldoSB6[2]
		Else
			nQuant    := SD2->D2_QUANT
		Endif
		
		If !Empty(SF2->F2_REAJUST)		// Por Formula
			nValReaj  := Formula(SF2->F2_REAJUST)
			nReajItem := nValReaj * nQuant
		Else					// Por Tabela de Preco
			dbSelectarea("SC5")
			dbSetOrder(1)
			MsSeek(xFilial("SC5")+SD2->D2_PEDIDO)
			
			nValReaj := (MaTabPrVen(SC5->C5_TABELA,SD2->D2_COD,nQuant,SD2->D2_CLIENTE,SD2->D2_LOJA,SC5->C5_MOEDA)-SD2->D2_PRUNIT)
			
			If SC5->C5_DESC1 <> 0 .Or. SC5->C5_DESC2 <> 0 .Or. SC5->C5_DESC3 <> 0 .Or. SC5->C5_DESC4 <> 0
				nValReaj :=FtDescCab(nValReaj,{SC5->C5_DESC1,SC5->C5_DESC2,SC5->C5_DESC3,SC5->C5_DESC4})
			Endif
			
			If SD2->D2_DESC > 0
				nReajItem := (nValReaj - (nValReaj * (SD2->D2_DESC / 100))) * nQuant
			Else
				nReajItem := nValReaj * nQuant
			Endif
			
		EndIf
		
		
		dbSelectArea("SD2")
		@ Li,00 PSay SD2->D2_COD
		@ Li,16 PSay SB1->B1_DESC
		@ Li,47 PSay SD2->D2_QUANT		Picture PesqPict("SD2","D2_QUANT")
		@ Li,60 PSay SD2->D2_PRCVEN		Picture PesqPict("SD2","D2_PRCVEN")
		@ Li,77 PSay nValReaj			Picture PesqPict("SD2","D2_PRCVEN")
		@ Li,94 PSay nReajItem			Picture PesqPict("SD2","D2_PRCVEN")
		nTotReaj += nReajItem
		
		dbSelectArea("SD2")
		dbSkip()
		li++
	End
	
	@ Li,00 PSay STR0012	//"TOTAL DO REAJUSTE ---> "
	@ Li,94 PSay nTotReaj	Picture PesqPict("SD2","D2_PRCVEN")
	li+=1
	@ li,000 psay __PrtThinLine()
	li+=2
	
	dbSelectArea("SD2")
	IncRegua()
End

//Ŀ
// Restaura a Integridade dos dados                             
//
dbSelectArea("SD2")
dbClearFilter()
dbSetOrder(1)


If aReturn[5] = 1
	Set Printer TO
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return


/*


ͻ
Programa  AjustaSX1 Autor  Marco Bianchi        Data   02/05/05   
͹
Desc.      Cria grupo de pereguntas                                   
                                                                      
͹
Uso        MATR760                                                    
ͼ


*/
Static Function AjustaSX1()


PutSx1('MTR760', '01', 'Da Nota Fiscal   ?', 'De Factura ?', 'From Invoice ?'	, 'mv_ch1', 'C', 6, 0, 0, 'G', '', '', '', '', 'mv_par01',"","","","","","","",'','','','','','','','','',,,)
PutSx1('MTR760', '02', 'Ate a Nota Fiscal?', 'A Factura ?'	, 'To Invoice ?'		, 'mv_ch2', 'C', 6, 0, 0, 'G', '', '', '', '', 'mv_par02',"","","","","","","",'','','','','','','','','',,,)
PutSx1('MTR760', '03', 'Da Serie         ?', 'De Serie ?'	, 'From Series ?'		, 'mv_ch3', 'C', 3, 0, 0, 'G', '', '', '', '', 'mv_par03',"","","","","","","",'','','','','','','','','',,,)


Return
