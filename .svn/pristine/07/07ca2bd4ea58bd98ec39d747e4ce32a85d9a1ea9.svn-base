#INCLUDE "MATR640.CH"
#INCLUDE "FIVEWIN.CH"

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  Ё MATR640  Ё Autor Ё Marco Bianchi         Ё Data Ё 28/06/06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Relacao de Duplicatas			                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё SIGAFAT - R4                                               Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function MATR640()

Local oReport

If FindFunction("TRepInUse") .AND. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	MATR640R3()
EndIf

Return

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁReportDef Ё Autor Ё Marco Bianchi         Ё Data Ё 28/06/06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁA funcao estatica ReportDef devera ser criada para todos os Ё╠╠
╠╠Ё          Ёrelatorios que poderao ser agendados pelo usuario.          Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpO1: Objeto do relatСrio                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function ReportDef()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Declaracao de Variaveis                                      		   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local oReport
Local oDupl

#IFDEF TOP
	Local cAliasQry := GetNextAlias()
#ELSE
	Local cAliasQry := "SE1"
#ENDIF

Local oTotal
Local cPrefixo	:= ""
Local cNum		:= ""
Local cParcela	:= ""
Local cCliente	:= ""
Local cLoja 	:= ""
Local cNome		:= ""
Local dEmissao	:= CTOD("  /  /  ")
Local dVendRea	:= CTOD("  /  /  ")
Local nValor 	:= 0
Local cSupervi	:= ""
Local cVendedor	:= ""
Local cPedido	:= ""

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCriacao do componente de impressao                                      Ё
//Ё                                                                        Ё
//ЁTReport():New                                                           Ё
//ЁExpC1 : Nome do relatorio                                               Ё
//ЁExpC2 : Titulo                                                          Ё
//ЁExpC3 : Pergunte                                                        Ё
//ЁExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  Ё
//ЁExpC5 : Descricao                                                       Ё
//Ё                                                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oReport := TReport():New("MATR640",STR0025,"MTR640", {|oReport| ReportPrint(oReport,cAliasQry,oTotal,oDupl)},STR0026 + " " + STR0027 + " " + STR0028)	// "Relacao de Duplicatas"###"Este programa ira emitir a relacao de duplicatas"###"emitidas.Podera ser emitida por Ordem de Titulo, "###"Emissao, Vencimento, Vendedor ou Cliente."
oReport:SetTotalInLine(.F.)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica as perguntas selecionadas                           		   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
AjustaSX1()
Pergunte(oReport:uParam,.F.)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para parametros                         			  Ё
//Ё mv_par01             // Da Duplicata                         			  Ё
//Ё mv_par02             // Ate a Duplicata                      			  Ё
//Ё mv_par03             // Do Vendedor                          			  Ё
//Ё mv_par04             // Ate o vendedor                       			  Ё
//Ё mv_par05             // Todos/em Aberto                      			  Ё
//Ё mv_par06             // Serie da Duplicata                   			  Ё
//Ё mv_par07             // A partir da Emissao                  			  Ё
//Ё mv_par08             // Ate a data de Emissao                		      Ё
//Ё mv_par09             // A partir do vencimento               			  Ё
//Ё mv_par10             // Ate o vencimento                     			  Ё
//Ё mv_par11             // Do Cliente                           			  Ё
//Ё mv_par12             // Ate o Cliente                        			  Ё
//Ё mv_par13             // Qual moeda                           			  Ё
//Ё mv_par14             // Pula Pagina Quebra                   			  Ё
//Ё mv_par15             // Pagina Inicial                       			  Ё
//Ё mv_par16             // Considerar titulos (1=Todos,2=Desconsidera,3=DeduzЁ
//Ё mv_par17             // Serie Final                          			  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCriacao da secao utilizada pelo relatorio                               Ё
//Ё                                                                        Ё
//ЁTRSection():New                                                         Ё
//ЁExpO1 : Objeto TReport que a secao pertence                             Ё
//ЁExpC2 : Descricao da seГao                                              Ё
//ЁExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   Ё
//Ё        sera considerada como principal para a seГЦo.                   Ё
//ЁExpA4 : Array com as Ordens do relatСrio                                Ё
//ЁExpL5 : Carrega campos do SX3 como celulas                              Ё
//Ё        Default : False                                                 Ё
//ЁExpL6 : Carrega ordens do Sindex                                        Ё
//Ё        Default : False                                                 Ё
//Ё                                                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCriacao da celulas da secao do relatorio                                Ё
//Ё                                                                        Ё
//ЁTRCell():New                                                            Ё
//ЁExpO1 : Objeto TSection que a secao pertence                            Ё
//ЁExpC2 : Nome da celula do relatСrio. O SX3 serА consultado              Ё
//ЁExpC3 : Nome da tabela de referencia da celula                          Ё
//ЁExpC4 : Titulo da celula                                                Ё
//Ё        Default : X3Titulo()                                            Ё
//ЁExpC5 : Picture                                                         Ё
//Ё        Default : X3_PICTURE                                            Ё
//ЁExpC6 : Tamanho                                                         Ё
//Ё        Default : X3_TAMANHO                                            Ё
//ЁExpL7 : Informe se o tamanho esta em pixel                              Ё
//Ё        Default : False                                                 Ё
//ЁExpB8 : Bloco de cСdigo para impressao.                                 Ё
//Ё        Default : ExpC2                                                 Ё
//Ё                                                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oDupl := TRSection():New(oReport,STR0039,{"SE1","SA1","TRB"},{STR0029,STR0030,STR0031,STR0032,STR0033},/*Campos do SX3*/,/*Campos do SIX*/)		// "Duplicatas"###Numero###Emissao###Vencimento###Vendedor###Cliente
oDupl:SetTotalInLine(.F.)

TRCell():New(oDupl,"CPREFIXO"	,/*Tabela*/,RetTitle("E1_PREFIXO"	),PesqPict("SE1","E1_PREFIXO"	),TamSx3("E1_PREFIXO"	)[1],/*lPixel*/,{|| cPrefixo 	})	// Prefixo do Titulo
TRCell():New(oDupl,"CNUM"		,/*Tabela*/,RetTitle("E1_NUM"		),PesqPict("SE1","E1_NUM"		),TamSx3("E1_NUM"		)[1],/*lPixel*/,{|| cNum 		})	// Numero do Titulo
TRCell():New(oDupl,"CPARCELA"	,/*Tabela*/,RetTitle("E1_PARCELA"	),PesqPict("SE1","E1_PARCELA"	),TamSx3("E1_PARCELA"	)[1],/*lPixel*/,{|| cParcela 	})	// Parcela
TRCell():New(oDupl,"CCLIENTE"	,/*Tabela*/,RetTitle("E1_CLIENTE"	),PesqPict("SE1","E1_CLIENTE"	),TamSx3("E1_CLIENTE"	)[1],/*lPixel*/,{|| cCliente 	})	// Codigo do Cliente
TRCell():New(oDupl,"CLOJA"		,/*Tabela*/,RetTitle("E1_LOJA"		),PesqPict("SE1","E1_LOJA"		),TamSx3("E1_LOJA"		)[1],/*lPixel*/,{|| cLoja 		})	// Loja
TRCell():New(oDupl,"CNOME"		,/*Tabela*/,RetTitle("A1_NOME"		),PesqPict("SA1","A1_NOME"		),TamSx3("A1_NOME"		)[1],/*lPixel*/,{|| cNome 		})	// Razao Social
TRCell():New(oDupl,"DEMISSAO"	,/*Tabela*/,RetTitle("E1_EMISSAO"	),PesqPict("SE1","E1_EMISSAO"	),TamSx3("E1_EMISSAO"	)[1],/*lPixel*/,{|| dEmissao 	})	// Data Emissao do Titulo
TRCell():New(oDupl,"DVENCREA"	,/*Tabela*/,RetTitle("E1_VENCREA"	),PesqPict("SE1","E1_VENCREA"	),TamSx3("E1_VENCREA"	)[1],/*lPixel*/,{|| dVencRea 	})	// Vencimento do Titulo
TRCell():New(oDupl,"NVALOR"		,/*Tabela*/,RetTitle("E1_VALOR"		),PesqPict("SE1","E1_VALOR"		),TamSx3("E1_VALOR"		)[1],/*lPixel*/,{|| nValor 		})	// Valor
TRCell():New(oDupl,"CSUPERVI"	,/*Tabela*/,RetTitle("E1_SUPERVI"	),PesqPict("SE1","E1_SUPERVI"	),TamSx3("E1_SUPERVI"	)[1],/*lPixel*/,{|| cSupervi 	})	// Supervisor
TRCell():New(oDupl,"CVENDEDOR"	,/*Tabela*/,STR0032					 ,PesqPict("SE1","E1_VEND1"		),TamSx3("E1_VEND1"		)[1],/*lPixel*/,{|| cVendedor 	})	// "Vendedor"
TRCell():New(oDupl,"CPEDIDO"	,/*Tabela*/,RetTitle("E1_PEDIDO"	),PesqPict("SE1","E1_PEDIDO"	),TamSx3("E1_PEDIDO"	)[1],/*lPixel*/,{|| cPedido		})	// Numero do Pedido de Vendas

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Totalizador da Secao e Geral                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oTotal := TRFunction():New(oDupl:Cell("NVALOR"),/* cID */,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.T./*lEndSection*/,.T./*lEndReport*/,/*lEndPage*/)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Cabecalho no top da pagina                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oReport:Section(1):SetHeaderPage()

// Alinhamento a direita do titulo da coluna de valor
oDupl:Cell("NVALOR"):SetHeaderAlign("RIGHT")

Return(oReport)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁReportPrinЁ Autor ЁMarco Bianchi          Ё Data Ё28/06/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁA funcao estatica ReportDef devera ser criada para todos os Ё╠╠
╠╠Ё          Ёrelatorios que poderao ser agendados pelo usuario.          Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpO1: Objeto Report do RelatСrio                           Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function ReportPrint(oReport,cAliasQry,oTotal,oDupl)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDeclaracao de Variaveis                                                 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local lQuery    := .F.
Local nX	 	:= 0
Local nVend 	:= 0
Local cWhere 	:= ""
Local cOrder 	:= ""
LOCAL nAc1   	:= 0
Local nAg1   	:= 0
Local nNcc1  	:= 0                       
Local nNcc2  	:= 0
Local cVend		:= ""
Local aCampos	:= {}
Local cProxVend	:= ""
Local aTamSXG  	:= TamSXG("001")
Local aTamSXG2 	:= TamSXG("002")
Local nLin		:= 0
Local cCampo  	:= ""
Local cQuebra 	:= ""


#IFNDEF TOP
	Local cCondicao := ""
#ENDIF
              
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Recupera o numero de Vendedores que existe na estrutura  SE1 		   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nVend 	:= Fa440CntVen()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё SetBlock: faz com que as variaveis locais possam ser         Ё
//Ё utilizadas em outras funcoes nao precisando declara-las      Ё
//Ё como private											  	 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oReport:Section(1):Cell("CPREFIXO" 	):SetBlock({|| cPrefixo		})
oReport:Section(1):Cell("CNUM" 		):SetBlock({|| cNum			})
oReport:Section(1):Cell("CPARCELA" 	):SetBlock({|| cParcela		})
oReport:Section(1):Cell("CCLIENTE" 	):SetBlock({|| cCliente		})
oReport:Section(1):Cell("CLOJA" 	):SetBlock({|| cLoja		})
oReport:Section(1):Cell("CNOME" 	):SetBlock({|| cNome		})
oReport:Section(1):Cell("DEMISSAO" 	):SetBlock({|| dEmissao		})
oReport:Section(1):Cell("DVENCREA" 	):SetBlock({|| dVencRea		})
oReport:Section(1):Cell("NVALOR" 	):SetBlock({|| nValor		})
oReport:Section(1):Cell("CSUPERVI" 	):SetBlock({|| cSupervi		})
oReport:Section(1):Cell("CVENDEDOR"	):SetBlock({|| cVendedor	})
oReport:Section(1):Cell("CPEDIDO" 	):SetBlock({|| cPedido		})
cPrefixo	:= ""
cNum		:= ""
cParcela	:= ""
cCliente	:= ""
cLoja 		:= ""
cNome		:= ""
dEmissao	:= CTOD("  /  /  ")
dVendRea	:= CTOD("  /  /  ")
nValor 		:= 0
cSupervi	:= ""
cVendedor	:= ""
cPedido		:= ""

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se havera salto de pagina na quebra da secao   	 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1
	oBreak := TRBreak():New(oDupl,{ || (cAliasQry)->E1_PREFIXO+(cAliasQry)->E1_NUM+(cAliasQry)->E1_PARCELA },STR0034,.F.) 			// por Numero
ElseIf oReport:Section(1):GetOrder() == 2		
	oBreak := TRBreak():New(oDupl,oDupl:Cell("DEMISSAO" ),STR0034,.F.) 	// por Emissao
ElseIf oReport:Section(1):GetOrder() == 3		
	oBreak := TRBreak():New(oDupl,oDupl:Cell("DVENCREA" ),STR0035,.F.) 	// por Vencimento Real
ElseIf oReport:Section(1):GetOrder() == 4		
	oBreak := TRBreak():New(oDupl,oDupl:Cell("CVENDEDOR" ),STR0038,.F.) 	// por Vendedor
ElseIf oReport:Section(1):GetOrder() == 5		
	oBreak := TRBreak():New(oDupl,oDupl:Cell("CCLIENTE" ),STR0036,.F.) 	// por Cliente
EndIf

If (oReport:Section(1):GetOrder() > 1) .OR. (oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1)
	oTotal:SetBreak(oBreak)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Altera o Titulo do Relatorio de acordo com parametros	 	 Ё
//Ё Ordem: 1=Numero 2=Emissao 3=Vencimento 4=Vendedor 5=Cliente  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oReport:SetTitle(oReport:Title() + " - " + IIf(oReport:Section(1):GetOrder() == 1,STR0029,IIF(oReport:Section(1):GetOrder()==2,STR0030,IIF(oReport:Section(1):GetOrder()==3,STR0031,IIF(oReport:Section(1):GetOrder()==4,STR0032,STR0033)))) + " - " + GetMv("MV_MOEDA"+STR(mv_par13,1)) )	// "Relacao de Duplicatas"###"Numero"###"Emissao"###"Vencimento"###"Vendedor"###"Cliente"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se havera salto de pagina na quebra da secao   	 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If (oReport:Section(1):GetOrder() > 1) .OR. (oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1)
	If mv_par14 == 1		// Pula pagina na quebra da secao
		oBreak:SetPageBreak(.T.)
	Else	
		oBreak:SetPageBreak(.F.)
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁTransforma parametros Range em expressao SQL                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MakeSqlExpr(oReport:uParam)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFiltragem do relatСrio                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
#IFDEF TOP

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMonta Query de acordo com parametros                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	lQuery := .T.
	cWhere := "%"	
	If mv_par05 = 2 
		cWhere  += " AND E1_SALDO > 0"
	EndIf
	cWhere += "%"	

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁDefine Order de acordo com escolha do cliente                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SE1")
	cOrder := ""
	If oReport:Section(1):GetOrder() == 1			// por Titulo
		cOrder := "%E1_FILIAL,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_TIPO%"
	ElseIf oReport:Section(1):GetOrder() == 2		// por Emissao
		cOrder := "%E1_FILIAL,E1_EMISSAO,E1_NOMCLI,E1_PREFIXO,E1_NUM,E1_PARCELA%"
	ElseIf oReport:Section(1):GetOrder() == 3		// por Vencimento Real
		cOrder := "%E1_FILIAL,E1_VENCREA,E1_NOMCLI,E1_PREFIXO,E1_NUM,E1_PARCELA%"
	ElseIf oReport:Section(1):GetOrder() == 5		// por Cliente
		cOrder := "%E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_TIPO%"
	Else
		cOrder := "%E1_FILIAL,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_TIPO%"
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁQuery do relatСrio da secao 1                                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	oReport:Section(1):BeginQuery()	
	BeginSql Alias cAliasQry
	SELECT E1_FILIAL,E1_NUM,E1_PREFIXO,E1_EMISSAO,E1_VENCREA,E1_CLIENTE,E1_LOJA,E1_VALOR,
	E1_SUPERVI,E1_MOEDA,E1_PEDIDO,E1_TIPO,E1_PARCELA,E1_VEND1,E1_VEND2,E1_VEND3,E1_VEND4,E1_VEND5,A1_NOME
	FROM %Table:SE1% SE1, %Table:SA1% SA1
	WHERE E1_FILIAL = %xFilial:SE1%
		AND E1_NUM >= %Exp:mv_par01% AND E1_NUM <= %Exp:mv_par02%
		AND E1_PREFIXO >= %Exp:mv_par06% AND E1_PREFIXO <= %Exp:mv_par17%
		AND E1_EMISSAO >= %Exp:DTOS(mv_par07)% AND E1_EMISSAO <= %Exp:DTOS(mv_par08)%
		AND E1_VENCREA >= %Exp:DTOS(mv_par09)% AND E1_VENCREA <= %Exp:DTOS(mv_par10)%
		AND E1_CLIENTE >= %Exp:mv_par11% AND E1_CLIENTE <= %Exp:mv_par12%
		AND SE1.%NotDel% 
		AND A1_FILIAL = %xFilial:SA1% 
		AND E1_CLIENTE = A1_COD
		AND E1_LOJA = A1_LOJA
		AND SA1.%NotDel%
		%Exp:cWhere%
	ORDER BY %Exp:cOrder%	
	EndSql 
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
		
#ELSE

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁDefine Order de acordo com escolha do cliente                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea(cAliasQry)
	cOrder := ""
	If oReport:Section(1):GetOrder() == 1			// por Titulo
		dbSetOrder(1)
	ElseIf oReport:Section(1):GetOrder() == 2		// por Emissao
		dbSetOrder(6)
	ElseIf oReport:Section(1):GetOrder() == 3		// por Vencimento Real
		dbSetOrder(7)
	ElseIf oReport:Section(1):GetOrder() == 5		// por Cliente
		dbSetOrder(2)	
	EndIf
	cOrder := SE1->(IndexKey())

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Filtra Tabela de Duplicatas                                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cCondicao := 'E1_FILIAL=="'+xFilial("SE1")+'"
	cCondicao += '.AND. E1_NUM >= "'+mv_par01+'" .AND. E1_NUM <= "'+mv_par02+'"'
	cCondicao += '.AND. E1_PREFIXO >= "'+mv_par06+'" .AND. E1_PREFIXO <= "'+mv_par17+'"'
	cCondicao += '.AND. DTOS(E1_EMISSAO) >= "' + DTOS(mv_par07) + '" .AND. DTOS(E1_EMISSAO) <= "' + DTOS(mv_par08)+'"'
	cCondicao += '.AND. DTOS(E1_VENCREA) >= "' + DTOS(mv_par09) + '" .AND. DTOS(E1_VENCREA) <= "' + DTOS(mv_par10)+'"'
	cCondicao += '.AND. E1_CLIENTE >= "'+mv_par11 + '" .AND. E1_CLIENTE <= "' + mv_par12+'"'
	If mv_par05 = 2 
		cCondicao  += " .AND. E1_SALDO > 0"
	EndIf
	
	dbSelectArea(cAliasQry)
	oReport:Section(1):SetFilter(cCondicao,cOrder)
	
#ENDIF		


//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicio da impressao do fluxo do relatСrio                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If oReport:Section(1):GetOrder() == 4		// Relatorio por Vendedor
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Cria arquivo de trabalho                                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	AADD(aCampos,{ "TRB_CLIE"		,"C",aTamSXG[1]			,0	} )
	AADD(aCampos,{ "TRB_LOJA"		,"C",aTamSXG2[1]		,0	} )
	AADD(aCampos,{ "TRB_VEND"		,"C",06					,0	} )
	AADD(aCampos,{ "TRB_PREFIX"		,"C",03					,0	} )
	AADD(aCampos,{ "TRB_NUM"		,"C",TamSX3("F2_DOC")[1],0	} )
	AADD(aCampos,{ "TRB_PARCEL"		,"C",01					,0	} )
	AADD(aCampos,{ "TRB_EMISSA"		,"D",08					,0	} )
	AADD(aCampos,{ "TRB_VENCRE"		,"D",08					,0	} )
	AADD(aCampos,{ "TRB_VALOR"		,"N",17					,02 } )
	AADD(aCampos,{ "TRB_SUPERV"		,"C",06					,0 	} )
	AADD(aCampos,{ "TRB_PEDIDO"		,"C",06					,0 	} )
	AADD(aCampos,{ "TRB_TIPO"	   	,"C",03					,0 	} )
	cArq := CriaTrab(aCampos,.T.)
	dbUseArea( .T.,, cArq,"TRB", IF(.t. .OR. .f., !.f., nil), .f. )
	IndRegua("TRB",cArq,"TRB_VEND+TRB_CLIE+TRB_LOJA",,,"Selecionando Registros...")
	dbSelectArea(cAliasQry)
	dbGoTop()
	While !Eof() .AND. xFilial("SE1")=E1_FILIAL
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Desconsidera Abatimentos                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If	( E1_TIPO $ MVABATIM ) .AND. ( MV_PAR16 == 2 )
			dbSkip()
			Loop
		Endif
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Processa todos Vendedores existentes no arquivo SE1          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		cProxVend := "1"
		For nx := 1 To nVend
			cVend := FieldGet(FieldPos("E1_VEND"+cProxVend))
			If ( !Empty(cVend) .OR. nx==1 ).AND. ;
				(cVend >= mv_par03	.AND. cVend	<= mv_par04)
				RecLock("TRB",.T.)
				Replace	TRB_LOJA  	With (cAliasQry)->E1_LOJA,;
				TRB_CLIE	With (cAliasQry)->E1_CLIENTE,;
				TRB_VEND	With cVend,;
				TRB_PREFIX	With (cAliasQry)->E1_PREFIXO,;
				TRB_NUM	 	With (cAliasQry)->E1_NUM,;
				TRB_PARCEL 	With (cAliasQry)->E1_PARCELA,;
				TRB_EMISSA	With (cAliasQry)->E1_EMISSAO,;
				TRB_VENCRE	With (cAliasQry)->E1_VENCREA,;
				TRB_VALOR	With xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13,(cAliasQry)->E1_EMISSAO),;
				TRB_SUPERV 	With (cAliasQry)->E1_SUPERVI,;
				TRB_PEDIDO  With (cAliasQry)->E1_PEDIDO,;
				TRB_TIPO   	With (cAliasQry)->E1_TIPO
				MsUnlock()
				DbSelectArea(cAliasQry)
			Endif
			cProxVend := Soma1(cProxVend, 1)
		Next
		
		dbSelectArea(cAliasQry)
		dbSkip()
		oReport:IncMeter()
		
	EndDo
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Imprime os dados selecionados no arquivo de trabalho         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea( "TRB" )
	dbGoTop()
	oReport:SetMeter(RecCount())
	oReport:Section(1):Init()
	cVendAnt:= TRB->TRB_VEND
	nAc1 	:= 0
	nNcc2	:= 0
	
	While !Eof()
		
		dbSelectArea("SA3")
		dbSeek(xFilial("SA3")+TRB->TRB_VEND)
		
		dbSelectArea("SA1")
		dbSeek(xFilial()+TRB->TRB_CLIE+TRB->TRB_LOJA)
		dbSelectArea("TRB")
		
		cPrefixo	:= TRB->TRB_PREFIX
		cNum		:= TRB->TRB_NUM
		cParcela	:= TRB->TRB_PARCEL
		cCliente	:= TRB->TRB_CLIE
		cLoja 		:= TRB->TRB_LOJA
		cNome		:= SA1->A1_NOME
		dEmissao	:= TRB->TRB_EMISSA
		dVencRea	:= TRB->TRB_VENCRE
		nValor 		:= TRB->TRB_VALOR
		nValor 		:= IIf( (TRB->TRB_TIPO $ MVABATIM .AND. MV_PAR16 == 3),-nValor,nValor)
		cSupervi	:= TRB->TRB_SUPERV
		cVendedor	:= TRB_VEND
		cPedido		:= TRB->TRB_PEDIDO
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica Quebra para impressao do Total Liquido da Secao	   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If cVendAnt <> TRB->TRB_VEND
			nLin := oReport:Row() + 20
			If (nAc1 - nNcc2) > 0 
				oReport:PrintText(STR0037 + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")",nLin) 		// "Total Liquido"
				oReport:PrintText(Transform(nAc1 - nNcc2,PesqPict("SE1","E1_VALOR")),nLin,(oReport:Section(1):Cell("NVALOR"):ColPos()-40))
			EndIF 
			cVendAnt := TRB->TRB_VEND
			nAc1 := 0
			nNcc2:= 0
			oReport:SkipLine(2)
		EndIf
		
		// Imprime Linha de Detalhes
		oReport:Section(1):PrintLine()
		
		If ( TRB->TRB_TIPO $ MVABATIM ) .AND. !( MV_PAR16 == 1 )
			If MV_PAR16 == 3
				nAc1 -= TRB->TRB_VALOR
				nAg1 -= TRB->TRB_VALOR
			EndIf
		Else
			nAc1 += TRB->TRB_VALOR
			nAg1 += TRB->TRB_VALOR
		EndIf
		
		If ( TRB->TRB_TIPO $ MV_CRNEG+"/"+MVRECANT )
			nNcc1 += TRB->TRB_VALOR
			nNcc2 += TRB->TRB_VALOR
		Endif
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAltera texto do Total daa Secao                               Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		oBreak:SetTotalText(STR0038 + SA3->A3_COD + " " + SA3->A3_NOME)		// "Total do Vendedor ==> "
		
		dbSelectArea("TRB")
		dbSkip()
		
	EndDo
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Impressao do Total Liquido da ultima quebra					   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nLin := oReport:Row() + 20
	If (nAc1 - nNcc2) > 0 
		oReport:PrintText(STR0037 + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")",nLin) 		// "Total Liquido"
		oReport:PrintText(Transform(nAc1 - nNcc2,PesqPict("SE1","E1_VALOR")),nLin,(oReport:Section(1):Cell("NVALOR"):ColPos()-40))
	EndIf
	cVendAnt := TRB->TRB_VEND
	oReport:SkipLine(2)


	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Total Geral do Relatorio									   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	oReport:Section(1):Finish()
	oReport:Section(1):SetPageBreak(.T.)
	nLin := oReport:Row() + 80
	If (nAg1 - nNcc1) > 0 
		oReport:PrintText(STR0037 + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")",nLin)		// "Total Liquido"
		oReport:PrintText(Transform(nAg1 - nNcc1,PesqPict("SE1","E1_VALOR")),nLin,(oReport:Section(1):Cell("NVALOR"):ColPos()-40))
	EndIf
Else

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa Variaveis                                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1			// Quebra por Prefixo+ Numero titulo + Parcela
		cCampo  :=  "E1_PREFIXO+E1_NUM+E1_PARCELA"
		cQuebra := (cAliasQry)->E1_PREFIXO+(cAliasQry)->E1_NUM+(cAliasQry)->E1_PARCELA
	ElseIf oReport:Section(1):GetOrder() == 2							// Quebra por Data + Numero titulo
		cCampo  :=  "E1_EMISSAO"
		cQuebra := (cAliasQry)->E1_EMISSAO
	Elseif oReport:Section(1):GetOrder() == 3							// Quebra por Data Vencimento
		cCampo :=  "E1_VENCREA"
		cQuebra := (cAliasQry)->E1_VENCREA
	Elseif oReport:Section(1):GetOrder() == 5							// Quebra por Cliente
		cCampo :=  "E1_CLIENTE"
		cQuebra := (cAliasQry)->E1_CLIENTE		
	Endif
	nAc1 := 0
	nNcc2:= 0

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicia Impressao                                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	oReport:SetMeter((cAliasQry)->(LastRec()))
	oReport:Section(1):Init()
	dbSelectArea(cAliasQry)
	dbGoTop()

	While !oReport:Cancel() .AND. E1_FILIAL == xFilial("SE1") .AND. !(cAliasQry)->(Eof())
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Desconsidera Abatimentos                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If	( (cAliasQry)->E1_TIPO $ MVABATIM ) .AND. ( MV_PAR16 == 2 )
			dbSkip()
			Loop
		Endif
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra os vendedores selecionados na pergunte                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		cProxVend := "1"
		lProcVend := .F.
		For nx := 1 To nVend
			cVend := FieldGet(FieldPos("E1_VEND"+cProxVend))
			If ( cVend >= mv_par03	.AND. cVend	<= mv_par04 )
				lProcVend := .T.
				Exit
			Endif
			cProxVend := Soma1(cProxVend, 1)
		Next
		
		If !lProcVend
			dbskip()
			loop
		Endif
		
		cPrefixo	:= (cAliasQry)->E1_PREFIXO
		cNum		:= (cAliasQry)->E1_NUM
		cParcela	:= (cAliasQry)->E1_PARCELA
		cCliente	:= (cAliasQry)->E1_CLIENTE
		cLoja 		:= (cAliasQry)->E1_LOJA
		#IFDEF TOP
			cNome	:= (cAliasQry)->A1_NOME
		#ELSE
			dbSelectArea("SA1")
			dbSeek(xFilial()+(cAliasQry)->E1_CLIENTE+(cAliasQry)->E1_LOJA)
			cNome	:= SA1->A1_NOME
		#ENDIF
		dEmissao	:= (cAliasQry)->E1_EMISSAO
		dVencRea	:= (cAliasQry)->E1_VENCREA
		nValor 		:= xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13,(cAliasQry)->E1_EMISSAO)
		nValor 		:= IIf( ((cAliasQry)->E1_TIPO $ MVABATIM .AND. MV_PAR16 == 3),-nValor,nValor)
		cSupervi	:= (cAliasQry)->E1_SUPERVI
		cVendedor	:= cVend
		cPedido		:= (cAliasQry)->E1_PEDIDO
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica Quebra para impressao do Total Liquido da Secao	   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea(cAliasQry)		
		If oReport:Section(1):GetOrder() > 1
			If cQuebra <> &cCampo
				nLin := oReport:Row() + 20
				If (nAc1 - nNcc2) > 0
					oReport:PrintText(STR0037 + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")",nLin) 		// "Total Liquido"
					oReport:PrintText(Transform(nAc1 - nNcc2,PesqPict("SE1","E1_VALOR")),nLin,(oReport:Section(1):Cell("NVALOR"):ColPos()-40))
				EndIf
				cQuebra := &cCampo
				nAc1 := 0
				nNcc2:= 0
				oReport:SkipLine(2)
			EndIf
		ElseIf oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1
			cQuebra := &cCampo
			nAc1 := 0
			nNcc2:= 0
		EndIf
		
		// Imprime Linha de Detalhe
		oReport:Section(1):PrintLine()

		
		If ( (cAliasQry)->E1_TIPO $ MVABATIM ) .AND. !( MV_PAR16 == 1 )
			If MV_PAR16 == 3
				nAc1	-= xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13, (cAliasQry)->E1_EMISSAO)
				nAg1	-= xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13, (cAliasQry)->E1_EMISSAO)
			EndIf
		Else
			nAc1	+= xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13, (cAliasQry)->E1_EMISSAO)
			nAg1	+= xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13, (cAliasQry)->E1_EMISSAO)
		EndIf
		If ( (cAliasQry)->E1_TIPO $ MV_CRNEG+"/"+MVRECANT )
			nNcc1 += xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13, (cAliasQry)->E1_EMISSAO)
			nNcc2 += xMoeda((cAliasQry)->E1_VALOR,(cAliasQry)->E1_MOEDA,MV_PAR13, (cAliasQry)->E1_EMISSAO)
		Endif
		
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAltera texto do Total da Secao                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If oReport:Section(1):GetOrder() ==1 .AND. MV_PAR14 == 1
			oBreak:SetTotalText(RetTitle("E1_VALOR") + " ==> " + (&cCampo))		// "Valor do Titulo"		
		ElseIf oReport:Section(1):GetOrder() == 2
			oBreak:SetTotalText(STR0034 + DTOC(&cCampo))			 				// "Total dos Emitidos em ==> "
		Elseif oReport:Section(1):GetOrder() == 3
			oBreak:SetTotalText(STR0035 + DTOC(&cCampo))		   					// "Total dos Vencidos em ==> "
		Elseif oReport:Section(1):GetOrder() == 5
			dbSelectArea("SA1")
			dbSeek(xFilial()+cCliente)
			oBreak:SetTotalText(STR0036 + cCliente + " "+SA1->A1_NOME)				// "Total do Cliente ==> "
		EndIf
		
		dbSelectArea(cAliasQry)
		dbSkip()
		oReport:IncMeter()
		
	EndDo
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Impressao do Total Liquido da ultima quebra					   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If (oReport:Section(1):GetOrder() > 1) .OR. (oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1)
		nLin := oReport:Row() + 20
		If (nAc1 - nNcc2) > 0
			oReport:PrintText(STR0037 + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")",nLin) 		// "Total Liquido"
			oReport:PrintText(Transform(nAc1 - nNcc2,PesqPict("SE1","E1_VALOR")),nLin,(oReport:Section(1):Cell("NVALOR"):ColPos()-40))
		EndIf
		cCliAnt := (cAliasQry)->E1_CLIENTE
		oReport:SkipLine(2)
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Total Geral do Relatorio									   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If (oReport:Section(1):GetOrder() > 1) .OR. (oReport:Section(1):GetOrder() == 1 .AND. MV_PAR14 == 1)
		oReport:Section(1):Finish()
		oReport:Section(1):SetPageBreak(.T.)
	EndIf
	nLin := oReport:Row() + 80
	If (nAg1 - nNcc1) > 0
		oReport:PrintText(STR0037 + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")",nLin)		// "Total Liquido"
		oReport:PrintText(Transform(nAg1 - nNcc1,PesqPict("SE1","E1_VALOR")),nLin,(oReport:Section(1):Cell("NVALOR"):ColPos()-40))
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Fecha Query                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lQuery
	If !Empty(cAliasQry)
		(cAliasQry)->(dbCloseArea())
	EndIf
	If oReport:Section(1):GetOrder() == 4
		TRB->(dbCloseArea())	
	EndIf
ElseIf oReport:Section(1):GetOrder() == 4
	TRB->(dbCloseArea())	
EndIf

Return



/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё MATR640R3Ё Autor Ё Wagner Xavier         Ё Data Ё 16.11.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRelacao de Duplicatas                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё MATR640(void)                                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁVerificar indexacoes no programa                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     Ё╠╠
╠╠цддддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё PROGRAMADOR  Ё DATA   Ё BOPS Ё  MOTIVO DA ALTERACAO                   Ё╠╠
╠╠Ё              Ё        Ё      Ё                                        Ё╠╠
╠╠Ё Patricia Sal.Ё29/12/99ЁXXXXXXЁAcerto Layout e arq. de Trabalho(Fornec.Ё╠╠
╠╠Ё              Ё        Ё      Ёcom 20 pos./Loja com 4 pos./Num. Dupl.  Ё╠╠
╠╠Ё              Ё        Ё      Ёcom 12 pos.                             Ё╠╠
╠╠юддддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function Matr640R3()
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL wnrel
LOCAL tamanho:="M"
LOCAL limite :=132
LOCAL titulo := OemtoAnsi(STR0001)		//"Relacao de Duplicatas"
LOCAL cDesc1 := OemtoAnsi(STR0002)		//"Este programa ira emitir a relacao de duplicatas"
LOCAL cDesc2 := OemtoAnsi(STR0003)		//"emitidas.Podera ser emitida por Ordem de Data+Numero, "
LOCAL cDesc3 := OemtoAnsi(STR0004)		//"por Vendedor ou Supervisor."
LOCAL cString:= "SE1"
LOCAL aOrd   := { OemToAnsi(STR0005),;	//"Numero"
OemToAnsi(STR0006),;					//"Emissao"
OemToAnsi(STR0007),;					//"Vencimento"
OemToAnsi(STR0008),;					//"Vendedor"
OemToAnsi(STR0009) }	    			//"Cliente"
LOCAL nAc1   := 0
LOCAL nAg1   := 0
LOCAL lContinua := .T.

Private aReturn :={ OemToAnsi(STR0003), 1,OemToAnsi(STR0004), 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
PRIVATE nomeprog:="MATR640"
PRIVATE aLinha  := { },nLastKey := 0
PRIVATE cPerg   :="MTR640"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica as perguntas selecionadas                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
AjustaSX1()
pergunte("MTR640",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para parametros                         Ё
//Ё mv_par01             // Da Duplicata                         Ё
//Ё mv_par02             // Ate a Duplicata                      Ё
//Ё mv_par03             // Do Vendedor                          Ё
//Ё mv_par04             // Ate o vendedor                       Ё
//Ё mv_par05             // Todos/em Aberto                      Ё
//Ё mv_par06             // Serie da Duplicata                   Ё
//Ё mv_par07             // A partir da Emissao                  Ё
//Ё mv_par08             // Ate a data de Emissao                Ё
//Ё mv_par09             // A partir do vencimento               Ё
//Ё mv_par10             // Ate o vencimento                     Ё
//Ё mv_par11             // Do Cliente                           Ё
//Ё mv_par12             // Ate o Cliente                        Ё
//Ё mv_par13             // Qual moeda                           Ё
//Ё mv_par14             // Pula Pagina Quebra                   Ё
//Ё mv_par15             // Pagina Inicial                       Ё
//Ё mv_par16             // Considerar titulos                   Ё
//Ё mv_par17             // Serie Final                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Envia controle para a funcao SETPRINT                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
wnrel:="MATR640"

wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,Tamanho)

If nLastKey==27
	DbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey==27
	DbClearFilter()
	Return
Endif

RptStatus({|lEnd| C640Imp(@lEnd,wnRel,cString,aOrd)},Titulo)

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё C640IMP  Ё Autor Ё Rosane Luciane Chene  Ё Data Ё 09.11.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Chamada do Relatorio                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATR640	   	                                              Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function C640Imp(lEnd,WnRel,cString,aOrd)
LOCAL cabec1,cabec2
LOCAL nOrdem
LOCAL tamanho:="M"
LOCAL limite :=132
LOCAL nAc1   := 0, nTotal:=0
LOCAL nAg1   := 0
LOCAL nNcc1  := 0                       
LOCAL nNcc2  := 0
LOCAL lContinua := .T.
LOCAL cVend, aCampos:={}
LOCAL cCond1
LOCAL nCnt   := 0
LOCAL nVend, cProxVend
LOCAL nX
LOCAL aTamSXG  := TamSXG("001")
LOCAL aTamSXG2 := TamSXG("002")
LOCAL aCoord, aTam

nOrdem := aReturn[8]

m_pag := If(mv_par15>0,mv_par15,1)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Recupera o numero de Vendedores que existe na estrutura  SE1 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nVend 	:= Fa440CntVen()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para Imporessao do Cabecalho e Rodape   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cbcont   := 0
cbtxt    := SPACE(10)
li       := 80

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta o Cabecalho de acordo com o tipo de emissao            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
titulo := OemtoAnsi(STR0012) + aOrd[nOrdem] + " - " + GetMv("MV_MOEDA" + STR(mv_par13,1))//"RELACAO DAS DUPLICATAS EMITIDAS - "
If (aTamSXG[1] == aTamSXG[3]) .AND. (aTamSXG2[1] == aTamSXG2[3])
	cabec1 := OemtoAnsi(STR0013)	//"PRF NUMERO       P CODIGO LJ RAZAO SOCIAL                      EMISSAO     VENCTO                 VALOR   SUPERV. VENDEDOR    PEDIDO"
	cabec2 := OemtoAnsi(STR0014)	//"                                                                                                  TOTAL"
//                                     999 123456789012 1 123456 99 123456789012345678901234567890   99/99/9999 99/99/9999  999999999999999999   XXXXXXX XXXXXX      999999
//                                     0         1         2         3         4         5         6         7         8         9        10        11        12        13
//                                     012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	aCoord := {00, 04, 17, 19, 26, 29, 62, 73, 85, 106, 114, 126}
	aTam   := {30}
Else
	cabec1 := OemtoAnsi(STR0021)	//"PRF NUMERO       P CODIGO               LOJA RAZAO SOCIAL              EMISSAO     VENCTO                VALOR SUPERV. VEND.  PEDIDO"
	cabec2 := OemtoAnsi(STR0022)	//"                                                                                                         TOTAL"
//                                     999 123456789012 X 12345678901234567890 1234 123568789012345678901234 99/99/9999 99/99/9999 999999999999999999 XXXXXXX XXXXXX 999999
//                                     0         1         2         3         4         5         6         7         8         9        10        11        12        13
//                                     012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	aCoord := {00, 04, 17, 19, 40, 45, 70, 81, 92, 111, 119, 126}
	aTam   := {24}
EndIf

SetRegua(RecCount())		// Total de Elementos da regua

If nOrdem == 4		// Relatorio por Vendedor
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Cria arquivo de trabalho                                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	AADD(aCampos,{ "TRB_CLIE"		,"C",aTamSXG[1] ,0 } )
	AADD(aCampos,{ "TRB_LOJA"		,"C",aTamSXG2[1],0 } )
	AADD(aCampos,{ "TRB_VEND"		,"C",06,0 } )
	AADD(aCampos,{ "TRB_PREFIX"	,"C",03,0 } )
	AADD(aCampos,{ "TRB_NUM"		,"C",LEN("SE1->E1_NUM"),0 } )
	AADD(aCampos,{ "TRB_PARCEL"	,"C",01,0 } )
	AADD(aCampos,{ "TRB_EMISSA"	,"D",08,0 } )
	AADD(aCampos,{ "TRB_VENCRE"	,"D",08,0 } )
	AADD(aCampos,{ "TRB_VALOR"		,"N",17,02 } )
	AADD(aCampos,{ "TRB_SUPERV"	,"C",06,0 } )
	AADD(aCampos,{ "TRB_PEDIDO"	,"C",06,0 } )
	AADD(aCampos,{ "TRB_TIPO"	   ,"C",03,0 } )	
	cArq := CriaTrab(aCampos,.T.)
	dbUseArea( .T.,, cArq,"trb", IF(.t. .OR. .f., !.f., nil), .f. )
	IndRegua("TRB",cArq,"TRB_VEND+TRB_CLIE+TRB_LOJA",,,"Selecionando Registros...")
	dbSelectArea("SE1")
	dbSeek(xFilial())
	
	While !Eof() .AND. xFilial()=E1_FILIAL .AND. lContinua
		
		IncRegua()	// Incrementa regua de impressao
		
		If lEnd
			@Prow()+1,001 Psay OemtoAnsi(STR0015)	//"CANCELADO PELO OPERADOR"
			Exit
		Endif
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se processara apenas os titulos com saldo             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If mv_par05 = 2 .AND. SE1->E1_SALDO = 0
			dbSkip()
			Loop
		EndIF
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra arquivo SE1                                             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If	E1_NUM		< 	mv_par01	.OR. E1_NUM		 > mv_par02 .OR. ;
			!(E1_PREFIXO >= mv_par06 .AND. E1_PREFIXO <= mv_par17) .OR.;
			E1_EMISSAO  <  mv_par07 .OR. E1_EMISSAO > mv_par08 .OR. ;
			E1_VENCREA  <  mv_par09 .OR. E1_VENCREA > mv_par10 .OR. ;
			E1_CLIENTE  <  mv_par11 .OR. E1_CLIENTE > mv_par12 .OR. ;
			( ( E1_TIPO $ MVABATIM ) .AND. ( MV_PAR16 == 2 ) )
			dbSkip()
			Loop
		EndIF
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Processa todos Vendedores existentes no arquivo SE1          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		cProxVend := "1"
		For nx := 1 To nVend
			cVend := FieldGet(FieldPos("E1_VEND"+cProxVend))
			If ( !Empty(cVend) .OR. nx==1 ).AND. ;
				(cVend >= mv_par03	.AND. cVend	<= mv_par04)
				nTotal += xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
				RecLock("TRB",.T.)
				Replace 	TRB_LOJA  	With SE1->E1_LOJA,;
				TRB_CLIE	 	With SE1->E1_CLIENTE,;
				TRB_VEND	 	With cVend,;
				TRB_PREFIX	With SE1->E1_PREFIXO,;
				TRB_NUM	 	With SE1->E1_NUM,;
				TRB_PARCEL With SE1->E1_PARCELA,;
				TRB_EMISSA	With SE1->E1_EMISSAO,;
				TRB_VENCRE	With SE1->E1_VENCREA,;
				TRB_VALOR	With xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13,SE1->E1_EMISSAO),;
				TRB_SUPERV With SE1->E1_SUPERVI,;
				TRB_PEDIDO  With SE1->E1_PEDIDO,;
				TRB_TIPO   With SE1->E1_TIPO				
				MsUnlock()
				DbSelectArea("SE1")
			Endif
			cProxVend := Soma1(cProxVend, 1)
		Next
		dbSkip()
	EndDo
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Imprime os dados selecionados no arquivo de trabalho         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea( "TRB" )
	dbGoTop()
	While !EOF()
		
		IncRegua()
		
		If lEnd
			lContinua := .F.
			Exit
		EndIf
		
		If li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,Iif(aReturn[4]==1,GetMv("MV_COMP"),GetMv("MV_NORM")))
		EndIf
		cVend	:=	TRB_VEND
		nCnt	:=	0
		nAc1	:=	0               
		nNcc2 := 0
		Do While !Eof() .AND. TRB_VEND == cVend
		    If li > 58
			    cabec(titulo,cabec1,cabec2,nomeprog,tamanho,Iif(aReturn[4]==1,GetMv("MV_COMP"),GetMv("MV_NORM")))
		    EndIf
			R640Impr("TRB",TRB_CLIE,TRB_LOJA,TRB_VEND,aCoord,aTam)
			li++
			nCnt += 1

			If ( TRB->TRB_TIPO $ MVABATIM ) .AND. !( MV_PAR16 == 1 )
				If MV_PAR16 == 3
					nAc1 -= TRB->TRB_VALOR
					nAg1 -= TRB->TRB_VALOR
				EndIf
			Else
				nAc1 += TRB->TRB_VALOR
				nAg1 += TRB->TRB_VALOR
			EndIf

			If ( TRB->TRB_TIPO $ MV_CRNEG+"/"+MVRECANT )
				nNcc1 += TRB->TRB_VALOR
				nNcc2 += TRB->TRB_VALOR
			Endif

			dbSkip()
		EndDo
		If nCnt > 0
			dbSelectArea("SA3")
			dbSeek(xFilial()+cVend)
			@li,000       Psay OemtoAnsi(STR0016) + cVend + " "+SA3->A3_NOME		//Total Do Vendedor   ==>
			@li,aCoord[9] Psay nAc1	PicTure tm(nAc1,18)
			li++

			@li,000       Psay OemtoAnsi(STR0023) + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")" //Total Liquido"
			@li,aCoord[9] Psay nAc1 - nNcc2 PicTure tm(nAc1,18)
			li++
			
		EndIf
		@ li,  0 Psay __PrtThinLine()
		li++
		dbSelectArea("TRB")
		If nCnt > 0 .AND. mv_par14 == 1  //Pula pagina
			li := 60
		EndIf
	EndDo
Else
	dbSelectArea("SE1")
	If nOrdem = 1			// Numero
		dbSetOrder(1)
		dbSeek(xFilial()+mv_par06+mv_par01,.T.)
		cCond := "E1_NUM >= mv_par01 .AND. E1_NUM <= mv_par02"
	Elseif nOrdem == 2	// Emissao
		dbSetOrder(6)
		dbSeek(xFilial()+DTOS(mv_par07),.T.)
		cCond := "E1_EMISSAO >= mv_par07 .AND. E1_EMISSAO <= mv_par08"
	Elseif nOrdem == 3	// Vencrea
		dbSetOrder(7)
		dbSeek(xFilial()+DTOS(mv_par09),.T.)
		cCond := "E1_VENCREA >= mv_par09 .AND. E1_VENCREA <= mv_par10"
	Elseif nOrdem == 5
		dbSetOrder(2)
		dbSeek(xFilial()+mv_par11,.T.)
		cCond := "E1_CLIENTE >= mv_par11 .AND. E1_CLIENTE <= mv_par12"
	Endif
	
	While !Eof() .AND. xFilial()==E1_FILIAL .AND. &cCond
		
		IncRegua()							// Incrementa regua de impressao
		
		If lEnd
			@Prow()+1,001 Psay OemtoAnsi(STR0015)	//"CANCELADO PELO OPERADOR"
			Exit
		Endif
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra os titulo com saldo dependendo dos parametros da perguntЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If mv_par05 = 2 .AND. SE1->E1_SALDO = 0
			dbSkip()
			Loop
		EndIF
		
		If	E1_NUM		< 	mv_par01	.OR. E1_NUM		 > mv_par02 .OR. ;
			!(E1_PREFIXO >= mv_par06 .AND. E1_PREFIXO <= mv_par17) .OR. ;
			E1_EMISSAO  <  mv_par07 .OR. E1_EMISSAO > mv_par08 .OR. ;
			E1_VENCREA  <  mv_par09 .OR. E1_VENCREA > mv_par10 .OR. ;
			E1_CLIENTE  <  mv_par11 .OR. E1_CLIENTE > mv_par12 .OR. ;
			( ( E1_TIPO $ MVABATIM ) .AND. ( MV_PAR16 == 2 ) )			
			dbSkip()
			Loop
		Endif
		
		If nOrdem == 2						// Relatorio por Data + Numero titulo
			cCond2 := "E1_EMISSAO"
			cCampo :=  E1_EMISSAO
		Elseif nOrdem == 3								// Quebra por Data Vencimento
			cCond2 := "E1_VENCREA"
			cCampo :=  E1_VENCREA
		Elseif nOrdem == 1
			cCond2 := "E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO"
			cCampo :=  E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
		Elseif nOrdem == 5
			cCond2 := "E1_CLIENTE"
			cCampo :=  E1_CLIENTE
		Endif
		
		nAc1 := 0
		nNcc2:= 0
		nCnt := 0
		
		While !Eof() .AND. cCampo = &cCond2 .AND. E1_FILIAL == xFilial()
			
			IncRegua()
			If lEnd
				lContinua := .F.
				Exit
			EndIf
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Filtra os titulo com saldo dependendo dos parametros da perguntЁ
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If mv_par05 = 2 .AND. SE1->E1_SALDO = 0
				dbSkip()
				Loop
			EndIF
			
			If	E1_NUM		< 	mv_par01	.OR. E1_NUM		 > mv_par02 .OR. ;
				!(E1_PREFIXO >= mv_par06 .AND. E1_PREFIXO <= mv_par17) .OR.;
				E1_EMISSAO  <  mv_par07 .OR. E1_EMISSAO > mv_par08 .OR. ;
				E1_VENCREA  <  mv_par09 .OR. E1_VENCREA > mv_par10 .OR. ;
				E1_CLIENTE  <  mv_par11 .OR. E1_CLIENTE > mv_par12
				dbSkip()
				Loop
			Endif
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Filtra os vendedores selecionados na pergunte                Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			cProxVend := "1"
			lProcVend := .F.
			For nx := 1 To nVend
				cVend := FieldGet(FieldPos("E1_VEND"+cProxVend))
				If ( cVend >= mv_par03	.AND. cVend	<= mv_par04 )
					lProcVend := .T.
					Exit
				Endif
				cProxVend := Soma1(cProxVend, 1)	
			Next
			
			If !lProcVend
				dbskip()
				loop
			Endif
			
			If li > 58
				cabec(titulo,cabec1,cabec2,nomeprog,tamanho,Iif(aReturn[4]==1,GetMv("MV_COMP"),GetMv("MV_NORM")))
			EndIf
			
			R640Impr("SE1",SE1->E1_CLIENTE,SE1->E1_LOJA, cVend, aCoord, aTam)
			nCnt++
			li++

			If ( SE1->E1_TIPO $ MVABATIM ) .AND. !( MV_PAR16 == 1 )
				If MV_PAR16 == 3
					nAc1  -= xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
					nAg1	-= xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
				EndIf
			Else
				nAc1  += xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
				nAg1	+= xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
			EndIf

			If ( SE1->E1_TIPO $ MV_CRNEG+"/"+MVRECANT )
				nNcc1 += xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
				nNcc2 += xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO)
			Endif
			
			dbSkip()
			
		EndDo
		
		If nCnt > 0 .AND. nOrdem > 1
			If nOrdem == 2
				@li, 0 Psay OemtoAnsi(STR0017) + DTOC(cCampo) 	//"Total dos Emitidos em ==>
			Elseif nOrdem == 3
				@li, 0 Psay OemtoAnsi(STR0018) + DTOC(cCampo)	//"Total dos Vencidos em ==>
			Elseif nOrdem == 5
				dbSelectArea("SA1")
				dbSeek(xFilial()+cCampo)
				@li, 0 Psay OemtoAnsi(STR0019) + cCampo + " "+SA1->A1_NOME //"Total do Cliente ==>
			EndIf
			@ li,aCoord[9] Psay nAc1	PicTure tm(nAc1,18)
			li++
			
			@li,000       Psay OemtoAnsi(STR0023) + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")" //Total Liquido"
			@li,aCoord[9] Psay nAc1 - nNcc2 PicTure tm(nAc1,18)
			li++
			@ li,  0 Psay __PrtThinLine()
			li++
			
			
		Endif

		
		dbSelectArea("SE1")
		If nCnt > 0 .AND. mv_par14 == 1  //Pula pagina
			li := 60
		EndIf
	EndDo
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Totaliza o relatorio													    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li++
If li > 58 .OR. mv_par14 == 1  //Pula pagina
	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,Iif(aReturn[4]==1,GetMv("MV_COMP"),GetMv("MV_NORM")))
Endif
@prow()+1, 0 Psay OemtoAnsi(STR0020)	//"TOTAL ---------------->"
@prow()  ,aCoord[9] Psay nAg1		PicTure tm(nAg1,18)
@prow()+1, 0 Psay OemtoAnsi(STR0024) + "(-"+left(MV_CRNEG+"/"+MVRECANT,6)+")"	//"TOTAL LIQUIDO (-NCC)-->"
@prow()  ,aCoord[9] Psay nAg1 - nNcc1		PicTure tm(nAg1,18)

roda(cbcont,cbtxt,Titulo)

dbSelectArea("SE1")
RetIndex("SE1")
dbSetOrder(1)
DbClearFilter()

If nOrdem == 4
	dbSelectArea( "TRB" )
	dbCloseArea()
	If File(cArq+OrdBagExt())
		Ferase(cArq+OrdBagExt())
	Endif
	If File(cArq+GetDBExtension())
		Ferase(cArq+GetDBExtension())
	Endif
EndIf

dbSelectArea("SE1")

If aReturn[5] = 1
	Set Printer TO
	dbCommitAll()
	Ourspool(wnrel)
EndIf

MS_FLUSH()

Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R640Impr Ё Autor Ё Cristina M. Ogura     Ё Data Ё 18/07/96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Imprime os dados dos titulos                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё R640Impr()                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATR640                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R640Impr(cAlias,cCliente,cLoja,cVend,aCoord,aTam)
LOCAL nValor := Iif(cAlias=="SE1",xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR13, SE1->E1_EMISSAO),TRB->TRB_VALOR)
dbSelectArea("SA1")
dbSeek(xFilial()+cCliente+cLoja)
@li,aCoord[1]  Psay IIF(cAlias=="SE1",SE1->E1_PREFIXO,TRB->TRB_PREFIX)
@li,aCoord[2]  Psay IIF(cAlias=="SE1",SE1->E1_NUM,TRB->TRB_NUM)
@li,aCoord[3]  Psay IIF(cAlias=="SE1",SE1->E1_PARCELA,TRB->TRB_PARCEL)
@li,aCoord[4]  Psay IIF(cAlias=="SE1",SE1->E1_CLIENTE,TRB->TRB_CLIE)
@li,aCoord[5]  Psay IIF(cAlias=="SE1",SE1->E1_LOJA,TRB->TRB_LOJA)
@li,aCoord[6]  Psay Left(SA1->A1_NOME, aTam[1])
@li,aCoord[7]  Psay IIF(cAlias=="SE1",SE1->E1_EMISSAO,TRB->TRB_EMISSA)
@li,aCoord[8]  Psay IIF(cAlias=="SE1",SE1->E1_VENCREA,TRB->TRB_VENCRE)
@li,aCoord[9]  Psay If(((If(cAlias=="SE1",SE1->E1_TIPO,TRB->TRB_TIPO) $ MVABATIM) .AND. (MV_PAR16 == 3)),-nValor,nValor) Picture tm(nValor,18)
@li,aCoord[10] Psay IIF(cAlias=="SE1",SE1->E1_SUPERVI,TRB->TRB_SUPERV)
@li,aCoord[11] Psay cVend
@li,aCoord[12] Psay IIF(cAlias=="SE1",SE1->E1_PEDIDO,TRB->TRB_PEDIDO)
dbSelectArea(cAlias)
Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAjustaSX1 Ё Autor Ё Kleber Dias Gomes     Ё Data Ё05/10/2004Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Inclui pergunta no sx1                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATR640                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function AjustaSX1()

Local	aHelpPor := {}
Local	aHelpSpa := {}
Local	aHelpEng := {}

aHelpPor := {}
aAdd(aHelpPor,"Indique o que devera ser")
aAdd(aHelpPor,"considerado no valor da duplicata:")
aAdd(aHelpPor,"1-Considera todos os titulos")
aAdd(aHelpPor,"2-Desconsidera abatimentos")
aAdd(aHelpPor,"3-Deduz abatimentos")

aHelpSpa := {}
aAdd(aHelpSpa,"Indique quИ deberА ser")
aAdd(aHelpSpa,"considerado en el valor de la factura")
aAdd(aHelpSpa,"de crИdito:")
aAdd(aHelpSpa,"1-Considera todos los tМtulos")
aAdd(aHelpSpa,"2-Ignora deducciones")
aAdd(aHelpSpa,"3-Descuenta deducciones")

aHelpEng := {}
aAdd(aHelpEng,"Determine what must be")
aAdd(aHelpEng,"considered in the duplicate value:")
aAdd(aHelpEng,"1-Consider all bills")
aAdd(aHelpEng,"2-Do not consider discounts")
aAdd(aHelpEng,"3-Deduct discounts")

PutSx1("MTR640","16","Considerar titulos ?","©Considerar titulos?","Consider bills ?","mv_chg","N",1,0,1,"C","","","","","mv_par16",;
"1-Todos titulos","1-Todos titulos","1-All bills","","2-Desconsidera","2-Ignora","2-Not consider","3-Deduz","3-Deduce","3-Deduct","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Serie Final
aHelpPor := {}
aAdd(aHelpPor,"Serie final a ser considerada na")
aAdd(aHelpPor,"filtragem no cadastro de notas  ")
aAdd(aHelpPor,"fiscais.")

aHelpSpa := {}
aAdd(aHelpSpa,"Serie final a considerarse en la")
aAdd(aHelpSpa,"filtracion del archivo de facturas.")

aHelpEng := {}
aAdd(aHelpEng,"Final serie to be considered in ")
aAdd(aHelpEng,"the fil tering of invoices file.")

PutSx1("MTR640","17","Serie final ?","©Serie final ?","Final serie ?","mv_chh","C",3,0,0,"G","","","","S","mv_par17","","","","zzz","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

Return( .T. )
