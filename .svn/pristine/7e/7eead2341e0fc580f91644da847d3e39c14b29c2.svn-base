#INCLUDE "Protheus.ch"
#INCLUDE "TOPCONN.ch"
#INCLUDE "rwmake.ch"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ RatVaTr  บAutor  ณ Carlos Beltrใo     บ Data ณ  09/01/2015 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Relat๓rio para imprimir o rateio dos valores liquidos de   บฑฑ
ฑฑบ          ณ Adiantamento / Folha de Pagamento.                         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Gestao de Pessoal                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
User Function RatVaTr()

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Declaracao de Variaveis                                             ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
	Local cDesc1         := "Este programa tem como objetivo imprimir relatorio "
	Local cDesc2         := "de acordo com os parametros informados pelo usuario."
	Local cDesc3         := ""
	Local cPict          := ""
	Local cTitulo         := "Rateio Vale Transporte"
	Local nLin           := 80
							  //         10       20        30        40        50        60        70        80        90        100       110       120       130
							  //12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	Local Cabec1         := "Centro de Custo             Descri็ใo                                                              Porcentagem             Valores"
	Local Cabec2         := ""
	Local imprime        := .T.
	Local aOrd           := {}
	Local aRatVT			:= {}
	Local nTotal			:= 0
	Local nTaxa			:= 0
	Local cPeriodo		:= ""
	Local nBoleto			:= 0
	
	Private lEnd         := .F.
	Private lAbortPrint  := .F.
	Private CbTxt        := ""
	Private limite       := 132
	Private tamanho      := "M"
	Private nomeprog     := "RatVaTr"
	Private nTipo        := 15
	Private aReturn      := {"Zebrado", 1, "Administracao", 1, 2, 1, "", 1}
	Private nLastKey     := 0
	Private cbtxt        := Space(10)
	Private cbcont       := 00
	Private CONTFL       := 01
	Private m_pag        := 01
	Private wnrel        := "RatVaTr"
	Private cString      := ""
	Private cPerg        := "RATVTR"
	
	ValidPerg(cPerg)
	Pergunte(cPerg,.F.)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Monta a interface padrao com o usuario.                             ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
	wnrel := SetPrint(cString, NomeProg, cPerg, @cTitulo, cDesc1, cDesc2, cDesc3, .T., aOrd, .T., Tamanho, , .T.)
	
	If nLastKey == 27
		Return
	Endif
	
	SetDefault(aReturn,cString)
	
	If nLastKey == 27
		Return
	Endif
	
	nTipo := If(aReturn[4]==1,15,18)
	cPeriodo := AnoMes(mv_par07)
	nTaxa := mv_par08
	nBoleto := mv_par09
	
	// Executa a carga no vetor 
	nTotal := CarregarArray(aRatVT, cPeriodo)
	
	// Calcula a taxa de servi็o
	CalcularTaxaServico(aRatVT, nTotal, nTaxa, nBoleto)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Processamento. RPTSTATUS monta janela com a regua de processamento. ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If mv_par10 == 1 // 1 = Imprimir, 2 = Planilha	
		RptStatus({|| Imprimir(Cabec1, Cabec2, cTitulo, nLin, aRatVT) }, cTitulo)
	Else
		RptStatus({|| ExportarExcel(aRatVT, cTitulo) }, cTitulo)
	EndIf

	MS_FLUSH()

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFun็ใo    ณ CarregarArray บAutor  ณ Carlos Beltrใo     บ Data ณ  09/01/2015  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Essa fun็ใo carrega o vetor e o valor total do m๊s de ref- บฑฑ
ฑฑบ          ณ erencia.                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Gestao de Pessoal                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CarregarArray(aRatVT, cPeriodo)

	Local cQuery := ""
	Local nTotal := 0
	
	/*
	MV_PAR01 - De Filial
	MV_PAR02 - At้ Filial
	MV_PAR03 - Do CC 
	MV_PAR04 - Ate CC
	mv_par07 - Data Ref. 
	mv_par08 - Taxa	
	mv_par09 - Tipo
	*/
	
	cQuery := "SELECT RG2_FILIAL 'FILIAL', RG2_CC 'CCUSTO', CTT_DESC01 'DESC', 0 'PORC', SUM(RG2_VALCAL) 'VALOR' "
	cQuery += "FROM " + RetSqlName("RG2") + " RG2 "
	cQuery += "INNER JOIN " + RetSqlName("CTT") + " CTT "
	cQuery += "ON RG2_FILIAL = CTT_FILIAL AND "
	cQuery += "RG2_CC = CTT_CUSTO "    
	cQuery += "WHERE RG2.D_E_L_E_T_ <> '*' AND "
	cQuery += "CTT.D_E_L_E_T_ <> '*' AND "
	cQuery += "RG2_PERIOD = '" + cPeriodo + "' AND "
	cQuery += "RG2_PERC <> 100 AND "
	cQuery += "RG2_FILIAL >= '" + mv_par01 + "' AND "
	cQuery += "RG2_FILIAL <= '" + mv_par02 + "' AND "
	cQuery += "RG2_CC >= '" + mv_par03 + "' AND "
	cQuery += "RG2_CC <= '" + mv_par04 + "' AND "
	cQuery += "RG2_CODIGO >= '" + mv_par05 + "' AND "
	cQuery += "RG2_CODIGO <= '" + mv_par06 + "' "
	cQuery += "GROUP BY RG2_FILIAL, RG2_CC, CTT_DESC01 "
	cQuery += "ORDER BY RG2_FILIAL, RG2_CC, CTT_DESC01 "
	
	cQuery := ChangeQuery(cQuery)
	MsAguarde({|| dbUseArea(.T., "TOPCONN", TCGenQry( , , cQuery), "XMA", .F., .T.)}, OemToAnsi("Processando"))
	XMA->(dbGoTop())
	
	/*
		aRatVT[x][1] = Filial
		aRatVT[x][2] = Centro de Custo
		aRatVT[x][3] = Descri็ใo do Centro de Custo
		aRatVT[x][4] = Porcentagem
		aRatVT[x][5] = Valor
	*/
	
	While XMA->(!EOF())
		AADD(aRatVT, {XMA->FILIAL, XMA->CCUSTO, XMA->DESC, XMA->PORC, XMA->VALOR})
		nTotal += XMA->VALOR
		XMA->(dbSkip())
	EndDo
	
	dbSelectArea("XMA")
	XMA->(dbCloseArea())

Return nTotal


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFun็ใo    ณ CalcularTaxaServico บAutor  ณ Carlos Beltrใo     บ Data ณ  09/01/2015 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Essa fun็ใo calcula os valores do rateio aplicando o perc- บฑฑ
ฑฑบ          ณ entual de servi็o cobrado pela Grande Recife.              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CalcularTaxaServico(aRatVT, nTotal, nTaxa, nBoleto)
	
	Local nValServ 	:= 0
	Local nValBole 	:= 0
	
	nValServ := nTotal * (nTaxa / 100) + nBoleto
	nValBole := nTotal + nValServ
	
	/*
		aRatVT[x][1] = Filial
		aRatVT[x][2] = Centro de Custo
		aRatVT[x][3] = Descri็ใo do Centro de Custo
		aRatVT[x][4] = Porcentagem
		aRatVT[x][5] = Valor
	*/
	
	For c:= 1 To Len(aRatVT)
		aRatVT[c][4] := aRatVT[c][5] / nTotal * 100
		aRatVT[c][5] := aRatVT[c][5] + (aRatVT[c][4] / 100 * nValServ)
	Next

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFun็ใo    ณ Imprimir บAutor  ณ Carlos Beltrใo     บ Data ณ  09/01/2015 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao auxiliar chamada pela RPTSTATUS. A funcao RPTSTATUS บฑฑ
ฑฑบ          ณ monta a janela com a regua de processamento.               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Gestao de Pessoal                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Imprimir(Cabec1, Cabec2, cTitulo, nLin, aRatVT)

	Local nOrdem
	Local nTotFil := 0
	Local nTotal	:= 0
	Local nPorFil := 0
	Local nPorTot := 0
	Local i		:= 1
	Local cFil 	:= ""
	
	SetRegua(Len(aRatVT))
	While i <= Len(aRatVT)
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Verifica o cancelamento pelo usuario...                             ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If lAbortPrint
			@nLin,00 PSAY "*** CANCELADO PELO OPERADOR ***"
			Exit
		Endif
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Impressao do cabecalho do relatorio. . .                            ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If nLin > 65 // Salto de Pแgina. Neste caso o formulario tem 55 linhas...
			Cabec(cTitulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
			nLin := 9
		Endif
		
		//"cTitulo  Doct Dt.Emissao For/Cli  Nome Fantasia  Receita    Despesa      Valor"
		//          10         20        30        40        50        60        70        80        90        100       110       120
		// 0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
		// 999999  XXX 11/11/1111  999999 XXXXXXXXXXXXXXX 999.999,99  999.999,99  999.999,99
		cFil 		:= aRatVT[i][1]
		nTotFil 	:= 0
		nCol 		:= 0
		nPorFil	:= 0
																																	
		@nLin++,001 Psay Replicate("-", 130)
		@nLin++,001 Psay Alltrim(cFil+" - "+AllTrim(Posicione("SM0",1,SM0->M0_CODIGO+cFil,"M0_FILIAL")))	// Filial
		@nLin++,001 Psay Replicate("-", 130)
		nlin++
		
		While cFil = aRatVT[i][1] .And. i <= Len(aRatVT)
			
			If nLin > 65 // Salto de Pแgina. Neste caso o formulario tem 55 linhas...
				Cabec(cTitulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
				nLin := 9
			Endif
			
			IncRegua()
					
			@nLin,001 Psay Alltrim(aRatVT[i][02])																// Centro de Custo
			@nLin,020 Psay Alltrim(aRatVT[i][03])																// Descri็ใo
			@nLin,100 Psay aRatVT[i][04] Picture "@E 99.99" + "%" 											// Pocentagem
			@nLin++,117 Psay aRatVT[i][05] Picture "@E 999,999,999.99" 										// Valor
			
			nPorFil 	+= aRatVT[i][04]																			// Porcentagem Filial
			nPorTot	+= aRatVT[i][04]																			// Porcentagem Total
			
			nTotFil 	+= aRatVT[i][05] 																			// Total Filial
			nTotal 	+= aRatVT[i][05] 																			// Total Geral
			
			i++
			If i > Len(aRatVT)
				Exit
			Endif
			
			If nLin > 65 // Salto de Pแgina. Neste caso o formulario tem 55 linhas...
				Cabec(cTitulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
				nLin := 9
			Endif
		EndDo

		nLin++
		If nLin > 65 // Salto de Pแgina. Neste caso o formulario tem 55 linhas...
			Cabec(cTitulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
			nLin := 9
		Endif
		
		@nLin,020 Psay "Total da Filial "+cFil+" - " + AllTrim(Posicione("SM0", 1, SM0->M0_CODIGO + cFil, "M0_FILIAL"))	// Descri็ใo
		@nLin,082 Psay "-----> "
		@nLin,099 Psay nPorFil Picture "@E 999.99" + "%"															// Porcentagem Filial
		@nLin++,117 Psay nTotFil Picture "@E 999,999,999.99" 													// Total Filial
		nLin++	
		If i > Len(aRatVT)
			Exit
		Endif
	EndDo
	
	If nLin > 65 // Salto de Pแgina. Neste caso o formulario tem 55 linhas...
		Cabec(cTitulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
		nLin := 9
	Endif
	
	@nLin++,001 Psay Replicate("-", 130)
	@nLin,020 Psay "TOTAL"																							// Descri็ใo
	@nLin,082 Psay "---------> "
	@nLin,099 Psay nPorTot Picture "@E 999.99" + "%"																// Porcentagem Total
	@nLin++,117 Psay nTotal Picture "@E 999,999,999.99" 															// Total Geral
	@nLin++,001 Psay Replicate("-", 130)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Finaliza a execucao do relatorio...                                 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
	SET DEVICE TO SCREEN
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Se impressao em disco, chama o gerenciador de impressao...          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		
	If aReturn[5]==1
		dbCommitAll()
		SET PRINTER TO
		OurSpool(wnrel)
	Endif

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFun็ใo    ณ ExportarExcel บAutor  ณ Carlos Beltrใo     บ Data ณ 09/01/2015 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao que exporta os dados do relat๓rio para uma planilha บฑฑ
ฑฑบ          ณ de excel.                                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Gestao de Pessoal                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ExportarExcel(aRatVT, cTitulo)

	Local aCabec	:= {}
	Local aItem	:= {}
	Local aDados	:= {}
	Local cBan 	:= ""
	Local cFil 	:= ""
	
	If !ApOleClient("MSExcel") 
		MsgAlert("Microsoft Excel nใo instalado!")
		Return
	EndIf
	
	AADD(aCabec, {"Filial"			,"C", 60	, 0})
	AADD(aCabec, {"Centro de Custo"	,"C", 100	, 0})
	AADD(aCabec, {"Valores"			,"N", 10	, 2})

	SetRegua(Len(aRatVT))

	FOR i := 1 To Len(aRatVT)
		IncRegua()
		aItem := Array(Len(aCabec)+1)
		// Carregando as variaveis
		aItem[1] := aRatVT[i][1]+" - "+AllTrim(Posicione("SM0",1,SM0->M0_CODIGO+aRatVT[i][1],"M0_FILIAL"))	// Filial
		aItem[2] := aRatVT[i][2] + " - " + Alltrim(aRatVT[i][3])													// Centro de Custo
		aItem[3] := Round(aRatVT[i][4],2)																				// Valores
		aItem[4] := "" // Tive que adicionar essa coluna em branco pois a ultima coluna sempre estแ sendo suprimida
		AAdd(aDados, aItem)
		Item := {}
	NEXT i

	DlgToExcel({ {"GETDADOS", cTitulo, aCabec, aDados} })

Return


/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFun็ใo    ณ VALIDPERG ณ Autor ณ Carlos Beltrใo     ณ Data ณ 09/01/2015 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ Cria as perguntas dos parametros                           ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function ValidPerg()

	_sAlias := Alias()
	DBSelectArea("SX1")
	DBSetOrder(1)
	cPerg := PADR(cPerg,10)
	aRegs:={}
	
	/*
	MV_PAR01 - Periodo (AAAAMM)
	MV_PAR02 - Imprimir Folha
	MV_PAR03 - Da Filial 
	MV_PAR04 - Ate a Filial
	mv_par07 - Do C.Custo 
	mv_par08 - Ate o C.Custo	
	mv_par09 - Tipo
	*/
	
	// 	Grupo/Ordem/Pergunta/Variavel/Tipo/Tamanho/Decimal/Presel/GSC/Valid/Var01/Def01/Cnt01/Var02/Def02/Cnt02/Var03/Def03/Cnt03/Var04/Def04/Cnt04/Var05/Def05/Cnt05
	aAdd(aRegs,{cPerg,'01' ,'Da Filial          ?',''				 ,''			 ,'mv_ch1','C'  ,02     ,0      ,0     ,'G','                                ','mv_par01','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'XM0',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'02' ,'Ate a Filial       ?',''				 ,''			 ,'mv_ch2','C'  ,02     ,0      ,0     ,'G','naovazio                        ','mv_par02','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'XM0',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'03' ,'Do C.Custo         ?',''				 ,''			 ,'mv_ch3','C'  ,09     ,0      ,0     ,'G','                                ','mv_par03','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'CTT',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'04' ,'Ate o C.Custo      ?',''				 ,''			 ,'mv_ch4','C'  ,09     ,0      ,0     ,'G','naovazio                        ','mv_par04','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'CTT',''	,'' ,'','',''	})	
	aAdd(aRegs,{cPerg,'05' ,'De Meio de Trans.  ?',''				 ,''			 ,'mv_ch5','C'  ,02     ,0      ,0     ,'G','naovazio                        ','mv_par05','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'SRN',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'06' ,'Ate Meio de Trans. ?',''				 ,''			 ,'mv_ch6','C'  ,02     ,0      ,0     ,'G','naovazio                        ','mv_par06','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'SRN',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'07' ,'Data Ref.          ?',''				 ,''			 ,'mv_ch7','D'  ,08     ,0      ,0     ,'G','naovazio                        ','mv_par07','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'   ',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'08' ,'Taxa de Serv. (%)  ?',''				 ,''			 ,'mv_ch8','N'  ,05     ,2      ,0     ,'G','naovazio                        ','mv_par08','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'   ',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'09' ,'Custo do Boleto    ?',''				 ,''			 ,'mv_ch9','N'  ,05     ,2      ,0     ,'G','naovazio                        ','mv_par09','           ',''		 ,''	 ,'               ',''   , '               ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'   ',''	,'' ,'','',''	})
	aAdd(aRegs,{cPerg,'10' ,'Tipo               ?',''				 ,''			 ,'mv_chA','C'  ,01     ,0      ,0     ,'C','                                ','mv_par10','Impressใo  ',''		 ,''	 ,'               ',''   , 'Planilha       ',''		 ,''	 , ''    ,''    ,'              ',''		 ,''	 , ''    ,''    ,'              ',''    ,''	  ,''    ,''    ,'          ',''	   ,''		 ,''       ,'   ',''	,'' ,'','',''	})
	
	For i:=1 to Len(aRegs)
		If ! DBSeek(cPerg+aRegs[i,2])
			RecLock("SX1",.T.)
			For j:=1 to Max(39, Len(aRegs[i])) //fCount()
				FieldPut(j,aRegs[i,j])
			Next
			MsUnlock()
		Endif
	Next
	
	DBSkip()
	
	Do While x1_grupo == cPerg
		RecLock("SX1", .F.)
		DBDelete()
		DBSkip()
	EndDo
	
	DBSelectArea(_sAlias)

Return