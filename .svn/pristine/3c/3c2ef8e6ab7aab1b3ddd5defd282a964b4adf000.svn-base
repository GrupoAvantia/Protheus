#Include "MATR190.CH"
#Include "PROTHEUS.CH"
/*/


Ŀ
Funo     MATR190   Autor  Alexandre Inacio Lemes Data 19/05/2006
Ĵ
Descrio  Emisso da relao de amarracao Produto X Fornecedor       
Ĵ
Sintaxe    MATR190(void)                                              
Ĵ
 Uso       Generico                                                   
Ĵ


/*/
Function Matr190()

Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	//Ŀ
	//Interface de impressao                                                  
	//
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	MATR190R3()
EndIf

Return

/*/


Ŀ
Programa  ReportDef Autor  Alexandre Inacio Lemes Data  19.05.2006
Ĵ
Descrio  Emisso da relao de amarracao Produto X Fornecedor       
Ĵ
Retorno   ExpO1: Objeto do relatrio                                  
Ĵ
ParametrosNenhum                                                      
ٱ


/*/
Static Function ReportDef()

Local oReport 
Local oSection 
Local oCell         

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MATR190",STR0003,"MTR190", {|oReport| ReportPrint(oReport)},STR0001+" "+STR0002) //"Relacao de Amarracao Produtos x Fornecedor"##"Este programa tem como objetivo , relacionar os produtos e seus"##"respectivos Fornecedores."
Pergunte("MTR190",.F.)
//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
oSection := TRSection():New(oReport,STR0009,{"SA5","SA2"},/*{Array com as ordens do relatrio}*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Carga"
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//

oSection:SetHeaderPage()

TRCell():New(oSection,"A5_PRODUTO","SA5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,30,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"B1_TIPO"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"B1_GRUPO"  ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"B1_UM"     ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"A5_FORNECE","SA5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"A5_LOJA"   ,"SA5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"A2_NOME"   ,"SA2",/*Titulo*/,/*Picture*/,40,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"A5_CODPRF" ,"SA5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSection:SetNoFilter("SA2")

Return(oReport)

/*/


Ŀ
Programa  ReportPrinAutor  Alexandre Inacio Lemes Data  19/05/2006
Ĵ
Descrio Imprime o Relatorio definido pela funcao ReportDef MATR190. 
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
ٱ


/*/
Static Function ReportPrint(oReport)

Local oSection  := oReport:Section(1)
Local cAliasSA5 := "SA5"
Local cProduto  := ""
#IFNDEF TOP
	Local cCondicao := ""
#ENDIF
//Ŀ
//Filtragem do relatrio                                                  
//
dbSelectArea("SA5")
dbSetOrder(2)
#IFDEF TOP
	//Ŀ
	//Transforma parametros Range em expressao SQL                            	
	//
	MakeSqlExpr(oReport:uParam)
	//Ŀ
	//Query do relatrio da secao 1                                           
	//
	oReport:Section(1):BeginQuery()	
	
	cAliasSA5 := GetNextAlias()
	
	BeginSql Alias cAliasSA5
	SELECT A5_FILIAL,A5_PRODUTO,A5_FORNECE,A5_LOJA,A5_CODPRF
	
	FROM %table:SA5% SA5
	
	WHERE A5_FILIAL = %xFilial:SA5% AND 
		A5_PRODUTO >= %Exp:mv_par01% AND 
		A5_PRODUTO <= %Exp:mv_par02% AND 
		A5_FORNECE >= %Exp:mv_par03% AND 
		A5_FORNECE <= %Exp:mv_par04% AND
		SA5.%notDel%
		
	ORDER BY %Order:SA5% 
			
	EndSql 
	//Ŀ
	//Metodo EndQuery ( Classe TRSection )                                    
	//                                                                        
	//Prepara o relatrio para executar o Embedded SQL.                       
	//                                                                        
	//ExpA1 : Array com os parametros do tipo Range                           
	//                                                                        
	//
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)

#ELSE
	//Ŀ
	//Transforma parametros Range em expressao SQL                            
	//
	MakeAdvplExpr(oReport:uParam)

	cCondicao := 'A5_FILIAL == "'+xFilial("SA5")+'".And.' 
	cCondicao += 'A5_PRODUTO >= "'+mv_par01+'".And.A5_PRODUTO <="'+mv_par02+'".And.'
	cCondicao += 'A5_FORNECE >= "'+mv_par03+'".And.A5_FORNECE <="'+mv_par04+'" '
	
	oReport:Section(1):SetFilter(cCondicao,IndexKey())
#ENDIF		
//Ŀ
//Metodo TrPosition()                                                     
//                                                                        
//Posiciona em um registro de uma outra tabela. O posicionamento ser     
//realizado antes da impressao de cada linha do relatrio.                
//                                                                        
//                                                                        
//ExpO1 : Objeto Report da Secao                                          
//ExpC2 : Alias da Tabela                                                 
//ExpX3 : Ordem ou NickName de pesquisa                                   
//ExpX4 : String ou Bloco de cdigo para pesquisa. A string ser macroexe-
//        cutada.                                                         
//                                                                        				
//
TRPosition():New(oSection,"SA2",1,{|| xFilial("SA2") + (cAliasSA5)->A5_FORNECE + (cAliasSA5)->A5_LOJA})
TRPosition():New(oSection,"SB1",1,{|| xFilial("SB1") + (cAliasSA5)->A5_PRODUTO})
//Ŀ
//Inicio da impressao do fluxo do relatrio                               
//

oReport:SetMeter(SA5->(LastRec()))

oSection:Init()

dbSelectArea(cAliasSA5)
While !oReport:Cancel() .And. !(cAliasSA5)->(Eof())

	If oReport:Cancel()
		Exit
	EndIf
	
    If cProduto <> (cAliasSA5)->A5_PRODUTO
		oSection:Cell("A5_PRODUTO"):Show()
		oSection:Cell("B1_DESC"):Show()
		oSection:Cell("B1_TIPO"):Show()
		oSection:Cell("B1_GRUPO"):Show()
		oSection:Cell("B1_UM"):Show()
   		oSection:PrintLine()
		cProduto := (cAliasSA5)->A5_PRODUTO
    Else
		oSection:Cell("A5_PRODUTO"):Hide()
		oSection:Cell("B1_DESC"):Hide()
		oSection:Cell("B1_TIPO"):Hide()
		oSection:Cell("B1_GRUPO"):Hide()
		oSection:Cell("B1_UM"):Hide()
	   	oSection:PrintLine()	
    EndIf   
	    
   	DbSelectArea(cAliasSA5)
   	DbSkip()

    If cProduto <> (cAliasSA5)->A5_PRODUTO
		oReport:ThinLine() 
		oReport:SkipLine()
    EndIf
    
	oReport:IncMeter()

EndDo

(cAliasSA5)->(DbCloseArea())

oSection:Finish()

Return NIL

/*/


Ŀ
Funo     MATR190R3 Autor  Paulo Boschetti        Data  02.03.93 
Ĵ
Descrio  Emisso da relao de amarracao produto X fornecedor       
Ĵ
Sintaxe    MATR190R3(void)                                            
Ĵ
 Uso       Generico                                                   
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   
Ĵ
 Edson   M.   30/03/99XXXXXXPassar o tamanho na SetPrint.           
 Patricia Sal.22/12/99XXXXXXAcerto LayOut, Fornec. c/ 20 pos. e Loja
                            com 4 pos.                              
ٱ


/*/
Function Matr190R3()
//Ŀ
// Define Variaveis                                             
//
LOCAL wnrel

LOCAL cDesc1 :=STR0001	//"Este programa tem como objetivo , relacionar os produtos e seus"
LOCAL cDesc2 :=STR0002	//"respectivos Fornecedores."
LOCAL cDesc3 :=""
LOCAL cString:="SA5"
PRIVATE titulo  :=STR0003	//"Relacao de Amarracao Produto x Fornecedor"
PRIVATE tamanho :="M"
PRIVATE cPerg   := "MTR190"
PRIVATE aReturn := { STR0004, 1,STR0005, 2, 2, 1, "",1 }		//"Zebrado"###"Administracao"
PRIVATE nomeprog:= "MATR190",nLastKey := 0

//Ŀ
// Variaveis utilizadas para Imporessao do Cabecalho e Rodape   
//
wnrel := "MATR190"
//Ŀ
// Verifica as perguntas selecionadas                           
//
pergunte("MTR190",.F.)
//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01             // Produto de           	      		  
// mv_par02             // Produto Ate                          
// mv_par03             // Fornecedor De           	           
// mv_par04             // Fornecedor Ate                       
//
//Ŀ
// Envia controle para a funcao SETPRINT                        
//
wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",,tamanho)

If nLastKey == 27
	Set Filter To
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Set Filter To
	Return
Endif

RptStatus({|lEnd| R190Imp(@lEnd,wnrel,cString)},Titulo)

Return .T.

/*/


Ŀ
Funo     R190IMP   Autor  Cristina M. Ogura      Data  09.11.95 
Ĵ
Descrio  Chamada do Relatorio                                       
Ĵ
 Uso       MATR190			                                            
ٱ


/*/
Static Function R190Imp(lEnd,wnrel,cString)
LOCAL CbTxt,cbCont:=00
LOCAL lFirst := .t.
LOCAL cProdAnt := space(15)
LOCAL cProd    := space(132)
LOCAL nCount := 0
LOCAL nPosNome := 70
LOCAL nTamNome := 40

STATIC aTamSXG, aTamSXG2

// Verifica conteudo das variaveis Grupo Fornecedor (001) e Loja (002)
aTamSXG := If(aTamSXG == NIL, TamSXG("001"), aTamSXG)
aTamSXG2 := If(aTamSXG2 == NIL, TamSXG("002"), aTamSXG2)

// Verifica se utilizara LayOut Max. (Fornec. com 20 pos. e Loja com 4 pos.)
If aTamSXG[1] != aTamSXG[3]
	cabec1:= STR0008  //"CODIGO PRODUTO  DESCRICAO DO PRODUTO           TP GRUPO UM  CODIGO               LJ   RAZAO SOCIAL              CODIGO NO FORNECEDOR"
	//	  	   		          123456789012345 123456789012345678901234567890 12 123   12  12345678901234567890 1234 123456789012345678901234  12345678901234567890
	//	  		                0         1         2         3         4         5         6         7         8         9        10        11        12        13
	//				             0123456789012345678901234567890123456789012345678901234567890123456789013245678901234567890123456789012345678901234567890123456789012
	
Else
	cabec1 := STR0006  //"CODIGO PRODUTO  DESCRICAO DO PRODUTO           TP GRUPO UM  CODIGO LJ  RAZAO SOCIAL                             CODIGO NO FORNECEDOR"
	//		   		          123456789012345 123456789012345678901234567890 12 123   12  123456 12 1234567890123456789012345678901234567890  123467901234567890
	//     			          0         1         2         3         4         5         6         7         8         9        10        11        12        13
	//					          0123456789012345678901234567890123456789012345678901234567890123456789013245678901234567890123456789012345678901234567890123456789012
Endif
cabec2:= ""
cabec3:= ""
cbtxt := SPACE(10)
Li    := 80
m_pag := 01
nTipo := IIF(aReturn[04]==1,15,18)
// Verif. se utilizara Tam. Max.  (Fornec. com 20 pos. e Loja com 4 pos.)
If aTamSXG[1] != aTamSXG[3]
	nPosNome += ((aTamSXG[4]-aTamSXG[3]) + (aTamSXG2[4] - aTamSXG2[3]))
	nTamNome -= ((aTamSXG[4]-aTamSXG[3]) + (aTamSXG2[4] - aTamSXG2[3]))
Endif
dbSelectArea("SA5")
dbSetOrder(2)
Set Softseek on
dbSeek(cFilial+mv_par01)
Set Softseek off

SetRegua(RecCount())

While !Eof() .and. A5_FILIAL+A5_PRODUTO <= cFilial+mv_par02
	
	If lEnd
		@PROW()+1,001 PSAY STR0007	//"CANCELADO PELO OPERADOR"
		Exit
	Endif
	
	IncRegua()
	
	If A5_FORNECEDOR < mv_par03 .or. A5_FORNECEDOR > mv_par04
		dbSkip()
		Loop
	Endif
	
	lFirst := .T.
	nCount := 0
	cProdAnt := A5_PRODUTO
	
	While !eof() .and. A5_FILIAL+A5_PRODUTO == cFilial+cProdAnt
		
		If lEnd
			@PROW()+1,001 PSAY STR0007	//"CANCELADO PELO OPERADOR"
			Exit
		Endif
		
		If A5_FORNECEDOR < mv_par03 .or. A5_FORNECEDOR > mv_par04
			dbSkip()
			Loop
		Endif
		
		If Li > 60
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		Endif
		
		If lFirst
			dbSelectArea("SB1")
			dbSeek(cFilial+SA5->A5_PRODUTO)
			cProd := SA5->A5_PRODUTO+" "+Substr(B1_DESC,1,30)+" "+B1_TIPO+" "+B1_GRUPO+"  "+B1_UM
			@Li , 00 PSAY cProd
			lFirst := .f.
			dbSelectArea("SA5")
		Endif
		
		If Li > 60
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		Endif
		
		@Li , 60 PSAY A5_FORNECE+" "+A5_LOJA
		
		dbSelectArea("SA2")
		dbSeek(cFilial+SA5->A5_FORNECE+SA5->A5_LOJA)
		
		@Li , nPosNome PSAY SUBS(A2_NOME,1,nTamNome)
		
		dbSelectArea("SA5")
		
		@Li ,112 PSAY A5_CODPRF
		
		nCount++
		Li++
		dbSkip()
		
	End
	
	If nCount > 0
		@Li , 00 PSAY __PrtThinLine()
		Li++
	Endif
	
End

If Li != 80
	roda(CbCont,cbtxt,"M")
EnDif

//Ŀ
// Restaura a Integridade dos dados                             
//

dbSelectArea("SA5")
Set Filter To
dbSetOrder(1)

If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return .T.
